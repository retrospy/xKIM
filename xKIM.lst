AS65 Assembler for R6502 [1.42].                                     Page    1
---------------------------------- xKIM.asm ----------------------------------

2630 lines read, no errors in pass 1.
                        ;*****************************************************
                        ; Extended KIM monitor, written 2015 by Bob Applegate
                        ; K2UT - bob@corshamtech.com
                        ;
                        ; This code was written as part of the Corsham Tech
                        ; 60K RAM/EPROM board.  It provides a working area for
                        ; an extended version of the KIM-1 monitor capabilitie
                        ; mostly based on a TTY interface, not the keypad and
                        ; hex display.
                        ;
                        ; This extensions contains an assortment of basic
                        ; tools that are missing from the KIM-1's built in
                        ; console monitor.  It has commands for loading hex
                        ; files (as opposed to KIM format), accessing an SD
                        ; card system, memory edit, hex dump, etc.  It also
                        ; has the ability for user-defined extensions to be
                        ; added.  And a number of common entry points have
                        ; vectors so the monitor can be modified without
                        ; breaking programs that use it.
                        ;
                        ; I'm not claiming copyright; I wrote most of this,
                        ; but also borrowed from others (credit is given in
                        ; those sections of code).  All of the portions I've
                        ; written are free to use, but please keep my name
                        ; in the comments somewhere.  Even better, consider
                        ; buying a board from us: www.corshamtech.com
                        ;
                        ; 12/01/2015	Bob Applegate
                        ;		Initial development - V0.X
                        ; 03/15/2016	Bob Applegate
                        ;		v1.0 - First official release
                        ; 01/03/2017	Bob Applegate
                        ;		v1.1 - Added S command
                        ; 09/20/2018	Bob Applegate
                        ;		v1.2 - Added auto-run vector
                        ; 01/25/2019	Bob Applegate
                        ;		v1.3 - Added 'X' command.
                        ;		Added the 'C' command to get time.
                        ; 03/09/2019	Bob Applegate
                        ;		v1.4 - Added 'O' command.
                        ; 07/26/2020	Bob Applegate
                        ;		v1.5 - Fixed bug that caused the S
                        ;		command to create empty file with no
                        ;		contents.
                        ;		Minor typo fixes.
                        ;		Added CLD instructions.
                        ; 11/14/2020	Bob Applegate
                        ;		v1.6 - On SD error, display reason code.
                        ; 09/15/2021	Bob Applegate
                        ;		v1.7
                        ;		Added offset calculator command O.
                        ;		Added R command in Edit mode.
                        ;		Removed '.' when loading from console.
                        ; 09/20/2021	Bob Applegate
                        ;		v1.8
                        ;		Made a lot of the command handlers
                        ;		into subroutines and added vectors so
                        ;		external programs can call them.
                        ;		Fixed bugs in Edit mode.
                        ;
                        ;*****************************************************
AS65 Assembler for R6502 [1.42].                                     Page    2
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Useful constants
                        ;
0000 =                  false		equ	0
ffff =                  true		equ	~false
                        ;
                        ; Version number
                        ;
0001 =                  VERSION		equ	1
0008 =                  REVISION	equ	8
0000 =                  BETA_VER	equ	0
                        ;
                        ; Options.  If RAM_BASED is set then the code is put
                        ; into RAM, else it's in ROM.  Very handy for testing
                        ; new code; load the new code with the EPROM version
                        ; and then test away.
                        ;
0000 =                  RAM_BASED	equ	false
af80 =                  RAM_DATA_BASE	equ	$af80
b000 =                  RAM_CODE_BASE	equ	$b000
                        ;
                        ; Set the EPROM base addresses
                        ;
e000 =                  ROM_START	equ	$e000
df80 =                  RAM_START	equ	$df80
                        ;
                        ; This is something that needs more investigation.
                        ; If the SD system isn't reset enough then bad stuff
                        ; happens after the RS button is pressed.  Set this to
                        ; true to only reset the SD system on cold starts.
                        ;
0000 =                  SD_ONLY_COLD	equ	false
                        ;
                        ; Set this to true to include Bob's Tiny BASIC.  It's
                        ; a minimal (ie, tiny) BASIC interpreter good for
                        ; playing around.
                        ;
0000 =                  TINY_BASIC	equ	false
                        ;
                        ; Set this to true to include a command to get/display
                        ; the current time from Corsham Tech SD card.
                        ;
ffff =                  SHOW_RTC	equ	true
                        ;
                        ; Non-printable ASCII constants
                        ;
0000 =                  NUL		equ	$00
0008 =                  BS		equ	$08
000a =                  LF		equ	$0a
000d =                  CR		equ	$0d
001b =                  ESC		equ	$1b
0020 =                  SPC		equ	$20
                        ;
                        ; Intel HEX record types
                        ;
0000 =                  DATA_RECORD	equ	$00
0001 =                  EOF_RECORD	equ	$01
                        ;
                        ; These are various buffer sizes
                        ;
000c =                  FILENAME_SIZE	equ	12
0040 =                  BUFFER_SIZE	equ	64
AS65 Assembler for R6502 [1.42].                                     Page    3
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Max number of bytes per line for hex dump
                        ;
0010 =                  BYTESLINE	equ	16
                        ;
                        ; Flag values used to detect a cold start vs warm
                        ;
0019 =                  COLD_FLAG_1	equ	$19
0062 =                  COLD_FLAG_2	equ	$62
                        ;
                        ;=====================================================
                        ; This macro is used to verify that the current addres
                        ; meets a required value.  Used mostly to guarantee
                        ; changes don't cause entry points to move.
                        ;
                        VERIFY		macro	expected
                        		if * != expected
                        		fail	Not at requested address (expected)
                        		endif
                        		endm
                        ;
                        ;=====================================================
                        ; KIM memory locations
                        ;
                        		bss
00ef =                  		org	$00ef
00ef =                  PCL		ds	1
00f0 =                  PCH		ds	1
00f1 =                  PREG		ds	1
00f2 =                  SPUSER		ds	1
00f3 =                  ACC		ds	1
00f4 =                  YREG		ds	1
00f5 =                  XREG		ds	1
00f6 =                  CHKHI		ds	1
00f7 =                  CHKSUM		ds	1
00f8 =                  INL	    	ds	1
00f9 =                  INH		ds	1
00fa =                  POINTL		ds	1
00fb =                  POINTH		ds	1
00fc =                  TEMP		ds	1
00fd =                  TMPX		ds	1
00fe =                  CHAR		ds	1
00ff =                  MODE		ds	1
                        ;
17e7 =                  		org	$17e7
17e7 =                  CHKL		ds	1
17e8 =                  CHKH		ds	1
17e9 =                  SAVX		ds	3
17ec =                  VEB	    	ds	6
17f2 =                  CNTL30		ds	1
17f3 =                  CNTH30		ds	1
17f4 =                  TIMH		ds	1
17f5 =                  SAL		ds	1
17f6 =                  SAH	    	ds	1
17f7 =                  EAL		ds	1
17f8 =                  EAH	    	ds	1
17f9 =                  ID  		ds	1
                        ;
                        ;=====================================================
                        ; KIM I/O locations
                        ;
1740 =                  SAD		equ	$1740
AS65 Assembler for R6502 [1.42].                                     Page    4
---------------------------------- xKIM.asm ----------------------------------

1741 =                  PADD		equ	$1741
                        ;
                        ;=====================================================
                        ; KIM subroutines located in the ROMs
                        ;
1c1c =                  NMIT		equ	$1c1c	;NMI handler
1c1f =                  IRQT		equ	$1c1f	;IRQ handler
1c22 =                  RST	    	equ	$1c22	;RESET handler
1c77 =                  TTYKB		equ	$1c77	;do keyboard monitor
1e2f =                  CRLF		equ	$1e2f	;print CR/LF
1e1e =                  PRTPNT		equ	$1e1e	;print POINT
1e3b =                  PRTBYT		equ	$1e3b	;print A as two hex digits
1e5a =                  GETCH		equ	$1e5a	;get a key from tty into A
1e9e =                  OUTSP		equ	$1e9e	;print a space
1ea0 =                  OUTCH		equ	$1ea0	;print A to TTY
1dac =                  SHOW		equ	$1dac
1daf =                  SHOW1		equ	$1daf
1f63 =                  INCPT		equ	$1f63	;inc POINTL/POINTH
                        ;
                        ;=====================================================
                        ; KIM IEC save and load memory locations
                        ;
17f5 =                  IECSAL		equ $17F5
17f6 =                  IECSAH		equ $17F6
17f7 =                  IECEAL		equ $17F7
17f8 =                  IECEAH		equ $17F8
17f9 =                  IECFID		equ $17F9
f148 =                  IECSAV		equ $F148
f000 =                  IECLOD		equ $F000
                        ;
                        ;=====================================================
                        ; I assume the RAM goes from 2000 to DFFF, so carve ou
                        ; a bit for use by the monitor.
                        ;
                        		bss		;uninitialized data area
                        	if	RAM_BASED
                        		org     RAM_DATA_BASE
                        	else
df80 =                  		org	RAM_START
                        	endif
df80 =                  LowestAddress	equ	*
                        ;
                        ; Storage for registers for some monitor calls.
                        ;
df80 =                  saveA		ds	1
df81 =                  saveX		ds	1
df82 =                  saveY		ds	1
                        ;
                        ; Pointer to the subroutine that gets the next input
                        ; character.  Used for doing disk/console input.
                        ;
df83 =                  inputVector	ds	2
                        ;
                        ; Same thing for output.
                        ;
df85 =                  outputVector	ds	2
                        ;
df87 =                  filename	ds	FILENAME_SIZE+1
df94 =                  buffer		ds	BUFFER_SIZE
dfd4 =                  diskBufOffset	ds	1
dfd5 =                  diskBufLength	ds	1
                        ;
AS65 Assembler for R6502 [1.42].                                     Page    5
---------------------------------- xKIM.asm ----------------------------------

dfd6 =                  Temp16L		ds	1
dfd7 =                  Temp16H		ds	1
df81 =                  byteCount	equ	saveX
                        ;
                        ; The clock functions need some storage, but just
                        ; overlay the names onto the buffer.
                        ;
df94 =                  month		equ	buffer
df95 =                  day		equ	month+1
df96 =                  year_high	equ	day+1
df97 =                  year_low	equ	year_high+1
df98 =                  hour		equ	year_low+1
df99 =                  minute		equ	hour+1
df9a =                  second		equ	minute+1
df9b =                  day_of_week	equ	second+1
df9c =                  clock_end	equ	day_of_week+1
                        ;
                        ; The next group of memory are public and can be
                        ; used by user programs, so don't modify where any
                        ; of these are.  If you want to add new public data,
                        ; put them here, always before existing items.  Be
                        ; sure to adjust the ORG.
                        ;
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE-8
                        	else
dff8 =                  		org	ROM_START-8
                        	endif
                        ;
                        ; Before loading a hex file, the MSB of this vector
                        ; is set to FF.  After loading the file, if the MSB
                        ; is no longer FF then the address in this vector is
                        ; jumped to.  Ie, it can auto-run a file.
                        ;
dff8 =                  AutoRun		ds	2
                        ;
                        ; ColdFlag is used to determine if the extended
                        ; monitoring is doing a warm or cold start.
                        ;
dffa =                  ColdFlag	ds	2
                        ;
                        ; Address of a command table for user-created
                        ; extensions to the monitor.
                        ;
dffc =                  ExtensionAddr	ds	2
                        ;
                        ; This is the higest location in RAM usable by user
                        ; programs.  Nobody should go past this address.  If
                        ; you are writing extentions to the monitor, it's
                        ; okay to load before the address and then adjust
                        ; this down to keep others from stomping on your
                        ; extention.
                        ;
                        ; If your program modifies this value, it needs to
                        ; set it back before terminating.
                        ;
dffe =                  HighestAddress	ds	2
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    6
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Code starts at E000 and goes until FFFF, except for
                        ; the 6502 vectors at the end of memory.
                        ;
                        		code
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE
                        	else
e000 =                  		org	ROM_START
                        	endif
                        ;
                        ; Vector table of useful and fun stuff!  These must
                        ; not change or else existing code won't be calling
                        ; the right functions.
                        ;
e000 =                  BASE		equ	*
e000 : 4c60e0           reentry		jmp	extKim	;extended monitor
e003 : 4ca01e           		jmp	OUTCH	;output A to console
e006 : 4c5a1e           		jmp	GETCH	;get a key and echo
e009 : 4c5a1e           		jmp	GETCH	;no echo - KIM can't do it
e00c : 4c5ae0           		jmp	dummyRet	;future - console stat
e00f : 4c82ea           		jmp	putsil	;print string after JSR
e012 : 4cb1ea           		jmp	getHex	;get hex value in A
e015 : 4c3b1e           		jmp	PRTBYT	;print A as hex
e018 : 4ce9ea           		jmp	getStartAddr
e01b : 4cfbea           		jmp	getEndAddr
e01e : 4c0deb           		jmp	getAddrRange
e021 : 4c01e4           		jmp	doHexDump	;perform a hex dump
e024 : 4c9ce5           		jmp	doEdit		;edit memory
e027 : 4cbae6           		jmp	loadHexConsole	;load hex file via console
e02a : 4c8be6           		jmp	loadHexFile	;load hex file from SD
e02d : 4c4ce9           		jmp	doSDDiskDir	;do a disk directory
e030 : 4c1eeb           		jmp	ComputeOffset	;compute relative offset
                        ;
                        ; SD card functions
                        ;
                        		VERIFY	BASE+$0033
                        
e033 : 4c29ec           		jmp	xParInit	;initialization
e036 : 4c43ec           		jmp	xParSetWrite
e039 : 4c53ec           		jmp	xParSetRead
e03c : 4c61ec           		jmp	xParWriteByte
e03f : 4c8cec           		jmp	xParReadByte
e042 : 4cb0ec           		jmp	DiskPing
e045 : 4cc3ec           		jmp	DiskDir
e048 : 4ccdec           		jmp	DiskDirNext
e04b : 4cfeec           		jmp	DiskOpenRead
e04e : 4c2fed           		jmp	DiskRead
e051 : 4c98ed           		jmp	DiskClose
e054 : 4c2aed           		jmp	DiskOpenWrite
e057 : 4c61ed           		jmp	DiskWrite
                        ;
                        ; Even more vectors as I ran out of the reserve area!
                        ;
                        
                        ;
                        ;=====================================================
                        ;=====================================================
                        ; Anything past this point can be moved in future
                        ; releases so none of these functions nor data should
                        ; be directly accessed by user programs!!!
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    7
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is a dummy function for unimplemented functions
                        ; that basically just clears carry and returns.
                        ;
e05a : 18               dummyRet	clc
e05b : 60               		rts
                        ;
e05c : 00               defaultExt	db	0
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    8
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is the start of the extended KIM monitor.
                        ;
e05d : 4c771c           notty		jmp	TTYKB
e060 : a2ff             extKim		ldx	#$ff
e062 : 9a               		txs
e063 : a901             		lda	#$01	;see if in tty mode
e065 : 2c4017           		bit	SAD
e068 : d0f3             		bne	notty	;branch if in keyboard mode
                        
                        	if	~SD_ONLY_COLD
e06a : 2029ec           		jsr	xParInit
                        	endif
                        ;
                        ; Determine if this is a cold or warm start
                        ;
e06d : adfadf           		lda	ColdFlag
e070 : c919             		cmp	#COLD_FLAG_1
e072 : d00a             		bne	coldStart
e074 : adfbdf           		lda	ColdFlag+1
e077 : c962             		cmp	#COLD_FLAG_2
e079 : d003             		bne	coldStart
e07b : 4cf1e0           		jmp	extKimLoop	;it's a warm start
                        ;
                        ; Cold start
                        ;
e07e : a919             coldStart	lda	#COLD_FLAG_1	;indicate we've done cold
e080 : 8dfadf           		sta	ColdFlag
e083 : a962             		lda	#COLD_FLAG_2
e085 : 8dfbdf           		sta	ColdFlag+1
                        ;
                        ; Point to an empty extension set by default.
                        ;
e088 : a95c             		lda	#defaultExt&$ff	;set extension pointers
e08a : 8dfcdf           		sta	ExtensionAddr
e08d : a9e0             		lda	#defaultExt/256
e08f : 8dfddf           		sta	ExtensionAddr+1
                        ;
                        ; Set HighestAddress to just before our RAM area.
                        ;
e092 : a97f             		lda	#(LowestAddress-1)&$ff
e094 : 8dfedf           		sta	HighestAddress
e097 : a9df             		lda	#(LowestAddress-1)/256
e099 : 8dffdf           		sta	HighestAddress+1
                        	if	SD_ONLY_COLD
                        ;
                        ; Initialize the interface to the SD card.  There seem
                        ; be a problem if this is done too often, so do it jus
                        ; and be done.
                        ;
                        		jsr	xParInit
                        	endif
                        ;
                        ; Display our welcome text
                        ;
e09c : 2082ea           		jsr	putsil
e09f : 0d0a0d0a         		db	CR,LF,CR,LF
e0a3 : 457874656e6465.. 		db	"Extended KIM Monitor v"
e0b9 : 312e3820         		db	VERSION+'0','.',REVISION+'0',' '
                        	if	BETA_VER
                        		db	"BETA "
                        		db	BETA_VER+'0'
AS65 Assembler for R6502 [1.42].                                     Page    9
---------------------------------- xKIM.asm ----------------------------------

                        		db	' '
                        	endif
e0bd : 627920436f7273.. 		db	"by Corsham Technologies, LLC"
e0d9 : 0d0a             		db	CR,LF
e0db : 7777772e636f72.. 		db	"www.corshamtech.com"
e0ee : 0d0a             		db	CR,LF
                        	if	RAM_BASED
                        		db	CR,LF
                        		db	"*** RAM BASED VERSION ***"
                        		db	CR,LF
                        	endif
e0f0 : 00               		db	0
                        ;
                        ; Main command loop.  Put out prompt, get command, etc
                        ; Prints a slightly different prompt for the RAM versi
                        ;
e0f1 : d8               extKimLoop	cld
e0f2 : 20a8eb           		jsr	setInputConsole
e0f5 : 2082ea           		jsr	putsil	;output prompt
e0f8 : 0d0a             		db	CR,LF	;feel free to change it
                        	if	RAM_BASED
                        		db	"RAM"
                        	endif
e0fa : 3e00             		db	">",0
e0fc : 205a1e           		jsr	GETCH
e0ff : c90d             		cmp	#CR
e101 : f0ee             		beq	extKimLoop
e103 : c90a             		cmp	#LF
e105 : f0ea             		beq	extKimLoop
e107 : 85f3             		sta	ACC	;save key
                        ;
                        ; Now cycle through the list of commands looking for
                        ; what the user just pressed.
                        ;
e109 : a92f             		lda	#commandTable&$ff
e10b : 85fa             		sta	POINTL
e10d : a9e1             		lda	#commandTable/256
e10f : 85fb             		sta	POINTH
e111 : 2030ea           		jsr	searchCmd	;try to find it
                        ;
                        ; Hmmm... wasn't one of the built in commands, so
                        ; see if it's an extended command.
                        ;
e114 : adfcdf           		lda	ExtensionAddr
e117 : 85fa             		sta	POINTL
e119 : adfddf           		lda	ExtensionAddr+1
e11c : 85fb             		sta	POINTH
e11e : 2030ea           		jsr	searchCmd
                        ;
                        ; If that returns, then the command was not found.
                        ; Print that it's unknown.
                        ;
e121 : 2082ea           		jsr	putsil
e124 : 202d204875683f00 		db	" - Huh?",0
e12c : 4cf1e0           cmdFound	jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Vector table of commands.  Each entry consists of a
                        ; single ASCII character (the command), a pointer to
                        ; the function which handles the command, and a pointe
                        ; to a string that describes the command.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   10
---------------------------------- xKIM.asm ----------------------------------

e12f : 3f               commandTable	db	'?'
e130 : 9ae3             		dw	showHelp
e132 : 85e1             		dw	quesDesc
                        ;
                        	if	TINY_BASIC
                        		db	'B'
                        		dw	TBasicCold
                        		dw	bDesc
                        	endif
                        ;
                        	if	SHOW_RTC
e134 : 43               		db	'C'
e135 : 9ded             		dw	doShowClock
e137 : a2e1             		dw	cDesc
                        	endif
e139 : 44               		db	'D'
e13a : 2fe9             		dw	doDiskDir
e13c : bbe1             		dw	dDesc
                        ;
e13e : 45               		db	'E'	;edit memory
e13f : 81e5             		dw	editMemory
e141 : d8e1             		dw	eDesc
                        ;
e143 : 48               		db	'H'	;hex dump
e144 : f3e3             		dw	hexDump
e146 : f2e1             		dw	hDesc
                        ;
e148 : 4a               		db	'J'	;jump to address
e149 : ffe8             		dw	jumpAddress
e14b : 10e2             		dw	jDesc
                        ;
e14d : 4b               		db	'K'	;return to KIM monitor
e14e : 6ee3             		dw	returnKim
e150 : 2ee2             		dw	kDesc
                        ;
e152 : 4c               		db	'L'	;load Intel HEX file
e153 : 2fe6             		dw	loadHex
e155 : 4ee2             		dw	lDesc
                        ;
e157 : 4d               		db	'M'	;perform memory test
e158 : 86e4             		dw	memTest
e15a : 6ae2             		dw	mDesc
                        ;
e15c : 4f               		db	'O'	;branch offset calculator
e15d : c0e9             		dw	offCalc
e15f : 84e2             		dw	oDesc
                        ;
e161 : 50               		db	'P'	;ping remote disk
e162 : 10e9             		dw	pingDisk
e164 : aae2             		dw	pDesc
                        ;
e166 : 53               		db	'S'	;save memory as hex file
e167 : c6e7             		dw	saveHex
e169 : cde2             		dw	sDesc
                        ;
e16b : 54               		db	'T'	;type a file on SD
e16c : 6be9             		dw	typeFile
e16e : efe2             		dw	tDesc
                        ;
e170 : 58               		db	'X'	;return to KIM monitor
e171 : 6ee3             		dw	returnKim
e173 : 2ee2             		dw	kDesc
AS65 Assembler for R6502 [1.42].                                     Page   11
---------------------------------- xKIM.asm ----------------------------------

                        ;
e175 : 59               		db	'Y'	;save memory to IEC
e176 : 32ee             		dw	saveIEC
e178 : 2ee2             		dw	kDesc
                        ;
e17a : 58               		db	'X'	;load memory from IEC
e17b : 79ee             		dw	loadIEC
e17d : 2ee2             		dw	kDesc
                        ;
e17f : 21               		db	'!'	;do cold restart
e180 : 94e3             		dw	doCold
e182 : 50e3             		dw	bangDesc
                        ;
e184 : 00               		db	0	;marks end of table
                        ;
                        ;=====================================================
                        ; Descriptions for each command in the command table.
                        ; This wastes a lot of space... I'm open for any
                        ; suggestions to keep the commands clear but reducing
                        ; the amount of space this table consumes.
                        ;
e185 : 3f202e2e2e2e2e.. quesDesc	db	"? ........... Show this help",0
                        	if	TINY_BASIC
                        bDesc		db	"B ........... Bob's Tiny BASIC",0
                        	endif
                        	if	SHOW_RTC
e1a2 : 43202e2e2e2e2e.. cDesc		db	"C ........... Show clock",0
                        	endif
e1bb : 44202e2e2e2e2e.. dDesc		db	"D ........... Disk directory",0
e1d8 : 45207878787820.. eDesc		db	"E xxxx ...... Edit memory",0
e1f2 : 48207878787820.. hDesc		db	"H xxxx xxxx . Hex dump memory",0
e210 : 4a207878787820.. jDesc		db	"J xxxx ...... Jump to address",0
e22e : 4b202e2e2e2e2e.. kDesc		db	"K ........... Go to KIM monitor",0
e24e : 4c202e2e2e2e2e.. lDesc		db	"L ........... Load HEX file",0
e26a : 4d207878787820.. mDesc		db	"M xxxx xxxx . Memory test",0
e284 : 4f207878787820.. oDesc		db	"O xxxx xxxx . Calculate branch offset",0
e2aa : 50202e2e2e2e2e.. pDesc		db	"P ........... Ping disk controller",0
e2cd : 53207878787820.. sDesc		db	"S xxxx xxxx . Save memory to file",0
e2ef : 54202e2e2e2e2e.. tDesc		db	"T ........... Type disk file",0
e30c : 5920787878782d.. yDesc		db  "Y xxxx-xxxx . Save memory to IEC",0
e32d : 5a202e2e2e2e2e.. zDesc		db 	"Z ........... Load memory from IEC",0
e350 : 21202e2e2e2e2e.. bangDesc	db	"! ........... Do a cold start",0
                        ;
                        ;=====================================================
                        ; Return to KIM monitor.  Before returning, set the
                        ; "open address" to the start of the extended monitor
                        ; so the KIM monitor is pointing to it by default.
                        ;
e36e : 2082ea           returnKim	jsr	putsil
e371 : 0d0a             		db	CR,LF
e373 : 52657475726e69.. 		db	"Returning to KIM..."
e386 : 0d0a00           		db	CR,LF,0
e389 : a900             		lda	#reentry&$0ff
e38b : 85fa             		sta	POINTL	;point back to start...
e38d : a9e0             		lda	#reentry/256
e38f : 85fb             		sta	POINTH	;...of this code
e391 : 4caf1d           		jmp	SHOW1	;return to KIM
                        ;
                        ;=====================================================
                        ; Force a cold start.
                        ;
e394 : eefadf           doCold		inc	ColdFlag	;foul up flag
AS65 Assembler for R6502 [1.42].                                     Page   12
---------------------------------- xKIM.asm ----------------------------------

e397 : 4c60e0           		jmp	extKim		;...and restart
                        ;
                        ;=====================================================
                        ; Command handler for the ? command
                        ;
e39a : 2082ea           showHelp	jsr	putsil
e39d : 0d0a             		db	CR,LF
e39f : 417661696c6162.. 		db	"Available commands:"
e3b2 : 0d0a0a00         		db	CR,LF,LF,0
                        ;
                        ; Print help for built-in commands...
                        ;
e3b6 : a92f             		lda	#commandTable&$ff
e3b8 : 85fa             		sta	POINTL
e3ba : a9e1             		lda	#commandTable/256
e3bc : 85fb             		sta	POINTH
e3be : 2053ea           		jsr	displayHelp	;display help
                        ;
                        ; Now print help for the extension commands...
                        ;
e3c1 : adfcdf           		lda	ExtensionAddr
e3c4 : 85fa             		sta	POINTL
e3c6 : adfddf           		lda	ExtensionAddr+1
e3c9 : 85fb             		sta	POINTH
e3cb : 2053ea           		jsr	displayHelp
e3ce : 202f1e           		jsr	CRLF
e3d1 : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This is a generic "not done yet" holder.  Any
                        ; unimplemented commands should point here.
                        ;
e3d4 : 2082ea           NDY		jsr	putsil
e3d7 : 0d0a             		db	CR,LF
e3d9 : 536f7272792c20.. 		db	"Sorry, not done yet."
e3ed : 0d0a00           		db	CR,LF,0
e3f0 : 4cf1e0           NDYdone		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a hex dump of a region of memory.  This code was
                        ; taken from MICRO issue 5, from an article by
                        ; J.C. Williams.  I changed it a bit, but it's still
                        ; basically the same code.
                        ;
                        ; Slight bug: the starting address is rounded down to
                        ; a multiple of 16.  I'll fix it eventually.
                        ;
e3f3 : 200deb           hexDump		jsr	getAddrRange
e3f6 : b0f8             		bcs	NDYdone
e3f8 : 202f1e           		jsr	CRLF
e3fb : 2001e4           		jsr	doHexDump	;subroutine does it
e3fe : 4cf1e0           cmdRet2		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine does a hex dump from the address in
                        ; SAL/H to EAL/H.
                        ;
                        ; Move start address to POINT but rounded down to the
                        ; 16 byte boundary.
                        ;
e401 : adf617           doHexDump	lda	SAH
e404 : 85fb             		sta	POINTH
AS65 Assembler for R6502 [1.42].                                     Page   13
---------------------------------- xKIM.asm ----------------------------------

e406 : adf517           		lda	SAL
e409 : 29f0             		and	#$f0	;force to 16 byte
e40b : 85fa             		sta	POINTL
                        ;
                        ; This starts each line.  Set flag to indcate we're
                        ; doing the hex portion, print address, etc.
                        ;
e40d : a900             hexdump1	lda	#0	;set flag to hex mode
e40f : 8df917           		sta	ID
e412 : 202f1e           		jsr	CRLF
e415 : 201e1e           		jsr	PRTPNT	;print the address
e418 : a5fa             hexdump2	lda	POINTL	;push start of line...
e41a : 48               		pha		;...address onto stack
e41b : a5fb             		lda	POINTH
e41d : 48               		pha
e41e : 207cea           		jsr	space2
e421 : a20f             		ldx	#BYTESLINE-1	;number of bytes per line
e423 : 207cea           		jsr	space2	;space before data
                        
e426 : a000             hexdump3	ldy	#0	;get next byte...
e428 : b1fa             		lda	(POINTL),y
e42a : 2cf917           		bit	ID	;hex or ASCII mode?
e42d : 1010             		bpl	hexptbt	;branch if hex mode
                        ;
                        ; Print char if printable, else print a dot
                        ;
e42f : c920             		cmp	#' '
e431 : 9004             		bcc	hexdot
e433 : c97e             		cmp	#'~'
e435 : 9002             		bcc	hexpr
e437 : a92e             hexdot		lda	#'.'
e439 : 20a01e           hexpr		jsr	OUTCH
e43c : 4c45e4           		jmp	hexend
                        ;
                        ; Print character as hex.  
                        ;
e43f : 203b1e           hexptbt 	jsr	PRTBYT	;print as hex
e442 : 207fea           		jsr	space	;and follow with a space
                        ;
                        ; See if we just dumped the last address.  If not, the
                        ; increment to the next address and continue.
                        ;
e445 : a5fa             hexend  	lda	POINTL	;compare first
e447 : cdf717           		cmp	EAL
e44a : a5fb             		lda	POINTH
e44c : edf817           		sbc	EAH
                        ;
                        ; Now increment to the next address
                        ;
e44f : 08               		php
e450 : 20631f           		jsr	INCPT
e453 : 28               		plp
e454 : 901a             		bcc	hexlntst
                        ;
e456 : 2cf917           		bit	ID
e459 : 3022             		bmi	hexdone
e45b : ca               		dex
e45c : 3006             		bmi	hexdomap
e45e : 2079ea           hexdump5	jsr	space3
e461 : ca               		dex
e462 : 10fa             		bpl	hexdump5
e464 : cef917           hexdomap	dec	ID
AS65 Assembler for R6502 [1.42].                                     Page   14
---------------------------------- xKIM.asm ----------------------------------

e467 : 68               		pla
e468 : 85fb             		sta	POINTH
e46a : 68               		pla
e46b : 85fa             		sta	POINTL
e46d : 4c18e4           		jmp     hexdump2
e470 : ca               hexlntst	dex
e471 : 10b3             		bpl	hexdump3
e473 : 2cf917           		bit	ID
e476 : 10ec             		bpl	hexdomap
e478 : 68               		pla
e479 : 68               		pla
e47a : 4c0de4           		jmp	hexdump1
                        ;
                        ; Clean up the stack and we're done
                        ;
e47d : 202f1e           hexdone		jsr	CRLF
e480 : 68               		pla
e481 : 68               		pla
e482 : 60               		rts
                        ;
                        ;=====================================================
                        ; This does a memory test of a region of memory.  One
                        ; problem with the KIM is that there is no routine to
                        ; see if a new character is starting, so this loop
                        ; just runs forever unless the user presses RESET.
                        ;
                        ; Asks for the starting and ending locations.
                        ;
                        ; This cycles a rolling bit, then adds a ninth
                        ; pattern to help detect shorted address bits.
                        ; Ie: 01, 02, 04, 08, 10, 20, 40, 80, BA
                        ;
17e7 =                  pattern		equ	CHKL		;re-use some KIM locations
17e8 =                  original	equ	CHKH
                        ;
                        ; Test patterns
                        ;
0001 =                  PATTERN_0	equ	$01
00ba =                  PATTERN_9	equ	$ba
                        ;
e483 : 4cf1e0           cmdRet5		jmp	extKimLoop
e486 : 200deb           memTest		jsr	getAddrRange	;get range
e489 : b0f8             		bcs	cmdRet5		;branch if abort
                        ;
e48b : 2082ea           		jsr	putsil
e48e : 0d0a             		db	CR,LF
e490 : 54657374696e67.. 		db	"Testing memory.  Press RESET to abort"
e4b5 : 00               		db	0
e4b6 : a901             		lda	#PATTERN_0	;only set initial...
e4b8 : 8de717           		sta	pattern		;..pattern once
                        ;
                        ; Start of loop.  This fills/tests one complete pass
                        ; of memory.
                        ;
e4bb : adf517           memTestMain	lda	SAL	;reset pointer to start
e4be : 85fa             		sta	POINTL
e4c0 : adf617           		lda	SAH
e4c3 : 85fb             		sta	POINTH
                        ;
                        ; Fill memory with the rolling pattern until the last
                        ; location is filled.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   15
---------------------------------- xKIM.asm ----------------------------------

e4c5 : a000             		ldy	#0
e4c7 : ade717           		lda	pattern
e4ca : 8de817           		sta	original
e4cd : 91fa             memTestFill	sta	(POINTL),y
e4cf : c9ba             		cmp	#PATTERN_9	;at last pattern?
e4d1 : d005             		bne	memFill3
e4d3 : a901             		lda	#PATTERN_0	;restart pattern
e4d5 : 4cdde4           		jmp	memFill4
                        ;
                        ; Rotate pattern left one bit
                        ;
e4d8 : 0a               memFill3	asl	a
e4d9 : 9002             		bcc	memFill4	;branch if not overflow
e4db : a9ba             		lda	#PATTERN_9	;ninth pattern
                        ;
                        ; The new pattern is in A.  Now see if we've reached
                        ; the end of the area to be tested.
                        ;
e4dd : 48               memFill4	pha			;save pattern
e4de : a5fa             		lda	POINTL
e4e0 : cdf717           		cmp	EAL
e4e3 : d007             		bne	memFill5
e4e5 : a5fb             		lda	POINTH
e4e7 : cdf817           		cmp	EAH
e4ea : f007             		beq	memCheck
                        ;
                        ; Not done, so move to next address and keep going.
                        ;
e4ec : 20631f           memFill5	jsr	INCPT
e4ef : 68               		pla			;recover pattern
e4f0 : 4ccde4           		jmp	memTestFill
                        ;
                        ; Okay, memory is filled, so now go back and test it.
                        ; We kept a backup copy of the initial pattern to
                        ; use, but save the current pattern as the starting
                        ; point for the next pass.
                        ;
e4f3 : 68               memCheck	pla
e4f4 : 8de717           		sta	pattern		;for next pass
e4f7 : adf517           		lda	SAL		;reset pointer to start
e4fa : 85fa             		sta	POINTL
e4fc : adf617           		lda	SAH
e4ff : 85fb             		sta	POINTH
e501 : ade817           		lda	original	;restore initial pattern
e504 : a000             		ldy	#0
e506 : d1fa             memTest2	cmp	(POINTL),y
e508 : d02c             		bne	memFail
e50a : c9ba             		cmp	#PATTERN_9
e50c : d004             		bne	memTest3
                        ;
                        ; Time to reload the pattern
                        ;
e50e : a901             		lda	#PATTERN_0
e510 : d005             		bne	memTest4
                        ;
                        ; Rotate pattern left one bit
                        ;
e512 : 0a               memTest3	asl	a
e513 : 9002             		bcc	memTest4
e515 : a9ba             		lda	#PATTERN_9
                        ;
                        ; The new pattern is in A.
AS65 Assembler for R6502 [1.42].                                     Page   16
---------------------------------- xKIM.asm ----------------------------------

                        ;
e517 : 48               memTest4	pha			;save pattern
e518 : a5fa             		lda	POINTL
e51a : cdf717           		cmp	EAL
e51d : d007             		bne	memTest5	;not at end
e51f : a5fb             		lda	POINTH
e521 : cdf817           		cmp	EAH
e524 : f007             		beq	memDone		;at end of pass
                        ;
                        ; Not at end yet, so inc pointer and continue
                        ;
e526 : 20631f           memTest5	jsr	INCPT
e529 : 68               		pla
e52a : 4c06e5           		jmp	memTest2
                        ;
                        ; Another pass has completed.
                        ;
e52d : 68               memDone		pla
e52e : a92e             		lda	#'.'
e530 : 20a01e           		jsr	OUTCH
e533 : 4cbbe4           		jmp	memTestMain
                        ;
                        ; Failure.  Display the failed address, the expected
                        ; value and what was actually there.
                        ;
e536 : 48               memFail		pha		;save pattern for error report
e537 : 2082ea           		jsr	putsil
e53a : 0d0a             		db	CR,LF
e53c : 4661696c757265.. 		db	"Failure at address ",0
e550 : 201e1e           		jsr	PRTPNT
e553 : 2082ea           		jsr	putsil
e556 : 2e202045787065.. 		db	".  Expected ",0
e563 : 68               		pla
e564 : 203b1e           		jsr	PRTBYT
e567 : 2082ea           		jsr	putsil
e56a : 2062757420676f.. 		db	" but got ",0
e574 : a000             		ldy	#0
e576 : b1fa             		lda	(POINTL),y
e578 : 203b1e           		jsr	PRTBYT
e57b : 202f1e           		jsr	CRLF
e57e : 4cf1e0           cmdRet4		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Edit memory.  This waits for a starting address to b
                        ; entered.  It will display the current address and it
                        ; contents.  Possible user inputs and actions:
                        ;
                        ;   Two hex digits will place that value in memory
                        ;   RETURN moves to next address
                        ;   BACKSPACE moves back one address
                        ;
e581 : 207fea           editMemory      jsr	space
e584 : 20e9ea           		jsr	getStartAddr
e587 : b0f5             		bcs	cmdRet4
e589 : adf517           		lda	SAL		;move address into...
e58c : 85fa             		sta	POINTL		;...POINT
e58e : adf617           		lda	SAH
e591 : 85fb             		sta	POINTH
e593 : 202f1e           		jsr	CRLF
e596 : 209ce5           		jsr	doEdit
e599 : 4cf1e0           		jmp	extKimLoop
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   17
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This subroutine edits memory.  On entry, POINT has
                        ; the first address to edit.  Upon exit, POINT will
                        ; have been updated to next address to edit.
                        ;
                        ; Display the current location
                        ;
e59c : 201e1e           doEdit		jsr	PRTPNT		;print address
e59f : 207fea           		jsr	space
e5a2 : a000             		ldy	#0
e5a4 : b1fa             		lda	(POINTL),y	;get byte
e5a6 : 203b1e           		jsr	PRTBYT		;print it
e5a9 : 207fea           		jsr	space
                        ;
e5ac : 20b1ea           		jsr	getHex
e5af : b00d             		bcs	editMem2	;not hex
e5b1 : a000             editMem7	ldy	#0
e5b3 : 91fa             		sta	(POINTL),y	;save new value
                        ;
                        ; Bump POINT to next location
                        ;
e5b5 : 202f1e           editMem3	jsr	CRLF
e5b8 : 20631f           		jsr	INCPT
e5bb : 4c9ce5           		jmp	doEdit
                        ;
                        ; Not hex, so see if another command.  Valid commands 
                        ;
                        ;    CR = advance to next memory location
                        ;    BS = move to previous location
                        ;    R  = compute relative offset
                        ;
e5be : c952             editMem2	cmp	#'R'		;compute relative branch
e5c0 : f017             		beq	editMem4
e5c2 : c90d             		cmp	#CR
e5c4 : f0ef             		beq	editMem3	;move to next
e5c6 : c908             		cmp	#BS
e5c8 : d00e             		bne     editexit		;else exit
                        ;
                        ; Move back one location
                        ;
e5ca : a5fa             		lda	POINTL
e5cc : d002             		bne	editMem8
e5ce : c6fb             		dec	POINTH
e5d0 : c6fa             editMem8	dec	POINTL
e5d2 : 202f1e           		jsr	CRLF
e5d5 : 4c9ce5           		jmp	doEdit
                        ;
e5d8 : 60               editexit	rts
                        ;
                        ; They want to calculate a relative offset
                        ;
e5d9 : 2082ea           editMem4	jsr	putsil
e5dc : 656c6174697665.. 		db	"elative offset to: ",0
e5f0 : 20fbea           		jsr	getEndAddr
e5f3 : b0a7             		bcs	doEdit		;bad input
                        ;
                        ; Need to load POINTL/POINTH into SAL/SAH and then
                        ; decrement by one.
                        ;
e5f5 : a5fb             		lda	POINTH
e5f7 : 8df617           		sta	SAH
e5fa : a5fa             		lda	POINTL
AS65 Assembler for R6502 [1.42].                                     Page   18
---------------------------------- xKIM.asm ----------------------------------

e5fc : 8df517           		sta	SAL
e5ff : d003             		bne	editMem5
e601 : cef617           		dec	SAH
e604 : cef517           editMem5	dec	SAL
                        ;
e607 : 201eeb           		jsr	ComputeOffset
e60a : 9016             		bcc	editMem6	;value good
e60c : 2082ea           		jsr	putsil
e60f : 202d206f757420.. 		db	" - out of range",0
e61f : 4c9ce5           		jmp	doEdit
                        ;
                        ; Relative offset is in A.
                        ;
e622 : 48               editMem6	pha
e623 : 207fea           		jsr	space
e626 : 68               		pla
e627 : 48               		pha
e628 : 203b1e           		jsr	PRTBYT		;print it
e62b : 68               		pla
e62c : 4cb1e5           		jmp	editMem7	;store it
                        ;
                        ;=====================================================
                        ; This handles the Load hex command.
                        ;
e62f : 2082ea           loadHex		jsr	putsil
e632 : 0d0a             		db	CR,LF
e634 : 456e7465722066.. 		db	"Enter filename, or Enter to "
e650 : 6c6f6164206672.. 		db	"load from console: ",0
                        ;
e664 : 2050eb           		jsr	getFileName	;get filename
e667 : ad87df           		lda	filename	;null?
e66a : d006             		bne	loaddiskfile
e66c : 20bae6           		jsr	loadHexConsole	;load from console
e66f : 4c7ee6           		jmp	loadCheckAuto	;check auto-run
                        ;
                        ; Open the file
                        ;
e672 : a087             loaddiskfile	ldy	#filename&$ff
e674 : a2df             		ldx	#filename/256
e676 : a9ff             		lda	#$ff
e678 : 8df917           		sta	ID		;print dots
e67b : 208be6           		jsr	loadHexFile
                        ;
                        ; If the auto-run vector is no longer $ffff, then jump
                        ; to whatever it points to.
                        ;
e67e : adf9df           loadCheckAuto	lda	AutoRun+1
e681 : c9ff             		cmp	#$ff		;unchanged?
e683 : f003             		beq	lExit11
e685 : 6cf8df           		jmp	(AutoRun)	;execute!
e688 : 4cf1e0           lExit11		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine loads a hex file from the SD.  On
                        ; entry the pointer to the filename is in X (MSB) and
                        ; Y (LSB).
                        ;
e68b : a9ff             loadHexFile	lda	#$ff
e68d : 8df9df           		sta	AutoRun+1
e690 : 8df917           		sta	ID		;we want dots
e693 : 20feec           		jsr	DiskOpenRead
e696 : 901c             		bcc	loadHexOk	;opened ok
AS65 Assembler for R6502 [1.42].                                     Page   19
---------------------------------- xKIM.asm ----------------------------------

                        ;
e698 : 2082ea           openfail	jsr	putsil
e69b : 0d0a             		db	CR,LF
e69d : 4661696c656420.. 		db	"Failed to open file"
e6b0 : 0d0a00           		db	CR,LF,0
e6b3 : 60               		rts
                        ;
e6b4 : 20b3eb           loadHexOk	jsr	setInputFile	;redirect input
e6b7 : 4cf2e6           		jmp	loadStart
                        ;
                        ;=====================================================
                        ; This subroutine is called to load a hex file from
                        ; the console.
                        ;
e6ba : a9ff             loadHexConsole	lda	#$ff
e6bc : 8df9df           		sta	AutoRun+1
e6bf : a900             		lda	#0
e6c1 : 8df917           		sta	ID		;don't print dots
e6c4 : 2082ea           		jsr	putsil
e6c7 : 0d0a             		db	CR,LF
e6c9 : 57616974696e67.. 		db	"Waiting for file, or ESC to"
e6e4 : 20657869742e2e2e 		db	" exit..."
e6ec : 0d0a00           		db	CR,LF,0
e6ef : 20a8eb           		jsr	setInputConsole
                        ;
                        ; The start of a line.  First character should be a
                        ; colon, but toss out CRs, LFs, etc.  Anything else
                        ; causes an abort.
                        ;
e6f2 : 20a5eb           loadStart	jsr	redirectedGetch	;get start of line
e6f5 : c90d             		cmp	#CR
e6f7 : f0f9             		beq	loadStart
e6f9 : c90a             		cmp	#LF
e6fb : f0f5             		beq	loadStart
e6fd : c93a             		cmp	#':'		;what we expect
e6ff : d05a             		bne	loadAbortB
                        ;
                        ; Get the header of the record
                        ;
e701 : a900             		lda	#0
e703 : 8de717           		sta	CHKL		;initialize checksum
                        ;
e706 : 20b1ea           		jsr	getHex		;get byte count
e709 : b054             		bcs	loadAbortC
e70b : 8d81df           		sta	byteCount	;save byte count
e70e : 20f5e8           		jsr	updateCrc
e711 : 20b1ea           		jsr	getHex		;get the MSB of offset
e714 : b04d             		bcs	loadAbortD
e716 : 85fb             		sta	POINTH
e718 : 20f5e8           		jsr	updateCrc
e71b : 20b1ea           		jsr	getHex		;get LSB of offset
e71e : b047             		bcs	loadAbortE
e720 : 85fa             		sta	POINTL
e722 : 20f5e8           		jsr	updateCrc
e725 : 20b1ea           		jsr	getHex		;get the record type
e728 : b041             		bcs	loadAbortF
e72a : 20f5e8           		jsr	updateCrc
                        ;
                        ; Only handle two record types:
                        ;    00 = data record
                        ;    01 = end of file record
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   20
---------------------------------- xKIM.asm ----------------------------------

e72d : c900             		cmp	#DATA_RECORD
e72f : f05d             		beq	loadDataRec
e731 : c901             		cmp	#EOF_RECORD
e733 : f042             		beq	loadEof
                        ;
                        ; Unknown record type
                        ;
e735 : a941             		lda	#'A'		;reason
                        ;
                        ; This is the common error handler for various reasons
                        ; On entry A contains an ASCII character which is outp
                        ; to indicate the specific error reason.
                        ;
e737 : 48               loadAbort       pha			;save reason
e738 : 2082ea           		jsr	putsil
e73b : 0d0a             		db	CR,LF
e73d : 41626f7274696e.. 		db	"Aborting, reason: "
e74f : 00               		db	0
e750 : 68               		pla			;restore and...
e751 : 20a01e           		jsr	OUTCH		;...display reason
e754 : 202f1e           		jsr	CRLF
e757 : 20a8eb           loadExit	jsr	setInputConsole
e75a : 60               		rts
                        ;
                        ; Various error reason codes.  This was meant to be
                        ; very temporary as I worked out the real problem, but
                        ; this debug code immediately "solved" the problem so
                        ; I just left these as-is until the root cause is
                        ; discovered.
                        ;
e75b : a942             loadAbortB	lda	#'B'
e75d : d0d8             		bne	loadAbort
                        ;
e75f : a943             loadAbortC	lda	#'C'
e761 : d0d4             		bne	loadAbort
                        ;
e763 : a944             loadAbortD	lda	#'D'
e765 : d0d0             		bne	loadAbort
                        ;
e767 : a945             loadAbortE	lda	#'E'
e769 : d0cc             		bne	loadAbort
                        ;
e76b : a946             loadAbortF	lda	#'F'
e76d : d0c8             		bne	loadAbort
                        ;
e76f : a947             loadAbortG	lda	#'G'
e771 : d0c4             		bne	loadAbort
                        ;
e773 : a948             loadAbortH	lda	#'H'
e775 : d0c0             		bne	loadAbort
                        ;
                        ; EOF is easy
                        ;
e777 : 20b1ea           loadEof		jsr	getHex		;get checksum
e77a : 20a8eb           		jsr	setInputConsole	;reset input vector
e77d : 2082ea           		jsr	putsil
e780 : 0d0a             		db	CR,LF
e782 : 5375636365737321 		db	"Success!"
e78a : 0d0a00           		db	CR,LF,0
e78d : 60               		rts
                        ;
                        ; Data records have more work.  After processing the
AS65 Assembler for R6502 [1.42].                                     Page   21
---------------------------------- xKIM.asm ----------------------------------

                        ; line, print a dot to indicate progress.  This should
                        ; be re-thought as it could slow down loading a really
                        ; big file if the console speed is slow.
                        ;
e78e : ae81df           loadDataRec	ldx	byteCount	;byte count
e791 : a000             		ldy	#0		;offset
e793 : 8e81df           loadData1	stx	byteCount
e796 : 8c82df           		sty	saveY
e799 : 20b1ea           		jsr	getHex
e79c : b0d1             		bcs	loadAbortG
e79e : 20f5e8           		jsr	updateCrc
e7a1 : ac82df           		ldy	saveY
e7a4 : ae81df           		ldx	byteCount
e7a7 : 91fa             		sta	(POINTL),y
e7a9 : c8               		iny
e7aa : ca               		dex
e7ab : d0e6             		bne	loadData1
                        ;
                        ; All the bytes were read so get the checksum and see
                        ; if it agrees.  The checksum is a twos-complement, so
                        ; just add the checksum into what we've been calculati
                        ; and if the result is zero then the record is good.
                        ;
e7ad : 20b1ea           		jsr	getHex		;get checksum
e7b0 : 18               		clc
e7b1 : 6de717           		adc	CHKL
e7b4 : d0bd             		bne	loadAbortH	;non-zero is error
                        ;
                        ; If loading from an SD file then print a dot at the
                        ; end of each record.  Doing this for serial input is
                        ; very bad because the terminal program is sending the
                        ; next character while this is sending the dot.  Ie,
                        ; data is lost.
                        ;
e7b6 : adf917           		lda	ID
e7b9 : f005             		beq	lrecdone	;jump if not file
e7bb : a92e             		lda	#'.'		;sanity indicator when
e7bd : 20a01e           		jsr	OUTCH		;...loading from file
e7c0 : 4cf2e6           lrecdone	jmp	loadStart
e7c3 : 4cf1e0           lExit1		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory as a
                        ; file on the SD.
                        ;
e7c6 : 200deb           saveHex		jsr	getAddrRange	;get range to dump
e7c9 : b0f8             		bcs	lExit1	;abort on error
                        ;
                        ; Get the filename to save to
                        ;
e7cb : 2082ea           		jsr	putsil
e7ce : 0d0a             		db	CR,LF
e7d0 : 456e7465722066.. 		db	"Enter filename, or Enter to "
e7ec : 646973706c6179.. 		db	"display to console: ",0
                        ;
e801 : 2050eb           		jsr	getFileName	;get filename
e804 : ad87df           		lda	filename	;null?
e807 : f012             		beq	saveHexConsole	;dump to console
                        ;
                        ; They selected a file, so try to open it.
                        ;
e809 : a2df             		ldx	#filename>>8
AS65 Assembler for R6502 [1.42].                                     Page   22
---------------------------------- xKIM.asm ----------------------------------

e80b : a087             		ldy	#filename&$ff
e80d : 202aed           		jsr	DiskOpenWrite	;attempt to open file
e810 : 9003             		bcc	sopenok		;branch if opened ok
e812 : 4c98e6           		jmp	openfail
                        ;
e815 : 20fdeb           sopenok		jsr	setOutputFile
e818 : 4c1ee8           		jmp	savehex2
                        ;
                        ; They are saving to the console.  Set up the output
                        ; vector and do the job.
                        ;
e81b : 20f2eb           saveHexConsole	jsr	setOutputConsole
                        ;
                        ; Compute the number of bytes to dump
                        ;
e81e : 38               savehex2	sec
e81f : adf717           		lda	EAL
e822 : edf517           		sbc	SAL
e825 : 8dd6df           		sta	Temp16L
e828 : adf817           		lda	EAH
e82b : edf617           		sbc	SAH
e82e : 8dd7df           		sta	Temp16H
e831 : 9042             		bcc	SDone	;start > end
e833 : 0900             		ora	#0
e835 : 303e             		bmi	SDone	;more than 32K seems wrong
                        ;
                        ; Add one to the count
                        ;
e837 : eed6df           		inc	Temp16L
e83a : d003             		bne	slab1
e83c : eed7df           		inc	Temp16H
                        ;
                        ; Move pointer to zero page
                        ;
e83f : adf517           slab1		lda	SAL
e842 : 85fa             		sta	POINTL
e844 : adf617           		lda	SAH
e847 : 85fb             		sta	POINTH
                        ;
                        ; Top of each loop.  Start by seeing if there are any 
                        ; left to dump.
                        ;
e849 : add7df           Sloop1		lda	Temp16H
e84c : d02a             		bne	Sgo	;more to do
e84e : add6df           		lda	Temp16L
e851 : d025             		bne	Sgo	;more to do
                        ;
                        ; At end of the region, so output an end record.  This
                        ; probably looks like overkill but keep in mind this
                        ; might be going to a file so we can't use the normal
                        ; string put functions.
                        ;
e853 : a93a             		lda	#':'
e855 : 20e0eb           		jsr	redirectedOutch
e858 : a900             		lda	#0
e85a : 20c6eb           		jsr	HexToOutput
e85d : 20c6eb           		jsr	HexToOutput
e860 : 20c6eb           		jsr	HexToOutput
e863 : a901             		lda	#1
e865 : 20c6eb           		jsr	HexToOutput
e868 : a9ff             		lda	#$ff
e86a : 20c6eb           		jsr	HexToOutput
AS65 Assembler for R6502 [1.42].                                     Page   23
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; If output to file, flush and close the file.
                        ;
e86d : ad87df           		lda	filename
e870 : f003             		beq	SDone		;it's going to console
e872 : 20e3eb           		jsr	CloseOutFile
e875 : 4cf1e0           SDone		jmp	extKimLoop	;back to the monitor
                        ;
                        ; This dumps the next line.  See how many bytes are le
                        ; and if more than BYTESLINE, then just do BYTESLINE.
                        ;
e878 : add7df           Sgo		lda	Temp16H
e87b : d007             		bne	Sdef	;do default number of bytes
e87d : add6df           		lda	Temp16L
e880 : c910             		cmp	#BYTESLINE
e882 : 9002             		bcc	Scnt	;more than max per line
e884 : a910             Sdef		lda	#BYTESLINE
e886 : 8de917           Scnt		sta	SAVX	;for decrementing
e889 : 8df917           		sta	ID	;for subtracting
                        ;
                        ; Put out the header
                        ;
e88c : a93a             		lda	#':'
e88e : 20e0eb           		jsr	redirectedOutch
                        ;
e891 : ade917           		lda	SAVX
e894 : 8de717           		sta	CHKL	;start checksum
e897 : 20c6eb           		jsr	HexToOutput
                        ;
e89a : a5fb             		lda	POINTH	;starting address
e89c : 20f5e8           		jsr	updateCrc
e89f : 20c6eb           		jsr	HexToOutput
e8a2 : a5fa             		lda	POINTL
e8a4 : 20f5e8           		jsr	updateCrc
e8a7 : 20c6eb           		jsr	HexToOutput
                        ;
e8aa : a900             		lda	#0	;record type - data
e8ac : 20c6eb           		jsr	HexToOutput
                        ;
                        ; Now print the proper number of bytes
                        ;
e8af : a000             Sloop2		ldy	#0
e8b1 : b1fa             		lda	(POINTL),y	;get byte
e8b3 : 20f5e8           		jsr	updateCrc
e8b6 : 20c6eb           		jsr	HexToOutput
e8b9 : 20631f           		jsr	INCPT	;increment pointer
                        ;
e8bc : cee917           sdec		dec	SAVX
e8bf : d0ee             		bne	Sloop2
                        ;
                        ; Now print checksum
                        ;
e8c1 : ade717           		lda	CHKL
e8c4 : 49ff             		eor	#$ff	;one's complement
e8c6 : 18               		clc
e8c7 : 6901             		adc	#1	;two's complement
e8c9 : 20c6eb           		jsr	HexToOutput
                        ;
                        ; Output a CR/LF
                        ;
e8cc : a90d             		lda	#CR
e8ce : 20e0eb           		jsr	redirectedOutch
AS65 Assembler for R6502 [1.42].                                     Page   24
---------------------------------- xKIM.asm ----------------------------------

e8d1 : a90a             		lda	#LF
e8d3 : 20e0eb           		jsr	redirectedOutch
                        ;
                        ; If saving to disk, output a dot to indicate progress
                        ;
e8d6 : ad87df           		lda	filename
e8d9 : f005             		beq	shf2
                        ;
e8db : a92e             		lda	#'.'
e8dd : 20a01e           		jsr	OUTCH	;goes to console
                        ;
e8e0 : 38               shf2		sec
e8e1 : add6df           		lda	Temp16L
e8e4 : edf917           		sbc	ID
e8e7 : 8dd6df           		sta	Temp16L
e8ea : add7df           		lda	Temp16H
e8ed : e900             		sbc	#0
e8ef : 8dd7df           		sta	Temp16H
                        ;
e8f2 : 4c49e8           		jmp	Sloop1
                        ;
                        ;=====================================================
                        ; Adds the character in A to the CRC.  Preserves A.
                        ;
e8f5 : 48               updateCrc	pha
e8f6 : 18               		clc
e8f7 : 6de717           		adc	CHKL
e8fa : 8de717           		sta	CHKL
e8fd : 68               		pla
e8fe : 60               		rts
                        ;
                        ;=====================================================
                        ; Handles the command to prompt for an address and the
                        ; jump to it.
                        ;
e8ff : 207fea           jumpAddress     jsr	space
e902 : 20e9ea           		jsr	getStartAddr
e905 : b006             		bcs	cmdRet	;branch on bad address
e907 : 202f1e           		jsr	CRLF
e90a : 6cf517           		jmp	(SAL)	;else jump to address
                        ;
e90d : 4cf1e0           cmdRet		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Ping the Arduno disk controller.  This just sends th
                        ; PING command gets back one character, then returns.
                        ; Not much of a test but is sufficient to prove the
                        ; link is working.
                        ;
e910 :                  pingDisk
                        ;		jsr	xParInit	;init interface
e910 : 2082ea           		jsr	putsil
e913 : 696e672e2e2e2000 		db	"ing... ",0
e91b : 20b0ec           		jsr	DiskPing
e91e : 2082ea           		jsr	putsil
e921 : 7375636365737321 		db	"success!"
e929 : 0d0a00           		db	CR,LF,0
e92c : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a disk directory of the SD card.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   25
---------------------------------- xKIM.asm ----------------------------------

e92f : 2082ea           doDiskDir	jsr	putsil
e932 : 69736b20446972.. 		db	"isk Directory..."
e942 : 0d0a00           		db	CR,LF,0
e945 : 204ce9           		jsr	doSDDiskDir
e948 : 4cf1e0           		jmp	extKimLoop
e94b : 60               doDiskDirEnd	rts
                        ;
                        ;=====================================================
                        ; Subroutine to do a disk directory.  Prints filenames
                        ; to the console.
                        ;
e94c : 2029ec           doSDDiskDir	jsr	xParInit
e94f : 20c3ec           		jsr	DiskDir
                        ;
                        ; Get/Display each entry
                        ;
e952 : a2df             doDiskDirLoop   ldx	#filename/256	;pointer to buffer
e954 : a087             		ldy	#filename&$ff
e956 : 86f9             		stx	INH		;save for puts
e958 : 84f8             		sty	INL
e95a : 20cdec           		jsr	DiskDirNext	;get next entry
e95d : b0ec             		bcs	doDiskDirEnd	;carry = end of list
e95f : 2079ea           		jsr	space3
e962 : 2096ea           		jsr	puts		;else print name
e965 : 202f1e           		jsr	CRLF
e968 : 4c52e9           		jmp	doDiskDirLoop	;do next entry
                        ;
                        ;=====================================================
                        ; Type the contents of an SD file to console.
                        ;
e96b : 2082ea           typeFile	jsr	putsil
e96e : 202d20456e7465.. 		db	" - Enter filename: ",0
e982 : 2050eb           		jsr	getFileName
e985 : a087             		ldy	#filename&$ff
e987 : a2df             		ldx	#filename/256
                        ;		jsr	xParInit
e989 : 20feec           		jsr	DiskOpenRead
e98c : 901e             		bcc	typeFile1	;opened ok
                        ;
e98e : 2082ea           		jsr	putsil
e991 : 0d0a             		db	CR,LF
e993 : 4661696c656420.. 		db	"Failed to open file"
e9a6 : 0d0a00           		db	CR,LF,0
e9a9 : 4cf1e0           		jmp	extKimLoop
                        ;
                        ; Now just keep reading in bytes and displaying them.
                        ;
e9ac : 20b3eb           typeFile1	jsr	setInputFile	;reading from file
e9af : 2076eb           typeFileLoop	jsr	getNextFileByte
e9b2 : b006             		bcs	typeEof
e9b4 : 20a01e           		jsr	OUTCH	;display character
e9b7 : 4cafe9           		jmp	typeFileLoop
                        ;
e9ba : 2098ed           typeEof		jsr	DiskClose
e9bd : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Calculate the offset for a relative branch.  6502
                        ; relative branch calculations are well known.
                        ;
                        ; Offset from branch (BASE) = TARGET - (BASE+2).
                        ; If the result is positive, upper byte must be
AS65 Assembler for R6502 [1.42].                                     Page   26
---------------------------------- xKIM.asm ----------------------------------

                        ; zero.  If negative, upper byte must be FF.
                        ;
                        ; BASE	TARGET	Computed	Actual
                        ; 0200	0200	0200-(0200+2)	FFFE
                        ; 0200	020E	020E-(0200+2)	000C
                        ; 0226	0220	0220-(0226+2)	FFF8
                        ; 0156	015A	015A-(0156+2)	0002
                        ; 015C	012D	012D-(015C+2)	FFCF
                        ; 0200	0300	0300-(0200+2)	00FE - out of range
                        ; 0300	0200	0200-(0300+2)	FEFE - out of range
                        ;
e9c0 : 2082ea           offCalc		jsr	putsil
e9c3 : 202d204272616e.. 		db	" - Branch instruction address: "
e9e2 : 00               		db	0
e9e3 : 20e9ea           		jsr	getStartAddr
e9e6 : b030             		bcs	calcExit
e9e8 : 2082ea           		jsr	putsil
e9eb : 2c206272616e63.. 		db	", branch to: "
e9f8 : 00               		db	0
e9f9 : 20fbea           		jsr	getEndAddr
e9fc : b01a             		bcs	calcExit
e9fe : 201eeb           		jsr	ComputeOffset	;does the work
ea01 : 9018             		bcc	relgood		;if good offset
                        ;
                        ; Branch is out of range.
                        ;
ea03 : 2082ea           		jsr	putsil
ea06 : 202d206f757420.. 		db	" - out of range",CR,LF,0
ea18 : 4cf1e0           calcExit	jmp	extKimLoop
                        ;
                        ; Branch is in range so dislay the value.
                        ;
ea1b : 48               relgood		pha			;save offset
ea1c : 2082ea           		jsr	putsil
ea1f : 204f6666736574.. 		db	" Offset: ",0
ea29 : 68               		pla
ea2a : 203b1e           		jsr	PRTBYT
ea2d : 4cf1e0           		jmp	extKimLoop
                        ;
                        ; Add new commands here...
                        ;
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   27
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This subroutine will search for a command in a table
                        ; and call the appropriate handler.  See the command
                        ; table near the start of the code for what the format
                        ; is.  If a match is found, pop off the return address
                        ; from the stack and jump to the code.  Else, return.
                        ;
ea30 : a000             searchCmd	ldy	#0
ea32 : b1fa             cmdLoop		lda	(POINTL),y
ea34 : f01c             		beq	cmdNotFound
ea36 : c5f3             		cmp	ACC	;compare to user's input
ea38 : f007             		beq	cmdMatch
ea3a : c8               		iny		;start of function ptr
ea3b : c8               		iny
ea3c : c8               		iny		;start of help
ea3d : c8               		iny
ea3e : c8               		iny		;move to next command
ea3f : d0f1             		bne	cmdLoop
                        ;
                        ; It's found!  Load up the address of the code to call
                        ; pop the return address off the stack and jump to the
                        ; handler.
                        ;
ea41 : c8               cmdMatch	iny
ea42 : b1fa             		lda	(POINTL),y	;handler LSB
ea44 : 48               		pha
ea45 : c8               		iny
ea46 : b1fa             		lda	(POINTL),y	;handler MSB
ea48 : 85fb             		sta	POINTH
ea4a : 68               		pla
ea4b : 85fa             		sta	POINTL
ea4d : 68               		pla		;pop return address
ea4e : 68               		pla
ea4f : 6cfa00           		jmp	(POINTL)
                        ;
                        ; Not found, so just return.
                        ;
ea52 : 60               cmdNotFound	rts
                        ;
                        ;=====================================================
                        ; Given a pointer to a command table in POINT, display
                        ; the help text for all commands in the table.
                        ;
ea53 : a000             displayHelp	ldy	#0	;index into command table
ea55 : b1fa             showHelpLoop	lda	(POINTL),y	;get command
ea57 : f01f             		beq	showHelpDone	;jump if at end
                        ;
                        ; Display this entry's descriptive text
                        ;
ea59 : c8               		iny		;skip over command
ea5a : c8               		iny		;skip over function ptr
ea5b : c8               		iny
ea5c : b1fa             		lda	(POINTL),y
ea5e : 85f8             		sta	INL
ea60 : c8               		iny
ea61 : b1fa             		lda	(POINTL),y
ea63 : 85f9             		sta	INH
ea65 : 98               		tya
ea66 : 48               		pha
ea67 : 209e1e           		jsr	OUTSP
ea6a : 209e1e           		jsr	OUTSP
AS65 Assembler for R6502 [1.42].                                     Page   28
---------------------------------- xKIM.asm ----------------------------------

ea6d : 2096ea           		jsr	puts	;print description
ea70 : 202f1e           		jsr	CRLF
ea73 : 68               		pla
ea74 : a8               		tay
ea75 : c8               		iny		;point to next entry
ea76 : d0dd             		bne	showHelpLoop
ea78 : 60               showHelpDone	rts
                        ;
                        ;=====================================================
                        ; Print some spaces.
                        ;
ea79 : 207fea           space3		jsr	space
ea7c : 207fea           space2		jsr	space
ea7f : 4c9e1e           space   	jmp	OUTSP
                        ;
                        ;=====================================================
                        ; This prints the null-terminated string that
                        ; immediately follows the JSR to this function.  This
                        ; version was written by Ross Archer and is at:
                        ;
                        ;    www.6502.org/source/io/primm.htm
                        ;
ea82 : 68               putsil		pla
ea83 : 85f8             		sta	INL
ea85 : 68               		pla
ea86 : 85f9             		sta	INH
ea88 : a001             		ldy	#1
ea8a : 2098ea           		jsr	putsy
ea8d : e6f8             		inc	INL
ea8f : d002             		bne	puts2
ea91 : e6f9             		inc	INH
ea93 : 6cf800           puts2		jmp	(INL)
                        ;
                        ;=====================================================
                        ; This prints the null terminated string pointed to by
                        ; INL and INH.  Modifies those locations to point to
                        ; the end of the string.
                        ;
ea96 : a000             puts		ldy	#0
ea98 : b1f8             putsy		lda	(INL),y
ea9a : e6f8             		inc	INL
ea9c : d002             		bne	puts1
ea9e : e6f9             		inc	INH
eaa0 : 0900             puts1		ora	#0
eaa2 : f00c             		beq	putsdone
eaa4 : 8c82df           		sty	saveY
eaa7 : 20a01e           		jsr	OUTCH	;print character
eaaa : ac82df           		ldy	saveY
eaad : 4c98ea           		jmp	putsy
eab0 : 60               putsdone	rts
                        ;
                        ;=====================================================
                        ; This gets two hex characters and returns the value
                        ; in A with carry clear.  If a non-hex digit is
                        ; entered, then A contans the offending character and
                        ; carry is set.
                        ;
eab1 : 20c7ea           getHex		jsr	getNibble
eab4 : b01e             		bcs	getNibBad
eab6 : 0a               		asl	a
eab7 : 0a               		asl	a
eab8 : 0a               		asl	a
AS65 Assembler for R6502 [1.42].                                     Page   29
---------------------------------- xKIM.asm ----------------------------------

eab9 : 0a               		asl	a
eaba : 8d80df           		sta	saveA
eabd : 20c7ea           		jsr	getNibble
eac0 : b012             		bcs	getNibBad
eac2 : 0d80df           		ora	saveA
eac5 : 18               		clc
eac6 : 60               		rts
                        ;
                        ; Helper.  Gets next input char and converts to a
                        ; value from 0-F in A and returns C clear.  If not a
                        ; valid hex character, return C set.
                        ;
eac7 : 20a5eb           getNibble	jsr	redirectedGetch
eaca : a20f             		ldx	#nibbleHexEnd-nibbleHex-1
eacc : ddd9ea           getNibble1	cmp	nibbleHex,x
eacf : f005             		beq	getNibF	;got match
ead1 : ca               		dex
ead2 : 10f8             		bpl	getNibble1
ead4 : 38               getNibBad	sec
ead5 : 60               		rts
                        
ead6 : 8a               getNibF		txa		;index is value
ead7 : 18               		clc
ead8 : 60               		rts
                        ;
ead9 : 30313233343536.. nibbleHex	db	"0123456789ABCDEF"
eae9 =                  nibbleHexEnd	equ	*
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; SAL and SAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eae9 : 20b1ea           getStartAddr	jsr	getHex
eaec : b00c             		bcs	getDone
eaee : 8df617           		sta	SAH
eaf1 : 20b1ea           		jsr	getHex
eaf4 : b004             		bcs	getDone
eaf6 : 8df517           		sta	SAL
eaf9 : 18               		clc
eafa : 60               getDone		rts
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; EAL and EAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eafb : 20b1ea           getEndAddr	jsr	getHex
eafe : b0fa             		bcs	getDone
eb00 : 8df817           		sta	EAH
eb03 : 20b1ea           		jsr	getHex
eb06 : b0f2             		bcs	getDone
eb08 : 8df717           		sta	EAL
eb0b : 18               		clc
eb0c : 60               		rts
                        ;
                        ;=====================================================
                        ; Get an address range and leave them in SAL and EAL.
                        ;
eb0d : 207fea           getAddrRange    jsr	space
eb10 : 20e9ea           		jsr	getStartAddr
eb13 : b0e5             		bcs	getDone
AS65 Assembler for R6502 [1.42].                                     Page   30
---------------------------------- xKIM.asm ----------------------------------

eb15 : a92d             		lda	#'-'
eb17 : 20a01e           		jsr	OUTCH
eb1a : 20fbea           		jsr	getEndAddr
eb1d : 60               		rts
                        ;
                        ;=====================================================
                        ; This computes the relative offset between the
                        ; address in SAL/SAH (address of branch instruction)
                        ; and EAL/EAH (address to jump to).  If a valid range,
                        ; returns C clear and the offset in A.  If the branch
                        ; is out of range, C is set and A undefined.  Modifies
                        ; A, SAL and SAH.
                        ;
eb1e :                  ComputeOffset
                        ;
                        ; Add two to the end (BASE) address.  For calculations
                        ;   BASE = SAL/SAH
                        ;   TARGET = EAL/EAH
                        ;
eb1e : 18               		clc
eb1f : adf517           		lda	SAL
eb22 : 6902             		adc	#2
eb24 : 8df517           		sta	SAL
eb27 : 9003             		bcc	coffsub
eb29 : eef617           		inc	SAH
                        ;
                        ; Subtract the BASE (end) address from the TARGET (sta
                        ;
eb2c : 38               coffsub		sec
eb2d : adf717           		lda	EAL
eb30 : edf517           		sbc	SAL
eb33 : 48               		pha		;save for later
eb34 : 8df517           		sta	SAL
eb37 : adf817           		lda	EAH
eb3a : edf617           		sbc	SAH
eb3d : 8df617           		sta	SAH	;SAL/SAH contain offset
                        ;
                        ; High part must be either FF for negative branch or
                        ; 00 for a positive branch.  Cheat a bit here by rolli
                        ; the MSBit into C and adding to the MSByte.  If the
                        ; result is zero then everything is cool.
                        ;
eb40 : 68               		pla		;restore LSB of offset
eb41 : 48               		pha
eb42 : 0a               		asl	a	;put sign into C
eb43 : adf617           		lda	SAH
eb46 : 6900             		adc	#0
eb48 : f003             		beq	cogood	;branch if in range
                        ;
eb4a : 68               		pla		;clean up stack
eb4b : 38               		sec		;error
eb4c : 60               		rts
                        ;
eb4d : 68               cogood		pla		;get back offset
eb4e : 18               		clc
eb4f : 60               		rts
                        ;
                        ;=====================================================
                        ; Get a disk filename.  The KIM's behavior of echoing
                        ; every key prevents this from being too fancy, but it
                        ; good enough.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   31
---------------------------------- xKIM.asm ----------------------------------

eb50 : a200             getFileName	ldx	#0
eb52 : 205a1e           getFilename1	jsr	GETCH	;get next key
eb55 : c90d             		cmp	#CR	;end of the input?
eb57 : f014             		beq	getFnDone
eb59 : c908             		cmp	#BS	;backspace?
eb5b : f00a             		beq	getFnDel
eb5d : e00c             		cpx	#FILENAME_SIZE	;check size
eb5f : f0f1             		beq	getFilename1	;at length limit
eb61 : 9d87df           		sta	filename,x	;else save it
eb64 : e8               		inx
eb65 : d0eb             		bne	getFilename1
                        ;
eb67 : ca               getFnDel	dex		;back up one
eb68 : 10e8             		bpl	getFilename1
eb6a : e8               		inx		;can't go past start
eb6b : f0e5             		beq	getFilename1
eb6d : a900             getFnDone       lda	#0	;terminate line
eb6f : 9d87df           		sta	filename,x
eb72 : 202f1e           		jsr	CRLF
eb75 : 60               		rts
                        ;
                        ;=====================================================
                        ; This gets the next byte from an open disk file.  If
                        ; there are no more bytes left, this returns C set.
                        ; Else, C is clear and A contains the character.
                        ;
eb76 : aed4df           getNextFileByte ldx 	diskBufOffset
eb79 : ecd5df           		cpx	diskBufLength
eb7c : d014             		bne	hasdata		;branch if still data
                        ;
                        ; There is no data left in the buffer, so read a
                        ; block from the SD system.
                        ;
eb7e : a940             		lda	#BUFFER_SIZE
eb80 : a2df             		ldx	#buffer>>8
eb82 : a094             		ldy	#buffer&$ff
eb84 : 202fed           		jsr	DiskRead
eb87 : b012             		bcs	getNextEof
                        ;
                        ; A contains the number of bytes actually read.
                        ;
eb89 : 8dd5df           		sta	diskBufLength	;save length
eb8c : c900             		cmp	#0		;shouldn't happen
eb8e : f00b             		beq	getNextEof
                        ;
eb90 : a200             		ldx	#0
eb92 : bd94df           hasdata		lda	buffer,x
eb95 : e8               		inx
eb96 : 8ed4df           		stx	diskBufOffset
eb99 : 18               		clc
eb9a : 60               		rts
                        ;
eb9b : a900             getNextEof	lda	#0
eb9d : 8dd4df           		sta	diskBufOffset
eba0 : 8dd5df           		sta	diskBufLength
eba3 : 38               		sec
eba4 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   32
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the input vector
                        ; pointer to get the next input character.
                        ;
eba5 : 6c83df           redirectedGetch	jmp	(inputVector)
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to the normal
                        ; console input subroutine.
                        ;
eba8 : a95a             setInputConsole	lda	#GETCH&$ff
ebaa : 8d83df           		sta     inputVector
ebad : a91e             		lda	#GETCH/256
ebaf : 8d84df           		sta	inputVector+1
ebb2 : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to a file read
                        ; subroutine.
                        ;
ebb3 : a976             setInputFile    lda	#getNextFileByte&$ff
ebb5 : 8d83df           		sta     inputVector
ebb8 : a9eb             		lda	#getNextFileByte/256
ebba : 8d84df           		sta	inputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ebbd : a900             		lda	#0
ebbf : 8dd4df           		sta	diskBufOffset
ebc2 : 8dd5df           		sta	diskBufLength
ebc5 : 60               		rts
                        ;
                        ;=====================================================
                        ; Print character in A as two hex digits to the
                        ; current output device (console or file).
                        ;
ebc6 : 48               HexToOutput	pha		;save return value
ebc7 : 48               		pha
ebc8 : 4a               		lsr	a	;move top nibble to bottom
ebc9 : 4a               		lsr	a
ebca : 4a               		lsr	a
ebcb : 4a               		lsr	a
ebcc : 20d5eb           		jsr	hexta	;output nibble
ebcf : 68               		pla
ebd0 : 20d5eb           		jsr	hexta
ebd3 : 68               		pla		;restore
ebd4 : 60               		rts
                        ;
ebd5 : 290f             hexta		and	#%0001111
ebd7 : c90a             		cmp	#$0a
ebd9 : 18               		clc
ebda : 3002             		bmi	hexta1
ebdc : 6907             		adc	#7
ebde : 6930             hexta1		adc	#'0'	;then fall into...
                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the output vector
                        ; pointer to send the character in A to the proper
AS65 Assembler for R6502 [1.42].                                     Page   33
---------------------------------- xKIM.asm ----------------------------------

                        ; device.
                        ;
ebe0 : 6c85df           redirectedOutch	jmp	(outputVector)
                        ;
                        ;=====================================================
                        ; This flushes any data remaining in the disk buffer
                        ; and then closes the file.
                        ;
ebe3 : add4df           CloseOutFile	lda	diskBufOffset
ebe6 : f007             		beq	closeonly
ebe8 : a2df             		ldx	#buffer>>8
ebea : a094             		ldy	#buffer&$ff
ebec : 2061ed           		jsr	DiskWrite
                        ;
ebef : 2098ed           closeonly	jsr	DiskClose
                        ;
                        ; Fall through...
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to the normal
                        ; console output subroutine.
                        ;
ebf2 :                  setOutputConsole
ebf2 : a9a0             		lda	#OUTCH&$ff
ebf4 : 8d85df           		sta     outputVector
ebf7 : a91e             		lda	#OUTCH/256
ebf9 : 8d86df           		sta	outputVector+1
ebfc : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to a file write
                        ; subroutine.
                        ;
ebfd : a90d             setOutputFile    lda	#putNextFileByte&$ff
ebff : 8d85df           		sta     outputVector
ec02 : a9ec             		lda	#putNextFileByte/256
ec04 : 8d86df           		sta	outputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ec07 : a900             		lda	#0
ec09 : 8dd4df           		sta	diskBufOffset
ec0c : 60               		rts
                        ;
                        ;=====================================================
                        ; Add the byte in A to the output buffer.  If the
                        ; buffer is full, flush it to disk.
                        ;
ec0d : aed4df           putNextFileByte	ldx	diskBufOffset
ec10 : e040             		cpx	#BUFFER_SIZE	;buffer full?
ec12 : d00d             		bne	pNFB		;no
                        ;
                        ; The buffer is full, so write it out.
                        ;
ec14 : 48               		pha			;save byte
ec15 : a940             		lda	#BUFFER_SIZE
ec17 : a2df             		ldx	#buffer>>8
ec19 : a094             		ldy	#buffer&$ff
ec1b : 2061ed           		jsr	DiskWrite
                        ;
ec1e : a200             		ldx	#0		;reset index
AS65 Assembler for R6502 [1.42].                                     Page   34
---------------------------------- xKIM.asm ----------------------------------

ec20 : 68               		pla
ec21 : 9d94df           pNFB		sta	buffer,x
ec24 : e8               		inx
ec25 : 8ed4df           		stx	diskBufOffset
ec28 : 60               		rts
                        ;
                        		include	"pario.asm"
                        ;*****************************************************
                        ; These are the low-level I/O routines to talk to the
                        ; Arduino processor connected to the KIM's I/O port.
                        ;
                        ; August 2014, Bob Applegate K2UT, bob@corshamtech.com
                        ;
                        ; Which port bits are used for what:
                        ;
                        ; A0 = Data 0, alternates input/output
                        ; A1 = Data 1, alternates input/output
                        ; A2 = Data 2, alternates input/output
                        ; A3 = Data 3, alternates input/output
                        ; A4 = Data 4, alternates input/output
                        ; A5 = Data 5, alternates input/output
                        ; A6 = Data 6, alternates input/output
                        ; A7 = Data 7, alternates input/output
                        ;
                        ; B0 = Direction bit, always output
                        ; B1 = Write strobe or ACK, always output
                        ; B2 = Read stroke or ACK, always input
                        ;
                        ;----------------------------------------------------
                        ; Bits in the B register
                        ;
0001 =                  DIRECTION	equ	%00000001
0002 =                  PSTROBE		equ	%00000010
0004 =                  ACK		equ	%00000100
                        ;
                        ;----------------------------------------------------
                        ; The KIM has a 6530 with the two I/O ports at $1700.
                        ;
                        ; Addresses of 6530 registers
                        ;
1700 =                  PIABASE		equ	$1700
1700 =                  PIAREGA		equ	PIABASE		;data reg A
1701 =                  PIADDRA		equ	PIABASE+1	;data dir reg A
1702 =                  PIAREGB		equ	PIABASE+2	;data reg B
1703 =                  PIADDRB		equ	PIABASE+3	;data dir reg B
                        		code
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   35
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This is the initialization function.  Call before
                        ; doing anything else with the parallel port.
                        ;
ec29 :                  xParInit
                        ;
                        ; Set up the data direction register for port B so tha
                        ; the DIRECTION and PSTROBE bits are output.  Only tou
                        ; the bits we're using; leave the rest as-is.
                        ;
ec29 : ad0317           		lda	PIADDRB	;get current value
ec2c : 29fb             		and	#~ACK
ec2e : 0903             		ora	#DIRECTION | PSTROBE
ec30 : 8d0317           		sta	PIADDRB
                        ;
ec33 : a9ff             		lda	#$ff
ec35 : 8d0117           		sta	PIADDRA	;set data for write
                        ;
                        ; Now that the data is set, set the direction
                        ; registers.  This prevents weird problems like
                        ; driving lines to the wrong state.
                        ;
ec38 : ad0217           		lda	PIAREGB
ec3b : 29f9             		and	#~(PSTROBE | ACK)
ec3d : 0901             		ora	#DIRECTION
ec3f : 8d0217           		sta	PIAREGB
ec42 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   36
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for writing to the Arduino.  Sets up
                        ; direction registers, drives the direction bit, etc.
                        ;
ec43 : a9ff             xParSetWrite    lda	#$ff	;set bits for output
ec45 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to output, clear ACK bit
                        ;
ec48 : ad0217           		lda	PIAREGB
ec4b : 29f9             		and	#~(PSTROBE | ACK)
ec4d : 0901             		ora	#DIRECTION
ec4f : 8d0217           		sta	PIAREGB
ec52 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   37
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for reading from the Arduino.  Sets up
                        ; direction registers, clears the direction bit, etc.
                        ;
ec53 : a900             xParSetRead     lda	#$00	;set bits for input
ec55 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to input, clear ACK bit
                        ;
ec58 : ad0217           		lda	PIAREGB
ec5b : 29f8             		and	#~(DIRECTION | PSTROBE | ACK)
ec5d : 8d0217           		sta	PIAREGB
ec60 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   38
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This writes a single byte to the Arduino.  On entry,
                        ; the byte to write is in A.  This assumes ParSetWrite
                        ; was already called.
                        ;
                        ; Destroys A, all other registers preserved.
                        ;
                        ; Write cycle:
                        ;
                        ;    1. Wait for other side to lower ACK.
                        ;    2. Put data onto the bus.
                        ;    3. Set DIRECTION and PSTROBE to indicate data
                        ;       is valid and ready to read.
                        ;    4. Wait for ACK line to go high, indicating the
                        ;       other side has read the data.
                        ;    5. Lower PSTROBE.
                        ;    6. Wait for ACK to go low, indicating end of
                        ;       transfer.
                        ;
ec61 : 48               xParWriteByte	pha		;save data
ec62 : ad0217           Parwl22		lda	PIAREGB	;check status
ec65 : 2904             		and	#ACK
ec67 : d0f9             		bne	Parwl22	;wait for ACK to go low
                        ;
                        ; Now put the data onto the bus
                        ;
ec69 : 68               		pla
ec6a : 8d0017           		sta	PIAREGA
                        ;
                        ; Raise the strobe so the Arduino knows there is
                        ; new data.
                        ;
ec6d : ad0217           		lda	PIAREGB
ec70 : 0902             		ora	#PSTROBE
ec72 : 8d0217           		sta	PIAREGB
                        ;
                        ; Wait for ACK to go high, indicating the Arduino has
                        ; pulled the data and is ready for more.
                        ;
ec75 : ad0217           Parwl33		lda	PIAREGB
ec78 : 2904             		and	#ACK
ec7a : f0f9             		beq	Parwl33
                        ;
                        ; Now lower the strobe, then wait for the Arduino to
                        ; lower ACK.
                        ;
ec7c : ad0217           		lda	PIAREGB
ec7f : 29fd             		and	#~PSTROBE
ec81 : 8d0217           		sta	PIAREGB
ec84 : ad0217           Parwl44		lda	PIAREGB
ec87 : 2904             		and	#ACK
ec89 : d0f9             		bne	Parwl44
ec8b : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   39
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This reads a byte from the Arduino and returns it in
                        ; A.  Assumes ParSetRead was called before.
                        ;
                        ; This does not have a time-out.
                        ;
                        ; Preserves all other registers.
                        ;
                        ; Read cycle:
                        ;
                        ;    1. Wait for other side to raise ACK, indicating
                        ;       data is ready.
                        ;    2. Read data.
                        ;    3. Raise PSTROBE indicating data was read.
                        ;    4. Wait for ACK to go low.
                        ;    5. Lower PSTROBE.
                        ;
ec8c : ad0217           xParReadByte    lda	PIAREGB
ec8f : 2904             		and	#ACK	;is their strobe high?
ec91 : f0f9             		beq	xParReadByte	;nope, no data
                        ;
                        ; Data is available, so grab and save it.
                        ;
ec93 : ad0017           		lda	PIAREGA
ec96 : 48               		pha
                        ;
                        ; Now raise our strobe (their ACK), then wait for
                        ; them to lower their strobe.
                        ;
ec97 : ad0217           		lda	PIAREGB
ec9a : 0902             		ora	#PSTROBE
ec9c : 8d0217           		sta	PIAREGB
ec9f : ad0217           Parrlp1		lda	PIAREGB
eca2 : 2904             		and	#ACK
eca4 : d0f9             		bne	Parrlp1	;still active
                        ;
                        ; Lower our ack, then were done.
                        ;
eca6 : ad0217           		lda	PIAREGB
eca9 : 29fd             		and	#~PSTROBE
ecab : 8d0217           		sta	PIAREGB
ecae : 68               		pla
ecaf : 60               		rts
                        
                        		include	"parproto.inc"
                        ;*****************************************************
                        ; Parallel port protocol
                        ;
                        ; This is the header file for making applications
                        ; compliant with The Remote Disk Protocol Guide which
                        ; is on the Corsham Technologies web page somewhere:
                        ;
                        ;    www.corshamtech.com
                        ;
                        ; This was updated 06/13/2015 to be compliant with the
                        ; official specification, so the opcode values changed
                        ;
                        ; Another update on 02/10/2019
                        ;
                        ;=====================================================
                        ; Commands from host to Arduino
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   40
---------------------------------- xKIM.asm ----------------------------------

0001 =                  PC_GET_VERSION	equ	$01
0005 =                  PC_PING		equ	$05	;ping Arduino
0006 =                  PC_LED_CONTROL	equ	$06	;LED control
0007 =                  PC_GET_CLOCK	equ	$07	;get clock data
0008 =                  PC_SET_CLOCK	equ	$08	;set clock
0010 =                  PC_GET_DIR	equ	$10	;Get directory
0011 =                  PC_GET_MOUNTED	equ	$11	;Get mounted drive list
0012 =                  PC_MOUNT	equ	$12	;Mount drive
0013 =                  PC_UNMOUNT	equ	$13	;Unmount drive
0014 =                  PC_GET_STATUS	equ	$14	;Get status for one drive
0015 =                  PC_DONE		equ	$15	;Stop data
0015 =                  PC_ABORT	equ	PC_DONE
0016 =                  PC_READ_FILE	equ	$16	;Read regular file (non-DSK)
0017 =                  PC_READ_BYTES	equ	$17	;Read sequential bytes
0018 =                  PC_RD_SECTOR	equ	$18	;Read FLEX sector
0019 =                  PC_WR_SECTOR	equ	$19	;Write FLEX sector
001a =                  PC_GET_MAX	equ	$1a	;Get maximum drives
001b =                  PC_WRITE_FILE   equ	$1b	;Open file for writing
001c =                  PC_WRITE_BYTES	equ	$1c	;Data to be written
001d =                  PC_SAVE_CONFIG	equ	$1d	;Save SD.CFG with current value
001e =                  PC_SET_TIMER	equ	$1e	;Set RTC timer
001f =                  PC_RD_SEC_LONG	equ	$1f	;Read sector with long sec num
0020 =                  PC_WR_SEC_LONG	equ	$20	;Write sector with long sec num
                        ;
                        ;=====================================================
                        ; Responses from Arduino to host
                        ;
0081 =                  PR_VERSION_INFO	equ	$81	;Contains version information
0082 =                  PR_ACK		equ	$82	;ACK (no additional information)
0083 =                  PR_NAK		equ	$83	;NAK - one status byte follows
0085 =                  PR_PONG		equ	$85	;Reply to a ping
0087 =                  PR_CLOCK_DATA	equ	$87	;Clock data
0090 =                  PR_DIR_ENTRY	equ	$90	;Directory entry
0091 =                  PR_DIR_END	equ	$91	;End of directory entries
0092 =                  PR_FILE_DATA	equ	$92	;File data
0093 =                  PR_STATUS	equ	$93	;Drive status
0094 =                  PR_SECTOR_DATA	equ	$94	;Sector data
0095 =                  PR_MOUNT_INFO	equ	$95	;Mount entry
0096 =                  PR_MAX_DRIVES	equ	$96	;Maximum number of drives
                        ;
                        ;=====================================================
                        ; Error codes for NAK events.  Yes, these are in
                        ; decimal due to an error in the original
                        ; documentation.
                        ;
0000 =                  ERR_NONE	equ	0
000a =                  ERR_NOT_MOUNTED	equ	10
000b =                  ERR_MOUNTED	equ	11
000c =                  ERR_NOT_FOUND	equ	12
000d =                  ERR_READ_ONLY	equ	13
000e =                  ERR_BAD_DRIVE	equ	14
000f =                  ERR_BAD_TRACK	equ	15
0010 =                  ERR_BAD_SECTOR	equ	16
0011 =                  ERR_READ_ERROR	equ	17
0012 =                  ERR_WRITE_ERROR	equ	18
0013 =                  ERR_NOT_PRESENT	equ	19
0014 =                  ERR_NOT_IMPL	equ	20	;Command not implemented
                        
                        
                        		include	"diskfunc.asm"
                        		list
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   41
---------------------------------- xKIM.asm ----------------------------------

                        ; This is a collection of functions for performing
                        ; higher level disk functions.  This hides the nasty
                        ; details of communications with the remote disk
                        ; system.
                        ;
                        ; August 20, 2014 - Bob Applegate
                        ;                   bob@corshamtech.com
                        ;
                        ; 06/14/2015 - Bob Applegate
                        ;		Now that there is an official standard
                        ;		for the protocol between the host (this
                        ;		code) and the DCP (Arduino code), this
                        ;		code has been updated to be compliant.
                        ;
                        ; 01/14/2016 - Bob Applegate
                        ;              Finally converted to 6502.
                        ;
                        ;		include	"parproto.inc"
                        ;
                        ; Number of drives emulated
                        ;
0004 =                  DRIVES		equ	4
                        ;
                        ;=====================================================
                        ; This is a sanity check to verify connectivity to the
                        ; Arduino code is working.  Returns C clear if all is
                        ; good, or C set if not.
                        ;
ecb0 : 2043ec           DiskPing	jsr	xParSetWrite
ecb3 : a905             		lda	#PC_PING	;command
ecb5 : 2061ec           		jsr	xParWriteByte	;send to Arduino
ecb8 : 2053ec           		jsr	xParSetRead
ecbb : 208cec           		jsr	xParReadByte	;read their reply
ecbe : 2043ec           DiskRetGood	jsr	xParSetWrite
ecc1 : 18               		clc			;assume it's good
ecc2 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   42
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This starts a directory read of the raw drive, not
                        ; the mounted drive.  No input parameters.  This simpl
                        ; sets up for reading the entries, then the user must
                        ; read each entry.
                        ;
                        ; Returns with C clear on success.  If error, C is set
                        ; and A contains the error code.
                        ;
ecc3 : 2043ec           DiskDir		jsr	xParSetWrite
ecc6 : a910             		lda	#PC_GET_DIR	;send command
ecc8 : 2061ec           		jsr	xParWriteByte
eccb : 18               		clc		;assume it works
eccc : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   43
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Read the next directory entry.  On input, X (MSB)
                        ; and Y (LSB) point to a 13 byte area to receive the
                        ; drive data.
                        ;
                        ; Returns C set if end of directory (ie, attempt to
                        ; read and there are none left).  Else, C is clear
                        ; and X/Y point to the null at end of filename.
                        ;
eccd : 86fb             DiskDirNext	stx	POINTH
eccf : 84fa             		sty	POINTL
ecd1 : 2053ec           		jsr	xParSetRead	;read results
ecd4 : 208cec           		jsr	xParReadByte	;get response code
ecd7 : c983             		cmp	#PR_NAK		;error?
ecd9 : f01e             		beq	DDNErr
ecdb : c991             		cmp	#PR_DIR_END	;end?
ecdd : f01a             		beq	DDNErr
                        ;
                        ; This contains a directory entry.
                        ;
ecdf : a000             		ldy	#0
ece1 : 208cec           DDNloop		jsr	xParReadByte
ece4 : 91fa             		sta	(POINTL),y
ece6 : c900             		cmp	#0	;end?
ece8 : f006             		beq	DDNEnd
ecea : 20631f           		jsr	INCPT
eced : 4ce1ec           		jmp	DDNloop
ecf0 : 2043ec           DDNEnd		jsr	xParSetWrite
ecf3 : a6fb             		ldx	POINTH
ecf5 : a4fa             		ldy	POINTL
ecf7 : 18               		clc		;not end of files
ecf8 : 60               		rts
                        ;
                        ; Error.  Set C and return.  This is not really
                        ; proper, since this implies a simple end of the
                        ; directory rather than an error.
                        ;
ecf9 : 2043ec           DDNErr		jsr	xParSetWrite
ecfc : 38               		sec
ecfd : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   44
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This opens a file on the SD for reading.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error (usually means the
                        ; file does not exist.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ecfe : a916             DiskOpenRead	lda	#PC_READ_FILE
ed00 :                  DiskOpen
ed00 : 84f8                     sty	INL	;save ptr to filename
ed02 : 86f9             		stx	INH
ed04 : 2061ec           		jsr	xParWriteByte
ed07 : a0ff             		ldy	#-1
ed09 : c8               DiskOpenLoop	iny
ed0a : b1f8             		lda	(INL),y
ed0c : 2061ec           		jsr	xParWriteByte
ed0f : b1f8             		lda	(INL),y
ed11 : d0f6             		bne	DiskOpenLoop
ed13 : 2053ec           		jsr	xParSetRead
ed16 : 208cec           		jsr	xParReadByte	;get response
ed19 : c982             		cmp	#PR_ACK
ed1b : d005             		bne	DiskOpenErr
ed1d : 2043ec           		jsr	xParSetWrite	;back to write mode
ed20 : 18               		clc
ed21 : 60               		rts
                        ;
                        ; Got an error.
                        ;
ed22 : 208cec           DiskOpenErr	jsr	xParReadByte	;get error code
ed25 : 2043ec           		jsr	xParSetWrite	;back to write mode
ed28 : 38               		sec
ed29 : 60               		rts
                        ;=====================================================
                        ; This opens a file on the SD for writing.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed2a : a91b             DiskOpenWrite	lda	#PC_WRITE_FILE
ed2c : 4c00ed           		jmp	DiskOpen	;jump into common code
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to read
                        ; from the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to put the data.  On return, C will
                        ; be set if EOF was reached (and no data read), or
                        ; C will be clear and A contains the number of bytes
                        ; actually read into the buffer.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed2f : 84f8             DiskRead	sty	INL	;save ptr to buffer
ed31 : 86f9             		stx	INH
ed33 : 48               		pha
ed34 : a917             		lda	#PC_READ_BYTES
ed36 : 2061ec           		jsr	xParWriteByte	;command
ed39 : 68               		pla		;number of bytes to get
AS65 Assembler for R6502 [1.42].                                     Page   45
---------------------------------- xKIM.asm ----------------------------------

ed3a : 2061ec           		jsr	xParWriteByte
ed3d : 2053ec           		jsr	xParSetRead	;get ready for response
ed40 : 208cec           		jsr	xParReadByte	;assume PR_FILE_DATA
ed43 : 208cec           		jsr	xParReadByte	;length
ed46 : 48               		pha
ed47 : aa               		tax			;count
ed48 : f011             		beq	DiskReadEof	;zero = EOF
ed4a : a000             		ldy	#0	;offset
ed4c : 208cec           DiskReadLoop	jsr	xParReadByte
ed4f : 91f8             		sta	(INL),y
ed51 : c8               		iny		;next offset
ed52 : ca               		dex
ed53 : d0f7             		bne	DiskReadLoop
ed55 : 2043ec           		jsr	xParSetWrite
ed58 : 68               		pla		;retrieve byte count
ed59 : 18               DiskOk		clc	
ed5a : 60               		rts
ed5b : 2043ec           DiskReadEof	jsr	xParSetWrite
ed5e : 68               		pla
ed5f : 38               		sec
ed60 : 60               		rts
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to write
                        ; to the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to get the data.  On return, C will
                        ; be set if an error was detected, or C will be clear
                        ; if no error.  Note that if A contains 0 on entry,
                        ; no bytes are written.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed61 : c900             DiskWrite	cmp	#0
ed63 : f0f4             		beq	DiskOk
ed65 : 84f8             		sty	INL	;save ptr to filename
ed67 : 86f9             		stx	INH
ed69 : 48               		pha
ed6a : a91c             		lda	#PC_WRITE_BYTES
ed6c : 2061ec           		jsr	xParWriteByte	;command
ed6f : 68               		pla		;number of bytes to write
ed70 : 48               		pha		;save again
ed71 : 2061ec           		jsr	xParWriteByte
ed74 : 68               		pla
ed75 : aa               		tax			;count
ed76 : a000             		ldy	#0	;offset
ed78 : b1f8             DiskWriteLoop	lda	(INL),y	;get next byte
ed7a : 2061ec           		jsr	xParWriteByte
ed7d : c8               		iny		;next offset
ed7e : ca               		dex
ed7f : d0f7             		bne	DiskWriteLoop
ed81 : 2053ec           		jsr	xParSetRead ;read the status
ed84 : 208cec           		jsr	xParReadByte
ed87 : c982             		cmp	#PR_ACK
ed89 : f008             		beq	DiskOk1	;all good
ed8b : 208cec           		jsr	xParReadByte	;read error code
ed8e : 2043ec           		jsr	xParSetWrite 
ed91 : 38               		sec	
ed92 : 60               		rts
                        ;
ed93 : 2043ec           DiskOk1		jsr	xParSetWrite
ed96 : 18               		clc
AS65 Assembler for R6502 [1.42].                                     Page   46
---------------------------------- xKIM.asm ----------------------------------

ed97 : 60               		rts
                        ;
                        ;=====================================================
                        ; Call this to close any open file.  No parameters
                        ; and no return status.
                        ;
ed98 : a915             DiskClose	lda	#PC_DONE
ed9a : 4c61ec           		jmp	xParWriteByte
                        
                        
                        
                        ;
                        ;=====================================================
                        ; Show current clock
                        ;
                        	if	SHOW_RTC
ed9d : 2043ec           doShowClock	jsr	xParSetWrite
eda0 : a907             		lda	#PC_GET_CLOCK
eda2 : 2061ec           		jsr	xParWriteByte
eda5 : 2053ec           		jsr	xParSetRead	;prepare to read
                        ;
eda8 : 208cec           		jsr	xParReadByte
                        ;
                        ; Loop to read the raw data
                        ;
edab : a200             		ldx	#0
edad : 8e81df           clockread	stx	saveX
edb0 : 208cec           		jsr	xParReadByte
edb3 : ae81df           		ldx	saveX
edb6 : 9d94df           		sta	month,x
edb9 : e8               		inx
edba : e008             		cpx	#clock_end-month
edbc : d0ef             		bne	clockread
                        ;
                        ; Set back to write mode to finish up; all apps are
                        ; supposed to leave the SD interface in write mode.
                        ;
edbe : 2043ec           		jsr	xParSetWrite
                        ;
                        ; Now display the data in a user-friendly format.  Eac
                        ; numberic value is in binary, so convert to decimal
                        ; for display.
                        ;
edc1 : 2082ea           		jsr	putsil
edc4 : 0d0a             		db	CR,LF
edc6 : 446174653a2000   		db	"Date: ",0
                        ;
edcd : ad94df           		lda	month
edd0 : 2016ee           		jsr	outdec
edd3 : a92f             		lda	#'/'
edd5 : 20a01e           		jsr	OUTCH
edd8 : ad95df           		lda	day
eddb : 2016ee           		jsr	outdec
                        ;
                        ; Always force the high part of the year to "/20"
                        ;
edde : 2082ea           		jsr	putsil
ede1 : 2f323000         		db	"/20",0
ede5 : ad97df           		lda	year_low
ede8 : 2016ee           		jsr	outdec
                        ;
                        ; Space over, then do the time
AS65 Assembler for R6502 [1.42].                                     Page   47
---------------------------------- xKIM.asm ----------------------------------

                        ;
edeb : 2082ea           		jsr	putsil
edee : 2c2000           		db	", ",0
                        ;
edf1 : ad98df           		lda	hour
edf4 : 2016ee           		jsr	outdec
edf7 : a93a             		lda	#':'
edf9 : 20a01e           		jsr	OUTCH
edfc : ad99df           		lda	minute
edff : 2016ee           		jsr	outdec
ee02 : a93a             		lda	#':'
ee04 : 20a01e           		jsr	OUTCH
ee07 : ad9adf           		lda	second
ee0a : 2016ee           		jsr	outdec
                        ;
ee0d : 2082ea           		jsr	putsil
ee10 : 0d0a00           		db	CR,LF,0
ee13 : 4c60e0           		jmp	extKim	;return to monitor
                        ;
                        ;=====================================================
                        ; Given a binary value in A, display it as two decimal
                        ; digits.  The input can't be greater than 99.  Always
                        ; print a leading zero if less than 10.
                        ;
ee16 : a000             outdec		ldy	#0	;counts 10s
ee18 : c90a             out1		cmp	#10
ee1a : 9007             		bcc	out2	;below 10
ee1c : c8               		iny		;count 10
ee1d : 38               		sec
ee1e : e90a             		sbc	#10
ee20 : 4c18ee           		jmp	out1
                        ;
ee23 : 48               out2		pha		;save ones
ee24 : 98               		tya		;get tens
ee25 : 2029ee           		jsr	out3	;print tens digit
ee28 : 68               		pla		;restore ones
                        ;
ee29 : 0930             out3		ora	#'0'
ee2b : 20a01e           		jsr	OUTCH
ee2e : 60               		rts
                        
                        	endif
                        ;
                        	if	TINY_BASIC
                        		include "mytb.asm"
                        	endif
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   48
---------------------------------- xKIM.asm ----------------------------------

                        		
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory to
                        ; an IEC device.
                        ;
ee2f : 4cf1e0           lExit111		jmp	extKimLoop
ee32 : 200deb           saveIEC		jsr	getAddrRange	;get range to dump
ee35 : b0f8             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;
ee37 : 2082ea           		jsr	putsil
ee3a : 0d0a             		db	CR,LF
ee3c : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
ee55 : 20b1ea           		jsr	getHex
ee58 : 8df917           		sta	IECFID
                        		
ee5b : adf617           		lda SAH
ee5e : 8df617           		sta IECSAH
ee61 : adf517           		lda SAL
ee64 : 8df517           		sta IECSAL
ee67 : adf817           		lda EAH
ee6a : 8df817           		sta IECEAH
ee6d : adf717           		lda EAL
ee70 : 8df717           		sta IECEAL
                        		
ee73 : 4c48f1           		jmp IECSAV
ee76 : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to load a region of memory from
                        ; an IEC device.
                        ;
ee79 : 200deb           loadIEC		jsr	getAddrRange	;get range to dump
ee7c : b0b1             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;
ee7e : 2082ea           		jsr	putsil
ee81 : 0d0a             		db	CR,LF
ee83 : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
ee9c : 20b1ea           		jsr	getHex
ee9f : 8df917           		sta	IECFID
                        		
eea2 : 4c00f0           		jmp IECLOD
eea5 : 4cf1e0           		jmp	extKimLoop
                        
                        ;
                        ;=====================================================
                        ; These are the 6502 vectors.  On the Corsham
                        ; Technologies RAM/ROM board the user can select
                        ; whether or not to use these based on a switch
                        ; setting on the board.
                        ;
fffa =                  		org	$fffa
fffa : 1c1c             NMI		dw	NMIT	;in KIM
fffc : 221c             RESET		dw	RST	;in KIM
fffe : 1f1c             IRQ		dw	IRQT	;in KIM
                        ;
                        		end
                        
AS65 Assembler for R6502 [1.42].                                     Page   49
---------------------------------- xKIM.asm ----------------------------------

                        
No errors in pass 2.
Wrote binary from address $e000 through $ffff.
Total size 8192 bytes.
