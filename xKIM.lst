AS65 Assembler for R6502 [1.42].                                     Page    1
---------------------------------- xKIM.asm ----------------------------------

2701 lines read, no errors in pass 1.
                        ;*****************************************************
                        ; Extended KIM monitor, written 2015 by Bob Applegate
                        ; K2UT - bob@corshamtech.com
                        ;
                        ; This code was written as part of the Corsham Tech
                        ; 60K RAM/EPROM board.  It provides a working area for
                        ; an extended version of the KIM-1 monitor capabilitie
                        ; mostly based on a TTY interface, not the keypad and
                        ; hex display.
                        ;
                        ; This extensions contains an assortment of basic
                        ; tools that are missing from the KIM-1's built in
                        ; console monitor.  It has commands for loading hex
                        ; files (as opposed to KIM format), accessing an SD
                        ; card system, memory edit, hex dump, etc.  It also
                        ; has the ability for user-defined extensions to be
                        ; added.  And a number of common entry points have
                        ; vectors so the monitor can be modified without
                        ; breaking programs that use it.
                        ;
                        ; I'm not claiming copyright; I wrote most of this,
                        ; but also borrowed from others (credit is given in
                        ; those sections of code).  All of the portions I've
                        ; written are free to use, but please keep my name
                        ; in the comments somewhere.  Even better, consider
                        ; buying a board from us: www.corshamtech.com
                        ;
                        ; 12/01/2015	Bob Applegate
                        ;		Initial development - V0.X
                        ; 03/15/2016	Bob Applegate
                        ;		v1.0 - First official release
                        ; 01/03/2017	Bob Applegate
                        ;		v1.1 - Added S command
                        ; 09/20/2018	Bob Applegate
                        ;		v1.2 - Added auto-run vector
                        ; 01/25/2019	Bob Applegate
                        ;		v1.3 - Added 'X' command.
                        ;		Added the 'C' command to get time.
                        ; 03/09/2019	Bob Applegate
                        ;		v1.4 - Added 'O' command.
                        ; 07/26/2020	Bob Applegate
                        ;		v1.5 - Fixed bug that caused the S
                        ;		command to create empty file with no
                        ;		contents.
                        ;		Minor typo fixes.
                        ;		Added CLD instructions.
                        ; 11/14/2020	Bob Applegate
                        ;		v1.6 - On SD error, display reason code.
                        ; 09/15/2021	Bob Applegate
                        ;		v1.7
                        ;		Added offset calculator command O.
                        ;		Added R command in Edit mode.
                        ;		Removed '.' when loading from console.
                        ; 09/20/2021	Bob Applegate
                        ;		v1.8
                        ;		Made a lot of the command handlers
                        ;		into subroutines and added vectors so
                        ;		external programs can call them.
                        ;		Fixed bugs in Edit mode.
                        ; 12/06/2023	Christopher 'Zoggins' Mallery
                        ;		v1.9
AS65 Assembler for R6502 [1.42].                                     Page    2
---------------------------------- xKIM.asm ----------------------------------

                        ;		Added IEC Save and Load commands
                        ;
                        ;*****************************************************
                        ;
                        ; Useful constants
                        ;
0000 =                  false		equ	0
ffff =                  true		equ	~false
                        ;
                        ; Version number
                        ;
0001 =                  VERSION		equ	1
0009 =                  REVISION	equ	9
0000 =                  BETA_VER	equ	0
                        ;
                        ; Options.  If RAM_BASED is set then the code is put
                        ; into RAM, else it's in ROM.  Very handy for testing
                        ; new code; load the new code with the EPROM version
                        ; and then test away.
                        ;
0000 =                  RAM_BASED	equ	false
af80 =                  RAM_DATA_BASE	equ	$af80
b000 =                  RAM_CODE_BASE	equ	$b000
                        ;
                        ; Set the EPROM base addresses
                        ;
e000 =                  ROM_START	equ	$e000
df80 =                  RAM_START	equ	$df80
                        ;
                        ; This is something that needs more investigation.
                        ; If the SD system isn't reset enough then bad stuff
                        ; happens after the RS button is pressed.  Set this to
                        ; true to only reset the SD system on cold starts.
                        ;
0000 =                  SD_ONLY_COLD	equ	false
                        ;
                        ; Set this to true to include Bob's Tiny BASIC.  It's
                        ; a minimal (ie, tiny) BASIC interpreter good for
                        ; playing around.
                        ;
0000 =                  TINY_BASIC	equ	false
                        ;
                        ; Set this to true to include a command to get/display
                        ; the current time from Corsham Tech SD card.
                        ;
ffff =                  SHOW_RTC	equ	true
                        ;
                        ; Non-printable ASCII constants
                        ;
0000 =                  NUL		equ	$00
0008 =                  BS		equ	$08
000a =                  LF		equ	$0a
000d =                  CR		equ	$0d
001b =                  ESC		equ	$1b
0020 =                  SPC		equ	$20
                        ;
                        ; Intel HEX record types
                        ;
0000 =                  DATA_RECORD	equ	$00
0001 =                  EOF_RECORD	equ	$01
                        ;
                        ; These are various buffer sizes
AS65 Assembler for R6502 [1.42].                                     Page    3
---------------------------------- xKIM.asm ----------------------------------

                        ;
000c =                  FILENAME_SIZE	equ	12
0040 =                  BUFFER_SIZE	equ	64
                        ;
                        ; Max number of bytes per line for hex dump
                        ;
0010 =                  BYTESLINE	equ	16
                        ;
                        ; Flag values used to detect a cold start vs warm
                        ;
0019 =                  COLD_FLAG_1	equ	$19
0062 =                  COLD_FLAG_2	equ	$62
                        ;
                        ;=====================================================
                        ; This macro is used to verify that the current addres
                        ; meets a required value.  Used mostly to guarantee
                        ; changes don't cause entry points to move.
                        ;
                        VERIFY		macro	expected
                        		if * != expected
                        		fail	Not at requested address (expected)
                        		endif
                        		endm
                        ;
                        ;=====================================================
                        ; KIM memory locations
                        ;
                        		bss
00ef =                  		org	$00ef
00ef =                  PCL		ds	1
00f0 =                  PCH		ds	1
00f1 =                  PREG		ds	1
00f2 =                  SPUSER		ds	1
00f3 =                  ACC		ds	1
00f4 =                  YREG		ds	1
00f5 =                  XREG		ds	1
00f6 =                  CHKHI		ds	1
00f7 =                  CHKSUM		ds	1
00f8 =                  INL	    	ds	1
00f9 =                  INH		ds	1
00fa =                  POINTL		ds	1
00fb =                  POINTH		ds	1
00fc =                  TEMP		ds	1
00fd =                  TMPX		ds	1
00fe =                  CHAR		ds	1
00ff =                  MODE		ds	1
                        ;
17e7 =                  		org	$17e7
17e7 =                  CHKL		ds	1
17e8 =                  CHKH		ds	1
17e9 =                  SAVX		ds	3
17ec =                  VEB	    	ds	6
17f2 =                  CNTL30		ds	1
17f3 =                  CNTH30		ds	1
17f4 =                  TIMH		ds	1
17f5 =                  SAL		ds	1
17f6 =                  SAH	    	ds	1
17f7 =                  EAL		ds	1
17f8 =                  EAH	    	ds	1
17f9 =                  ID  		ds	1
                        ;
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    4
---------------------------------- xKIM.asm ----------------------------------

                        ; KIM I/O locations
                        ;
1740 =                  SAD		equ	$1740
1741 =                  PADD		equ	$1741
                        ;
                        ;=====================================================
                        ; KIM subroutines located in the ROMs
                        ;
1c1c =                  NMIT		equ	$1c1c	;NMI handler
1c1f =                  IRQT		equ	$1c1f	;IRQ handler
1c22 =                  RST	    	equ	$1c22	;RESET handler
1c77 =                  TTYKB		equ	$1c77	;do keyboard monitor
1e2f =                  CRLF		equ	$1e2f	;print CR/LF
1e1e =                  PRTPNT		equ	$1e1e	;print POINT
1e3b =                  PRTBYT		equ	$1e3b	;print A as two hex digits
1e5a =                  GETCH		equ	$1e5a	;get a key from tty into A
1e9e =                  OUTSP		equ	$1e9e	;print a space
1ea0 =                  OUTCH		equ	$1ea0	;print A to TTY
1dac =                  SHOW		equ	$1dac
1daf =                  SHOW1		equ	$1daf
1f63 =                  INCPT		equ	$1f63	;inc POINTL/POINTH
                        ;
                        ;=====================================================
                        ; KIM IEC save and load memory locations
                        ;
17f5 =                  IECSAL		equ $17F5
17f6 =                  IECSAH		equ $17F6
17f7 =                  IECEAL		equ $17F7
17f8 =                  IECEAH		equ $17F8
17f9 =                  IECFID		equ $17F9
f148 =                  IECSAV		equ $F148
f000 =                  IECLOD		equ $F000
                        ;
17fa =                  NMIL		equ $17FA
17fb =                  NMIH		equ $17FB
17fc =                  RSTL		equ $17FC
17fd =                  RSTH		equ $17FD
17fe =                  IRQL		equ $17FE
17ff =                  IRQH		equ $17FF
                        ;
                        ;=====================================================
                        ; I assume the RAM goes from 2000 to DFFF, so carve ou
                        ; a bit for use by the monitor.
                        ;
                        		bss		;uninitialized data area
                        	if	RAM_BASED
                        		org     RAM_DATA_BASE
                        	else
df80 =                  		org	RAM_START
                        	endif
df80 =                  LowestAddress	equ	*
                        ;
                        ; Storage for registers for some monitor calls.
                        ;
df80 =                  saveA		ds	1
df81 =                  saveX		ds	1
df82 =                  saveY		ds	1
                        
df83 =                  saveNMIL	ds	1
df84 =                  saveNMIH	ds	1
df85 =                  saveRSTL	ds	1
df86 =                  saveRSTH	ds	1
AS65 Assembler for R6502 [1.42].                                     Page    5
---------------------------------- xKIM.asm ----------------------------------

df87 =                  saveIRQL	ds	1
df88 =                  saveIRQH	ds	1
                        
                        ;
                        ; Pointer to the subroutine that gets the next input
                        ; character.  Used for doing disk/console input.
                        ;
df89 =                  inputVector	ds	2
                        ;
                        ; Same thing for output.
                        ;
df8b =                  outputVector	ds	2
                        ;
df8d =                  filename	ds	FILENAME_SIZE+1
df9a =                  buffer		ds	BUFFER_SIZE
dfda =                  diskBufOffset	ds	1
dfdb =                  diskBufLength	ds	1
                        ;
dfdc =                  Temp16L		ds	1
dfdd =                  Temp16H		ds	1
df81 =                  byteCount	equ	saveX
                        ;
                        ; The clock functions need some storage, but just
                        ; overlay the names onto the buffer.
                        ;
df9a =                  month		equ	buffer
df9b =                  day		equ	month+1
df9c =                  year_high	equ	day+1
df9d =                  year_low	equ	year_high+1
df9e =                  hour		equ	year_low+1
df9f =                  minute		equ	hour+1
dfa0 =                  second		equ	minute+1
dfa1 =                  day_of_week	equ	second+1
dfa2 =                  clock_end	equ	day_of_week+1
                        ;
                        ; The next group of memory are public and can be
                        ; used by user programs, so don't modify where any
                        ; of these are.  If you want to add new public data,
                        ; put them here, always before existing items.  Be
                        ; sure to adjust the ORG.
                        ;
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE-8
                        	else
dff8 =                  		org	ROM_START-8
                        	endif
                        ;
                        ; Before loading a hex file, the MSB of this vector
                        ; is set to FF.  After loading the file, if the MSB
                        ; is no longer FF then the address in this vector is
                        ; jumped to.  Ie, it can auto-run a file.
                        ;
dff8 =                  AutoRun		ds	2
                        ;
                        ; ColdFlag is used to determine if the extended
                        ; monitoring is doing a warm or cold start.
                        ;
dffa =                  ColdFlag	ds	2
                        ;
                        ; Address of a command table for user-created
                        ; extensions to the monitor.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page    6
---------------------------------- xKIM.asm ----------------------------------

dffc =                  ExtensionAddr	ds	2
                        ;
                        ; This is the higest location in RAM usable by user
                        ; programs.  Nobody should go past this address.  If
                        ; you are writing extentions to the monitor, it's
                        ; okay to load before the address and then adjust
                        ; this down to keep others from stomping on your
                        ; extention.
                        ;
                        ; If your program modifies this value, it needs to
                        ; set it back before terminating.
                        ;
dffe =                  HighestAddress	ds	2
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    7
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Code starts at E000 and goes until FFFF, except for
                        ; the 6502 vectors at the end of memory.
                        ;
                        		code
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE
                        	else
e000 =                  		org	ROM_START
                        	endif
                        ;
                        ; Vector table of useful and fun stuff!  These must
                        ; not change or else existing code won't be calling
                        ; the right functions.
                        ;
e000 =                  BASE		equ	*
e000 : 4c60e0           reentry		jmp	extKim	;extended monitor
e003 : 4ca01e           		jmp	OUTCH	;output A to console
e006 : 4c5a1e           		jmp	GETCH	;get a key and echo
e009 : 4c5a1e           		jmp	GETCH	;no echo - KIM can't do it
e00c : 4c5ae0           		jmp	dummyRet	;future - console stat
e00f : 4cc0ea           		jmp	putsil	;print string after JSR
e012 : 4cefea           		jmp	getHex	;get hex value in A
e015 : 4c3b1e           		jmp	PRTBYT	;print A as hex
e018 : 4c27eb           		jmp	getStartAddr
e01b : 4c39eb           		jmp	getEndAddr
e01e : 4c4beb           		jmp	getAddrRange
e021 : 4c3fe4           		jmp	doHexDump	;perform a hex dump
e024 : 4cdae5           		jmp	doEdit		;edit memory
e027 : 4cf8e6           		jmp	loadHexConsole	;load hex file via console
e02a : 4cc9e6           		jmp	loadHexFile	;load hex file from SD
e02d : 4c8ae9           		jmp	doSDDiskDir	;do a disk directory
e030 : 4c5ceb           		jmp	ComputeOffset	;compute relative offset
                        ;
                        ; SD card functions
                        ;
                        		VERIFY	BASE+$0033
                        
e033 : 4c67ec           		jmp	xParInit	;initialization
e036 : 4c81ec           		jmp	xParSetWrite
e039 : 4c91ec           		jmp	xParSetRead
e03c : 4c9fec           		jmp	xParWriteByte
e03f : 4ccaec           		jmp	xParReadByte
e042 : 4ceeec           		jmp	DiskPing
e045 : 4c01ed           		jmp	DiskDir
e048 : 4c0bed           		jmp	DiskDirNext
e04b : 4c3ced           		jmp	DiskOpenRead
e04e : 4c6ded           		jmp	DiskRead
e051 : 4cd6ed           		jmp	DiskClose
e054 : 4c68ed           		jmp	DiskOpenWrite
e057 : 4c9fed           		jmp	DiskWrite
                        ;
                        ; Even more vectors as I ran out of the reserve area!
                        ;
                        
                        ;
                        ;=====================================================
                        ;=====================================================
                        ; Anything past this point can be moved in future
                        ; releases so none of these functions nor data should
                        ; be directly accessed by user programs!!!
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    8
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is a dummy function for unimplemented functions
                        ; that basically just clears carry and returns.
                        ;
e05a : 18               dummyRet	clc
e05b : 60               		rts
                        ;
e05c : 00               defaultExt	db	0
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    9
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is the start of the extended KIM monitor.
                        ;
e05d : 4c771c           notty		jmp	TTYKB
e060 : a2ff             extKim		ldx	#$ff
e062 : 9a               		txs
e063 : a901             		lda	#$01	;see if in tty mode
e065 : 2c4017           		bit	SAD
e068 : d0f3             		bne	notty	;branch if in keyboard mode
                        
                        	if	~SD_ONLY_COLD
e06a : 2067ec           		jsr	xParInit
                        	endif
                        ;
                        ; Determine if this is a cold or warm start
                        ;
e06d : adfadf           		lda	ColdFlag
e070 : c919             		cmp	#COLD_FLAG_1
e072 : d00a             		bne	coldStart
e074 : adfbdf           		lda	ColdFlag+1
e077 : c962             		cmp	#COLD_FLAG_2
e079 : d003             		bne	coldStart
e07b : 4c2fe1           		jmp	extKimLoop	;it's a warm start
                        ;
                        ; Cold start
                        ;
e07e : d8               coldStart	cld	
e07f : a919             		lda	#COLD_FLAG_1	;indicate we've done cold
e081 : 8dfadf           		sta	ColdFlag
e084 : a962             		lda	#COLD_FLAG_2
e086 : 8dfbdf           		sta	ColdFlag+1
                        ;
                        ; Point to an empty extension set by default.
                        ;
e089 : a95c             		lda	#defaultExt&$ff	;set extension pointers
e08b : 8dfcdf           		sta	ExtensionAddr
e08e : a9e0             		lda	#defaultExt/256
e090 : 8dfddf           		sta	ExtensionAddr+1
                        ;
                        ; Set HighestAddress to just before our RAM area.
                        ;
e093 : a97f             		lda	#(LowestAddress-1)&$ff
e095 : 8dfedf           		sta	HighestAddress
e098 : a9df             		lda	#(LowestAddress-1)/256
e09a : 8dffdf           		sta	HighestAddress+1
                        	if	SD_ONLY_COLD
                        ;
                        ; Initialize the interface to the SD card.  There seem
                        ; be a problem if this is done too often, so do it jus
                        ; and be done.
                        ;
                        		jsr	xParInit
                        	endif
                        ;
                        ; Display our welcome text
                        ;
e09d : 20c0ea           		jsr	putsil
e0a0 : 0d0a0d0a         		db	CR,LF,CR,LF
e0a4 : 457874656e6465.. 		db	"Extended KIM Monitor v"
e0ba : 312e3920         		db	VERSION+'0','.',REVISION+'0',' '
                        	if	BETA_VER
                        		db	"BETA "
AS65 Assembler for R6502 [1.42].                                     Page   10
---------------------------------- xKIM.asm ----------------------------------

                        		db	BETA_VER+'0'
                        		db	' '
                        	endif
e0be : 62792052657472.. 		db  "by RetroSpy Technologies"
e0d6 : 0d0a             		db	CR,LF				
e0d8 : 726574726f2d73.. 		db  "retro-spy.com"
e0e5 : 0d0a             		db	CR,LF
e0e7 : 0d0a             		db	CR,LF
e0e9 : 4261736564206f.. 		db	"Based on the work of Corsham Technologies, LLC"
e117 : 0d0a             		db	CR,LF
e119 : 7777772e636f72.. 		db	"www.corshamtech.com"
e12c : 0d0a             		db	CR,LF
                        	if	RAM_BASED
                        		db	CR,LF
                        		db	"*** RAM BASED VERSION ***"
                        		db	CR,LF
                        	endif
e12e : 00               		db	0
                        ;
                        ; Main command loop.  Put out prompt, get command, etc
                        ; Prints a slightly different prompt for the RAM versi
                        ;
e12f : d8               extKimLoop	cld
e130 : 20e6eb           		jsr	setInputConsole
e133 : 20c0ea           		jsr	putsil	;output prompt
e136 : 0d0a             		db	CR,LF	;feel free to change it
                        	if	RAM_BASED
                        		db	"RAM"
                        	endif
e138 : 3e00             		db	">",0
e13a : 205a1e           		jsr	GETCH
e13d : c90d             		cmp	#CR
e13f : f0ee             		beq	extKimLoop
e141 : c90a             		cmp	#LF
e143 : f0ea             		beq	extKimLoop
e145 : 85f3             		sta	ACC	;save key
                        ;
                        ; Now cycle through the list of commands looking for
                        ; what the user just pressed.
                        ;
e147 : a96d             		lda	#commandTable&$ff
e149 : 85fa             		sta	POINTL
e14b : a9e1             		lda	#commandTable/256
e14d : 85fb             		sta	POINTH
e14f : 206eea           		jsr	searchCmd	;try to find it
                        ;
                        ; Hmmm... wasn't one of the built in commands, so
                        ; see if it's an extended command.
                        ;
e152 : adfcdf           		lda	ExtensionAddr
e155 : 85fa             		sta	POINTL
e157 : adfddf           		lda	ExtensionAddr+1
e15a : 85fb             		sta	POINTH
e15c : 206eea           		jsr	searchCmd
                        ;
                        ; If that returns, then the command was not found.
                        ; Print that it's unknown.
                        ;
e15f : 20c0ea           		jsr	putsil
e162 : 202d204875683f00 		db	" - Huh?",0
e16a : 4c2fe1           cmdFound	jmp	extKimLoop
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   11
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Vector table of commands.  Each entry consists of a
                        ; single ASCII character (the command), a pointer to
                        ; the function which handles the command, and a pointe
                        ; to a string that describes the command.
                        ;
e16d : 3f               commandTable	db	'?'
e16e : d8e3             		dw	showHelp
e170 : c3e1             		dw	quesDesc
                        ;
                        	if	TINY_BASIC
                        		db	'B'
                        		dw	TBasicCold
                        		dw	bDesc
                        	endif
                        ;
                        	if	SHOW_RTC
e172 : 43               		db	'C'
e173 : dbed             		dw	doShowClock
e175 : e0e1             		dw	cDesc
                        	endif
e177 : 44               		db	'D'
e178 : 6de9             		dw	doDiskDir
e17a : f9e1             		dw	dDesc
                        ;
e17c : 45               		db	'E'	;edit memory
e17d : bfe5             		dw	editMemory
e17f : 16e2             		dw	eDesc
                        ;
e181 : 48               		db	'H'	;hex dump
e182 : 31e4             		dw	hexDump
e184 : 30e2             		dw	hDesc
                        ;
e186 : 4a               		db	'J'	;jump to address
e187 : 3de9             		dw	jumpAddress
e189 : 4ee2             		dw	jDesc
                        ;
e18b : 4b               		db	'K'	;return to KIM monitor
e18c : ace3             		dw	returnKim
e18e : 6ce2             		dw	kDesc
                        ;
e190 : 4c               		db	'L'	;load Intel HEX file
e191 : 6de6             		dw	loadHex
e193 : 8ce2             		dw	lDesc
                        ;
e195 : 4d               		db	'M'	;perform memory test
e196 : c4e4             		dw	memTest
e198 : a8e2             		dw	mDesc
                        ;
e19a : 4f               		db	'O'	;branch offset calculator
e19b : fee9             		dw	offCalc
e19d : c2e2             		dw	oDesc
                        ;
e19f : 50               		db	'P'	;ping remote disk
e1a0 : 4ee9             		dw	pingDisk
e1a2 : e8e2             		dw	pDesc
                        ;
e1a4 : 53               		db	'S'	;save memory as hex file
e1a5 : 04e8             		dw	saveHex
e1a7 : 0be3             		dw	sDesc
                        ;
e1a9 : 54               		db	'T'	;type a file on SD
AS65 Assembler for R6502 [1.42].                                     Page   12
---------------------------------- xKIM.asm ----------------------------------

e1aa : a9e9             		dw	typeFile
e1ac : 2de3             		dw	tDesc
                        ;
e1ae : 58               		db	'X'	;return to KIM monitor
e1af : ace3             		dw	returnKim
e1b1 : 6ce2             		dw	kDesc
                        ;
e1b3 : 59               		db	'Y'	;save memory to IEC
e1b4 : 70ee             		dw	saveIEC
e1b6 : 4ae3             		dw	yDesc
                        ;
e1b8 : 5a               		db	'Z'	;load memory from IEC
e1b9 : aaee             		dw	loadIEC
e1bb : 6be3             		dw	zDesc
                        ;
e1bd : 21               		db	'!'	;do cold restart
e1be : d2e3             		dw	doCold
e1c0 : 8ee3             		dw	bangDesc
                        ;
e1c2 : 00               		db	0	;marks end of table
                        ;
                        ;=====================================================
                        ; Descriptions for each command in the command table.
                        ; This wastes a lot of space... I'm open for any
                        ; suggestions to keep the commands clear but reducing
                        ; the amount of space this table consumes.
                        ;
e1c3 : 3f202e2e2e2e2e.. quesDesc	db	"? ........... Show this help",0
                        	if	TINY_BASIC
                        bDesc		db	"B ........... Bob's Tiny BASIC",0
                        	endif
                        	if	SHOW_RTC
e1e0 : 43202e2e2e2e2e.. cDesc		db	"C ........... Show clock",0
                        	endif
e1f9 : 44202e2e2e2e2e.. dDesc		db	"D ........... Disk directory",0
e216 : 45207878787820.. eDesc		db	"E xxxx ...... Edit memory",0
e230 : 48207878787820.. hDesc		db	"H xxxx xxxx . Hex dump memory",0
e24e : 4a207878787820.. jDesc		db	"J xxxx ...... Jump to address",0
e26c : 4b202e2e2e2e2e.. kDesc		db	"K ........... Go to KIM monitor",0
e28c : 4c202e2e2e2e2e.. lDesc		db	"L ........... Load HEX file",0
e2a8 : 4d207878787820.. mDesc		db	"M xxxx xxxx . Memory test",0
e2c2 : 4f207878787820.. oDesc		db	"O xxxx xxxx . Calculate branch offset",0
e2e8 : 50202e2e2e2e2e.. pDesc		db	"P ........... Ping disk controller",0
e30b : 53207878787820.. sDesc		db	"S xxxx xxxx . Save memory to file",0
e32d : 54202e2e2e2e2e.. tDesc		db	"T ........... Type disk file",0
e34a : 5920787878782d.. yDesc		db  "Y xxxx-xxxx . Save memory to IEC",0
e36b : 5a202e2e2e2e2e.. zDesc		db 	"Z ........... Load memory from IEC",0
e38e : 21202e2e2e2e2e.. bangDesc	db	"! ........... Do a cold start",0
                        ;
                        ;=====================================================
                        ; Return to KIM monitor.  Before returning, set the
                        ; "open address" to the start of the extended monitor
                        ; so the KIM monitor is pointing to it by default.
                        ;
e3ac : 20c0ea           returnKim	jsr	putsil
e3af : 0d0a             		db	CR,LF
e3b1 : 52657475726e69.. 		db	"Returning to KIM..."
e3c4 : 0d0a00           		db	CR,LF,0
e3c7 : a900             		lda	#reentry&$0ff
e3c9 : 85fa             		sta	POINTL	;point back to start...
e3cb : a9e0             		lda	#reentry/256
e3cd : 85fb             		sta	POINTH	;...of this code
AS65 Assembler for R6502 [1.42].                                     Page   13
---------------------------------- xKIM.asm ----------------------------------

e3cf : 4caf1d           		jmp	SHOW1	;return to KIM
                        ;
                        ;=====================================================
                        ; Force a cold start.
                        ;
e3d2 : eefadf           doCold		inc	ColdFlag	;foul up flag
e3d5 : 4c60e0           		jmp	extKim		;...and restart
                        ;
                        ;=====================================================
                        ; Command handler for the ? command
                        ;
e3d8 : 20c0ea           showHelp	jsr	putsil
e3db : 0d0a             		db	CR,LF
e3dd : 417661696c6162.. 		db	"Available commands:"
e3f0 : 0d0a0a00         		db	CR,LF,LF,0
                        ;
                        ; Print help for built-in commands...
                        ;
e3f4 : a96d             		lda	#commandTable&$ff
e3f6 : 85fa             		sta	POINTL
e3f8 : a9e1             		lda	#commandTable/256
e3fa : 85fb             		sta	POINTH
e3fc : 2091ea           		jsr	displayHelp	;display help
                        ;
                        ; Now print help for the extension commands...
                        ;
e3ff : adfcdf           		lda	ExtensionAddr
e402 : 85fa             		sta	POINTL
e404 : adfddf           		lda	ExtensionAddr+1
e407 : 85fb             		sta	POINTH
e409 : 2091ea           		jsr	displayHelp
e40c : 202f1e           		jsr	CRLF
e40f : 4c2fe1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This is a generic "not done yet" holder.  Any
                        ; unimplemented commands should point here.
                        ;
e412 : 20c0ea           NDY		jsr	putsil
e415 : 0d0a             		db	CR,LF
e417 : 536f7272792c20.. 		db	"Sorry, not done yet."
e42b : 0d0a00           		db	CR,LF,0
e42e : 4c2fe1           NDYdone		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a hex dump of a region of memory.  This code was
                        ; taken from MICRO issue 5, from an article by
                        ; J.C. Williams.  I changed it a bit, but it's still
                        ; basically the same code.
                        ;
                        ; Slight bug: the starting address is rounded down to
                        ; a multiple of 16.  I'll fix it eventually.
                        ;
e431 : 204beb           hexDump		jsr	getAddrRange
e434 : b0f8             		bcs	NDYdone
e436 : 202f1e           		jsr	CRLF
e439 : 203fe4           		jsr	doHexDump	;subroutine does it
e43c : 4c2fe1           cmdRet2		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine does a hex dump from the address in
                        ; SAL/H to EAL/H.
AS65 Assembler for R6502 [1.42].                                     Page   14
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Move start address to POINT but rounded down to the
                        ; 16 byte boundary.
                        ;
e43f : adf617           doHexDump	lda	SAH
e442 : 85fb             		sta	POINTH
e444 : adf517           		lda	SAL
e447 : 29f0             		and	#$f0	;force to 16 byte
e449 : 85fa             		sta	POINTL
                        ;
                        ; This starts each line.  Set flag to indcate we're
                        ; doing the hex portion, print address, etc.
                        ;
e44b : a900             hexdump1	lda	#0	;set flag to hex mode
e44d : 8df917           		sta	ID
e450 : 202f1e           		jsr	CRLF
e453 : 201e1e           		jsr	PRTPNT	;print the address
e456 : a5fa             hexdump2	lda	POINTL	;push start of line...
e458 : 48               		pha		;...address onto stack
e459 : a5fb             		lda	POINTH
e45b : 48               		pha
e45c : 20baea           		jsr	space2
e45f : a20f             		ldx	#BYTESLINE-1	;number of bytes per line
e461 : 20baea           		jsr	space2	;space before data
                        
e464 : a000             hexdump3	ldy	#0	;get next byte...
e466 : b1fa             		lda	(POINTL),y
e468 : 2cf917           		bit	ID	;hex or ASCII mode?
e46b : 1010             		bpl	hexptbt	;branch if hex mode
                        ;
                        ; Print char if printable, else print a dot
                        ;
e46d : c920             		cmp	#' '
e46f : 9004             		bcc	hexdot
e471 : c97e             		cmp	#'~'
e473 : 9002             		bcc	hexpr
e475 : a92e             hexdot		lda	#'.'
e477 : 20a01e           hexpr		jsr	OUTCH
e47a : 4c83e4           		jmp	hexend
                        ;
                        ; Print character as hex.  
                        ;
e47d : 203b1e           hexptbt 	jsr	PRTBYT	;print as hex
e480 : 20bdea           		jsr	space	;and follow with a space
                        ;
                        ; See if we just dumped the last address.  If not, the
                        ; increment to the next address and continue.
                        ;
e483 : a5fa             hexend  	lda	POINTL	;compare first
e485 : cdf717           		cmp	EAL
e488 : a5fb             		lda	POINTH
e48a : edf817           		sbc	EAH
                        ;
                        ; Now increment to the next address
                        ;
e48d : 08               		php
e48e : 20631f           		jsr	INCPT
e491 : 28               		plp
e492 : 901a             		bcc	hexlntst
                        ;
e494 : 2cf917           		bit	ID
e497 : 3022             		bmi	hexdone
AS65 Assembler for R6502 [1.42].                                     Page   15
---------------------------------- xKIM.asm ----------------------------------

e499 : ca               		dex
e49a : 3006             		bmi	hexdomap
e49c : 20b7ea           hexdump5	jsr	space3
e49f : ca               		dex
e4a0 : 10fa             		bpl	hexdump5
e4a2 : cef917           hexdomap	dec	ID
e4a5 : 68               		pla
e4a6 : 85fb             		sta	POINTH
e4a8 : 68               		pla
e4a9 : 85fa             		sta	POINTL
e4ab : 4c56e4           		jmp     hexdump2
e4ae : ca               hexlntst	dex
e4af : 10b3             		bpl	hexdump3
e4b1 : 2cf917           		bit	ID
e4b4 : 10ec             		bpl	hexdomap
e4b6 : 68               		pla
e4b7 : 68               		pla
e4b8 : 4c4be4           		jmp	hexdump1
                        ;
                        ; Clean up the stack and we're done
                        ;
e4bb : 202f1e           hexdone		jsr	CRLF
e4be : 68               		pla
e4bf : 68               		pla
e4c0 : 60               		rts
                        ;
                        ;=====================================================
                        ; This does a memory test of a region of memory.  One
                        ; problem with the KIM is that there is no routine to
                        ; see if a new character is starting, so this loop
                        ; just runs forever unless the user presses RESET.
                        ;
                        ; Asks for the starting and ending locations.
                        ;
                        ; This cycles a rolling bit, then adds a ninth
                        ; pattern to help detect shorted address bits.
                        ; Ie: 01, 02, 04, 08, 10, 20, 40, 80, BA
                        ;
17e7 =                  pattern		equ	CHKL		;re-use some KIM locations
17e8 =                  original	equ	CHKH
                        ;
                        ; Test patterns
                        ;
0001 =                  PATTERN_0	equ	$01
00ba =                  PATTERN_9	equ	$ba
                        ;
e4c1 : 4c2fe1           cmdRet5		jmp	extKimLoop
e4c4 : 204beb           memTest		jsr	getAddrRange	;get range
e4c7 : b0f8             		bcs	cmdRet5		;branch if abort
                        ;
e4c9 : 20c0ea           		jsr	putsil
e4cc : 0d0a             		db	CR,LF
e4ce : 54657374696e67.. 		db	"Testing memory.  Press RESET to abort"
e4f3 : 00               		db	0
e4f4 : a901             		lda	#PATTERN_0	;only set initial...
e4f6 : 8de717           		sta	pattern		;..pattern once
                        ;
                        ; Start of loop.  This fills/tests one complete pass
                        ; of memory.
                        ;
e4f9 : adf517           memTestMain	lda	SAL	;reset pointer to start
e4fc : 85fa             		sta	POINTL
AS65 Assembler for R6502 [1.42].                                     Page   16
---------------------------------- xKIM.asm ----------------------------------

e4fe : adf617           		lda	SAH
e501 : 85fb             		sta	POINTH
                        ;
                        ; Fill memory with the rolling pattern until the last
                        ; location is filled.
                        ;
e503 : a000             		ldy	#0
e505 : ade717           		lda	pattern
e508 : 8de817           		sta	original
e50b : 91fa             memTestFill	sta	(POINTL),y
e50d : c9ba             		cmp	#PATTERN_9	;at last pattern?
e50f : d005             		bne	memFill3
e511 : a901             		lda	#PATTERN_0	;restart pattern
e513 : 4c1be5           		jmp	memFill4
                        ;
                        ; Rotate pattern left one bit
                        ;
e516 : 0a               memFill3	asl	a
e517 : 9002             		bcc	memFill4	;branch if not overflow
e519 : a9ba             		lda	#PATTERN_9	;ninth pattern
                        ;
                        ; The new pattern is in A.  Now see if we've reached
                        ; the end of the area to be tested.
                        ;
e51b : 48               memFill4	pha			;save pattern
e51c : a5fa             		lda	POINTL
e51e : cdf717           		cmp	EAL
e521 : d007             		bne	memFill5
e523 : a5fb             		lda	POINTH
e525 : cdf817           		cmp	EAH
e528 : f007             		beq	memCheck
                        ;
                        ; Not done, so move to next address and keep going.
                        ;
e52a : 20631f           memFill5	jsr	INCPT
e52d : 68               		pla			;recover pattern
e52e : 4c0be5           		jmp	memTestFill
                        ;
                        ; Okay, memory is filled, so now go back and test it.
                        ; We kept a backup copy of the initial pattern to
                        ; use, but save the current pattern as the starting
                        ; point for the next pass.
                        ;
e531 : 68               memCheck	pla
e532 : 8de717           		sta	pattern		;for next pass
e535 : adf517           		lda	SAL		;reset pointer to start
e538 : 85fa             		sta	POINTL
e53a : adf617           		lda	SAH
e53d : 85fb             		sta	POINTH
e53f : ade817           		lda	original	;restore initial pattern
e542 : a000             		ldy	#0
e544 : d1fa             memTest2	cmp	(POINTL),y
e546 : d02c             		bne	memFail
e548 : c9ba             		cmp	#PATTERN_9
e54a : d004             		bne	memTest3
                        ;
                        ; Time to reload the pattern
                        ;
e54c : a901             		lda	#PATTERN_0
e54e : d005             		bne	memTest4
                        ;
                        ; Rotate pattern left one bit
AS65 Assembler for R6502 [1.42].                                     Page   17
---------------------------------- xKIM.asm ----------------------------------

                        ;
e550 : 0a               memTest3	asl	a
e551 : 9002             		bcc	memTest4
e553 : a9ba             		lda	#PATTERN_9
                        ;
                        ; The new pattern is in A.
                        ;
e555 : 48               memTest4	pha			;save pattern
e556 : a5fa             		lda	POINTL
e558 : cdf717           		cmp	EAL
e55b : d007             		bne	memTest5	;not at end
e55d : a5fb             		lda	POINTH
e55f : cdf817           		cmp	EAH
e562 : f007             		beq	memDone		;at end of pass
                        ;
                        ; Not at end yet, so inc pointer and continue
                        ;
e564 : 20631f           memTest5	jsr	INCPT
e567 : 68               		pla
e568 : 4c44e5           		jmp	memTest2
                        ;
                        ; Another pass has completed.
                        ;
e56b : 68               memDone		pla
e56c : a92e             		lda	#'.'
e56e : 20a01e           		jsr	OUTCH
e571 : 4cf9e4           		jmp	memTestMain
                        ;
                        ; Failure.  Display the failed address, the expected
                        ; value and what was actually there.
                        ;
e574 : 48               memFail		pha		;save pattern for error report
e575 : 20c0ea           		jsr	putsil
e578 : 0d0a             		db	CR,LF
e57a : 4661696c757265.. 		db	"Failure at address ",0
e58e : 201e1e           		jsr	PRTPNT
e591 : 20c0ea           		jsr	putsil
e594 : 2e202045787065.. 		db	".  Expected ",0
e5a1 : 68               		pla
e5a2 : 203b1e           		jsr	PRTBYT
e5a5 : 20c0ea           		jsr	putsil
e5a8 : 2062757420676f.. 		db	" but got ",0
e5b2 : a000             		ldy	#0
e5b4 : b1fa             		lda	(POINTL),y
e5b6 : 203b1e           		jsr	PRTBYT
e5b9 : 202f1e           		jsr	CRLF
e5bc : 4c2fe1           cmdRet4		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Edit memory.  This waits for a starting address to b
                        ; entered.  It will display the current address and it
                        ; contents.  Possible user inputs and actions:
                        ;
                        ;   Two hex digits will place that value in memory
                        ;   RETURN moves to next address
                        ;   BACKSPACE moves back one address
                        ;
e5bf : 20bdea           editMemory      jsr	space
e5c2 : 2027eb           		jsr	getStartAddr
e5c5 : b0f5             		bcs	cmdRet4
e5c7 : adf517           		lda	SAL		;move address into...
e5ca : 85fa             		sta	POINTL		;...POINT
AS65 Assembler for R6502 [1.42].                                     Page   18
---------------------------------- xKIM.asm ----------------------------------

e5cc : adf617           		lda	SAH
e5cf : 85fb             		sta	POINTH
e5d1 : 202f1e           		jsr	CRLF
e5d4 : 20dae5           		jsr	doEdit
e5d7 : 4c2fe1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine edits memory.  On entry, POINT has
                        ; the first address to edit.  Upon exit, POINT will
                        ; have been updated to next address to edit.
                        ;
                        ; Display the current location
                        ;
e5da : 201e1e           doEdit		jsr	PRTPNT		;print address
e5dd : 20bdea           		jsr	space
e5e0 : a000             		ldy	#0
e5e2 : b1fa             		lda	(POINTL),y	;get byte
e5e4 : 203b1e           		jsr	PRTBYT		;print it
e5e7 : 20bdea           		jsr	space
                        ;
e5ea : 20efea           		jsr	getHex
e5ed : b00d             		bcs	editMem2	;not hex
e5ef : a000             editMem7	ldy	#0
e5f1 : 91fa             		sta	(POINTL),y	;save new value
                        ;
                        ; Bump POINT to next location
                        ;
e5f3 : 202f1e           editMem3	jsr	CRLF
e5f6 : 20631f           		jsr	INCPT
e5f9 : 4cdae5           		jmp	doEdit
                        ;
                        ; Not hex, so see if another command.  Valid commands 
                        ;
                        ;    CR = advance to next memory location
                        ;    BS = move to previous location
                        ;    R  = compute relative offset
                        ;
e5fc : c952             editMem2	cmp	#'R'		;compute relative branch
e5fe : f017             		beq	editMem4
e600 : c90d             		cmp	#CR
e602 : f0ef             		beq	editMem3	;move to next
e604 : c908             		cmp	#BS
e606 : d00e             		bne     editexit		;else exit
                        ;
                        ; Move back one location
                        ;
e608 : a5fa             		lda	POINTL
e60a : d002             		bne	editMem8
e60c : c6fb             		dec	POINTH
e60e : c6fa             editMem8	dec	POINTL
e610 : 202f1e           		jsr	CRLF
e613 : 4cdae5           		jmp	doEdit
                        ;
e616 : 60               editexit	rts
                        ;
                        ; They want to calculate a relative offset
                        ;
e617 : 20c0ea           editMem4	jsr	putsil
e61a : 656c6174697665.. 		db	"elative offset to: ",0
e62e : 2039eb           		jsr	getEndAddr
e631 : b0a7             		bcs	doEdit		;bad input
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   19
---------------------------------- xKIM.asm ----------------------------------

                        ; Need to load POINTL/POINTH into SAL/SAH and then
                        ; decrement by one.
                        ;
e633 : a5fb             		lda	POINTH
e635 : 8df617           		sta	SAH
e638 : a5fa             		lda	POINTL
e63a : 8df517           		sta	SAL
e63d : d003             		bne	editMem5
e63f : cef617           		dec	SAH
e642 : cef517           editMem5	dec	SAL
                        ;
e645 : 205ceb           		jsr	ComputeOffset
e648 : 9016             		bcc	editMem6	;value good
e64a : 20c0ea           		jsr	putsil
e64d : 202d206f757420.. 		db	" - out of range",0
e65d : 4cdae5           		jmp	doEdit
                        ;
                        ; Relative offset is in A.
                        ;
e660 : 48               editMem6	pha
e661 : 20bdea           		jsr	space
e664 : 68               		pla
e665 : 48               		pha
e666 : 203b1e           		jsr	PRTBYT		;print it
e669 : 68               		pla
e66a : 4cefe5           		jmp	editMem7	;store it
                        ;
                        ;=====================================================
                        ; This handles the Load hex command.
                        ;
e66d : 20c0ea           loadHex		jsr	putsil
e670 : 0d0a             		db	CR,LF
e672 : 456e7465722066.. 		db	"Enter filename, or Enter to "
e68e : 6c6f6164206672.. 		db	"load from console: ",0
                        ;
e6a2 : 208eeb           		jsr	getFileName	;get filename
e6a5 : ad8ddf           		lda	filename	;null?
e6a8 : d006             		bne	loaddiskfile
e6aa : 20f8e6           		jsr	loadHexConsole	;load from console
e6ad : 4cbce6           		jmp	loadCheckAuto	;check auto-run
                        ;
                        ; Open the file
                        ;
e6b0 : a08d             loaddiskfile	ldy	#filename&$ff
e6b2 : a2df             		ldx	#filename/256
e6b4 : a9ff             		lda	#$ff
e6b6 : 8df917           		sta	ID		;print dots
e6b9 : 20c9e6           		jsr	loadHexFile
                        ;
                        ; If the auto-run vector is no longer $ffff, then jump
                        ; to whatever it points to.
                        ;
e6bc : adf9df           loadCheckAuto	lda	AutoRun+1
e6bf : c9ff             		cmp	#$ff		;unchanged?
e6c1 : f003             		beq	lExit11
e6c3 : 6cf8df           		jmp	(AutoRun)	;execute!
e6c6 : 4c2fe1           lExit11		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine loads a hex file from the SD.  On
                        ; entry the pointer to the filename is in X (MSB) and
                        ; Y (LSB).
AS65 Assembler for R6502 [1.42].                                     Page   20
---------------------------------- xKIM.asm ----------------------------------

                        ;
e6c9 : a9ff             loadHexFile	lda	#$ff
e6cb : 8df9df           		sta	AutoRun+1
e6ce : 8df917           		sta	ID		;we want dots
e6d1 : 203ced           		jsr	DiskOpenRead
e6d4 : 901c             		bcc	loadHexOk	;opened ok
                        ;
e6d6 : 20c0ea           openfail	jsr	putsil
e6d9 : 0d0a             		db	CR,LF
e6db : 4661696c656420.. 		db	"Failed to open file"
e6ee : 0d0a00           		db	CR,LF,0
e6f1 : 60               		rts
                        ;
e6f2 : 20f1eb           loadHexOk	jsr	setInputFile	;redirect input
e6f5 : 4c30e7           		jmp	loadStart
                        ;
                        ;=====================================================
                        ; This subroutine is called to load a hex file from
                        ; the console.
                        ;
e6f8 : a9ff             loadHexConsole	lda	#$ff
e6fa : 8df9df           		sta	AutoRun+1
e6fd : a900             		lda	#0
e6ff : 8df917           		sta	ID		;don't print dots
e702 : 20c0ea           		jsr	putsil
e705 : 0d0a             		db	CR,LF
e707 : 57616974696e67.. 		db	"Waiting for file, or ESC to"
e722 : 20657869742e2e2e 		db	" exit..."
e72a : 0d0a00           		db	CR,LF,0
e72d : 20e6eb           		jsr	setInputConsole
                        ;
                        ; The start of a line.  First character should be a
                        ; colon, but toss out CRs, LFs, etc.  Anything else
                        ; causes an abort.
                        ;
e730 : 20e3eb           loadStart	jsr	redirectedGetch	;get start of line
e733 : c90d             		cmp	#CR
e735 : f0f9             		beq	loadStart
e737 : c90a             		cmp	#LF
e739 : f0f5             		beq	loadStart
e73b : c93a             		cmp	#':'		;what we expect
e73d : d05a             		bne	loadAbortB
                        ;
                        ; Get the header of the record
                        ;
e73f : a900             		lda	#0
e741 : 8de717           		sta	CHKL		;initialize checksum
                        ;
e744 : 20efea           		jsr	getHex		;get byte count
e747 : b054             		bcs	loadAbortC
e749 : 8d81df           		sta	byteCount	;save byte count
e74c : 2033e9           		jsr	updateCrc
e74f : 20efea           		jsr	getHex		;get the MSB of offset
e752 : b04d             		bcs	loadAbortD
e754 : 85fb             		sta	POINTH
e756 : 2033e9           		jsr	updateCrc
e759 : 20efea           		jsr	getHex		;get LSB of offset
e75c : b047             		bcs	loadAbortE
e75e : 85fa             		sta	POINTL
e760 : 2033e9           		jsr	updateCrc
e763 : 20efea           		jsr	getHex		;get the record type
e766 : b041             		bcs	loadAbortF
AS65 Assembler for R6502 [1.42].                                     Page   21
---------------------------------- xKIM.asm ----------------------------------

e768 : 2033e9           		jsr	updateCrc
                        ;
                        ; Only handle two record types:
                        ;    00 = data record
                        ;    01 = end of file record
                        ;
e76b : c900             		cmp	#DATA_RECORD
e76d : f05d             		beq	loadDataRec
e76f : c901             		cmp	#EOF_RECORD
e771 : f042             		beq	loadEof
                        ;
                        ; Unknown record type
                        ;
e773 : a941             		lda	#'A'		;reason
                        ;
                        ; This is the common error handler for various reasons
                        ; On entry A contains an ASCII character which is outp
                        ; to indicate the specific error reason.
                        ;
e775 : 48               loadAbort       pha			;save reason
e776 : 20c0ea           		jsr	putsil
e779 : 0d0a             		db	CR,LF
e77b : 41626f7274696e.. 		db	"Aborting, reason: "
e78d : 00               		db	0
e78e : 68               		pla			;restore and...
e78f : 20a01e           		jsr	OUTCH		;...display reason
e792 : 202f1e           		jsr	CRLF
e795 : 20e6eb           loadExit	jsr	setInputConsole
e798 : 60               		rts
                        ;
                        ; Various error reason codes.  This was meant to be
                        ; very temporary as I worked out the real problem, but
                        ; this debug code immediately "solved" the problem so
                        ; I just left these as-is until the root cause is
                        ; discovered.
                        ;
e799 : a942             loadAbortB	lda	#'B'
e79b : d0d8             		bne	loadAbort
                        ;
e79d : a943             loadAbortC	lda	#'C'
e79f : d0d4             		bne	loadAbort
                        ;
e7a1 : a944             loadAbortD	lda	#'D'
e7a3 : d0d0             		bne	loadAbort
                        ;
e7a5 : a945             loadAbortE	lda	#'E'
e7a7 : d0cc             		bne	loadAbort
                        ;
e7a9 : a946             loadAbortF	lda	#'F'
e7ab : d0c8             		bne	loadAbort
                        ;
e7ad : a947             loadAbortG	lda	#'G'
e7af : d0c4             		bne	loadAbort
                        ;
e7b1 : a948             loadAbortH	lda	#'H'
e7b3 : d0c0             		bne	loadAbort
                        ;
                        ; EOF is easy
                        ;
e7b5 : 20efea           loadEof		jsr	getHex		;get checksum
e7b8 : 20e6eb           		jsr	setInputConsole	;reset input vector
e7bb : 20c0ea           		jsr	putsil
AS65 Assembler for R6502 [1.42].                                     Page   22
---------------------------------- xKIM.asm ----------------------------------

e7be : 0d0a             		db	CR,LF
e7c0 : 5375636365737321 		db	"Success!"
e7c8 : 0d0a00           		db	CR,LF,0
e7cb : 60               		rts
                        ;
                        ; Data records have more work.  After processing the
                        ; line, print a dot to indicate progress.  This should
                        ; be re-thought as it could slow down loading a really
                        ; big file if the console speed is slow.
                        ;
e7cc : ae81df           loadDataRec	ldx	byteCount	;byte count
e7cf : a000             		ldy	#0		;offset
e7d1 : 8e81df           loadData1	stx	byteCount
e7d4 : 8c82df           		sty	saveY
e7d7 : 20efea           		jsr	getHex
e7da : b0d1             		bcs	loadAbortG
e7dc : 2033e9           		jsr	updateCrc
e7df : ac82df           		ldy	saveY
e7e2 : ae81df           		ldx	byteCount
e7e5 : 91fa             		sta	(POINTL),y
e7e7 : c8               		iny
e7e8 : ca               		dex
e7e9 : d0e6             		bne	loadData1
                        ;
                        ; All the bytes were read so get the checksum and see
                        ; if it agrees.  The checksum is a twos-complement, so
                        ; just add the checksum into what we've been calculati
                        ; and if the result is zero then the record is good.
                        ;
e7eb : 20efea           		jsr	getHex		;get checksum
e7ee : 18               		clc
e7ef : 6de717           		adc	CHKL
e7f2 : d0bd             		bne	loadAbortH	;non-zero is error
                        ;
                        ; If loading from an SD file then print a dot at the
                        ; end of each record.  Doing this for serial input is
                        ; very bad because the terminal program is sending the
                        ; next character while this is sending the dot.  Ie,
                        ; data is lost.
                        ;
e7f4 : adf917           		lda	ID
e7f7 : f005             		beq	lrecdone	;jump if not file
e7f9 : a92e             		lda	#'.'		;sanity indicator when
e7fb : 20a01e           		jsr	OUTCH		;...loading from file
e7fe : 4c30e7           lrecdone	jmp	loadStart
e801 : 4c2fe1           lExit1		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory as a
                        ; file on the SD.
                        ;
e804 : 204beb           saveHex		jsr	getAddrRange	;get range to dump
e807 : b0f8             		bcs	lExit1	;abort on error
                        ;
                        ; Get the filename to save to
                        ;
e809 : 20c0ea           		jsr	putsil
e80c : 0d0a             		db	CR,LF
e80e : 456e7465722066.. 		db	"Enter filename, or Enter to "
e82a : 646973706c6179.. 		db	"display to console: ",0
                        ;
e83f : 208eeb           		jsr	getFileName	;get filename
AS65 Assembler for R6502 [1.42].                                     Page   23
---------------------------------- xKIM.asm ----------------------------------

e842 : ad8ddf           		lda	filename	;null?
e845 : f012             		beq	saveHexConsole	;dump to console
                        ;
                        ; They selected a file, so try to open it.
                        ;
e847 : a2df             		ldx	#filename>>8
e849 : a08d             		ldy	#filename&$ff
e84b : 2068ed           		jsr	DiskOpenWrite	;attempt to open file
e84e : 9003             		bcc	sopenok		;branch if opened ok
e850 : 4cd6e6           		jmp	openfail
                        ;
e853 : 203bec           sopenok		jsr	setOutputFile
e856 : 4c5ce8           		jmp	savehex2
                        ;
                        ; They are saving to the console.  Set up the output
                        ; vector and do the job.
                        ;
e859 : 2030ec           saveHexConsole	jsr	setOutputConsole
                        ;
                        ; Compute the number of bytes to dump
                        ;
e85c : 38               savehex2	sec
e85d : adf717           		lda	EAL
e860 : edf517           		sbc	SAL
e863 : 8ddcdf           		sta	Temp16L
e866 : adf817           		lda	EAH
e869 : edf617           		sbc	SAH
e86c : 8ddddf           		sta	Temp16H
e86f : 9042             		bcc	SDone	;start > end
e871 : 0900             		ora	#0
e873 : 303e             		bmi	SDone	;more than 32K seems wrong
                        ;
                        ; Add one to the count
                        ;
e875 : eedcdf           		inc	Temp16L
e878 : d003             		bne	slab1
e87a : eedddf           		inc	Temp16H
                        ;
                        ; Move pointer to zero page
                        ;
e87d : adf517           slab1		lda	SAL
e880 : 85fa             		sta	POINTL
e882 : adf617           		lda	SAH
e885 : 85fb             		sta	POINTH
                        ;
                        ; Top of each loop.  Start by seeing if there are any 
                        ; left to dump.
                        ;
e887 : addddf           Sloop1		lda	Temp16H
e88a : d02a             		bne	Sgo	;more to do
e88c : addcdf           		lda	Temp16L
e88f : d025             		bne	Sgo	;more to do
                        ;
                        ; At end of the region, so output an end record.  This
                        ; probably looks like overkill but keep in mind this
                        ; might be going to a file so we can't use the normal
                        ; string put functions.
                        ;
e891 : a93a             		lda	#':'
e893 : 201eec           		jsr	redirectedOutch
e896 : a900             		lda	#0
e898 : 2004ec           		jsr	HexToOutput
AS65 Assembler for R6502 [1.42].                                     Page   24
---------------------------------- xKIM.asm ----------------------------------

e89b : 2004ec           		jsr	HexToOutput
e89e : 2004ec           		jsr	HexToOutput
e8a1 : a901             		lda	#1
e8a3 : 2004ec           		jsr	HexToOutput
e8a6 : a9ff             		lda	#$ff
e8a8 : 2004ec           		jsr	HexToOutput
                        ;
                        ; If output to file, flush and close the file.
                        ;
e8ab : ad8ddf           		lda	filename
e8ae : f003             		beq	SDone		;it's going to console
e8b0 : 2021ec           		jsr	CloseOutFile
e8b3 : 4c2fe1           SDone		jmp	extKimLoop	;back to the monitor
                        ;
                        ; This dumps the next line.  See how many bytes are le
                        ; and if more than BYTESLINE, then just do BYTESLINE.
                        ;
e8b6 : addddf           Sgo		lda	Temp16H
e8b9 : d007             		bne	Sdef	;do default number of bytes
e8bb : addcdf           		lda	Temp16L
e8be : c910             		cmp	#BYTESLINE
e8c0 : 9002             		bcc	Scnt	;more than max per line
e8c2 : a910             Sdef		lda	#BYTESLINE
e8c4 : 8de917           Scnt		sta	SAVX	;for decrementing
e8c7 : 8df917           		sta	ID	;for subtracting
                        ;
                        ; Put out the header
                        ;
e8ca : a93a             		lda	#':'
e8cc : 201eec           		jsr	redirectedOutch
                        ;
e8cf : ade917           		lda	SAVX
e8d2 : 8de717           		sta	CHKL	;start checksum
e8d5 : 2004ec           		jsr	HexToOutput
                        ;
e8d8 : a5fb             		lda	POINTH	;starting address
e8da : 2033e9           		jsr	updateCrc
e8dd : 2004ec           		jsr	HexToOutput
e8e0 : a5fa             		lda	POINTL
e8e2 : 2033e9           		jsr	updateCrc
e8e5 : 2004ec           		jsr	HexToOutput
                        ;
e8e8 : a900             		lda	#0	;record type - data
e8ea : 2004ec           		jsr	HexToOutput
                        ;
                        ; Now print the proper number of bytes
                        ;
e8ed : a000             Sloop2		ldy	#0
e8ef : b1fa             		lda	(POINTL),y	;get byte
e8f1 : 2033e9           		jsr	updateCrc
e8f4 : 2004ec           		jsr	HexToOutput
e8f7 : 20631f           		jsr	INCPT	;increment pointer
                        ;
e8fa : cee917           sdec		dec	SAVX
e8fd : d0ee             		bne	Sloop2
                        ;
                        ; Now print checksum
                        ;
e8ff : ade717           		lda	CHKL
e902 : 49ff             		eor	#$ff	;one's complement
e904 : 18               		clc
e905 : 6901             		adc	#1	;two's complement
AS65 Assembler for R6502 [1.42].                                     Page   25
---------------------------------- xKIM.asm ----------------------------------

e907 : 2004ec           		jsr	HexToOutput
                        ;
                        ; Output a CR/LF
                        ;
e90a : a90d             		lda	#CR
e90c : 201eec           		jsr	redirectedOutch
e90f : a90a             		lda	#LF
e911 : 201eec           		jsr	redirectedOutch
                        ;
                        ; If saving to disk, output a dot to indicate progress
                        ;
e914 : ad8ddf           		lda	filename
e917 : f005             		beq	shf2
                        ;
e919 : a92e             		lda	#'.'
e91b : 20a01e           		jsr	OUTCH	;goes to console
                        ;
e91e : 38               shf2		sec
e91f : addcdf           		lda	Temp16L
e922 : edf917           		sbc	ID
e925 : 8ddcdf           		sta	Temp16L
e928 : addddf           		lda	Temp16H
e92b : e900             		sbc	#0
e92d : 8ddddf           		sta	Temp16H
                        ;
e930 : 4c87e8           		jmp	Sloop1
                        ;
                        ;=====================================================
                        ; Adds the character in A to the CRC.  Preserves A.
                        ;
e933 : 48               updateCrc	pha
e934 : 18               		clc
e935 : 6de717           		adc	CHKL
e938 : 8de717           		sta	CHKL
e93b : 68               		pla
e93c : 60               		rts
                        ;
                        ;=====================================================
                        ; Handles the command to prompt for an address and the
                        ; jump to it.
                        ;
e93d : 20bdea           jumpAddress     jsr	space
e940 : 2027eb           		jsr	getStartAddr
e943 : b006             		bcs	cmdRet	;branch on bad address
e945 : 202f1e           		jsr	CRLF
e948 : 6cf517           		jmp	(SAL)	;else jump to address
                        ;
e94b : 4c2fe1           cmdRet		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Ping the Arduno disk controller.  This just sends th
                        ; PING command gets back one character, then returns.
                        ; Not much of a test but is sufficient to prove the
                        ; link is working.
                        ;
e94e :                  pingDisk
                        ;		jsr	xParInit	;init interface
e94e : 20c0ea           		jsr	putsil
e951 : 696e672e2e2e2000 		db	"ing... ",0
e959 : 20eeec           		jsr	DiskPing
e95c : 20c0ea           		jsr	putsil
e95f : 7375636365737321 		db	"success!"
AS65 Assembler for R6502 [1.42].                                     Page   26
---------------------------------- xKIM.asm ----------------------------------

e967 : 0d0a00           		db	CR,LF,0
e96a : 4c2fe1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a disk directory of the SD card.
                        ;
e96d : 20c0ea           doDiskDir	jsr	putsil
e970 : 69736b20446972.. 		db	"isk Directory..."
e980 : 0d0a00           		db	CR,LF,0
e983 : 208ae9           		jsr	doSDDiskDir
e986 : 4c2fe1           		jmp	extKimLoop
e989 : 60               doDiskDirEnd	rts
                        ;
                        ;=====================================================
                        ; Subroutine to do a disk directory.  Prints filenames
                        ; to the console.
                        ;
e98a : 2067ec           doSDDiskDir	jsr	xParInit
e98d : 2001ed           		jsr	DiskDir
                        ;
                        ; Get/Display each entry
                        ;
e990 : a2df             doDiskDirLoop   ldx	#filename/256	;pointer to buffer
e992 : a08d             		ldy	#filename&$ff
e994 : 86f9             		stx	INH		;save for puts
e996 : 84f8             		sty	INL
e998 : 200bed           		jsr	DiskDirNext	;get next entry
e99b : b0ec             		bcs	doDiskDirEnd	;carry = end of list
e99d : 20b7ea           		jsr	space3
e9a0 : 20d4ea           		jsr	puts		;else print name
e9a3 : 202f1e           		jsr	CRLF
e9a6 : 4c90e9           		jmp	doDiskDirLoop	;do next entry
                        ;
                        ;=====================================================
                        ; Type the contents of an SD file to console.
                        ;
e9a9 : 20c0ea           typeFile	jsr	putsil
e9ac : 202d20456e7465.. 		db	" - Enter filename: ",0
e9c0 : 208eeb           		jsr	getFileName
e9c3 : a08d             		ldy	#filename&$ff
e9c5 : a2df             		ldx	#filename/256
                        ;		jsr	xParInit
e9c7 : 203ced           		jsr	DiskOpenRead
e9ca : 901e             		bcc	typeFile1	;opened ok
                        ;
e9cc : 20c0ea           		jsr	putsil
e9cf : 0d0a             		db	CR,LF
e9d1 : 4661696c656420.. 		db	"Failed to open file"
e9e4 : 0d0a00           		db	CR,LF,0
e9e7 : 4c2fe1           		jmp	extKimLoop
                        ;
                        ; Now just keep reading in bytes and displaying them.
                        ;
e9ea : 20f1eb           typeFile1	jsr	setInputFile	;reading from file
e9ed : 20b4eb           typeFileLoop	jsr	getNextFileByte
e9f0 : b006             		bcs	typeEof
e9f2 : 20a01e           		jsr	OUTCH	;display character
e9f5 : 4cede9           		jmp	typeFileLoop
                        ;
e9f8 : 20d6ed           typeEof		jsr	DiskClose
e9fb : 4c2fe1           		jmp	extKimLoop
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   27
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Calculate the offset for a relative branch.  6502
                        ; relative branch calculations are well known.
                        ;
                        ; Offset from branch (BASE) = TARGET - (BASE+2).
                        ; If the result is positive, upper byte must be
                        ; zero.  If negative, upper byte must be FF.
                        ;
                        ; BASE	TARGET	Computed	Actual
                        ; 0200	0200	0200-(0200+2)	FFFE
                        ; 0200	020E	020E-(0200+2)	000C
                        ; 0226	0220	0220-(0226+2)	FFF8
                        ; 0156	015A	015A-(0156+2)	0002
                        ; 015C	012D	012D-(015C+2)	FFCF
                        ; 0200	0300	0300-(0200+2)	00FE - out of range
                        ; 0300	0200	0200-(0300+2)	FEFE - out of range
                        ;
e9fe : 20c0ea           offCalc		jsr	putsil
ea01 : 202d204272616e.. 		db	" - Branch instruction address: "
ea20 : 00               		db	0
ea21 : 2027eb           		jsr	getStartAddr
ea24 : b030             		bcs	calcExit
ea26 : 20c0ea           		jsr	putsil
ea29 : 2c206272616e63.. 		db	", branch to: "
ea36 : 00               		db	0
ea37 : 2039eb           		jsr	getEndAddr
ea3a : b01a             		bcs	calcExit
ea3c : 205ceb           		jsr	ComputeOffset	;does the work
ea3f : 9018             		bcc	relgood		;if good offset
                        ;
                        ; Branch is out of range.
                        ;
ea41 : 20c0ea           		jsr	putsil
ea44 : 202d206f757420.. 		db	" - out of range",CR,LF,0
ea56 : 4c2fe1           calcExit	jmp	extKimLoop
                        ;
                        ; Branch is in range so dislay the value.
                        ;
ea59 : 48               relgood		pha			;save offset
ea5a : 20c0ea           		jsr	putsil
ea5d : 204f6666736574.. 		db	" Offset: ",0
ea67 : 68               		pla
ea68 : 203b1e           		jsr	PRTBYT
ea6b : 4c2fe1           		jmp	extKimLoop
                        ;
                        ; Add new commands here...
                        ;
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   28
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This subroutine will search for a command in a table
                        ; and call the appropriate handler.  See the command
                        ; table near the start of the code for what the format
                        ; is.  If a match is found, pop off the return address
                        ; from the stack and jump to the code.  Else, return.
                        ;
ea6e : a000             searchCmd	ldy	#0
ea70 : b1fa             cmdLoop		lda	(POINTL),y
ea72 : f01c             		beq	cmdNotFound
ea74 : c5f3             		cmp	ACC	;compare to user's input
ea76 : f007             		beq	cmdMatch
ea78 : c8               		iny		;start of function ptr
ea79 : c8               		iny
ea7a : c8               		iny		;start of help
ea7b : c8               		iny
ea7c : c8               		iny		;move to next command
ea7d : d0f1             		bne	cmdLoop
                        ;
                        ; It's found!  Load up the address of the code to call
                        ; pop the return address off the stack and jump to the
                        ; handler.
                        ;
ea7f : c8               cmdMatch	iny
ea80 : b1fa             		lda	(POINTL),y	;handler LSB
ea82 : 48               		pha
ea83 : c8               		iny
ea84 : b1fa             		lda	(POINTL),y	;handler MSB
ea86 : 85fb             		sta	POINTH
ea88 : 68               		pla
ea89 : 85fa             		sta	POINTL
ea8b : 68               		pla		;pop return address
ea8c : 68               		pla
ea8d : 6cfa00           		jmp	(POINTL)
                        ;
                        ; Not found, so just return.
                        ;
ea90 : 60               cmdNotFound	rts
                        ;
                        ;=====================================================
                        ; Given a pointer to a command table in POINT, display
                        ; the help text for all commands in the table.
                        ;
ea91 : a000             displayHelp	ldy	#0	;index into command table
ea93 : b1fa             showHelpLoop	lda	(POINTL),y	;get command
ea95 : f01f             		beq	showHelpDone	;jump if at end
                        ;
                        ; Display this entry's descriptive text
                        ;
ea97 : c8               		iny		;skip over command
ea98 : c8               		iny		;skip over function ptr
ea99 : c8               		iny
ea9a : b1fa             		lda	(POINTL),y
ea9c : 85f8             		sta	INL
ea9e : c8               		iny
ea9f : b1fa             		lda	(POINTL),y
eaa1 : 85f9             		sta	INH
eaa3 : 98               		tya
eaa4 : 48               		pha
eaa5 : 209e1e           		jsr	OUTSP
eaa8 : 209e1e           		jsr	OUTSP
AS65 Assembler for R6502 [1.42].                                     Page   29
---------------------------------- xKIM.asm ----------------------------------

eaab : 20d4ea           		jsr	puts	;print description
eaae : 202f1e           		jsr	CRLF
eab1 : 68               		pla
eab2 : a8               		tay
eab3 : c8               		iny		;point to next entry
eab4 : d0dd             		bne	showHelpLoop
eab6 : 60               showHelpDone	rts
                        ;
                        ;=====================================================
                        ; Print some spaces.
                        ;
eab7 : 20bdea           space3		jsr	space
eaba : 20bdea           space2		jsr	space
eabd : 4c9e1e           space   	jmp	OUTSP
                        ;
                        ;=====================================================
                        ; This prints the null-terminated string that
                        ; immediately follows the JSR to this function.  This
                        ; version was written by Ross Archer and is at:
                        ;
                        ;    www.6502.org/source/io/primm.htm
                        ;
eac0 : 68               putsil		pla
eac1 : 85f8             		sta	INL
eac3 : 68               		pla
eac4 : 85f9             		sta	INH
eac6 : a001             		ldy	#1
eac8 : 20d6ea           		jsr	putsy
eacb : e6f8             		inc	INL
eacd : d002             		bne	puts2
eacf : e6f9             		inc	INH
ead1 : 6cf800           puts2		jmp	(INL)
                        ;
                        ;=====================================================
                        ; This prints the null terminated string pointed to by
                        ; INL and INH.  Modifies those locations to point to
                        ; the end of the string.
                        ;
ead4 : a000             puts		ldy	#0
ead6 : b1f8             putsy		lda	(INL),y
ead8 : e6f8             		inc	INL
eada : d002             		bne	puts1
eadc : e6f9             		inc	INH
eade : 0900             puts1		ora	#0
eae0 : f00c             		beq	putsdone
eae2 : 8c82df           		sty	saveY
eae5 : 20a01e           		jsr	OUTCH	;print character
eae8 : ac82df           		ldy	saveY
eaeb : 4cd6ea           		jmp	putsy
eaee : 60               putsdone	rts
                        ;
                        ;=====================================================
                        ; This gets two hex characters and returns the value
                        ; in A with carry clear.  If a non-hex digit is
                        ; entered, then A contans the offending character and
                        ; carry is set.
                        ;
eaef : 2005eb           getHex		jsr	getNibble
eaf2 : b01e             		bcs	getNibBad
eaf4 : 0a               		asl	a
eaf5 : 0a               		asl	a
eaf6 : 0a               		asl	a
AS65 Assembler for R6502 [1.42].                                     Page   30
---------------------------------- xKIM.asm ----------------------------------

eaf7 : 0a               		asl	a
eaf8 : 8d80df           		sta	saveA
eafb : 2005eb           		jsr	getNibble
eafe : b012             		bcs	getNibBad
eb00 : 0d80df           		ora	saveA
eb03 : 18               		clc
eb04 : 60               		rts
                        ;
                        ; Helper.  Gets next input char and converts to a
                        ; value from 0-F in A and returns C clear.  If not a
                        ; valid hex character, return C set.
                        ;
eb05 : 20e3eb           getNibble	jsr	redirectedGetch
eb08 : a20f             		ldx	#nibbleHexEnd-nibbleHex-1
eb0a : dd17eb           getNibble1	cmp	nibbleHex,x
eb0d : f005             		beq	getNibF	;got match
eb0f : ca               		dex
eb10 : 10f8             		bpl	getNibble1
eb12 : 38               getNibBad	sec
eb13 : 60               		rts
                        
eb14 : 8a               getNibF		txa		;index is value
eb15 : 18               		clc
eb16 : 60               		rts
                        ;
eb17 : 30313233343536.. nibbleHex	db	"0123456789ABCDEF"
eb27 =                  nibbleHexEnd	equ	*
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; SAL and SAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eb27 : 20efea           getStartAddr	jsr	getHex
eb2a : b00c             		bcs	getDone
eb2c : 8df617           		sta	SAH
eb2f : 20efea           		jsr	getHex
eb32 : b004             		bcs	getDone
eb34 : 8df517           		sta	SAL
eb37 : 18               		clc
eb38 : 60               getDone		rts
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; EAL and EAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eb39 : 20efea           getEndAddr	jsr	getHex
eb3c : b0fa             		bcs	getDone
eb3e : 8df817           		sta	EAH
eb41 : 20efea           		jsr	getHex
eb44 : b0f2             		bcs	getDone
eb46 : 8df717           		sta	EAL
eb49 : 18               		clc
eb4a : 60               		rts
                        ;
                        ;=====================================================
                        ; Get an address range and leave them in SAL and EAL.
                        ;
eb4b : 20bdea           getAddrRange    jsr	space
eb4e : 2027eb           		jsr	getStartAddr
eb51 : b0e5             		bcs	getDone
AS65 Assembler for R6502 [1.42].                                     Page   31
---------------------------------- xKIM.asm ----------------------------------

eb53 : a92d             		lda	#'-'
eb55 : 20a01e           		jsr	OUTCH
eb58 : 2039eb           		jsr	getEndAddr
eb5b : 60               		rts
                        ;
                        ;=====================================================
                        ; This computes the relative offset between the
                        ; address in SAL/SAH (address of branch instruction)
                        ; and EAL/EAH (address to jump to).  If a valid range,
                        ; returns C clear and the offset in A.  If the branch
                        ; is out of range, C is set and A undefined.  Modifies
                        ; A, SAL and SAH.
                        ;
eb5c :                  ComputeOffset
                        ;
                        ; Add two to the end (BASE) address.  For calculations
                        ;   BASE = SAL/SAH
                        ;   TARGET = EAL/EAH
                        ;
eb5c : 18               		clc
eb5d : adf517           		lda	SAL
eb60 : 6902             		adc	#2
eb62 : 8df517           		sta	SAL
eb65 : 9003             		bcc	coffsub
eb67 : eef617           		inc	SAH
                        ;
                        ; Subtract the BASE (end) address from the TARGET (sta
                        ;
eb6a : 38               coffsub		sec
eb6b : adf717           		lda	EAL
eb6e : edf517           		sbc	SAL
eb71 : 48               		pha		;save for later
eb72 : 8df517           		sta	SAL
eb75 : adf817           		lda	EAH
eb78 : edf617           		sbc	SAH
eb7b : 8df617           		sta	SAH	;SAL/SAH contain offset
                        ;
                        ; High part must be either FF for negative branch or
                        ; 00 for a positive branch.  Cheat a bit here by rolli
                        ; the MSBit into C and adding to the MSByte.  If the
                        ; result is zero then everything is cool.
                        ;
eb7e : 68               		pla		;restore LSB of offset
eb7f : 48               		pha
eb80 : 0a               		asl	a	;put sign into C
eb81 : adf617           		lda	SAH
eb84 : 6900             		adc	#0
eb86 : f003             		beq	cogood	;branch if in range
                        ;
eb88 : 68               		pla		;clean up stack
eb89 : 38               		sec		;error
eb8a : 60               		rts
                        ;
eb8b : 68               cogood		pla		;get back offset
eb8c : 18               		clc
eb8d : 60               		rts
                        ;
                        ;=====================================================
                        ; Get a disk filename.  The KIM's behavior of echoing
                        ; every key prevents this from being too fancy, but it
                        ; good enough.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   32
---------------------------------- xKIM.asm ----------------------------------

eb8e : a200             getFileName	ldx	#0
eb90 : 205a1e           getFilename1	jsr	GETCH	;get next key
eb93 : c90d             		cmp	#CR	;end of the input?
eb95 : f014             		beq	getFnDone
eb97 : c908             		cmp	#BS	;backspace?
eb99 : f00a             		beq	getFnDel
eb9b : e00c             		cpx	#FILENAME_SIZE	;check size
eb9d : f0f1             		beq	getFilename1	;at length limit
eb9f : 9d8ddf           		sta	filename,x	;else save it
eba2 : e8               		inx
eba3 : d0eb             		bne	getFilename1
                        ;
eba5 : ca               getFnDel	dex		;back up one
eba6 : 10e8             		bpl	getFilename1
eba8 : e8               		inx		;can't go past start
eba9 : f0e5             		beq	getFilename1
ebab : a900             getFnDone       lda	#0	;terminate line
ebad : 9d8ddf           		sta	filename,x
ebb0 : 202f1e           		jsr	CRLF
ebb3 : 60               		rts
                        ;
                        ;=====================================================
                        ; This gets the next byte from an open disk file.  If
                        ; there are no more bytes left, this returns C set.
                        ; Else, C is clear and A contains the character.
                        ;
ebb4 : aedadf           getNextFileByte ldx 	diskBufOffset
ebb7 : ecdbdf           		cpx	diskBufLength
ebba : d014             		bne	hasdata		;branch if still data
                        ;
                        ; There is no data left in the buffer, so read a
                        ; block from the SD system.
                        ;
ebbc : a940             		lda	#BUFFER_SIZE
ebbe : a2df             		ldx	#buffer>>8
ebc0 : a09a             		ldy	#buffer&$ff
ebc2 : 206ded           		jsr	DiskRead
ebc5 : b012             		bcs	getNextEof
                        ;
                        ; A contains the number of bytes actually read.
                        ;
ebc7 : 8ddbdf           		sta	diskBufLength	;save length
ebca : c900             		cmp	#0		;shouldn't happen
ebcc : f00b             		beq	getNextEof
                        ;
ebce : a200             		ldx	#0
ebd0 : bd9adf           hasdata		lda	buffer,x
ebd3 : e8               		inx
ebd4 : 8edadf           		stx	diskBufOffset
ebd7 : 18               		clc
ebd8 : 60               		rts
                        ;
ebd9 : a900             getNextEof	lda	#0
ebdb : 8ddadf           		sta	diskBufOffset
ebde : 8ddbdf           		sta	diskBufLength
ebe1 : 38               		sec
ebe2 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   33
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the input vector
                        ; pointer to get the next input character.
                        ;
ebe3 : 6c89df           redirectedGetch	jmp	(inputVector)
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to the normal
                        ; console input subroutine.
                        ;
ebe6 : a95a             setInputConsole	lda	#GETCH&$ff
ebe8 : 8d89df           		sta     inputVector
ebeb : a91e             		lda	#GETCH/256
ebed : 8d8adf           		sta	inputVector+1
ebf0 : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to a file read
                        ; subroutine.
                        ;
ebf1 : a9b4             setInputFile    lda	#getNextFileByte&$ff
ebf3 : 8d89df           		sta     inputVector
ebf6 : a9eb             		lda	#getNextFileByte/256
ebf8 : 8d8adf           		sta	inputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ebfb : a900             		lda	#0
ebfd : 8ddadf           		sta	diskBufOffset
ec00 : 8ddbdf           		sta	diskBufLength
ec03 : 60               		rts
                        ;
                        ;=====================================================
                        ; Print character in A as two hex digits to the
                        ; current output device (console or file).
                        ;
ec04 : 48               HexToOutput	pha		;save return value
ec05 : 48               		pha
ec06 : 4a               		lsr	a	;move top nibble to bottom
ec07 : 4a               		lsr	a
ec08 : 4a               		lsr	a
ec09 : 4a               		lsr	a
ec0a : 2013ec           		jsr	hexta	;output nibble
ec0d : 68               		pla
ec0e : 2013ec           		jsr	hexta
ec11 : 68               		pla		;restore
ec12 : 60               		rts
                        ;
ec13 : 290f             hexta		and	#%0001111
ec15 : c90a             		cmp	#$0a
ec17 : 18               		clc
ec18 : 3002             		bmi	hexta1
ec1a : 6907             		adc	#7
ec1c : 6930             hexta1		adc	#'0'	;then fall into...
                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the output vector
                        ; pointer to send the character in A to the proper
AS65 Assembler for R6502 [1.42].                                     Page   34
---------------------------------- xKIM.asm ----------------------------------

                        ; device.
                        ;
ec1e : 6c8bdf           redirectedOutch	jmp	(outputVector)
                        ;
                        ;=====================================================
                        ; This flushes any data remaining in the disk buffer
                        ; and then closes the file.
                        ;
ec21 : addadf           CloseOutFile	lda	diskBufOffset
ec24 : f007             		beq	closeonly
ec26 : a2df             		ldx	#buffer>>8
ec28 : a09a             		ldy	#buffer&$ff
ec2a : 209fed           		jsr	DiskWrite
                        ;
ec2d : 20d6ed           closeonly	jsr	DiskClose
                        ;
                        ; Fall through...
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to the normal
                        ; console output subroutine.
                        ;
ec30 :                  setOutputConsole
ec30 : a9a0             		lda	#OUTCH&$ff
ec32 : 8d8bdf           		sta     outputVector
ec35 : a91e             		lda	#OUTCH/256
ec37 : 8d8cdf           		sta	outputVector+1
ec3a : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to a file write
                        ; subroutine.
                        ;
ec3b : a94b             setOutputFile    lda	#putNextFileByte&$ff
ec3d : 8d8bdf           		sta     outputVector
ec40 : a9ec             		lda	#putNextFileByte/256
ec42 : 8d8cdf           		sta	outputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ec45 : a900             		lda	#0
ec47 : 8ddadf           		sta	diskBufOffset
ec4a : 60               		rts
                        ;
                        ;=====================================================
                        ; Add the byte in A to the output buffer.  If the
                        ; buffer is full, flush it to disk.
                        ;
ec4b : aedadf           putNextFileByte	ldx	diskBufOffset
ec4e : e040             		cpx	#BUFFER_SIZE	;buffer full?
ec50 : d00d             		bne	pNFB		;no
                        ;
                        ; The buffer is full, so write it out.
                        ;
ec52 : 48               		pha			;save byte
ec53 : a940             		lda	#BUFFER_SIZE
ec55 : a2df             		ldx	#buffer>>8
ec57 : a09a             		ldy	#buffer&$ff
ec59 : 209fed           		jsr	DiskWrite
                        ;
ec5c : a200             		ldx	#0		;reset index
AS65 Assembler for R6502 [1.42].                                     Page   35
---------------------------------- xKIM.asm ----------------------------------

ec5e : 68               		pla
ec5f : 9d9adf           pNFB		sta	buffer,x
ec62 : e8               		inx
ec63 : 8edadf           		stx	diskBufOffset
ec66 : 60               		rts
                        ;
                        		include	"pario.asm"
                        ;*****************************************************
                        ; These are the low-level I/O routines to talk to the
                        ; Arduino processor connected to the KIM's I/O port.
                        ;
                        ; August 2014, Bob Applegate K2UT, bob@corshamtech.com
                        ;
                        ; Which port bits are used for what:
                        ;
                        ; A0 = Data 0, alternates input/output
                        ; A1 = Data 1, alternates input/output
                        ; A2 = Data 2, alternates input/output
                        ; A3 = Data 3, alternates input/output
                        ; A4 = Data 4, alternates input/output
                        ; A5 = Data 5, alternates input/output
                        ; A6 = Data 6, alternates input/output
                        ; A7 = Data 7, alternates input/output
                        ;
                        ; B0 = Direction bit, always output
                        ; B1 = Write strobe or ACK, always output
                        ; B2 = Read stroke or ACK, always input
                        ;
                        ;----------------------------------------------------
                        ; Bits in the B register
                        ;
0001 =                  DIRECTION	equ	%00000001
0002 =                  PSTROBE		equ	%00000010
0004 =                  ACK		equ	%00000100
                        ;
                        ;----------------------------------------------------
                        ; The KIM has a 6530 with the two I/O ports at $1700.
                        ;
                        ; Addresses of 6530 registers
                        ;
1700 =                  PIABASE		equ	$1700
1700 =                  PIAREGA		equ	PIABASE		;data reg A
1701 =                  PIADDRA		equ	PIABASE+1	;data dir reg A
1702 =                  PIAREGB		equ	PIABASE+2	;data reg B
1703 =                  PIADDRB		equ	PIABASE+3	;data dir reg B
                        		code
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   36
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This is the initialization function.  Call before
                        ; doing anything else with the parallel port.
                        ;
ec67 :                  xParInit
                        ;
                        ; Set up the data direction register for port B so tha
                        ; the DIRECTION and PSTROBE bits are output.  Only tou
                        ; the bits we're using; leave the rest as-is.
                        ;
ec67 : ad0317           		lda	PIADDRB	;get current value
ec6a : 29fb             		and	#~ACK
ec6c : 0903             		ora	#DIRECTION | PSTROBE
ec6e : 8d0317           		sta	PIADDRB
                        ;
ec71 : a9ff             		lda	#$ff
ec73 : 8d0117           		sta	PIADDRA	;set data for write
                        ;
                        ; Now that the data is set, set the direction
                        ; registers.  This prevents weird problems like
                        ; driving lines to the wrong state.
                        ;
ec76 : ad0217           		lda	PIAREGB
ec79 : 29f9             		and	#~(PSTROBE | ACK)
ec7b : 0901             		ora	#DIRECTION
ec7d : 8d0217           		sta	PIAREGB
ec80 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   37
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for writing to the Arduino.  Sets up
                        ; direction registers, drives the direction bit, etc.
                        ;
ec81 : a9ff             xParSetWrite    lda	#$ff	;set bits for output
ec83 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to output, clear ACK bit
                        ;
ec86 : ad0217           		lda	PIAREGB
ec89 : 29f9             		and	#~(PSTROBE | ACK)
ec8b : 0901             		ora	#DIRECTION
ec8d : 8d0217           		sta	PIAREGB
ec90 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   38
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for reading from the Arduino.  Sets up
                        ; direction registers, clears the direction bit, etc.
                        ;
ec91 : a900             xParSetRead     lda	#$00	;set bits for input
ec93 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to input, clear ACK bit
                        ;
ec96 : ad0217           		lda	PIAREGB
ec99 : 29f8             		and	#~(DIRECTION | PSTROBE | ACK)
ec9b : 8d0217           		sta	PIAREGB
ec9e : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   39
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This writes a single byte to the Arduino.  On entry,
                        ; the byte to write is in A.  This assumes ParSetWrite
                        ; was already called.
                        ;
                        ; Destroys A, all other registers preserved.
                        ;
                        ; Write cycle:
                        ;
                        ;    1. Wait for other side to lower ACK.
                        ;    2. Put data onto the bus.
                        ;    3. Set DIRECTION and PSTROBE to indicate data
                        ;       is valid and ready to read.
                        ;    4. Wait for ACK line to go high, indicating the
                        ;       other side has read the data.
                        ;    5. Lower PSTROBE.
                        ;    6. Wait for ACK to go low, indicating end of
                        ;       transfer.
                        ;
ec9f : 48               xParWriteByte	pha		;save data
eca0 : ad0217           Parwl22		lda	PIAREGB	;check status
eca3 : 2904             		and	#ACK
eca5 : d0f9             		bne	Parwl22	;wait for ACK to go low
                        ;
                        ; Now put the data onto the bus
                        ;
eca7 : 68               		pla
eca8 : 8d0017           		sta	PIAREGA
                        ;
                        ; Raise the strobe so the Arduino knows there is
                        ; new data.
                        ;
ecab : ad0217           		lda	PIAREGB
ecae : 0902             		ora	#PSTROBE
ecb0 : 8d0217           		sta	PIAREGB
                        ;
                        ; Wait for ACK to go high, indicating the Arduino has
                        ; pulled the data and is ready for more.
                        ;
ecb3 : ad0217           Parwl33		lda	PIAREGB
ecb6 : 2904             		and	#ACK
ecb8 : f0f9             		beq	Parwl33
                        ;
                        ; Now lower the strobe, then wait for the Arduino to
                        ; lower ACK.
                        ;
ecba : ad0217           		lda	PIAREGB
ecbd : 29fd             		and	#~PSTROBE
ecbf : 8d0217           		sta	PIAREGB
ecc2 : ad0217           Parwl44		lda	PIAREGB
ecc5 : 2904             		and	#ACK
ecc7 : d0f9             		bne	Parwl44
ecc9 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   40
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This reads a byte from the Arduino and returns it in
                        ; A.  Assumes ParSetRead was called before.
                        ;
                        ; This does not have a time-out.
                        ;
                        ; Preserves all other registers.
                        ;
                        ; Read cycle:
                        ;
                        ;    1. Wait for other side to raise ACK, indicating
                        ;       data is ready.
                        ;    2. Read data.
                        ;    3. Raise PSTROBE indicating data was read.
                        ;    4. Wait for ACK to go low.
                        ;    5. Lower PSTROBE.
                        ;
ecca : ad0217           xParReadByte    lda	PIAREGB
eccd : 2904             		and	#ACK	;is their strobe high?
eccf : f0f9             		beq	xParReadByte	;nope, no data
                        ;
                        ; Data is available, so grab and save it.
                        ;
ecd1 : ad0017           		lda	PIAREGA
ecd4 : 48               		pha
                        ;
                        ; Now raise our strobe (their ACK), then wait for
                        ; them to lower their strobe.
                        ;
ecd5 : ad0217           		lda	PIAREGB
ecd8 : 0902             		ora	#PSTROBE
ecda : 8d0217           		sta	PIAREGB
ecdd : ad0217           Parrlp1		lda	PIAREGB
ece0 : 2904             		and	#ACK
ece2 : d0f9             		bne	Parrlp1	;still active
                        ;
                        ; Lower our ack, then were done.
                        ;
ece4 : ad0217           		lda	PIAREGB
ece7 : 29fd             		and	#~PSTROBE
ece9 : 8d0217           		sta	PIAREGB
ecec : 68               		pla
eced : 60               		rts
                        
                        		include	"parproto.inc"
                        ;*****************************************************
                        ; Parallel port protocol
                        ;
                        ; This is the header file for making applications
                        ; compliant with The Remote Disk Protocol Guide which
                        ; is on the Corsham Technologies web page somewhere:
                        ;
                        ;    www.corshamtech.com
                        ;
                        ; This was updated 06/13/2015 to be compliant with the
                        ; official specification, so the opcode values changed
                        ;
                        ; Another update on 02/10/2019
                        ;
                        ;=====================================================
                        ; Commands from host to Arduino
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   41
---------------------------------- xKIM.asm ----------------------------------

0001 =                  PC_GET_VERSION	equ	$01
0005 =                  PC_PING		equ	$05	;ping Arduino
0006 =                  PC_LED_CONTROL	equ	$06	;LED control
0007 =                  PC_GET_CLOCK	equ	$07	;get clock data
0008 =                  PC_SET_CLOCK	equ	$08	;set clock
0010 =                  PC_GET_DIR	equ	$10	;Get directory
0011 =                  PC_GET_MOUNTED	equ	$11	;Get mounted drive list
0012 =                  PC_MOUNT	equ	$12	;Mount drive
0013 =                  PC_UNMOUNT	equ	$13	;Unmount drive
0014 =                  PC_GET_STATUS	equ	$14	;Get status for one drive
0015 =                  PC_DONE		equ	$15	;Stop data
0015 =                  PC_ABORT	equ	PC_DONE
0016 =                  PC_READ_FILE	equ	$16	;Read regular file (non-DSK)
0017 =                  PC_READ_BYTES	equ	$17	;Read sequential bytes
0018 =                  PC_RD_SECTOR	equ	$18	;Read FLEX sector
0019 =                  PC_WR_SECTOR	equ	$19	;Write FLEX sector
001a =                  PC_GET_MAX	equ	$1a	;Get maximum drives
001b =                  PC_WRITE_FILE   equ	$1b	;Open file for writing
001c =                  PC_WRITE_BYTES	equ	$1c	;Data to be written
001d =                  PC_SAVE_CONFIG	equ	$1d	;Save SD.CFG with current value
001e =                  PC_SET_TIMER	equ	$1e	;Set RTC timer
001f =                  PC_RD_SEC_LONG	equ	$1f	;Read sector with long sec num
0020 =                  PC_WR_SEC_LONG	equ	$20	;Write sector with long sec num
                        ;
                        ;=====================================================
                        ; Responses from Arduino to host
                        ;
0081 =                  PR_VERSION_INFO	equ	$81	;Contains version information
0082 =                  PR_ACK		equ	$82	;ACK (no additional information)
0083 =                  PR_NAK		equ	$83	;NAK - one status byte follows
0085 =                  PR_PONG		equ	$85	;Reply to a ping
0087 =                  PR_CLOCK_DATA	equ	$87	;Clock data
0090 =                  PR_DIR_ENTRY	equ	$90	;Directory entry
0091 =                  PR_DIR_END	equ	$91	;End of directory entries
0092 =                  PR_FILE_DATA	equ	$92	;File data
0093 =                  PR_STATUS	equ	$93	;Drive status
0094 =                  PR_SECTOR_DATA	equ	$94	;Sector data
0095 =                  PR_MOUNT_INFO	equ	$95	;Mount entry
0096 =                  PR_MAX_DRIVES	equ	$96	;Maximum number of drives
                        ;
                        ;=====================================================
                        ; Error codes for NAK events.  Yes, these are in
                        ; decimal due to an error in the original
                        ; documentation.
                        ;
0000 =                  ERR_NONE	equ	0
000a =                  ERR_NOT_MOUNTED	equ	10
000b =                  ERR_MOUNTED	equ	11
000c =                  ERR_NOT_FOUND	equ	12
000d =                  ERR_READ_ONLY	equ	13
000e =                  ERR_BAD_DRIVE	equ	14
000f =                  ERR_BAD_TRACK	equ	15
0010 =                  ERR_BAD_SECTOR	equ	16
0011 =                  ERR_READ_ERROR	equ	17
0012 =                  ERR_WRITE_ERROR	equ	18
0013 =                  ERR_NOT_PRESENT	equ	19
0014 =                  ERR_NOT_IMPL	equ	20	;Command not implemented
                        
                        
                        		include	"diskfunc.asm"
                        		list
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   42
---------------------------------- xKIM.asm ----------------------------------

                        ; This is a collection of functions for performing
                        ; higher level disk functions.  This hides the nasty
                        ; details of communications with the remote disk
                        ; system.
                        ;
                        ; August 20, 2014 - Bob Applegate
                        ;                   bob@corshamtech.com
                        ;
                        ; 06/14/2015 - Bob Applegate
                        ;		Now that there is an official standard
                        ;		for the protocol between the host (this
                        ;		code) and the DCP (Arduino code), this
                        ;		code has been updated to be compliant.
                        ;
                        ; 01/14/2016 - Bob Applegate
                        ;              Finally converted to 6502.
                        ;
                        ;		include	"parproto.inc"
                        ;
                        ; Number of drives emulated
                        ;
0004 =                  DRIVES		equ	4
                        ;
                        ;=====================================================
                        ; This is a sanity check to verify connectivity to the
                        ; Arduino code is working.  Returns C clear if all is
                        ; good, or C set if not.
                        ;
ecee : 2081ec           DiskPing	jsr	xParSetWrite
ecf1 : a905             		lda	#PC_PING	;command
ecf3 : 209fec           		jsr	xParWriteByte	;send to Arduino
ecf6 : 2091ec           		jsr	xParSetRead
ecf9 : 20caec           		jsr	xParReadByte	;read their reply
ecfc : 2081ec           DiskRetGood	jsr	xParSetWrite
ecff : 18               		clc			;assume it's good
ed00 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   43
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This starts a directory read of the raw drive, not
                        ; the mounted drive.  No input parameters.  This simpl
                        ; sets up for reading the entries, then the user must
                        ; read each entry.
                        ;
                        ; Returns with C clear on success.  If error, C is set
                        ; and A contains the error code.
                        ;
ed01 : 2081ec           DiskDir		jsr	xParSetWrite
ed04 : a910             		lda	#PC_GET_DIR	;send command
ed06 : 209fec           		jsr	xParWriteByte
ed09 : 18               		clc		;assume it works
ed0a : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   44
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Read the next directory entry.  On input, X (MSB)
                        ; and Y (LSB) point to a 13 byte area to receive the
                        ; drive data.
                        ;
                        ; Returns C set if end of directory (ie, attempt to
                        ; read and there are none left).  Else, C is clear
                        ; and X/Y point to the null at end of filename.
                        ;
ed0b : 86fb             DiskDirNext	stx	POINTH
ed0d : 84fa             		sty	POINTL
ed0f : 2091ec           		jsr	xParSetRead	;read results
ed12 : 20caec           		jsr	xParReadByte	;get response code
ed15 : c983             		cmp	#PR_NAK		;error?
ed17 : f01e             		beq	DDNErr
ed19 : c991             		cmp	#PR_DIR_END	;end?
ed1b : f01a             		beq	DDNErr
                        ;
                        ; This contains a directory entry.
                        ;
ed1d : a000             		ldy	#0
ed1f : 20caec           DDNloop		jsr	xParReadByte
ed22 : 91fa             		sta	(POINTL),y
ed24 : c900             		cmp	#0	;end?
ed26 : f006             		beq	DDNEnd
ed28 : 20631f           		jsr	INCPT
ed2b : 4c1fed           		jmp	DDNloop
ed2e : 2081ec           DDNEnd		jsr	xParSetWrite
ed31 : a6fb             		ldx	POINTH
ed33 : a4fa             		ldy	POINTL
ed35 : 18               		clc		;not end of files
ed36 : 60               		rts
                        ;
                        ; Error.  Set C and return.  This is not really
                        ; proper, since this implies a simple end of the
                        ; directory rather than an error.
                        ;
ed37 : 2081ec           DDNErr		jsr	xParSetWrite
ed3a : 38               		sec
ed3b : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   45
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This opens a file on the SD for reading.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error (usually means the
                        ; file does not exist.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed3c : a916             DiskOpenRead	lda	#PC_READ_FILE
ed3e :                  DiskOpen
ed3e : 84f8                     sty	INL	;save ptr to filename
ed40 : 86f9             		stx	INH
ed42 : 209fec           		jsr	xParWriteByte
ed45 : a0ff             		ldy	#-1
ed47 : c8               DiskOpenLoop	iny
ed48 : b1f8             		lda	(INL),y
ed4a : 209fec           		jsr	xParWriteByte
ed4d : b1f8             		lda	(INL),y
ed4f : d0f6             		bne	DiskOpenLoop
ed51 : 2091ec           		jsr	xParSetRead
ed54 : 20caec           		jsr	xParReadByte	;get response
ed57 : c982             		cmp	#PR_ACK
ed59 : d005             		bne	DiskOpenErr
ed5b : 2081ec           		jsr	xParSetWrite	;back to write mode
ed5e : 18               		clc
ed5f : 60               		rts
                        ;
                        ; Got an error.
                        ;
ed60 : 20caec           DiskOpenErr	jsr	xParReadByte	;get error code
ed63 : 2081ec           		jsr	xParSetWrite	;back to write mode
ed66 : 38               		sec
ed67 : 60               		rts
                        ;=====================================================
                        ; This opens a file on the SD for writing.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed68 : a91b             DiskOpenWrite	lda	#PC_WRITE_FILE
ed6a : 4c3eed           		jmp	DiskOpen	;jump into common code
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to read
                        ; from the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to put the data.  On return, C will
                        ; be set if EOF was reached (and no data read), or
                        ; C will be clear and A contains the number of bytes
                        ; actually read into the buffer.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed6d : 84f8             DiskRead	sty	INL	;save ptr to buffer
ed6f : 86f9             		stx	INH
ed71 : 48               		pha
ed72 : a917             		lda	#PC_READ_BYTES
ed74 : 209fec           		jsr	xParWriteByte	;command
ed77 : 68               		pla		;number of bytes to get
AS65 Assembler for R6502 [1.42].                                     Page   46
---------------------------------- xKIM.asm ----------------------------------

ed78 : 209fec           		jsr	xParWriteByte
ed7b : 2091ec           		jsr	xParSetRead	;get ready for response
ed7e : 20caec           		jsr	xParReadByte	;assume PR_FILE_DATA
ed81 : 20caec           		jsr	xParReadByte	;length
ed84 : 48               		pha
ed85 : aa               		tax			;count
ed86 : f011             		beq	DiskReadEof	;zero = EOF
ed88 : a000             		ldy	#0	;offset
ed8a : 20caec           DiskReadLoop	jsr	xParReadByte
ed8d : 91f8             		sta	(INL),y
ed8f : c8               		iny		;next offset
ed90 : ca               		dex
ed91 : d0f7             		bne	DiskReadLoop
ed93 : 2081ec           		jsr	xParSetWrite
ed96 : 68               		pla		;retrieve byte count
ed97 : 18               DiskOk		clc	
ed98 : 60               		rts
ed99 : 2081ec           DiskReadEof	jsr	xParSetWrite
ed9c : 68               		pla
ed9d : 38               		sec
ed9e : 60               		rts
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to write
                        ; to the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to get the data.  On return, C will
                        ; be set if an error was detected, or C will be clear
                        ; if no error.  Note that if A contains 0 on entry,
                        ; no bytes are written.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed9f : c900             DiskWrite	cmp	#0
eda1 : f0f4             		beq	DiskOk
eda3 : 84f8             		sty	INL	;save ptr to filename
eda5 : 86f9             		stx	INH
eda7 : 48               		pha
eda8 : a91c             		lda	#PC_WRITE_BYTES
edaa : 209fec           		jsr	xParWriteByte	;command
edad : 68               		pla		;number of bytes to write
edae : 48               		pha		;save again
edaf : 209fec           		jsr	xParWriteByte
edb2 : 68               		pla
edb3 : aa               		tax			;count
edb4 : a000             		ldy	#0	;offset
edb6 : b1f8             DiskWriteLoop	lda	(INL),y	;get next byte
edb8 : 209fec           		jsr	xParWriteByte
edbb : c8               		iny		;next offset
edbc : ca               		dex
edbd : d0f7             		bne	DiskWriteLoop
edbf : 2091ec           		jsr	xParSetRead ;read the status
edc2 : 20caec           		jsr	xParReadByte
edc5 : c982             		cmp	#PR_ACK
edc7 : f008             		beq	DiskOk1	;all good
edc9 : 20caec           		jsr	xParReadByte	;read error code
edcc : 2081ec           		jsr	xParSetWrite 
edcf : 38               		sec	
edd0 : 60               		rts
                        ;
edd1 : 2081ec           DiskOk1		jsr	xParSetWrite
edd4 : 18               		clc
AS65 Assembler for R6502 [1.42].                                     Page   47
---------------------------------- xKIM.asm ----------------------------------

edd5 : 60               		rts
                        ;
                        ;=====================================================
                        ; Call this to close any open file.  No parameters
                        ; and no return status.
                        ;
edd6 : a915             DiskClose	lda	#PC_DONE
edd8 : 4c9fec           		jmp	xParWriteByte
                        
                        
                        
                        ;
                        ;=====================================================
                        ; Show current clock
                        ;
                        	if	SHOW_RTC
eddb : 2081ec           doShowClock	jsr	xParSetWrite
edde : a907             		lda	#PC_GET_CLOCK
ede0 : 209fec           		jsr	xParWriteByte
ede3 : 2091ec           		jsr	xParSetRead	;prepare to read
                        ;
ede6 : 20caec           		jsr	xParReadByte
                        ;
                        ; Loop to read the raw data
                        ;
ede9 : a200             		ldx	#0
edeb : 8e81df           clockread	stx	saveX
edee : 20caec           		jsr	xParReadByte
edf1 : ae81df           		ldx	saveX
edf4 : 9d9adf           		sta	month,x
edf7 : e8               		inx
edf8 : e008             		cpx	#clock_end-month
edfa : d0ef             		bne	clockread
                        ;
                        ; Set back to write mode to finish up; all apps are
                        ; supposed to leave the SD interface in write mode.
                        ;
edfc : 2081ec           		jsr	xParSetWrite
                        ;
                        ; Now display the data in a user-friendly format.  Eac
                        ; numberic value is in binary, so convert to decimal
                        ; for display.
                        ;
edff : 20c0ea           		jsr	putsil
ee02 : 0d0a             		db	CR,LF
ee04 : 446174653a2000   		db	"Date: ",0
                        ;
ee0b : ad9adf           		lda	month
ee0e : 2054ee           		jsr	outdec
ee11 : a92f             		lda	#'/'
ee13 : 20a01e           		jsr	OUTCH
ee16 : ad9bdf           		lda	day
ee19 : 2054ee           		jsr	outdec
                        ;
                        ; Always force the high part of the year to "/20"
                        ;
ee1c : 20c0ea           		jsr	putsil
ee1f : 2f323000         		db	"/20",0
ee23 : ad9ddf           		lda	year_low
ee26 : 2054ee           		jsr	outdec
                        ;
                        ; Space over, then do the time
AS65 Assembler for R6502 [1.42].                                     Page   48
---------------------------------- xKIM.asm ----------------------------------

                        ;
ee29 : 20c0ea           		jsr	putsil
ee2c : 2c2000           		db	", ",0
                        ;
ee2f : ad9edf           		lda	hour
ee32 : 2054ee           		jsr	outdec
ee35 : a93a             		lda	#':'
ee37 : 20a01e           		jsr	OUTCH
ee3a : ad9fdf           		lda	minute
ee3d : 2054ee           		jsr	outdec
ee40 : a93a             		lda	#':'
ee42 : 20a01e           		jsr	OUTCH
ee45 : ada0df           		lda	second
ee48 : 2054ee           		jsr	outdec
                        ;
ee4b : 20c0ea           		jsr	putsil
ee4e : 0d0a00           		db	CR,LF,0
ee51 : 4c60e0           		jmp	extKim	;return to monitor
                        ;
                        ;=====================================================
                        ; Given a binary value in A, display it as two decimal
                        ; digits.  The input can't be greater than 99.  Always
                        ; print a leading zero if less than 10.
                        ;
ee54 : a000             outdec		ldy	#0	;counts 10s
ee56 : c90a             out1		cmp	#10
ee58 : 9007             		bcc	out2	;below 10
ee5a : c8               		iny		;count 10
ee5b : 38               		sec
ee5c : e90a             		sbc	#10
ee5e : 4c56ee           		jmp	out1
                        ;
ee61 : 48               out2		pha		;save ones
ee62 : 98               		tya		;get tens
ee63 : 2067ee           		jsr	out3	;print tens digit
ee66 : 68               		pla		;restore ones
                        ;
ee67 : 0930             out3		ora	#'0'
ee69 : 20a01e           		jsr	OUTCH
ee6c : 60               		rts
                        
                        	endif
                        ;
                        	if	TINY_BASIC
                        		include "mytb.asm"
                        	endif
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   49
---------------------------------- xKIM.asm ----------------------------------

                        		
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory to
                        ; an IEC device.
                        ;
ee6d : 4c2fe1           lExit111		jmp	extKimLoop
ee70 : 204beb           saveIEC		jsr	getAddrRange	;get range to dump
ee73 : b0f8             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;		
ee75 : 20c0ea           		jsr	putsil
ee78 : 0d0a             		db	CR,LF
ee7a : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
ee93 : 20efea           		jsr	getHex
ee96 : b0d5             		bcs	lExit111		;abort on error
ee98 : 8df917           		sta	IECFID
                        		
ee9b : 20dcee           		jsr savevec
                        		
ee9e : 2048f1           		jsr IECSAV
                        		
eea1 : 201def           		jsr loadvec
                        		
eea4 : 4c2fe1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to load a region of memory from
                        ; an IEC device.
                        ;
eea7 : 4c2fe1           lExit1111		jmp	extKimLoop
eeaa :                  loadIEC		
                        ; Get the file id to save to
                        ;		
eeaa : 20c0ea           		jsr	putsil
eead : 0d0a             		db	CR,LF
eeaf : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
eec8 : 20efea           		jsr	getHex
eecb : b0da             		bcs	lExit1111		;abort on error
eecd : 8df917           		sta	IECFID
                        		
eed0 : 20dcee           		jsr savevec
                        		
eed3 : 2000f0           		jsr IECLOD
                        
eed6 : 201def           		jsr loadvec
                        		
eed9 : 4c2fe1           		jmp	extKimLoop
                        
eedc :                  savevec 
eedc : 8c82df           		sty saveY
eedf : acfa17           		ldy NMIL
eee2 : 8c83df           		sty saveNMIL
eee5 : acfb17           		ldy NMIH
eee8 : 8c84df           		sty saveNMIH
eeeb : acfc17           		ldy RSTL
eeee : 8c85df           		sty saveRSTL
eef1 : acfd17           		ldy RSTH
eef4 : 8c86df           		sty saveRSTH
eef7 : acfe17           		ldy IRQL
eefa : 8c87df           		sty saveIRQL
AS65 Assembler for R6502 [1.42].                                     Page   50
---------------------------------- xKIM.asm ----------------------------------

eefd : acff17           		ldy IRQH
ef00 : 8c88df           		sty saveIRQH
ef03 : ac82df           		ldy saveY
                        		
ef06 : a900             		lda #$00
ef08 : 8dfa17           		sta NMIL
ef0b : 8dfc17           		sta RSTL
ef0e : 8dfe17           		sta IRQL
ef11 : a9e0             		lda #$E0
ef13 : 8dfb17           		sta NMIH
ef16 : 8dfd17           		sta RSTH
ef19 : 8dff17           		sta IRQH
                        		
ef1c : 60               		rts
                        		
ef1d :                  loadvec
ef1d : 8c82df           		sty saveY
ef20 : ac83df           		ldy saveNMIL
ef23 : 8cfa17           		sty NMIL
ef26 : ac84df           		ldy saveNMIH
ef29 : 8cfb17           		sty NMIH
ef2c : ac85df           		ldy saveRSTL
ef2f : 8cfc17           		sty RSTL
ef32 : ac86df           		ldy saveRSTH
ef35 : 8cfd17           		sty RSTH
ef38 : ac87df           		ldy saveIRQL
ef3b : 8cfe17           		sty IRQL
ef3e : ac88df           		ldy saveIRQH
ef41 : 8cff17           		sty IRQH
ef44 : ac82df           		ldy saveY
                        		
ef47 : 60               		rts
                        
                        ;
                        ;=====================================================
                        ; These are the 6502 vectors.  On the Corsham
                        ; Technologies RAM/ROM board the user can select
                        ; whether or not to use these based on a switch
                        ; setting on the board.
                        ;
fffa =                  		org	$fffa
fffa : 1c1c             NMI		dw	NMIT	;in KIM
fffc : 221c             RESET		dw	RST	;in KIM
fffe : 1f1c             IRQ		dw	IRQT	;in KIM
                        ;
                        		end
                        
                        
No errors in pass 2.
Wrote binary from address $e000 through $ffff.
Total size 8192 bytes.
