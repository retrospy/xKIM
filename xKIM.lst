AS65 Assembler for R6502 [1.42].                                     Page    1
---------------------------------- xKIM.asm ----------------------------------

2639 lines read, no errors in pass 1.
                        ;*****************************************************
                        ; Extended KIM monitor, written 2015 by Bob Applegate
                        ; K2UT - bob@corshamtech.com
                        ;
                        ; This code was written as part of the Corsham Tech
                        ; 60K RAM/EPROM board.  It provides a working area for
                        ; an extended version of the KIM-1 monitor capabilitie
                        ; mostly based on a TTY interface, not the keypad and
                        ; hex display.
                        ;
                        ; This extensions contains an assortment of basic
                        ; tools that are missing from the KIM-1's built in
                        ; console monitor.  It has commands for loading hex
                        ; files (as opposed to KIM format), accessing an SD
                        ; card system, memory edit, hex dump, etc.  It also
                        ; has the ability for user-defined extensions to be
                        ; added.  And a number of common entry points have
                        ; vectors so the monitor can be modified without
                        ; breaking programs that use it.
                        ;
                        ; I'm not claiming copyright; I wrote most of this,
                        ; but also borrowed from others (credit is given in
                        ; those sections of code).  All of the portions I've
                        ; written are free to use, but please keep my name
                        ; in the comments somewhere.  Even better, consider
                        ; buying a board from us: www.corshamtech.com
                        ;
                        ; 12/01/2015	Bob Applegate
                        ;		Initial development - V0.X
                        ; 03/15/2016	Bob Applegate
                        ;		v1.0 - First official release
                        ; 01/03/2017	Bob Applegate
                        ;		v1.1 - Added S command
                        ; 09/20/2018	Bob Applegate
                        ;		v1.2 - Added auto-run vector
                        ; 01/25/2019	Bob Applegate
                        ;		v1.3 - Added 'X' command.
                        ;		Added the 'C' command to get time.
                        ; 03/09/2019	Bob Applegate
                        ;		v1.4 - Added 'O' command.
                        ; 07/26/2020	Bob Applegate
                        ;		v1.5 - Fixed bug that caused the S
                        ;		command to create empty file with no
                        ;		contents.
                        ;		Minor typo fixes.
                        ;		Added CLD instructions.
                        ; 11/14/2020	Bob Applegate
                        ;		v1.6 - On SD error, display reason code.
                        ; 09/15/2021	Bob Applegate
                        ;		v1.7
                        ;		Added offset calculator command O.
                        ;		Added R command in Edit mode.
                        ;		Removed '.' when loading from console.
                        ; 09/20/2021	Bob Applegate
                        ;		v1.8
                        ;		Made a lot of the command handlers
                        ;		into subroutines and added vectors so
                        ;		external programs can call them.
                        ;		Fixed bugs in Edit mode.
                        ; 12/06/2023	Christopher 'Zoggins' Mallery
                        ;		v1.9
AS65 Assembler for R6502 [1.42].                                     Page    2
---------------------------------- xKIM.asm ----------------------------------

                        ;		Added IEC Save and Load commands
                        ;
                        ;*****************************************************
                        ;
                        ; Useful constants
                        ;
0000 =                  false		equ	0
ffff =                  true		equ	~false
                        ;
                        ; Version number
                        ;
0001 =                  VERSION		equ	1
0009 =                  REVISION	equ	9
0000 =                  BETA_VER	equ	0
                        ;
                        ; Options.  If RAM_BASED is set then the code is put
                        ; into RAM, else it's in ROM.  Very handy for testing
                        ; new code; load the new code with the EPROM version
                        ; and then test away.
                        ;
0000 =                  RAM_BASED	equ	false
af80 =                  RAM_DATA_BASE	equ	$af80
b000 =                  RAM_CODE_BASE	equ	$b000
                        ;
                        ; Set the EPROM base addresses
                        ;
e000 =                  ROM_START	equ	$e000
df80 =                  RAM_START	equ	$df80
                        ;
                        ; This is something that needs more investigation.
                        ; If the SD system isn't reset enough then bad stuff
                        ; happens after the RS button is pressed.  Set this to
                        ; true to only reset the SD system on cold starts.
                        ;
0000 =                  SD_ONLY_COLD	equ	false
                        ;
                        ; Set this to true to include Bob's Tiny BASIC.  It's
                        ; a minimal (ie, tiny) BASIC interpreter good for
                        ; playing around.
                        ;
0000 =                  TINY_BASIC	equ	false
                        ;
                        ; Set this to true to include a command to get/display
                        ; the current time from Corsham Tech SD card.
                        ;
ffff =                  SHOW_RTC	equ	true
                        ;
                        ; Non-printable ASCII constants
                        ;
0000 =                  NUL		equ	$00
0008 =                  BS		equ	$08
000a =                  LF		equ	$0a
000d =                  CR		equ	$0d
001b =                  ESC		equ	$1b
0020 =                  SPC		equ	$20
                        ;
                        ; Intel HEX record types
                        ;
0000 =                  DATA_RECORD	equ	$00
0001 =                  EOF_RECORD	equ	$01
                        ;
                        ; These are various buffer sizes
AS65 Assembler for R6502 [1.42].                                     Page    3
---------------------------------- xKIM.asm ----------------------------------

                        ;
000c =                  FILENAME_SIZE	equ	12
0040 =                  BUFFER_SIZE	equ	64
                        ;
                        ; Max number of bytes per line for hex dump
                        ;
0010 =                  BYTESLINE	equ	16
                        ;
                        ; Flag values used to detect a cold start vs warm
                        ;
0019 =                  COLD_FLAG_1	equ	$19
0062 =                  COLD_FLAG_2	equ	$62
                        ;
                        ;=====================================================
                        ; This macro is used to verify that the current addres
                        ; meets a required value.  Used mostly to guarantee
                        ; changes don't cause entry points to move.
                        ;
                        VERIFY		macro	expected
                        		if * != expected
                        		fail	Not at requested address (expected)
                        		endif
                        		endm
                        ;
                        ;=====================================================
                        ; KIM memory locations
                        ;
                        		bss
00ef =                  		org	$00ef
00ef =                  PCL		ds	1
00f0 =                  PCH		ds	1
00f1 =                  PREG		ds	1
00f2 =                  SPUSER		ds	1
00f3 =                  ACC		ds	1
00f4 =                  YREG		ds	1
00f5 =                  XREG		ds	1
00f6 =                  CHKHI		ds	1
00f7 =                  CHKSUM		ds	1
00f8 =                  INL	    	ds	1
00f9 =                  INH		ds	1
00fa =                  POINTL		ds	1
00fb =                  POINTH		ds	1
00fc =                  TEMP		ds	1
00fd =                  TMPX		ds	1
00fe =                  CHAR		ds	1
00ff =                  MODE		ds	1
                        ;
17e7 =                  		org	$17e7
17e7 =                  CHKL		ds	1
17e8 =                  CHKH		ds	1
17e9 =                  SAVX		ds	3
17ec =                  VEB	    	ds	6
17f2 =                  CNTL30		ds	1
17f3 =                  CNTH30		ds	1
17f4 =                  TIMH		ds	1
17f5 =                  SAL		ds	1
17f6 =                  SAH	    	ds	1
17f7 =                  EAL		ds	1
17f8 =                  EAH	    	ds	1
17f9 =                  ID  		ds	1
                        ;
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    4
---------------------------------- xKIM.asm ----------------------------------

                        ; KIM I/O locations
                        ;
1740 =                  SAD		equ	$1740
1741 =                  PADD		equ	$1741
                        ;
                        ;=====================================================
                        ; KIM subroutines located in the ROMs
                        ;
1c1c =                  NMIT		equ	$1c1c	;NMI handler
1c1f =                  IRQT		equ	$1c1f	;IRQ handler
1c22 =                  RST	    	equ	$1c22	;RESET handler
1c77 =                  TTYKB		equ	$1c77	;do keyboard monitor
1e2f =                  CRLF		equ	$1e2f	;print CR/LF
1e1e =                  PRTPNT		equ	$1e1e	;print POINT
1e3b =                  PRTBYT		equ	$1e3b	;print A as two hex digits
1e5a =                  GETCH		equ	$1e5a	;get a key from tty into A
1e9e =                  OUTSP		equ	$1e9e	;print a space
1ea0 =                  OUTCH		equ	$1ea0	;print A to TTY
1dac =                  SHOW		equ	$1dac
1daf =                  SHOW1		equ	$1daf
1f63 =                  INCPT		equ	$1f63	;inc POINTL/POINTH
                        ;
                        ;=====================================================
                        ; KIM IEC save and load memory locations
                        ;
17f5 =                  IECSAL		equ $17F5
17f6 =                  IECSAH		equ $17F6
17f7 =                  IECEAL		equ $17F7
17f8 =                  IECEAH		equ $17F8
17f9 =                  IECFID		equ $17F9
f148 =                  IECSAV		equ $F148
f000 =                  IECLOD		equ $F000
                        ;
                        ;=====================================================
                        ; I assume the RAM goes from 2000 to DFFF, so carve ou
                        ; a bit for use by the monitor.
                        ;
                        		bss		;uninitialized data area
                        	if	RAM_BASED
                        		org     RAM_DATA_BASE
                        	else
df80 =                  		org	RAM_START
                        	endif
df80 =                  LowestAddress	equ	*
                        ;
                        ; Storage for registers for some monitor calls.
                        ;
df80 =                  saveA		ds	1
df81 =                  saveX		ds	1
df82 =                  saveY		ds	1
                        ;
                        ; Pointer to the subroutine that gets the next input
                        ; character.  Used for doing disk/console input.
                        ;
df83 =                  inputVector	ds	2
                        ;
                        ; Same thing for output.
                        ;
df85 =                  outputVector	ds	2
                        ;
df87 =                  filename	ds	FILENAME_SIZE+1
df94 =                  buffer		ds	BUFFER_SIZE
AS65 Assembler for R6502 [1.42].                                     Page    5
---------------------------------- xKIM.asm ----------------------------------

dfd4 =                  diskBufOffset	ds	1
dfd5 =                  diskBufLength	ds	1
                        ;
dfd6 =                  Temp16L		ds	1
dfd7 =                  Temp16H		ds	1
df81 =                  byteCount	equ	saveX
                        ;
                        ; The clock functions need some storage, but just
                        ; overlay the names onto the buffer.
                        ;
df94 =                  month		equ	buffer
df95 =                  day		equ	month+1
df96 =                  year_high	equ	day+1
df97 =                  year_low	equ	year_high+1
df98 =                  hour		equ	year_low+1
df99 =                  minute		equ	hour+1
df9a =                  second		equ	minute+1
df9b =                  day_of_week	equ	second+1
df9c =                  clock_end	equ	day_of_week+1
                        ;
                        ; The next group of memory are public and can be
                        ; used by user programs, so don't modify where any
                        ; of these are.  If you want to add new public data,
                        ; put them here, always before existing items.  Be
                        ; sure to adjust the ORG.
                        ;
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE-8
                        	else
dff8 =                  		org	ROM_START-8
                        	endif
                        ;
                        ; Before loading a hex file, the MSB of this vector
                        ; is set to FF.  After loading the file, if the MSB
                        ; is no longer FF then the address in this vector is
                        ; jumped to.  Ie, it can auto-run a file.
                        ;
dff8 =                  AutoRun		ds	2
                        ;
                        ; ColdFlag is used to determine if the extended
                        ; monitoring is doing a warm or cold start.
                        ;
dffa =                  ColdFlag	ds	2
                        ;
                        ; Address of a command table for user-created
                        ; extensions to the monitor.
                        ;
dffc =                  ExtensionAddr	ds	2
                        ;
                        ; This is the higest location in RAM usable by user
                        ; programs.  Nobody should go past this address.  If
                        ; you are writing extentions to the monitor, it's
                        ; okay to load before the address and then adjust
                        ; this down to keep others from stomping on your
                        ; extention.
                        ;
                        ; If your program modifies this value, it needs to
                        ; set it back before terminating.
                        ;
dffe =                  HighestAddress	ds	2
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    6
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Code starts at E000 and goes until FFFF, except for
                        ; the 6502 vectors at the end of memory.
                        ;
                        		code
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE
                        	else
e000 =                  		org	ROM_START
                        	endif
                        ;
                        ; Vector table of useful and fun stuff!  These must
                        ; not change or else existing code won't be calling
                        ; the right functions.
                        ;
e000 =                  BASE		equ	*
e000 : 4c60e0           reentry		jmp	extKim	;extended monitor
e003 : 4ca01e           		jmp	OUTCH	;output A to console
e006 : 4c5a1e           		jmp	GETCH	;get a key and echo
e009 : 4c5a1e           		jmp	GETCH	;no echo - KIM can't do it
e00c : 4c5ae0           		jmp	dummyRet	;future - console stat
e00f : 4cbdea           		jmp	putsil	;print string after JSR
e012 : 4cecea           		jmp	getHex	;get hex value in A
e015 : 4c3b1e           		jmp	PRTBYT	;print A as hex
e018 : 4c24eb           		jmp	getStartAddr
e01b : 4c36eb           		jmp	getEndAddr
e01e : 4c48eb           		jmp	getAddrRange
e021 : 4c3ce4           		jmp	doHexDump	;perform a hex dump
e024 : 4cd7e5           		jmp	doEdit		;edit memory
e027 : 4cf5e6           		jmp	loadHexConsole	;load hex file via console
e02a : 4cc6e6           		jmp	loadHexFile	;load hex file from SD
e02d : 4c87e9           		jmp	doSDDiskDir	;do a disk directory
e030 : 4c59eb           		jmp	ComputeOffset	;compute relative offset
                        ;
                        ; SD card functions
                        ;
                        		VERIFY	BASE+$0033
                        
e033 : 4c64ec           		jmp	xParInit	;initialization
e036 : 4c7eec           		jmp	xParSetWrite
e039 : 4c8eec           		jmp	xParSetRead
e03c : 4c9cec           		jmp	xParWriteByte
e03f : 4cc7ec           		jmp	xParReadByte
e042 : 4cebec           		jmp	DiskPing
e045 : 4cfeec           		jmp	DiskDir
e048 : 4c08ed           		jmp	DiskDirNext
e04b : 4c39ed           		jmp	DiskOpenRead
e04e : 4c6aed           		jmp	DiskRead
e051 : 4cd3ed           		jmp	DiskClose
e054 : 4c65ed           		jmp	DiskOpenWrite
e057 : 4c9ced           		jmp	DiskWrite
                        ;
                        ; Even more vectors as I ran out of the reserve area!
                        ;
                        
                        ;
                        ;=====================================================
                        ;=====================================================
                        ; Anything past this point can be moved in future
                        ; releases so none of these functions nor data should
                        ; be directly accessed by user programs!!!
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    7
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is a dummy function for unimplemented functions
                        ; that basically just clears carry and returns.
                        ;
e05a : 18               dummyRet	clc
e05b : 60               		rts
                        ;
e05c : 00               defaultExt	db	0
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    8
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is the start of the extended KIM monitor.
                        ;
e05d : 4c771c           notty		jmp	TTYKB
e060 : a2ff             extKim		ldx	#$ff
e062 : 9a               		txs
e063 : a901             		lda	#$01	;see if in tty mode
e065 : 2c4017           		bit	SAD
e068 : d0f3             		bne	notty	;branch if in keyboard mode
                        
                        	if	~SD_ONLY_COLD
e06a : 2064ec           		jsr	xParInit
                        	endif
                        ;
                        ; Determine if this is a cold or warm start
                        ;
e06d : adfadf           		lda	ColdFlag
e070 : c919             		cmp	#COLD_FLAG_1
e072 : d00a             		bne	coldStart
e074 : adfbdf           		lda	ColdFlag+1
e077 : c962             		cmp	#COLD_FLAG_2
e079 : d003             		bne	coldStart
e07b : 4c2ce1           		jmp	extKimLoop	;it's a warm start
                        ;
                        ; Cold start
                        ;
e07e : a919             coldStart	lda	#COLD_FLAG_1	;indicate we've done cold
e080 : 8dfadf           		sta	ColdFlag
e083 : a962             		lda	#COLD_FLAG_2
e085 : 8dfbdf           		sta	ColdFlag+1
                        ;
                        ; Point to an empty extension set by default.
                        ;
e088 : a95c             		lda	#defaultExt&$ff	;set extension pointers
e08a : 8dfcdf           		sta	ExtensionAddr
e08d : a9e0             		lda	#defaultExt/256
e08f : 8dfddf           		sta	ExtensionAddr+1
                        ;
                        ; Set HighestAddress to just before our RAM area.
                        ;
e092 : a97f             		lda	#(LowestAddress-1)&$ff
e094 : 8dfedf           		sta	HighestAddress
e097 : a9df             		lda	#(LowestAddress-1)/256
e099 : 8dffdf           		sta	HighestAddress+1
                        	if	SD_ONLY_COLD
                        ;
                        ; Initialize the interface to the SD card.  There seem
                        ; be a problem if this is done too often, so do it jus
                        ; and be done.
                        ;
                        		jsr	xParInit
                        	endif
                        ;
                        ; Display our welcome text
                        ;
e09c : 20bdea           		jsr	putsil
e09f : 0d0a0d0a         		db	CR,LF,CR,LF
e0a3 : 457874656e6465.. 		db	"Extended KIM Monitor v"
e0b9 : 312e3920         		db	VERSION+'0','.',REVISION+'0',' '
                        	if	BETA_VER
                        		db	"BETA "
                        		db	BETA_VER+'0'
AS65 Assembler for R6502 [1.42].                                     Page    9
---------------------------------- xKIM.asm ----------------------------------

                        		db	' '
                        	endif
e0bd : 62792052657472.. 		db  "by RetroSpy Technologies"
e0d5 : 0d0a             		db	CR,LF				
e0d7 : 726574726f2d73.. 		db  "retro-spy.com"
e0e4 : 0d0a             		db	CR,LF
e0e6 : 4261736564206f.. 		db	"Based on the work of Corsham Technologies, LLC"
e114 : 0d0a             		db	CR,LF
e116 : 7777772e636f72.. 		db	"www.corshamtech.com"
e129 : 0d0a             		db	CR,LF
                        	if	RAM_BASED
                        		db	CR,LF
                        		db	"*** RAM BASED VERSION ***"
                        		db	CR,LF
                        	endif
e12b : 00               		db	0
                        ;
                        ; Main command loop.  Put out prompt, get command, etc
                        ; Prints a slightly different prompt for the RAM versi
                        ;
e12c : d8               extKimLoop	cld
e12d : 20e3eb           		jsr	setInputConsole
e130 : 20bdea           		jsr	putsil	;output prompt
e133 : 0d0a             		db	CR,LF	;feel free to change it
                        	if	RAM_BASED
                        		db	"RAM"
                        	endif
e135 : 3e00             		db	">",0
e137 : 205a1e           		jsr	GETCH
e13a : c90d             		cmp	#CR
e13c : f0ee             		beq	extKimLoop
e13e : c90a             		cmp	#LF
e140 : f0ea             		beq	extKimLoop
e142 : 85f3             		sta	ACC	;save key
                        ;
                        ; Now cycle through the list of commands looking for
                        ; what the user just pressed.
                        ;
e144 : a96a             		lda	#commandTable&$ff
e146 : 85fa             		sta	POINTL
e148 : a9e1             		lda	#commandTable/256
e14a : 85fb             		sta	POINTH
e14c : 206bea           		jsr	searchCmd	;try to find it
                        ;
                        ; Hmmm... wasn't one of the built in commands, so
                        ; see if it's an extended command.
                        ;
e14f : adfcdf           		lda	ExtensionAddr
e152 : 85fa             		sta	POINTL
e154 : adfddf           		lda	ExtensionAddr+1
e157 : 85fb             		sta	POINTH
e159 : 206bea           		jsr	searchCmd
                        ;
                        ; If that returns, then the command was not found.
                        ; Print that it's unknown.
                        ;
e15c : 20bdea           		jsr	putsil
e15f : 202d204875683f00 		db	" - Huh?",0
e167 : 4c2ce1           cmdFound	jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Vector table of commands.  Each entry consists of a
AS65 Assembler for R6502 [1.42].                                     Page   10
---------------------------------- xKIM.asm ----------------------------------

                        ; single ASCII character (the command), a pointer to
                        ; the function which handles the command, and a pointe
                        ; to a string that describes the command.
                        ;
e16a : 3f               commandTable	db	'?'
e16b : d5e3             		dw	showHelp
e16d : c0e1             		dw	quesDesc
                        ;
                        	if	TINY_BASIC
                        		db	'B'
                        		dw	TBasicCold
                        		dw	bDesc
                        	endif
                        ;
                        	if	SHOW_RTC
e16f : 43               		db	'C'
e170 : d8ed             		dw	doShowClock
e172 : dde1             		dw	cDesc
                        	endif
e174 : 44               		db	'D'
e175 : 6ae9             		dw	doDiskDir
e177 : f6e1             		dw	dDesc
                        ;
e179 : 45               		db	'E'	;edit memory
e17a : bce5             		dw	editMemory
e17c : 13e2             		dw	eDesc
                        ;
e17e : 48               		db	'H'	;hex dump
e17f : 2ee4             		dw	hexDump
e181 : 2de2             		dw	hDesc
                        ;
e183 : 4a               		db	'J'	;jump to address
e184 : 3ae9             		dw	jumpAddress
e186 : 4be2             		dw	jDesc
                        ;
e188 : 4b               		db	'K'	;return to KIM monitor
e189 : a9e3             		dw	returnKim
e18b : 69e2             		dw	kDesc
                        ;
e18d : 4c               		db	'L'	;load Intel HEX file
e18e : 6ae6             		dw	loadHex
e190 : 89e2             		dw	lDesc
                        ;
e192 : 4d               		db	'M'	;perform memory test
e193 : c1e4             		dw	memTest
e195 : a5e2             		dw	mDesc
                        ;
e197 : 4f               		db	'O'	;branch offset calculator
e198 : fbe9             		dw	offCalc
e19a : bfe2             		dw	oDesc
                        ;
e19c : 50               		db	'P'	;ping remote disk
e19d : 4be9             		dw	pingDisk
e19f : e5e2             		dw	pDesc
                        ;
e1a1 : 53               		db	'S'	;save memory as hex file
e1a2 : 01e8             		dw	saveHex
e1a4 : 08e3             		dw	sDesc
                        ;
e1a6 : 54               		db	'T'	;type a file on SD
e1a7 : a6e9             		dw	typeFile
e1a9 : 2ae3             		dw	tDesc
AS65 Assembler for R6502 [1.42].                                     Page   11
---------------------------------- xKIM.asm ----------------------------------

                        ;
e1ab : 58               		db	'X'	;return to KIM monitor
e1ac : a9e3             		dw	returnKim
e1ae : 69e2             		dw	kDesc
                        ;
e1b0 : 59               		db	'Y'	;save memory to IEC
e1b1 : 6dee             		dw	saveIEC
e1b3 : 69e2             		dw	kDesc
                        ;
e1b5 : 5a               		db	'Z'	;load memory from IEC
e1b6 : b6ee             		dw	loadIEC
e1b8 : 69e2             		dw	kDesc
                        ;
e1ba : 21               		db	'!'	;do cold restart
e1bb : cfe3             		dw	doCold
e1bd : 8be3             		dw	bangDesc
                        ;
e1bf : 00               		db	0	;marks end of table
                        ;
                        ;=====================================================
                        ; Descriptions for each command in the command table.
                        ; This wastes a lot of space... I'm open for any
                        ; suggestions to keep the commands clear but reducing
                        ; the amount of space this table consumes.
                        ;
e1c0 : 3f202e2e2e2e2e.. quesDesc	db	"? ........... Show this help",0
                        	if	TINY_BASIC
                        bDesc		db	"B ........... Bob's Tiny BASIC",0
                        	endif
                        	if	SHOW_RTC
e1dd : 43202e2e2e2e2e.. cDesc		db	"C ........... Show clock",0
                        	endif
e1f6 : 44202e2e2e2e2e.. dDesc		db	"D ........... Disk directory",0
e213 : 45207878787820.. eDesc		db	"E xxxx ...... Edit memory",0
e22d : 48207878787820.. hDesc		db	"H xxxx xxxx . Hex dump memory",0
e24b : 4a207878787820.. jDesc		db	"J xxxx ...... Jump to address",0
e269 : 4b202e2e2e2e2e.. kDesc		db	"K ........... Go to KIM monitor",0
e289 : 4c202e2e2e2e2e.. lDesc		db	"L ........... Load HEX file",0
e2a5 : 4d207878787820.. mDesc		db	"M xxxx xxxx . Memory test",0
e2bf : 4f207878787820.. oDesc		db	"O xxxx xxxx . Calculate branch offset",0
e2e5 : 50202e2e2e2e2e.. pDesc		db	"P ........... Ping disk controller",0
e308 : 53207878787820.. sDesc		db	"S xxxx xxxx . Save memory to file",0
e32a : 54202e2e2e2e2e.. tDesc		db	"T ........... Type disk file",0
e347 : 5920787878782d.. yDesc		db  "Y xxxx-xxxx . Save memory to IEC",0
e368 : 5a202e2e2e2e2e.. zDesc		db 	"Z ........... Load memory from IEC",0
e38b : 21202e2e2e2e2e.. bangDesc	db	"! ........... Do a cold start",0
                        ;
                        ;=====================================================
                        ; Return to KIM monitor.  Before returning, set the
                        ; "open address" to the start of the extended monitor
                        ; so the KIM monitor is pointing to it by default.
                        ;
e3a9 : 20bdea           returnKim	jsr	putsil
e3ac : 0d0a             		db	CR,LF
e3ae : 52657475726e69.. 		db	"Returning to KIM..."
e3c1 : 0d0a00           		db	CR,LF,0
e3c4 : a900             		lda	#reentry&$0ff
e3c6 : 85fa             		sta	POINTL	;point back to start...
e3c8 : a9e0             		lda	#reentry/256
e3ca : 85fb             		sta	POINTH	;...of this code
e3cc : 4caf1d           		jmp	SHOW1	;return to KIM
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   12
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Force a cold start.
                        ;
e3cf : eefadf           doCold		inc	ColdFlag	;foul up flag
e3d2 : 4c60e0           		jmp	extKim		;...and restart
                        ;
                        ;=====================================================
                        ; Command handler for the ? command
                        ;
e3d5 : 20bdea           showHelp	jsr	putsil
e3d8 : 0d0a             		db	CR,LF
e3da : 417661696c6162.. 		db	"Available commands:"
e3ed : 0d0a0a00         		db	CR,LF,LF,0
                        ;
                        ; Print help for built-in commands...
                        ;
e3f1 : a96a             		lda	#commandTable&$ff
e3f3 : 85fa             		sta	POINTL
e3f5 : a9e1             		lda	#commandTable/256
e3f7 : 85fb             		sta	POINTH
e3f9 : 208eea           		jsr	displayHelp	;display help
                        ;
                        ; Now print help for the extension commands...
                        ;
e3fc : adfcdf           		lda	ExtensionAddr
e3ff : 85fa             		sta	POINTL
e401 : adfddf           		lda	ExtensionAddr+1
e404 : 85fb             		sta	POINTH
e406 : 208eea           		jsr	displayHelp
e409 : 202f1e           		jsr	CRLF
e40c : 4c2ce1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This is a generic "not done yet" holder.  Any
                        ; unimplemented commands should point here.
                        ;
e40f : 20bdea           NDY		jsr	putsil
e412 : 0d0a             		db	CR,LF
e414 : 536f7272792c20.. 		db	"Sorry, not done yet."
e428 : 0d0a00           		db	CR,LF,0
e42b : 4c2ce1           NDYdone		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a hex dump of a region of memory.  This code was
                        ; taken from MICRO issue 5, from an article by
                        ; J.C. Williams.  I changed it a bit, but it's still
                        ; basically the same code.
                        ;
                        ; Slight bug: the starting address is rounded down to
                        ; a multiple of 16.  I'll fix it eventually.
                        ;
e42e : 2048eb           hexDump		jsr	getAddrRange
e431 : b0f8             		bcs	NDYdone
e433 : 202f1e           		jsr	CRLF
e436 : 203ce4           		jsr	doHexDump	;subroutine does it
e439 : 4c2ce1           cmdRet2		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine does a hex dump from the address in
                        ; SAL/H to EAL/H.
                        ;
                        ; Move start address to POINT but rounded down to the
AS65 Assembler for R6502 [1.42].                                     Page   13
---------------------------------- xKIM.asm ----------------------------------

                        ; 16 byte boundary.
                        ;
e43c : adf617           doHexDump	lda	SAH
e43f : 85fb             		sta	POINTH
e441 : adf517           		lda	SAL
e444 : 29f0             		and	#$f0	;force to 16 byte
e446 : 85fa             		sta	POINTL
                        ;
                        ; This starts each line.  Set flag to indcate we're
                        ; doing the hex portion, print address, etc.
                        ;
e448 : a900             hexdump1	lda	#0	;set flag to hex mode
e44a : 8df917           		sta	ID
e44d : 202f1e           		jsr	CRLF
e450 : 201e1e           		jsr	PRTPNT	;print the address
e453 : a5fa             hexdump2	lda	POINTL	;push start of line...
e455 : 48               		pha		;...address onto stack
e456 : a5fb             		lda	POINTH
e458 : 48               		pha
e459 : 20b7ea           		jsr	space2
e45c : a20f             		ldx	#BYTESLINE-1	;number of bytes per line
e45e : 20b7ea           		jsr	space2	;space before data
                        
e461 : a000             hexdump3	ldy	#0	;get next byte...
e463 : b1fa             		lda	(POINTL),y
e465 : 2cf917           		bit	ID	;hex or ASCII mode?
e468 : 1010             		bpl	hexptbt	;branch if hex mode
                        ;
                        ; Print char if printable, else print a dot
                        ;
e46a : c920             		cmp	#' '
e46c : 9004             		bcc	hexdot
e46e : c97e             		cmp	#'~'
e470 : 9002             		bcc	hexpr
e472 : a92e             hexdot		lda	#'.'
e474 : 20a01e           hexpr		jsr	OUTCH
e477 : 4c80e4           		jmp	hexend
                        ;
                        ; Print character as hex.  
                        ;
e47a : 203b1e           hexptbt 	jsr	PRTBYT	;print as hex
e47d : 20baea           		jsr	space	;and follow with a space
                        ;
                        ; See if we just dumped the last address.  If not, the
                        ; increment to the next address and continue.
                        ;
e480 : a5fa             hexend  	lda	POINTL	;compare first
e482 : cdf717           		cmp	EAL
e485 : a5fb             		lda	POINTH
e487 : edf817           		sbc	EAH
                        ;
                        ; Now increment to the next address
                        ;
e48a : 08               		php
e48b : 20631f           		jsr	INCPT
e48e : 28               		plp
e48f : 901a             		bcc	hexlntst
                        ;
e491 : 2cf917           		bit	ID
e494 : 3022             		bmi	hexdone
e496 : ca               		dex
e497 : 3006             		bmi	hexdomap
AS65 Assembler for R6502 [1.42].                                     Page   14
---------------------------------- xKIM.asm ----------------------------------

e499 : 20b4ea           hexdump5	jsr	space3
e49c : ca               		dex
e49d : 10fa             		bpl	hexdump5
e49f : cef917           hexdomap	dec	ID
e4a2 : 68               		pla
e4a3 : 85fb             		sta	POINTH
e4a5 : 68               		pla
e4a6 : 85fa             		sta	POINTL
e4a8 : 4c53e4           		jmp     hexdump2
e4ab : ca               hexlntst	dex
e4ac : 10b3             		bpl	hexdump3
e4ae : 2cf917           		bit	ID
e4b1 : 10ec             		bpl	hexdomap
e4b3 : 68               		pla
e4b4 : 68               		pla
e4b5 : 4c48e4           		jmp	hexdump1
                        ;
                        ; Clean up the stack and we're done
                        ;
e4b8 : 202f1e           hexdone		jsr	CRLF
e4bb : 68               		pla
e4bc : 68               		pla
e4bd : 60               		rts
                        ;
                        ;=====================================================
                        ; This does a memory test of a region of memory.  One
                        ; problem with the KIM is that there is no routine to
                        ; see if a new character is starting, so this loop
                        ; just runs forever unless the user presses RESET.
                        ;
                        ; Asks for the starting and ending locations.
                        ;
                        ; This cycles a rolling bit, then adds a ninth
                        ; pattern to help detect shorted address bits.
                        ; Ie: 01, 02, 04, 08, 10, 20, 40, 80, BA
                        ;
17e7 =                  pattern		equ	CHKL		;re-use some KIM locations
17e8 =                  original	equ	CHKH
                        ;
                        ; Test patterns
                        ;
0001 =                  PATTERN_0	equ	$01
00ba =                  PATTERN_9	equ	$ba
                        ;
e4be : 4c2ce1           cmdRet5		jmp	extKimLoop
e4c1 : 2048eb           memTest		jsr	getAddrRange	;get range
e4c4 : b0f8             		bcs	cmdRet5		;branch if abort
                        ;
e4c6 : 20bdea           		jsr	putsil
e4c9 : 0d0a             		db	CR,LF
e4cb : 54657374696e67.. 		db	"Testing memory.  Press RESET to abort"
e4f0 : 00               		db	0
e4f1 : a901             		lda	#PATTERN_0	;only set initial...
e4f3 : 8de717           		sta	pattern		;..pattern once
                        ;
                        ; Start of loop.  This fills/tests one complete pass
                        ; of memory.
                        ;
e4f6 : adf517           memTestMain	lda	SAL	;reset pointer to start
e4f9 : 85fa             		sta	POINTL
e4fb : adf617           		lda	SAH
e4fe : 85fb             		sta	POINTH
AS65 Assembler for R6502 [1.42].                                     Page   15
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Fill memory with the rolling pattern until the last
                        ; location is filled.
                        ;
e500 : a000             		ldy	#0
e502 : ade717           		lda	pattern
e505 : 8de817           		sta	original
e508 : 91fa             memTestFill	sta	(POINTL),y
e50a : c9ba             		cmp	#PATTERN_9	;at last pattern?
e50c : d005             		bne	memFill3
e50e : a901             		lda	#PATTERN_0	;restart pattern
e510 : 4c18e5           		jmp	memFill4
                        ;
                        ; Rotate pattern left one bit
                        ;
e513 : 0a               memFill3	asl	a
e514 : 9002             		bcc	memFill4	;branch if not overflow
e516 : a9ba             		lda	#PATTERN_9	;ninth pattern
                        ;
                        ; The new pattern is in A.  Now see if we've reached
                        ; the end of the area to be tested.
                        ;
e518 : 48               memFill4	pha			;save pattern
e519 : a5fa             		lda	POINTL
e51b : cdf717           		cmp	EAL
e51e : d007             		bne	memFill5
e520 : a5fb             		lda	POINTH
e522 : cdf817           		cmp	EAH
e525 : f007             		beq	memCheck
                        ;
                        ; Not done, so move to next address and keep going.
                        ;
e527 : 20631f           memFill5	jsr	INCPT
e52a : 68               		pla			;recover pattern
e52b : 4c08e5           		jmp	memTestFill
                        ;
                        ; Okay, memory is filled, so now go back and test it.
                        ; We kept a backup copy of the initial pattern to
                        ; use, but save the current pattern as the starting
                        ; point for the next pass.
                        ;
e52e : 68               memCheck	pla
e52f : 8de717           		sta	pattern		;for next pass
e532 : adf517           		lda	SAL		;reset pointer to start
e535 : 85fa             		sta	POINTL
e537 : adf617           		lda	SAH
e53a : 85fb             		sta	POINTH
e53c : ade817           		lda	original	;restore initial pattern
e53f : a000             		ldy	#0
e541 : d1fa             memTest2	cmp	(POINTL),y
e543 : d02c             		bne	memFail
e545 : c9ba             		cmp	#PATTERN_9
e547 : d004             		bne	memTest3
                        ;
                        ; Time to reload the pattern
                        ;
e549 : a901             		lda	#PATTERN_0
e54b : d005             		bne	memTest4
                        ;
                        ; Rotate pattern left one bit
                        ;
e54d : 0a               memTest3	asl	a
AS65 Assembler for R6502 [1.42].                                     Page   16
---------------------------------- xKIM.asm ----------------------------------

e54e : 9002             		bcc	memTest4
e550 : a9ba             		lda	#PATTERN_9
                        ;
                        ; The new pattern is in A.
                        ;
e552 : 48               memTest4	pha			;save pattern
e553 : a5fa             		lda	POINTL
e555 : cdf717           		cmp	EAL
e558 : d007             		bne	memTest5	;not at end
e55a : a5fb             		lda	POINTH
e55c : cdf817           		cmp	EAH
e55f : f007             		beq	memDone		;at end of pass
                        ;
                        ; Not at end yet, so inc pointer and continue
                        ;
e561 : 20631f           memTest5	jsr	INCPT
e564 : 68               		pla
e565 : 4c41e5           		jmp	memTest2
                        ;
                        ; Another pass has completed.
                        ;
e568 : 68               memDone		pla
e569 : a92e             		lda	#'.'
e56b : 20a01e           		jsr	OUTCH
e56e : 4cf6e4           		jmp	memTestMain
                        ;
                        ; Failure.  Display the failed address, the expected
                        ; value and what was actually there.
                        ;
e571 : 48               memFail		pha		;save pattern for error report
e572 : 20bdea           		jsr	putsil
e575 : 0d0a             		db	CR,LF
e577 : 4661696c757265.. 		db	"Failure at address ",0
e58b : 201e1e           		jsr	PRTPNT
e58e : 20bdea           		jsr	putsil
e591 : 2e202045787065.. 		db	".  Expected ",0
e59e : 68               		pla
e59f : 203b1e           		jsr	PRTBYT
e5a2 : 20bdea           		jsr	putsil
e5a5 : 2062757420676f.. 		db	" but got ",0
e5af : a000             		ldy	#0
e5b1 : b1fa             		lda	(POINTL),y
e5b3 : 203b1e           		jsr	PRTBYT
e5b6 : 202f1e           		jsr	CRLF
e5b9 : 4c2ce1           cmdRet4		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Edit memory.  This waits for a starting address to b
                        ; entered.  It will display the current address and it
                        ; contents.  Possible user inputs and actions:
                        ;
                        ;   Two hex digits will place that value in memory
                        ;   RETURN moves to next address
                        ;   BACKSPACE moves back one address
                        ;
e5bc : 20baea           editMemory      jsr	space
e5bf : 2024eb           		jsr	getStartAddr
e5c2 : b0f5             		bcs	cmdRet4
e5c4 : adf517           		lda	SAL		;move address into...
e5c7 : 85fa             		sta	POINTL		;...POINT
e5c9 : adf617           		lda	SAH
e5cc : 85fb             		sta	POINTH
AS65 Assembler for R6502 [1.42].                                     Page   17
---------------------------------- xKIM.asm ----------------------------------

e5ce : 202f1e           		jsr	CRLF
e5d1 : 20d7e5           		jsr	doEdit
e5d4 : 4c2ce1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine edits memory.  On entry, POINT has
                        ; the first address to edit.  Upon exit, POINT will
                        ; have been updated to next address to edit.
                        ;
                        ; Display the current location
                        ;
e5d7 : 201e1e           doEdit		jsr	PRTPNT		;print address
e5da : 20baea           		jsr	space
e5dd : a000             		ldy	#0
e5df : b1fa             		lda	(POINTL),y	;get byte
e5e1 : 203b1e           		jsr	PRTBYT		;print it
e5e4 : 20baea           		jsr	space
                        ;
e5e7 : 20ecea           		jsr	getHex
e5ea : b00d             		bcs	editMem2	;not hex
e5ec : a000             editMem7	ldy	#0
e5ee : 91fa             		sta	(POINTL),y	;save new value
                        ;
                        ; Bump POINT to next location
                        ;
e5f0 : 202f1e           editMem3	jsr	CRLF
e5f3 : 20631f           		jsr	INCPT
e5f6 : 4cd7e5           		jmp	doEdit
                        ;
                        ; Not hex, so see if another command.  Valid commands 
                        ;
                        ;    CR = advance to next memory location
                        ;    BS = move to previous location
                        ;    R  = compute relative offset
                        ;
e5f9 : c952             editMem2	cmp	#'R'		;compute relative branch
e5fb : f017             		beq	editMem4
e5fd : c90d             		cmp	#CR
e5ff : f0ef             		beq	editMem3	;move to next
e601 : c908             		cmp	#BS
e603 : d00e             		bne     editexit		;else exit
                        ;
                        ; Move back one location
                        ;
e605 : a5fa             		lda	POINTL
e607 : d002             		bne	editMem8
e609 : c6fb             		dec	POINTH
e60b : c6fa             editMem8	dec	POINTL
e60d : 202f1e           		jsr	CRLF
e610 : 4cd7e5           		jmp	doEdit
                        ;
e613 : 60               editexit	rts
                        ;
                        ; They want to calculate a relative offset
                        ;
e614 : 20bdea           editMem4	jsr	putsil
e617 : 656c6174697665.. 		db	"elative offset to: ",0
e62b : 2036eb           		jsr	getEndAddr
e62e : b0a7             		bcs	doEdit		;bad input
                        ;
                        ; Need to load POINTL/POINTH into SAL/SAH and then
                        ; decrement by one.
AS65 Assembler for R6502 [1.42].                                     Page   18
---------------------------------- xKIM.asm ----------------------------------

                        ;
e630 : a5fb             		lda	POINTH
e632 : 8df617           		sta	SAH
e635 : a5fa             		lda	POINTL
e637 : 8df517           		sta	SAL
e63a : d003             		bne	editMem5
e63c : cef617           		dec	SAH
e63f : cef517           editMem5	dec	SAL
                        ;
e642 : 2059eb           		jsr	ComputeOffset
e645 : 9016             		bcc	editMem6	;value good
e647 : 20bdea           		jsr	putsil
e64a : 202d206f757420.. 		db	" - out of range",0
e65a : 4cd7e5           		jmp	doEdit
                        ;
                        ; Relative offset is in A.
                        ;
e65d : 48               editMem6	pha
e65e : 20baea           		jsr	space
e661 : 68               		pla
e662 : 48               		pha
e663 : 203b1e           		jsr	PRTBYT		;print it
e666 : 68               		pla
e667 : 4cece5           		jmp	editMem7	;store it
                        ;
                        ;=====================================================
                        ; This handles the Load hex command.
                        ;
e66a : 20bdea           loadHex		jsr	putsil
e66d : 0d0a             		db	CR,LF
e66f : 456e7465722066.. 		db	"Enter filename, or Enter to "
e68b : 6c6f6164206672.. 		db	"load from console: ",0
                        ;
e69f : 208beb           		jsr	getFileName	;get filename
e6a2 : ad87df           		lda	filename	;null?
e6a5 : d006             		bne	loaddiskfile
e6a7 : 20f5e6           		jsr	loadHexConsole	;load from console
e6aa : 4cb9e6           		jmp	loadCheckAuto	;check auto-run
                        ;
                        ; Open the file
                        ;
e6ad : a087             loaddiskfile	ldy	#filename&$ff
e6af : a2df             		ldx	#filename/256
e6b1 : a9ff             		lda	#$ff
e6b3 : 8df917           		sta	ID		;print dots
e6b6 : 20c6e6           		jsr	loadHexFile
                        ;
                        ; If the auto-run vector is no longer $ffff, then jump
                        ; to whatever it points to.
                        ;
e6b9 : adf9df           loadCheckAuto	lda	AutoRun+1
e6bc : c9ff             		cmp	#$ff		;unchanged?
e6be : f003             		beq	lExit11
e6c0 : 6cf8df           		jmp	(AutoRun)	;execute!
e6c3 : 4c2ce1           lExit11		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine loads a hex file from the SD.  On
                        ; entry the pointer to the filename is in X (MSB) and
                        ; Y (LSB).
                        ;
e6c6 : a9ff             loadHexFile	lda	#$ff
AS65 Assembler for R6502 [1.42].                                     Page   19
---------------------------------- xKIM.asm ----------------------------------

e6c8 : 8df9df           		sta	AutoRun+1
e6cb : 8df917           		sta	ID		;we want dots
e6ce : 2039ed           		jsr	DiskOpenRead
e6d1 : 901c             		bcc	loadHexOk	;opened ok
                        ;
e6d3 : 20bdea           openfail	jsr	putsil
e6d6 : 0d0a             		db	CR,LF
e6d8 : 4661696c656420.. 		db	"Failed to open file"
e6eb : 0d0a00           		db	CR,LF,0
e6ee : 60               		rts
                        ;
e6ef : 20eeeb           loadHexOk	jsr	setInputFile	;redirect input
e6f2 : 4c2de7           		jmp	loadStart
                        ;
                        ;=====================================================
                        ; This subroutine is called to load a hex file from
                        ; the console.
                        ;
e6f5 : a9ff             loadHexConsole	lda	#$ff
e6f7 : 8df9df           		sta	AutoRun+1
e6fa : a900             		lda	#0
e6fc : 8df917           		sta	ID		;don't print dots
e6ff : 20bdea           		jsr	putsil
e702 : 0d0a             		db	CR,LF
e704 : 57616974696e67.. 		db	"Waiting for file, or ESC to"
e71f : 20657869742e2e2e 		db	" exit..."
e727 : 0d0a00           		db	CR,LF,0
e72a : 20e3eb           		jsr	setInputConsole
                        ;
                        ; The start of a line.  First character should be a
                        ; colon, but toss out CRs, LFs, etc.  Anything else
                        ; causes an abort.
                        ;
e72d : 20e0eb           loadStart	jsr	redirectedGetch	;get start of line
e730 : c90d             		cmp	#CR
e732 : f0f9             		beq	loadStart
e734 : c90a             		cmp	#LF
e736 : f0f5             		beq	loadStart
e738 : c93a             		cmp	#':'		;what we expect
e73a : d05a             		bne	loadAbortB
                        ;
                        ; Get the header of the record
                        ;
e73c : a900             		lda	#0
e73e : 8de717           		sta	CHKL		;initialize checksum
                        ;
e741 : 20ecea           		jsr	getHex		;get byte count
e744 : b054             		bcs	loadAbortC
e746 : 8d81df           		sta	byteCount	;save byte count
e749 : 2030e9           		jsr	updateCrc
e74c : 20ecea           		jsr	getHex		;get the MSB of offset
e74f : b04d             		bcs	loadAbortD
e751 : 85fb             		sta	POINTH
e753 : 2030e9           		jsr	updateCrc
e756 : 20ecea           		jsr	getHex		;get LSB of offset
e759 : b047             		bcs	loadAbortE
e75b : 85fa             		sta	POINTL
e75d : 2030e9           		jsr	updateCrc
e760 : 20ecea           		jsr	getHex		;get the record type
e763 : b041             		bcs	loadAbortF
e765 : 2030e9           		jsr	updateCrc
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   20
---------------------------------- xKIM.asm ----------------------------------

                        ; Only handle two record types:
                        ;    00 = data record
                        ;    01 = end of file record
                        ;
e768 : c900             		cmp	#DATA_RECORD
e76a : f05d             		beq	loadDataRec
e76c : c901             		cmp	#EOF_RECORD
e76e : f042             		beq	loadEof
                        ;
                        ; Unknown record type
                        ;
e770 : a941             		lda	#'A'		;reason
                        ;
                        ; This is the common error handler for various reasons
                        ; On entry A contains an ASCII character which is outp
                        ; to indicate the specific error reason.
                        ;
e772 : 48               loadAbort       pha			;save reason
e773 : 20bdea           		jsr	putsil
e776 : 0d0a             		db	CR,LF
e778 : 41626f7274696e.. 		db	"Aborting, reason: "
e78a : 00               		db	0
e78b : 68               		pla			;restore and...
e78c : 20a01e           		jsr	OUTCH		;...display reason
e78f : 202f1e           		jsr	CRLF
e792 : 20e3eb           loadExit	jsr	setInputConsole
e795 : 60               		rts
                        ;
                        ; Various error reason codes.  This was meant to be
                        ; very temporary as I worked out the real problem, but
                        ; this debug code immediately "solved" the problem so
                        ; I just left these as-is until the root cause is
                        ; discovered.
                        ;
e796 : a942             loadAbortB	lda	#'B'
e798 : d0d8             		bne	loadAbort
                        ;
e79a : a943             loadAbortC	lda	#'C'
e79c : d0d4             		bne	loadAbort
                        ;
e79e : a944             loadAbortD	lda	#'D'
e7a0 : d0d0             		bne	loadAbort
                        ;
e7a2 : a945             loadAbortE	lda	#'E'
e7a4 : d0cc             		bne	loadAbort
                        ;
e7a6 : a946             loadAbortF	lda	#'F'
e7a8 : d0c8             		bne	loadAbort
                        ;
e7aa : a947             loadAbortG	lda	#'G'
e7ac : d0c4             		bne	loadAbort
                        ;
e7ae : a948             loadAbortH	lda	#'H'
e7b0 : d0c0             		bne	loadAbort
                        ;
                        ; EOF is easy
                        ;
e7b2 : 20ecea           loadEof		jsr	getHex		;get checksum
e7b5 : 20e3eb           		jsr	setInputConsole	;reset input vector
e7b8 : 20bdea           		jsr	putsil
e7bb : 0d0a             		db	CR,LF
e7bd : 5375636365737321 		db	"Success!"
AS65 Assembler for R6502 [1.42].                                     Page   21
---------------------------------- xKIM.asm ----------------------------------

e7c5 : 0d0a00           		db	CR,LF,0
e7c8 : 60               		rts
                        ;
                        ; Data records have more work.  After processing the
                        ; line, print a dot to indicate progress.  This should
                        ; be re-thought as it could slow down loading a really
                        ; big file if the console speed is slow.
                        ;
e7c9 : ae81df           loadDataRec	ldx	byteCount	;byte count
e7cc : a000             		ldy	#0		;offset
e7ce : 8e81df           loadData1	stx	byteCount
e7d1 : 8c82df           		sty	saveY
e7d4 : 20ecea           		jsr	getHex
e7d7 : b0d1             		bcs	loadAbortG
e7d9 : 2030e9           		jsr	updateCrc
e7dc : ac82df           		ldy	saveY
e7df : ae81df           		ldx	byteCount
e7e2 : 91fa             		sta	(POINTL),y
e7e4 : c8               		iny
e7e5 : ca               		dex
e7e6 : d0e6             		bne	loadData1
                        ;
                        ; All the bytes were read so get the checksum and see
                        ; if it agrees.  The checksum is a twos-complement, so
                        ; just add the checksum into what we've been calculati
                        ; and if the result is zero then the record is good.
                        ;
e7e8 : 20ecea           		jsr	getHex		;get checksum
e7eb : 18               		clc
e7ec : 6de717           		adc	CHKL
e7ef : d0bd             		bne	loadAbortH	;non-zero is error
                        ;
                        ; If loading from an SD file then print a dot at the
                        ; end of each record.  Doing this for serial input is
                        ; very bad because the terminal program is sending the
                        ; next character while this is sending the dot.  Ie,
                        ; data is lost.
                        ;
e7f1 : adf917           		lda	ID
e7f4 : f005             		beq	lrecdone	;jump if not file
e7f6 : a92e             		lda	#'.'		;sanity indicator when
e7f8 : 20a01e           		jsr	OUTCH		;...loading from file
e7fb : 4c2de7           lrecdone	jmp	loadStart
e7fe : 4c2ce1           lExit1		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory as a
                        ; file on the SD.
                        ;
e801 : 2048eb           saveHex		jsr	getAddrRange	;get range to dump
e804 : b0f8             		bcs	lExit1	;abort on error
                        ;
                        ; Get the filename to save to
                        ;
e806 : 20bdea           		jsr	putsil
e809 : 0d0a             		db	CR,LF
e80b : 456e7465722066.. 		db	"Enter filename, or Enter to "
e827 : 646973706c6179.. 		db	"display to console: ",0
                        ;
e83c : 208beb           		jsr	getFileName	;get filename
e83f : ad87df           		lda	filename	;null?
e842 : f012             		beq	saveHexConsole	;dump to console
AS65 Assembler for R6502 [1.42].                                     Page   22
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; They selected a file, so try to open it.
                        ;
e844 : a2df             		ldx	#filename>>8
e846 : a087             		ldy	#filename&$ff
e848 : 2065ed           		jsr	DiskOpenWrite	;attempt to open file
e84b : 9003             		bcc	sopenok		;branch if opened ok
e84d : 4cd3e6           		jmp	openfail
                        ;
e850 : 2038ec           sopenok		jsr	setOutputFile
e853 : 4c59e8           		jmp	savehex2
                        ;
                        ; They are saving to the console.  Set up the output
                        ; vector and do the job.
                        ;
e856 : 202dec           saveHexConsole	jsr	setOutputConsole
                        ;
                        ; Compute the number of bytes to dump
                        ;
e859 : 38               savehex2	sec
e85a : adf717           		lda	EAL
e85d : edf517           		sbc	SAL
e860 : 8dd6df           		sta	Temp16L
e863 : adf817           		lda	EAH
e866 : edf617           		sbc	SAH
e869 : 8dd7df           		sta	Temp16H
e86c : 9042             		bcc	SDone	;start > end
e86e : 0900             		ora	#0
e870 : 303e             		bmi	SDone	;more than 32K seems wrong
                        ;
                        ; Add one to the count
                        ;
e872 : eed6df           		inc	Temp16L
e875 : d003             		bne	slab1
e877 : eed7df           		inc	Temp16H
                        ;
                        ; Move pointer to zero page
                        ;
e87a : adf517           slab1		lda	SAL
e87d : 85fa             		sta	POINTL
e87f : adf617           		lda	SAH
e882 : 85fb             		sta	POINTH
                        ;
                        ; Top of each loop.  Start by seeing if there are any 
                        ; left to dump.
                        ;
e884 : add7df           Sloop1		lda	Temp16H
e887 : d02a             		bne	Sgo	;more to do
e889 : add6df           		lda	Temp16L
e88c : d025             		bne	Sgo	;more to do
                        ;
                        ; At end of the region, so output an end record.  This
                        ; probably looks like overkill but keep in mind this
                        ; might be going to a file so we can't use the normal
                        ; string put functions.
                        ;
e88e : a93a             		lda	#':'
e890 : 201bec           		jsr	redirectedOutch
e893 : a900             		lda	#0
e895 : 2001ec           		jsr	HexToOutput
e898 : 2001ec           		jsr	HexToOutput
e89b : 2001ec           		jsr	HexToOutput
AS65 Assembler for R6502 [1.42].                                     Page   23
---------------------------------- xKIM.asm ----------------------------------

e89e : a901             		lda	#1
e8a0 : 2001ec           		jsr	HexToOutput
e8a3 : a9ff             		lda	#$ff
e8a5 : 2001ec           		jsr	HexToOutput
                        ;
                        ; If output to file, flush and close the file.
                        ;
e8a8 : ad87df           		lda	filename
e8ab : f003             		beq	SDone		;it's going to console
e8ad : 201eec           		jsr	CloseOutFile
e8b0 : 4c2ce1           SDone		jmp	extKimLoop	;back to the monitor
                        ;
                        ; This dumps the next line.  See how many bytes are le
                        ; and if more than BYTESLINE, then just do BYTESLINE.
                        ;
e8b3 : add7df           Sgo		lda	Temp16H
e8b6 : d007             		bne	Sdef	;do default number of bytes
e8b8 : add6df           		lda	Temp16L
e8bb : c910             		cmp	#BYTESLINE
e8bd : 9002             		bcc	Scnt	;more than max per line
e8bf : a910             Sdef		lda	#BYTESLINE
e8c1 : 8de917           Scnt		sta	SAVX	;for decrementing
e8c4 : 8df917           		sta	ID	;for subtracting
                        ;
                        ; Put out the header
                        ;
e8c7 : a93a             		lda	#':'
e8c9 : 201bec           		jsr	redirectedOutch
                        ;
e8cc : ade917           		lda	SAVX
e8cf : 8de717           		sta	CHKL	;start checksum
e8d2 : 2001ec           		jsr	HexToOutput
                        ;
e8d5 : a5fb             		lda	POINTH	;starting address
e8d7 : 2030e9           		jsr	updateCrc
e8da : 2001ec           		jsr	HexToOutput
e8dd : a5fa             		lda	POINTL
e8df : 2030e9           		jsr	updateCrc
e8e2 : 2001ec           		jsr	HexToOutput
                        ;
e8e5 : a900             		lda	#0	;record type - data
e8e7 : 2001ec           		jsr	HexToOutput
                        ;
                        ; Now print the proper number of bytes
                        ;
e8ea : a000             Sloop2		ldy	#0
e8ec : b1fa             		lda	(POINTL),y	;get byte
e8ee : 2030e9           		jsr	updateCrc
e8f1 : 2001ec           		jsr	HexToOutput
e8f4 : 20631f           		jsr	INCPT	;increment pointer
                        ;
e8f7 : cee917           sdec		dec	SAVX
e8fa : d0ee             		bne	Sloop2
                        ;
                        ; Now print checksum
                        ;
e8fc : ade717           		lda	CHKL
e8ff : 49ff             		eor	#$ff	;one's complement
e901 : 18               		clc
e902 : 6901             		adc	#1	;two's complement
e904 : 2001ec           		jsr	HexToOutput
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   24
---------------------------------- xKIM.asm ----------------------------------

                        ; Output a CR/LF
                        ;
e907 : a90d             		lda	#CR
e909 : 201bec           		jsr	redirectedOutch
e90c : a90a             		lda	#LF
e90e : 201bec           		jsr	redirectedOutch
                        ;
                        ; If saving to disk, output a dot to indicate progress
                        ;
e911 : ad87df           		lda	filename
e914 : f005             		beq	shf2
                        ;
e916 : a92e             		lda	#'.'
e918 : 20a01e           		jsr	OUTCH	;goes to console
                        ;
e91b : 38               shf2		sec
e91c : add6df           		lda	Temp16L
e91f : edf917           		sbc	ID
e922 : 8dd6df           		sta	Temp16L
e925 : add7df           		lda	Temp16H
e928 : e900             		sbc	#0
e92a : 8dd7df           		sta	Temp16H
                        ;
e92d : 4c84e8           		jmp	Sloop1
                        ;
                        ;=====================================================
                        ; Adds the character in A to the CRC.  Preserves A.
                        ;
e930 : 48               updateCrc	pha
e931 : 18               		clc
e932 : 6de717           		adc	CHKL
e935 : 8de717           		sta	CHKL
e938 : 68               		pla
e939 : 60               		rts
                        ;
                        ;=====================================================
                        ; Handles the command to prompt for an address and the
                        ; jump to it.
                        ;
e93a : 20baea           jumpAddress     jsr	space
e93d : 2024eb           		jsr	getStartAddr
e940 : b006             		bcs	cmdRet	;branch on bad address
e942 : 202f1e           		jsr	CRLF
e945 : 6cf517           		jmp	(SAL)	;else jump to address
                        ;
e948 : 4c2ce1           cmdRet		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Ping the Arduno disk controller.  This just sends th
                        ; PING command gets back one character, then returns.
                        ; Not much of a test but is sufficient to prove the
                        ; link is working.
                        ;
e94b :                  pingDisk
                        ;		jsr	xParInit	;init interface
e94b : 20bdea           		jsr	putsil
e94e : 696e672e2e2e2000 		db	"ing... ",0
e956 : 20ebec           		jsr	DiskPing
e959 : 20bdea           		jsr	putsil
e95c : 7375636365737321 		db	"success!"
e964 : 0d0a00           		db	CR,LF,0
e967 : 4c2ce1           		jmp	extKimLoop
AS65 Assembler for R6502 [1.42].                                     Page   25
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; Do a disk directory of the SD card.
                        ;
e96a : 20bdea           doDiskDir	jsr	putsil
e96d : 69736b20446972.. 		db	"isk Directory..."
e97d : 0d0a00           		db	CR,LF,0
e980 : 2087e9           		jsr	doSDDiskDir
e983 : 4c2ce1           		jmp	extKimLoop
e986 : 60               doDiskDirEnd	rts
                        ;
                        ;=====================================================
                        ; Subroutine to do a disk directory.  Prints filenames
                        ; to the console.
                        ;
e987 : 2064ec           doSDDiskDir	jsr	xParInit
e98a : 20feec           		jsr	DiskDir
                        ;
                        ; Get/Display each entry
                        ;
e98d : a2df             doDiskDirLoop   ldx	#filename/256	;pointer to buffer
e98f : a087             		ldy	#filename&$ff
e991 : 86f9             		stx	INH		;save for puts
e993 : 84f8             		sty	INL
e995 : 2008ed           		jsr	DiskDirNext	;get next entry
e998 : b0ec             		bcs	doDiskDirEnd	;carry = end of list
e99a : 20b4ea           		jsr	space3
e99d : 20d1ea           		jsr	puts		;else print name
e9a0 : 202f1e           		jsr	CRLF
e9a3 : 4c8de9           		jmp	doDiskDirLoop	;do next entry
                        ;
                        ;=====================================================
                        ; Type the contents of an SD file to console.
                        ;
e9a6 : 20bdea           typeFile	jsr	putsil
e9a9 : 202d20456e7465.. 		db	" - Enter filename: ",0
e9bd : 208beb           		jsr	getFileName
e9c0 : a087             		ldy	#filename&$ff
e9c2 : a2df             		ldx	#filename/256
                        ;		jsr	xParInit
e9c4 : 2039ed           		jsr	DiskOpenRead
e9c7 : 901e             		bcc	typeFile1	;opened ok
                        ;
e9c9 : 20bdea           		jsr	putsil
e9cc : 0d0a             		db	CR,LF
e9ce : 4661696c656420.. 		db	"Failed to open file"
e9e1 : 0d0a00           		db	CR,LF,0
e9e4 : 4c2ce1           		jmp	extKimLoop
                        ;
                        ; Now just keep reading in bytes and displaying them.
                        ;
e9e7 : 20eeeb           typeFile1	jsr	setInputFile	;reading from file
e9ea : 20b1eb           typeFileLoop	jsr	getNextFileByte
e9ed : b006             		bcs	typeEof
e9ef : 20a01e           		jsr	OUTCH	;display character
e9f2 : 4ceae9           		jmp	typeFileLoop
                        ;
e9f5 : 20d3ed           typeEof		jsr	DiskClose
e9f8 : 4c2ce1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Calculate the offset for a relative branch.  6502
AS65 Assembler for R6502 [1.42].                                     Page   26
---------------------------------- xKIM.asm ----------------------------------

                        ; relative branch calculations are well known.
                        ;
                        ; Offset from branch (BASE) = TARGET - (BASE+2).
                        ; If the result is positive, upper byte must be
                        ; zero.  If negative, upper byte must be FF.
                        ;
                        ; BASE	TARGET	Computed	Actual
                        ; 0200	0200	0200-(0200+2)	FFFE
                        ; 0200	020E	020E-(0200+2)	000C
                        ; 0226	0220	0220-(0226+2)	FFF8
                        ; 0156	015A	015A-(0156+2)	0002
                        ; 015C	012D	012D-(015C+2)	FFCF
                        ; 0200	0300	0300-(0200+2)	00FE - out of range
                        ; 0300	0200	0200-(0300+2)	FEFE - out of range
                        ;
e9fb : 20bdea           offCalc		jsr	putsil
e9fe : 202d204272616e.. 		db	" - Branch instruction address: "
ea1d : 00               		db	0
ea1e : 2024eb           		jsr	getStartAddr
ea21 : b030             		bcs	calcExit
ea23 : 20bdea           		jsr	putsil
ea26 : 2c206272616e63.. 		db	", branch to: "
ea33 : 00               		db	0
ea34 : 2036eb           		jsr	getEndAddr
ea37 : b01a             		bcs	calcExit
ea39 : 2059eb           		jsr	ComputeOffset	;does the work
ea3c : 9018             		bcc	relgood		;if good offset
                        ;
                        ; Branch is out of range.
                        ;
ea3e : 20bdea           		jsr	putsil
ea41 : 202d206f757420.. 		db	" - out of range",CR,LF,0
ea53 : 4c2ce1           calcExit	jmp	extKimLoop
                        ;
                        ; Branch is in range so dislay the value.
                        ;
ea56 : 48               relgood		pha			;save offset
ea57 : 20bdea           		jsr	putsil
ea5a : 204f6666736574.. 		db	" Offset: ",0
ea64 : 68               		pla
ea65 : 203b1e           		jsr	PRTBYT
ea68 : 4c2ce1           		jmp	extKimLoop
                        ;
                        ; Add new commands here...
                        ;
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   27
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This subroutine will search for a command in a table
                        ; and call the appropriate handler.  See the command
                        ; table near the start of the code for what the format
                        ; is.  If a match is found, pop off the return address
                        ; from the stack and jump to the code.  Else, return.
                        ;
ea6b : a000             searchCmd	ldy	#0
ea6d : b1fa             cmdLoop		lda	(POINTL),y
ea6f : f01c             		beq	cmdNotFound
ea71 : c5f3             		cmp	ACC	;compare to user's input
ea73 : f007             		beq	cmdMatch
ea75 : c8               		iny		;start of function ptr
ea76 : c8               		iny
ea77 : c8               		iny		;start of help
ea78 : c8               		iny
ea79 : c8               		iny		;move to next command
ea7a : d0f1             		bne	cmdLoop
                        ;
                        ; It's found!  Load up the address of the code to call
                        ; pop the return address off the stack and jump to the
                        ; handler.
                        ;
ea7c : c8               cmdMatch	iny
ea7d : b1fa             		lda	(POINTL),y	;handler LSB
ea7f : 48               		pha
ea80 : c8               		iny
ea81 : b1fa             		lda	(POINTL),y	;handler MSB
ea83 : 85fb             		sta	POINTH
ea85 : 68               		pla
ea86 : 85fa             		sta	POINTL
ea88 : 68               		pla		;pop return address
ea89 : 68               		pla
ea8a : 6cfa00           		jmp	(POINTL)
                        ;
                        ; Not found, so just return.
                        ;
ea8d : 60               cmdNotFound	rts
                        ;
                        ;=====================================================
                        ; Given a pointer to a command table in POINT, display
                        ; the help text for all commands in the table.
                        ;
ea8e : a000             displayHelp	ldy	#0	;index into command table
ea90 : b1fa             showHelpLoop	lda	(POINTL),y	;get command
ea92 : f01f             		beq	showHelpDone	;jump if at end
                        ;
                        ; Display this entry's descriptive text
                        ;
ea94 : c8               		iny		;skip over command
ea95 : c8               		iny		;skip over function ptr
ea96 : c8               		iny
ea97 : b1fa             		lda	(POINTL),y
ea99 : 85f8             		sta	INL
ea9b : c8               		iny
ea9c : b1fa             		lda	(POINTL),y
ea9e : 85f9             		sta	INH
eaa0 : 98               		tya
eaa1 : 48               		pha
eaa2 : 209e1e           		jsr	OUTSP
eaa5 : 209e1e           		jsr	OUTSP
AS65 Assembler for R6502 [1.42].                                     Page   28
---------------------------------- xKIM.asm ----------------------------------

eaa8 : 20d1ea           		jsr	puts	;print description
eaab : 202f1e           		jsr	CRLF
eaae : 68               		pla
eaaf : a8               		tay
eab0 : c8               		iny		;point to next entry
eab1 : d0dd             		bne	showHelpLoop
eab3 : 60               showHelpDone	rts
                        ;
                        ;=====================================================
                        ; Print some spaces.
                        ;
eab4 : 20baea           space3		jsr	space
eab7 : 20baea           space2		jsr	space
eaba : 4c9e1e           space   	jmp	OUTSP
                        ;
                        ;=====================================================
                        ; This prints the null-terminated string that
                        ; immediately follows the JSR to this function.  This
                        ; version was written by Ross Archer and is at:
                        ;
                        ;    www.6502.org/source/io/primm.htm
                        ;
eabd : 68               putsil		pla
eabe : 85f8             		sta	INL
eac0 : 68               		pla
eac1 : 85f9             		sta	INH
eac3 : a001             		ldy	#1
eac5 : 20d3ea           		jsr	putsy
eac8 : e6f8             		inc	INL
eaca : d002             		bne	puts2
eacc : e6f9             		inc	INH
eace : 6cf800           puts2		jmp	(INL)
                        ;
                        ;=====================================================
                        ; This prints the null terminated string pointed to by
                        ; INL and INH.  Modifies those locations to point to
                        ; the end of the string.
                        ;
ead1 : a000             puts		ldy	#0
ead3 : b1f8             putsy		lda	(INL),y
ead5 : e6f8             		inc	INL
ead7 : d002             		bne	puts1
ead9 : e6f9             		inc	INH
eadb : 0900             puts1		ora	#0
eadd : f00c             		beq	putsdone
eadf : 8c82df           		sty	saveY
eae2 : 20a01e           		jsr	OUTCH	;print character
eae5 : ac82df           		ldy	saveY
eae8 : 4cd3ea           		jmp	putsy
eaeb : 60               putsdone	rts
                        ;
                        ;=====================================================
                        ; This gets two hex characters and returns the value
                        ; in A with carry clear.  If a non-hex digit is
                        ; entered, then A contans the offending character and
                        ; carry is set.
                        ;
eaec : 2002eb           getHex		jsr	getNibble
eaef : b01e             		bcs	getNibBad
eaf1 : 0a               		asl	a
eaf2 : 0a               		asl	a
eaf3 : 0a               		asl	a
AS65 Assembler for R6502 [1.42].                                     Page   29
---------------------------------- xKIM.asm ----------------------------------

eaf4 : 0a               		asl	a
eaf5 : 8d80df           		sta	saveA
eaf8 : 2002eb           		jsr	getNibble
eafb : b012             		bcs	getNibBad
eafd : 0d80df           		ora	saveA
eb00 : 18               		clc
eb01 : 60               		rts
                        ;
                        ; Helper.  Gets next input char and converts to a
                        ; value from 0-F in A and returns C clear.  If not a
                        ; valid hex character, return C set.
                        ;
eb02 : 20e0eb           getNibble	jsr	redirectedGetch
eb05 : a20f             		ldx	#nibbleHexEnd-nibbleHex-1
eb07 : dd14eb           getNibble1	cmp	nibbleHex,x
eb0a : f005             		beq	getNibF	;got match
eb0c : ca               		dex
eb0d : 10f8             		bpl	getNibble1
eb0f : 38               getNibBad	sec
eb10 : 60               		rts
                        
eb11 : 8a               getNibF		txa		;index is value
eb12 : 18               		clc
eb13 : 60               		rts
                        ;
eb14 : 30313233343536.. nibbleHex	db	"0123456789ABCDEF"
eb24 =                  nibbleHexEnd	equ	*
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; SAL and SAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eb24 : 20ecea           getStartAddr	jsr	getHex
eb27 : b00c             		bcs	getDone
eb29 : 8df617           		sta	SAH
eb2c : 20ecea           		jsr	getHex
eb2f : b004             		bcs	getDone
eb31 : 8df517           		sta	SAL
eb34 : 18               		clc
eb35 : 60               getDone		rts
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; EAL and EAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eb36 : 20ecea           getEndAddr	jsr	getHex
eb39 : b0fa             		bcs	getDone
eb3b : 8df817           		sta	EAH
eb3e : 20ecea           		jsr	getHex
eb41 : b0f2             		bcs	getDone
eb43 : 8df717           		sta	EAL
eb46 : 18               		clc
eb47 : 60               		rts
                        ;
                        ;=====================================================
                        ; Get an address range and leave them in SAL and EAL.
                        ;
eb48 : 20baea           getAddrRange    jsr	space
eb4b : 2024eb           		jsr	getStartAddr
eb4e : b0e5             		bcs	getDone
AS65 Assembler for R6502 [1.42].                                     Page   30
---------------------------------- xKIM.asm ----------------------------------

eb50 : a92d             		lda	#'-'
eb52 : 20a01e           		jsr	OUTCH
eb55 : 2036eb           		jsr	getEndAddr
eb58 : 60               		rts
                        ;
                        ;=====================================================
                        ; This computes the relative offset between the
                        ; address in SAL/SAH (address of branch instruction)
                        ; and EAL/EAH (address to jump to).  If a valid range,
                        ; returns C clear and the offset in A.  If the branch
                        ; is out of range, C is set and A undefined.  Modifies
                        ; A, SAL and SAH.
                        ;
eb59 :                  ComputeOffset
                        ;
                        ; Add two to the end (BASE) address.  For calculations
                        ;   BASE = SAL/SAH
                        ;   TARGET = EAL/EAH
                        ;
eb59 : 18               		clc
eb5a : adf517           		lda	SAL
eb5d : 6902             		adc	#2
eb5f : 8df517           		sta	SAL
eb62 : 9003             		bcc	coffsub
eb64 : eef617           		inc	SAH
                        ;
                        ; Subtract the BASE (end) address from the TARGET (sta
                        ;
eb67 : 38               coffsub		sec
eb68 : adf717           		lda	EAL
eb6b : edf517           		sbc	SAL
eb6e : 48               		pha		;save for later
eb6f : 8df517           		sta	SAL
eb72 : adf817           		lda	EAH
eb75 : edf617           		sbc	SAH
eb78 : 8df617           		sta	SAH	;SAL/SAH contain offset
                        ;
                        ; High part must be either FF for negative branch or
                        ; 00 for a positive branch.  Cheat a bit here by rolli
                        ; the MSBit into C and adding to the MSByte.  If the
                        ; result is zero then everything is cool.
                        ;
eb7b : 68               		pla		;restore LSB of offset
eb7c : 48               		pha
eb7d : 0a               		asl	a	;put sign into C
eb7e : adf617           		lda	SAH
eb81 : 6900             		adc	#0
eb83 : f003             		beq	cogood	;branch if in range
                        ;
eb85 : 68               		pla		;clean up stack
eb86 : 38               		sec		;error
eb87 : 60               		rts
                        ;
eb88 : 68               cogood		pla		;get back offset
eb89 : 18               		clc
eb8a : 60               		rts
                        ;
                        ;=====================================================
                        ; Get a disk filename.  The KIM's behavior of echoing
                        ; every key prevents this from being too fancy, but it
                        ; good enough.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   31
---------------------------------- xKIM.asm ----------------------------------

eb8b : a200             getFileName	ldx	#0
eb8d : 205a1e           getFilename1	jsr	GETCH	;get next key
eb90 : c90d             		cmp	#CR	;end of the input?
eb92 : f014             		beq	getFnDone
eb94 : c908             		cmp	#BS	;backspace?
eb96 : f00a             		beq	getFnDel
eb98 : e00c             		cpx	#FILENAME_SIZE	;check size
eb9a : f0f1             		beq	getFilename1	;at length limit
eb9c : 9d87df           		sta	filename,x	;else save it
eb9f : e8               		inx
eba0 : d0eb             		bne	getFilename1
                        ;
eba2 : ca               getFnDel	dex		;back up one
eba3 : 10e8             		bpl	getFilename1
eba5 : e8               		inx		;can't go past start
eba6 : f0e5             		beq	getFilename1
eba8 : a900             getFnDone       lda	#0	;terminate line
ebaa : 9d87df           		sta	filename,x
ebad : 202f1e           		jsr	CRLF
ebb0 : 60               		rts
                        ;
                        ;=====================================================
                        ; This gets the next byte from an open disk file.  If
                        ; there are no more bytes left, this returns C set.
                        ; Else, C is clear and A contains the character.
                        ;
ebb1 : aed4df           getNextFileByte ldx 	diskBufOffset
ebb4 : ecd5df           		cpx	diskBufLength
ebb7 : d014             		bne	hasdata		;branch if still data
                        ;
                        ; There is no data left in the buffer, so read a
                        ; block from the SD system.
                        ;
ebb9 : a940             		lda	#BUFFER_SIZE
ebbb : a2df             		ldx	#buffer>>8
ebbd : a094             		ldy	#buffer&$ff
ebbf : 206aed           		jsr	DiskRead
ebc2 : b012             		bcs	getNextEof
                        ;
                        ; A contains the number of bytes actually read.
                        ;
ebc4 : 8dd5df           		sta	diskBufLength	;save length
ebc7 : c900             		cmp	#0		;shouldn't happen
ebc9 : f00b             		beq	getNextEof
                        ;
ebcb : a200             		ldx	#0
ebcd : bd94df           hasdata		lda	buffer,x
ebd0 : e8               		inx
ebd1 : 8ed4df           		stx	diskBufOffset
ebd4 : 18               		clc
ebd5 : 60               		rts
                        ;
ebd6 : a900             getNextEof	lda	#0
ebd8 : 8dd4df           		sta	diskBufOffset
ebdb : 8dd5df           		sta	diskBufLength
ebde : 38               		sec
ebdf : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   32
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the input vector
                        ; pointer to get the next input character.
                        ;
ebe0 : 6c83df           redirectedGetch	jmp	(inputVector)
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to the normal
                        ; console input subroutine.
                        ;
ebe3 : a95a             setInputConsole	lda	#GETCH&$ff
ebe5 : 8d83df           		sta     inputVector
ebe8 : a91e             		lda	#GETCH/256
ebea : 8d84df           		sta	inputVector+1
ebed : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to a file read
                        ; subroutine.
                        ;
ebee : a9b1             setInputFile    lda	#getNextFileByte&$ff
ebf0 : 8d83df           		sta     inputVector
ebf3 : a9eb             		lda	#getNextFileByte/256
ebf5 : 8d84df           		sta	inputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ebf8 : a900             		lda	#0
ebfa : 8dd4df           		sta	diskBufOffset
ebfd : 8dd5df           		sta	diskBufLength
ec00 : 60               		rts
                        ;
                        ;=====================================================
                        ; Print character in A as two hex digits to the
                        ; current output device (console or file).
                        ;
ec01 : 48               HexToOutput	pha		;save return value
ec02 : 48               		pha
ec03 : 4a               		lsr	a	;move top nibble to bottom
ec04 : 4a               		lsr	a
ec05 : 4a               		lsr	a
ec06 : 4a               		lsr	a
ec07 : 2010ec           		jsr	hexta	;output nibble
ec0a : 68               		pla
ec0b : 2010ec           		jsr	hexta
ec0e : 68               		pla		;restore
ec0f : 60               		rts
                        ;
ec10 : 290f             hexta		and	#%0001111
ec12 : c90a             		cmp	#$0a
ec14 : 18               		clc
ec15 : 3002             		bmi	hexta1
ec17 : 6907             		adc	#7
ec19 : 6930             hexta1		adc	#'0'	;then fall into...
                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the output vector
                        ; pointer to send the character in A to the proper
AS65 Assembler for R6502 [1.42].                                     Page   33
---------------------------------- xKIM.asm ----------------------------------

                        ; device.
                        ;
ec1b : 6c85df           redirectedOutch	jmp	(outputVector)
                        ;
                        ;=====================================================
                        ; This flushes any data remaining in the disk buffer
                        ; and then closes the file.
                        ;
ec1e : add4df           CloseOutFile	lda	diskBufOffset
ec21 : f007             		beq	closeonly
ec23 : a2df             		ldx	#buffer>>8
ec25 : a094             		ldy	#buffer&$ff
ec27 : 209ced           		jsr	DiskWrite
                        ;
ec2a : 20d3ed           closeonly	jsr	DiskClose
                        ;
                        ; Fall through...
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to the normal
                        ; console output subroutine.
                        ;
ec2d :                  setOutputConsole
ec2d : a9a0             		lda	#OUTCH&$ff
ec2f : 8d85df           		sta     outputVector
ec32 : a91e             		lda	#OUTCH/256
ec34 : 8d86df           		sta	outputVector+1
ec37 : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to a file write
                        ; subroutine.
                        ;
ec38 : a948             setOutputFile    lda	#putNextFileByte&$ff
ec3a : 8d85df           		sta     outputVector
ec3d : a9ec             		lda	#putNextFileByte/256
ec3f : 8d86df           		sta	outputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ec42 : a900             		lda	#0
ec44 : 8dd4df           		sta	diskBufOffset
ec47 : 60               		rts
                        ;
                        ;=====================================================
                        ; Add the byte in A to the output buffer.  If the
                        ; buffer is full, flush it to disk.
                        ;
ec48 : aed4df           putNextFileByte	ldx	diskBufOffset
ec4b : e040             		cpx	#BUFFER_SIZE	;buffer full?
ec4d : d00d             		bne	pNFB		;no
                        ;
                        ; The buffer is full, so write it out.
                        ;
ec4f : 48               		pha			;save byte
ec50 : a940             		lda	#BUFFER_SIZE
ec52 : a2df             		ldx	#buffer>>8
ec54 : a094             		ldy	#buffer&$ff
ec56 : 209ced           		jsr	DiskWrite
                        ;
ec59 : a200             		ldx	#0		;reset index
AS65 Assembler for R6502 [1.42].                                     Page   34
---------------------------------- xKIM.asm ----------------------------------

ec5b : 68               		pla
ec5c : 9d94df           pNFB		sta	buffer,x
ec5f : e8               		inx
ec60 : 8ed4df           		stx	diskBufOffset
ec63 : 60               		rts
                        ;
                        		include	"pario.asm"
                        ;*****************************************************
                        ; These are the low-level I/O routines to talk to the
                        ; Arduino processor connected to the KIM's I/O port.
                        ;
                        ; August 2014, Bob Applegate K2UT, bob@corshamtech.com
                        ;
                        ; Which port bits are used for what:
                        ;
                        ; A0 = Data 0, alternates input/output
                        ; A1 = Data 1, alternates input/output
                        ; A2 = Data 2, alternates input/output
                        ; A3 = Data 3, alternates input/output
                        ; A4 = Data 4, alternates input/output
                        ; A5 = Data 5, alternates input/output
                        ; A6 = Data 6, alternates input/output
                        ; A7 = Data 7, alternates input/output
                        ;
                        ; B0 = Direction bit, always output
                        ; B1 = Write strobe or ACK, always output
                        ; B2 = Read stroke or ACK, always input
                        ;
                        ;----------------------------------------------------
                        ; Bits in the B register
                        ;
0001 =                  DIRECTION	equ	%00000001
0002 =                  PSTROBE		equ	%00000010
0004 =                  ACK		equ	%00000100
                        ;
                        ;----------------------------------------------------
                        ; The KIM has a 6530 with the two I/O ports at $1700.
                        ;
                        ; Addresses of 6530 registers
                        ;
1700 =                  PIABASE		equ	$1700
1700 =                  PIAREGA		equ	PIABASE		;data reg A
1701 =                  PIADDRA		equ	PIABASE+1	;data dir reg A
1702 =                  PIAREGB		equ	PIABASE+2	;data reg B
1703 =                  PIADDRB		equ	PIABASE+3	;data dir reg B
                        		code
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   35
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This is the initialization function.  Call before
                        ; doing anything else with the parallel port.
                        ;
ec64 :                  xParInit
                        ;
                        ; Set up the data direction register for port B so tha
                        ; the DIRECTION and PSTROBE bits are output.  Only tou
                        ; the bits we're using; leave the rest as-is.
                        ;
ec64 : ad0317           		lda	PIADDRB	;get current value
ec67 : 29fb             		and	#~ACK
ec69 : 0903             		ora	#DIRECTION | PSTROBE
ec6b : 8d0317           		sta	PIADDRB
                        ;
ec6e : a9ff             		lda	#$ff
ec70 : 8d0117           		sta	PIADDRA	;set data for write
                        ;
                        ; Now that the data is set, set the direction
                        ; registers.  This prevents weird problems like
                        ; driving lines to the wrong state.
                        ;
ec73 : ad0217           		lda	PIAREGB
ec76 : 29f9             		and	#~(PSTROBE | ACK)
ec78 : 0901             		ora	#DIRECTION
ec7a : 8d0217           		sta	PIAREGB
ec7d : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   36
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for writing to the Arduino.  Sets up
                        ; direction registers, drives the direction bit, etc.
                        ;
ec7e : a9ff             xParSetWrite    lda	#$ff	;set bits for output
ec80 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to output, clear ACK bit
                        ;
ec83 : ad0217           		lda	PIAREGB
ec86 : 29f9             		and	#~(PSTROBE | ACK)
ec88 : 0901             		ora	#DIRECTION
ec8a : 8d0217           		sta	PIAREGB
ec8d : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   37
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for reading from the Arduino.  Sets up
                        ; direction registers, clears the direction bit, etc.
                        ;
ec8e : a900             xParSetRead     lda	#$00	;set bits for input
ec90 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to input, clear ACK bit
                        ;
ec93 : ad0217           		lda	PIAREGB
ec96 : 29f8             		and	#~(DIRECTION | PSTROBE | ACK)
ec98 : 8d0217           		sta	PIAREGB
ec9b : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   38
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This writes a single byte to the Arduino.  On entry,
                        ; the byte to write is in A.  This assumes ParSetWrite
                        ; was already called.
                        ;
                        ; Destroys A, all other registers preserved.
                        ;
                        ; Write cycle:
                        ;
                        ;    1. Wait for other side to lower ACK.
                        ;    2. Put data onto the bus.
                        ;    3. Set DIRECTION and PSTROBE to indicate data
                        ;       is valid and ready to read.
                        ;    4. Wait for ACK line to go high, indicating the
                        ;       other side has read the data.
                        ;    5. Lower PSTROBE.
                        ;    6. Wait for ACK to go low, indicating end of
                        ;       transfer.
                        ;
ec9c : 48               xParWriteByte	pha		;save data
ec9d : ad0217           Parwl22		lda	PIAREGB	;check status
eca0 : 2904             		and	#ACK
eca2 : d0f9             		bne	Parwl22	;wait for ACK to go low
                        ;
                        ; Now put the data onto the bus
                        ;
eca4 : 68               		pla
eca5 : 8d0017           		sta	PIAREGA
                        ;
                        ; Raise the strobe so the Arduino knows there is
                        ; new data.
                        ;
eca8 : ad0217           		lda	PIAREGB
ecab : 0902             		ora	#PSTROBE
ecad : 8d0217           		sta	PIAREGB
                        ;
                        ; Wait for ACK to go high, indicating the Arduino has
                        ; pulled the data and is ready for more.
                        ;
ecb0 : ad0217           Parwl33		lda	PIAREGB
ecb3 : 2904             		and	#ACK
ecb5 : f0f9             		beq	Parwl33
                        ;
                        ; Now lower the strobe, then wait for the Arduino to
                        ; lower ACK.
                        ;
ecb7 : ad0217           		lda	PIAREGB
ecba : 29fd             		and	#~PSTROBE
ecbc : 8d0217           		sta	PIAREGB
ecbf : ad0217           Parwl44		lda	PIAREGB
ecc2 : 2904             		and	#ACK
ecc4 : d0f9             		bne	Parwl44
ecc6 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   39
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This reads a byte from the Arduino and returns it in
                        ; A.  Assumes ParSetRead was called before.
                        ;
                        ; This does not have a time-out.
                        ;
                        ; Preserves all other registers.
                        ;
                        ; Read cycle:
                        ;
                        ;    1. Wait for other side to raise ACK, indicating
                        ;       data is ready.
                        ;    2. Read data.
                        ;    3. Raise PSTROBE indicating data was read.
                        ;    4. Wait for ACK to go low.
                        ;    5. Lower PSTROBE.
                        ;
ecc7 : ad0217           xParReadByte    lda	PIAREGB
ecca : 2904             		and	#ACK	;is their strobe high?
eccc : f0f9             		beq	xParReadByte	;nope, no data
                        ;
                        ; Data is available, so grab and save it.
                        ;
ecce : ad0017           		lda	PIAREGA
ecd1 : 48               		pha
                        ;
                        ; Now raise our strobe (their ACK), then wait for
                        ; them to lower their strobe.
                        ;
ecd2 : ad0217           		lda	PIAREGB
ecd5 : 0902             		ora	#PSTROBE
ecd7 : 8d0217           		sta	PIAREGB
ecda : ad0217           Parrlp1		lda	PIAREGB
ecdd : 2904             		and	#ACK
ecdf : d0f9             		bne	Parrlp1	;still active
                        ;
                        ; Lower our ack, then were done.
                        ;
ece1 : ad0217           		lda	PIAREGB
ece4 : 29fd             		and	#~PSTROBE
ece6 : 8d0217           		sta	PIAREGB
ece9 : 68               		pla
ecea : 60               		rts
                        
                        		include	"parproto.inc"
                        ;*****************************************************
                        ; Parallel port protocol
                        ;
                        ; This is the header file for making applications
                        ; compliant with The Remote Disk Protocol Guide which
                        ; is on the Corsham Technologies web page somewhere:
                        ;
                        ;    www.corshamtech.com
                        ;
                        ; This was updated 06/13/2015 to be compliant with the
                        ; official specification, so the opcode values changed
                        ;
                        ; Another update on 02/10/2019
                        ;
                        ;=====================================================
                        ; Commands from host to Arduino
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   40
---------------------------------- xKIM.asm ----------------------------------

0001 =                  PC_GET_VERSION	equ	$01
0005 =                  PC_PING		equ	$05	;ping Arduino
0006 =                  PC_LED_CONTROL	equ	$06	;LED control
0007 =                  PC_GET_CLOCK	equ	$07	;get clock data
0008 =                  PC_SET_CLOCK	equ	$08	;set clock
0010 =                  PC_GET_DIR	equ	$10	;Get directory
0011 =                  PC_GET_MOUNTED	equ	$11	;Get mounted drive list
0012 =                  PC_MOUNT	equ	$12	;Mount drive
0013 =                  PC_UNMOUNT	equ	$13	;Unmount drive
0014 =                  PC_GET_STATUS	equ	$14	;Get status for one drive
0015 =                  PC_DONE		equ	$15	;Stop data
0015 =                  PC_ABORT	equ	PC_DONE
0016 =                  PC_READ_FILE	equ	$16	;Read regular file (non-DSK)
0017 =                  PC_READ_BYTES	equ	$17	;Read sequential bytes
0018 =                  PC_RD_SECTOR	equ	$18	;Read FLEX sector
0019 =                  PC_WR_SECTOR	equ	$19	;Write FLEX sector
001a =                  PC_GET_MAX	equ	$1a	;Get maximum drives
001b =                  PC_WRITE_FILE   equ	$1b	;Open file for writing
001c =                  PC_WRITE_BYTES	equ	$1c	;Data to be written
001d =                  PC_SAVE_CONFIG	equ	$1d	;Save SD.CFG with current value
001e =                  PC_SET_TIMER	equ	$1e	;Set RTC timer
001f =                  PC_RD_SEC_LONG	equ	$1f	;Read sector with long sec num
0020 =                  PC_WR_SEC_LONG	equ	$20	;Write sector with long sec num
                        ;
                        ;=====================================================
                        ; Responses from Arduino to host
                        ;
0081 =                  PR_VERSION_INFO	equ	$81	;Contains version information
0082 =                  PR_ACK		equ	$82	;ACK (no additional information)
0083 =                  PR_NAK		equ	$83	;NAK - one status byte follows
0085 =                  PR_PONG		equ	$85	;Reply to a ping
0087 =                  PR_CLOCK_DATA	equ	$87	;Clock data
0090 =                  PR_DIR_ENTRY	equ	$90	;Directory entry
0091 =                  PR_DIR_END	equ	$91	;End of directory entries
0092 =                  PR_FILE_DATA	equ	$92	;File data
0093 =                  PR_STATUS	equ	$93	;Drive status
0094 =                  PR_SECTOR_DATA	equ	$94	;Sector data
0095 =                  PR_MOUNT_INFO	equ	$95	;Mount entry
0096 =                  PR_MAX_DRIVES	equ	$96	;Maximum number of drives
                        ;
                        ;=====================================================
                        ; Error codes for NAK events.  Yes, these are in
                        ; decimal due to an error in the original
                        ; documentation.
                        ;
0000 =                  ERR_NONE	equ	0
000a =                  ERR_NOT_MOUNTED	equ	10
000b =                  ERR_MOUNTED	equ	11
000c =                  ERR_NOT_FOUND	equ	12
000d =                  ERR_READ_ONLY	equ	13
000e =                  ERR_BAD_DRIVE	equ	14
000f =                  ERR_BAD_TRACK	equ	15
0010 =                  ERR_BAD_SECTOR	equ	16
0011 =                  ERR_READ_ERROR	equ	17
0012 =                  ERR_WRITE_ERROR	equ	18
0013 =                  ERR_NOT_PRESENT	equ	19
0014 =                  ERR_NOT_IMPL	equ	20	;Command not implemented
                        
                        
                        		include	"diskfunc.asm"
                        		list
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   41
---------------------------------- xKIM.asm ----------------------------------

                        ; This is a collection of functions for performing
                        ; higher level disk functions.  This hides the nasty
                        ; details of communications with the remote disk
                        ; system.
                        ;
                        ; August 20, 2014 - Bob Applegate
                        ;                   bob@corshamtech.com
                        ;
                        ; 06/14/2015 - Bob Applegate
                        ;		Now that there is an official standard
                        ;		for the protocol between the host (this
                        ;		code) and the DCP (Arduino code), this
                        ;		code has been updated to be compliant.
                        ;
                        ; 01/14/2016 - Bob Applegate
                        ;              Finally converted to 6502.
                        ;
                        ;		include	"parproto.inc"
                        ;
                        ; Number of drives emulated
                        ;
0004 =                  DRIVES		equ	4
                        ;
                        ;=====================================================
                        ; This is a sanity check to verify connectivity to the
                        ; Arduino code is working.  Returns C clear if all is
                        ; good, or C set if not.
                        ;
eceb : 207eec           DiskPing	jsr	xParSetWrite
ecee : a905             		lda	#PC_PING	;command
ecf0 : 209cec           		jsr	xParWriteByte	;send to Arduino
ecf3 : 208eec           		jsr	xParSetRead
ecf6 : 20c7ec           		jsr	xParReadByte	;read their reply
ecf9 : 207eec           DiskRetGood	jsr	xParSetWrite
ecfc : 18               		clc			;assume it's good
ecfd : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   42
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This starts a directory read of the raw drive, not
                        ; the mounted drive.  No input parameters.  This simpl
                        ; sets up for reading the entries, then the user must
                        ; read each entry.
                        ;
                        ; Returns with C clear on success.  If error, C is set
                        ; and A contains the error code.
                        ;
ecfe : 207eec           DiskDir		jsr	xParSetWrite
ed01 : a910             		lda	#PC_GET_DIR	;send command
ed03 : 209cec           		jsr	xParWriteByte
ed06 : 18               		clc		;assume it works
ed07 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   43
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Read the next directory entry.  On input, X (MSB)
                        ; and Y (LSB) point to a 13 byte area to receive the
                        ; drive data.
                        ;
                        ; Returns C set if end of directory (ie, attempt to
                        ; read and there are none left).  Else, C is clear
                        ; and X/Y point to the null at end of filename.
                        ;
ed08 : 86fb             DiskDirNext	stx	POINTH
ed0a : 84fa             		sty	POINTL
ed0c : 208eec           		jsr	xParSetRead	;read results
ed0f : 20c7ec           		jsr	xParReadByte	;get response code
ed12 : c983             		cmp	#PR_NAK		;error?
ed14 : f01e             		beq	DDNErr
ed16 : c991             		cmp	#PR_DIR_END	;end?
ed18 : f01a             		beq	DDNErr
                        ;
                        ; This contains a directory entry.
                        ;
ed1a : a000             		ldy	#0
ed1c : 20c7ec           DDNloop		jsr	xParReadByte
ed1f : 91fa             		sta	(POINTL),y
ed21 : c900             		cmp	#0	;end?
ed23 : f006             		beq	DDNEnd
ed25 : 20631f           		jsr	INCPT
ed28 : 4c1ced           		jmp	DDNloop
ed2b : 207eec           DDNEnd		jsr	xParSetWrite
ed2e : a6fb             		ldx	POINTH
ed30 : a4fa             		ldy	POINTL
ed32 : 18               		clc		;not end of files
ed33 : 60               		rts
                        ;
                        ; Error.  Set C and return.  This is not really
                        ; proper, since this implies a simple end of the
                        ; directory rather than an error.
                        ;
ed34 : 207eec           DDNErr		jsr	xParSetWrite
ed37 : 38               		sec
ed38 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   44
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This opens a file on the SD for reading.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error (usually means the
                        ; file does not exist.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed39 : a916             DiskOpenRead	lda	#PC_READ_FILE
ed3b :                  DiskOpen
ed3b : 84f8                     sty	INL	;save ptr to filename
ed3d : 86f9             		stx	INH
ed3f : 209cec           		jsr	xParWriteByte
ed42 : a0ff             		ldy	#-1
ed44 : c8               DiskOpenLoop	iny
ed45 : b1f8             		lda	(INL),y
ed47 : 209cec           		jsr	xParWriteByte
ed4a : b1f8             		lda	(INL),y
ed4c : d0f6             		bne	DiskOpenLoop
ed4e : 208eec           		jsr	xParSetRead
ed51 : 20c7ec           		jsr	xParReadByte	;get response
ed54 : c982             		cmp	#PR_ACK
ed56 : d005             		bne	DiskOpenErr
ed58 : 207eec           		jsr	xParSetWrite	;back to write mode
ed5b : 18               		clc
ed5c : 60               		rts
                        ;
                        ; Got an error.
                        ;
ed5d : 20c7ec           DiskOpenErr	jsr	xParReadByte	;get error code
ed60 : 207eec           		jsr	xParSetWrite	;back to write mode
ed63 : 38               		sec
ed64 : 60               		rts
                        ;=====================================================
                        ; This opens a file on the SD for writing.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed65 : a91b             DiskOpenWrite	lda	#PC_WRITE_FILE
ed67 : 4c3bed           		jmp	DiskOpen	;jump into common code
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to read
                        ; from the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to put the data.  On return, C will
                        ; be set if EOF was reached (and no data read), or
                        ; C will be clear and A contains the number of bytes
                        ; actually read into the buffer.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed6a : 84f8             DiskRead	sty	INL	;save ptr to buffer
ed6c : 86f9             		stx	INH
ed6e : 48               		pha
ed6f : a917             		lda	#PC_READ_BYTES
ed71 : 209cec           		jsr	xParWriteByte	;command
ed74 : 68               		pla		;number of bytes to get
AS65 Assembler for R6502 [1.42].                                     Page   45
---------------------------------- xKIM.asm ----------------------------------

ed75 : 209cec           		jsr	xParWriteByte
ed78 : 208eec           		jsr	xParSetRead	;get ready for response
ed7b : 20c7ec           		jsr	xParReadByte	;assume PR_FILE_DATA
ed7e : 20c7ec           		jsr	xParReadByte	;length
ed81 : 48               		pha
ed82 : aa               		tax			;count
ed83 : f011             		beq	DiskReadEof	;zero = EOF
ed85 : a000             		ldy	#0	;offset
ed87 : 20c7ec           DiskReadLoop	jsr	xParReadByte
ed8a : 91f8             		sta	(INL),y
ed8c : c8               		iny		;next offset
ed8d : ca               		dex
ed8e : d0f7             		bne	DiskReadLoop
ed90 : 207eec           		jsr	xParSetWrite
ed93 : 68               		pla		;retrieve byte count
ed94 : 18               DiskOk		clc	
ed95 : 60               		rts
ed96 : 207eec           DiskReadEof	jsr	xParSetWrite
ed99 : 68               		pla
ed9a : 38               		sec
ed9b : 60               		rts
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to write
                        ; to the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to get the data.  On return, C will
                        ; be set if an error was detected, or C will be clear
                        ; if no error.  Note that if A contains 0 on entry,
                        ; no bytes are written.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed9c : c900             DiskWrite	cmp	#0
ed9e : f0f4             		beq	DiskOk
eda0 : 84f8             		sty	INL	;save ptr to filename
eda2 : 86f9             		stx	INH
eda4 : 48               		pha
eda5 : a91c             		lda	#PC_WRITE_BYTES
eda7 : 209cec           		jsr	xParWriteByte	;command
edaa : 68               		pla		;number of bytes to write
edab : 48               		pha		;save again
edac : 209cec           		jsr	xParWriteByte
edaf : 68               		pla
edb0 : aa               		tax			;count
edb1 : a000             		ldy	#0	;offset
edb3 : b1f8             DiskWriteLoop	lda	(INL),y	;get next byte
edb5 : 209cec           		jsr	xParWriteByte
edb8 : c8               		iny		;next offset
edb9 : ca               		dex
edba : d0f7             		bne	DiskWriteLoop
edbc : 208eec           		jsr	xParSetRead ;read the status
edbf : 20c7ec           		jsr	xParReadByte
edc2 : c982             		cmp	#PR_ACK
edc4 : f008             		beq	DiskOk1	;all good
edc6 : 20c7ec           		jsr	xParReadByte	;read error code
edc9 : 207eec           		jsr	xParSetWrite 
edcc : 38               		sec	
edcd : 60               		rts
                        ;
edce : 207eec           DiskOk1		jsr	xParSetWrite
edd1 : 18               		clc
AS65 Assembler for R6502 [1.42].                                     Page   46
---------------------------------- xKIM.asm ----------------------------------

edd2 : 60               		rts
                        ;
                        ;=====================================================
                        ; Call this to close any open file.  No parameters
                        ; and no return status.
                        ;
edd3 : a915             DiskClose	lda	#PC_DONE
edd5 : 4c9cec           		jmp	xParWriteByte
                        
                        
                        
                        ;
                        ;=====================================================
                        ; Show current clock
                        ;
                        	if	SHOW_RTC
edd8 : 207eec           doShowClock	jsr	xParSetWrite
eddb : a907             		lda	#PC_GET_CLOCK
eddd : 209cec           		jsr	xParWriteByte
ede0 : 208eec           		jsr	xParSetRead	;prepare to read
                        ;
ede3 : 20c7ec           		jsr	xParReadByte
                        ;
                        ; Loop to read the raw data
                        ;
ede6 : a200             		ldx	#0
ede8 : 8e81df           clockread	stx	saveX
edeb : 20c7ec           		jsr	xParReadByte
edee : ae81df           		ldx	saveX
edf1 : 9d94df           		sta	month,x
edf4 : e8               		inx
edf5 : e008             		cpx	#clock_end-month
edf7 : d0ef             		bne	clockread
                        ;
                        ; Set back to write mode to finish up; all apps are
                        ; supposed to leave the SD interface in write mode.
                        ;
edf9 : 207eec           		jsr	xParSetWrite
                        ;
                        ; Now display the data in a user-friendly format.  Eac
                        ; numberic value is in binary, so convert to decimal
                        ; for display.
                        ;
edfc : 20bdea           		jsr	putsil
edff : 0d0a             		db	CR,LF
ee01 : 446174653a2000   		db	"Date: ",0
                        ;
ee08 : ad94df           		lda	month
ee0b : 2051ee           		jsr	outdec
ee0e : a92f             		lda	#'/'
ee10 : 20a01e           		jsr	OUTCH
ee13 : ad95df           		lda	day
ee16 : 2051ee           		jsr	outdec
                        ;
                        ; Always force the high part of the year to "/20"
                        ;
ee19 : 20bdea           		jsr	putsil
ee1c : 2f323000         		db	"/20",0
ee20 : ad97df           		lda	year_low
ee23 : 2051ee           		jsr	outdec
                        ;
                        ; Space over, then do the time
AS65 Assembler for R6502 [1.42].                                     Page   47
---------------------------------- xKIM.asm ----------------------------------

                        ;
ee26 : 20bdea           		jsr	putsil
ee29 : 2c2000           		db	", ",0
                        ;
ee2c : ad98df           		lda	hour
ee2f : 2051ee           		jsr	outdec
ee32 : a93a             		lda	#':'
ee34 : 20a01e           		jsr	OUTCH
ee37 : ad99df           		lda	minute
ee3a : 2051ee           		jsr	outdec
ee3d : a93a             		lda	#':'
ee3f : 20a01e           		jsr	OUTCH
ee42 : ad9adf           		lda	second
ee45 : 2051ee           		jsr	outdec
                        ;
ee48 : 20bdea           		jsr	putsil
ee4b : 0d0a00           		db	CR,LF,0
ee4e : 4c60e0           		jmp	extKim	;return to monitor
                        ;
                        ;=====================================================
                        ; Given a binary value in A, display it as two decimal
                        ; digits.  The input can't be greater than 99.  Always
                        ; print a leading zero if less than 10.
                        ;
ee51 : a000             outdec		ldy	#0	;counts 10s
ee53 : c90a             out1		cmp	#10
ee55 : 9007             		bcc	out2	;below 10
ee57 : c8               		iny		;count 10
ee58 : 38               		sec
ee59 : e90a             		sbc	#10
ee5b : 4c53ee           		jmp	out1
                        ;
ee5e : 48               out2		pha		;save ones
ee5f : 98               		tya		;get tens
ee60 : 2064ee           		jsr	out3	;print tens digit
ee63 : 68               		pla		;restore ones
                        ;
ee64 : 0930             out3		ora	#'0'
ee66 : 20a01e           		jsr	OUTCH
ee69 : 60               		rts
                        
                        	endif
                        ;
                        	if	TINY_BASIC
                        		include "mytb.asm"
                        	endif
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   48
---------------------------------- xKIM.asm ----------------------------------

                        		
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory to
                        ; an IEC device.
                        ;
ee6a : 4c2ce1           lExit111		jmp	extKimLoop
ee6d : 2048eb           saveIEC		jsr	getAddrRange	;get range to dump
ee70 : b0f8             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;
ee72 : 20bdea           		jsr	putsil
ee75 : 0d0a             		db	CR,LF
ee77 : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
ee90 : 20ecea           		jsr	getHex
ee93 : b0d5             		bcs	lExit111		;abort on error
ee95 : 8df917           		sta	IECFID
                        		
ee98 : adf617           		lda SAH
ee9b : 8df617           		sta IECSAH
ee9e : adf517           		lda SAL
eea1 : 8df517           		sta IECSAL
eea4 : adf817           		lda EAH
eea7 : 8df817           		sta IECEAH
eeaa : adf717           		lda EAL
eead : 8df717           		sta IECEAL
                        		
eeb0 : 4c48f1           		jmp IECSAV
eeb3 : 4c2ce1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to load a region of memory from
                        ; an IEC device.
                        ;
eeb6 : 2048eb           loadIEC		jsr	getAddrRange	;get range to dump
eeb9 : b0af             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;
eebb : 20bdea           		jsr	putsil
eebe : 0d0a             		db	CR,LF
eec0 : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
eed9 : 20ecea           		jsr	getHex
eedc : b08c             		bcs	lExit111		;abort on error
eede : 8df917           		sta	IECFID
                        		
eee1 : 4c00f0           		jmp IECLOD
eee4 : 4c2ce1           		jmp	extKimLoop
                        
                        ;
                        ;=====================================================
                        ; These are the 6502 vectors.  On the Corsham
                        ; Technologies RAM/ROM board the user can select
                        ; whether or not to use these based on a switch
                        ; setting on the board.
                        ;
fffa =                  		org	$fffa
fffa : 1c1c             NMI		dw	NMIT	;in KIM
fffc : 221c             RESET		dw	RST	;in KIM
fffe : 1f1c             IRQ		dw	IRQT	;in KIM
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   49
---------------------------------- xKIM.asm ----------------------------------

                        		end
                        
                        
No errors in pass 2.
Wrote binary from address $e000 through $ffff.
Total size 8192 bytes.
