AS65 Assembler for R6502 [1.42].                                     Page    1
---------------------------------- xKIM.asm ----------------------------------

2561 lines read, no errors in pass 1.
                        ;*****************************************************
                        ; Extended KIM monitor, written 2015 by Bob Applegate
                        ; K2UT - bob@corshamtech.com
                        ;
                        ; This code was written as part of the Corsham Tech
                        ; 60K RAM/EPROM board.  It provides a working area for
                        ; an extended version of the KIM-1 monitor capabilitie
                        ; mostly based on a TTY interface, not the keypad and
                        ; hex display.
                        ;
                        ; This extensions contains an assortment of basic
                        ; tools that are missing from the KIM-1's built in
                        ; console monitor.  It has commands for loading hex
                        ; files (as opposed to KIM format), accessing an SD
                        ; card system, memory edit, hex dump, etc.  It also
                        ; has the ability for user-defined extensions to be
                        ; added.  And a number of common entry points have
                        ; vectors so the monitor can be modified without
                        ; breaking programs that use it.
                        ;
                        ; I'm not claiming copyright; I wrote most of this,
                        ; but also borrowed from others (credit is given in
                        ; those sections of code).  All of the portions I've
                        ; written are free to use, but please keep my name
                        ; in the comments somewhere.  Even better, consider
                        ; buying a board from us: www.corshamtech.com
                        ;
                        ; 12/01/2015	Bob Applegate
                        ;		Initial development - V0.X
                        ; 03/15/2016	Bob Applegate
                        ;		v1.0 - First official release
                        ; 01/03/2017	Bob Applegate
                        ;		v1.1 - Added S command
                        ; 09/20/2018	Bob Applegate
                        ;		v1.2 - Added auto-run vector
                        ; 01/25/2019	Bob Applegate
                        ;		v1.3 - Added 'X' command.
                        ;		Added the 'C' command to get time.
                        ; 03/09/2019	Bob Applegate
                        ;		v1.4 - Added 'O' command.
                        ; 07/26/2020	Bob Applegate
                        ;		v1.5 - Fixed bug that caused the S
                        ;		command to create empty file with no
                        ;		contents.
                        ;		Minor typo fixes.
                        ;		Added CLD instructions.
                        ; 11/14/2020	Bob Applegate
                        ;		v1.6 - On SD error, display reason code.
                        ; 09/15/2021	Bob Applegate
                        ;		v1.7
                        ;		Added offset calculator command O.
                        ;		Added R command in Edit mode.
                        ;		Removed '.' when loading from console.
                        ; 09/20/2021	Bob Applegate
                        ;		v1.8
                        ;		Made a lot of the command handlers
                        ;		into subroutines and added vectors so
                        ;		external programs can call them.
                        ;		Fixed bugs in Edit mode.
                        ;
                        ;*****************************************************
AS65 Assembler for R6502 [1.42].                                     Page    2
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Useful constants
                        ;
0000 =                  false		equ	0
ffff =                  true		equ	~false
                        ;
                        ; Version number
                        ;
0001 =                  VERSION		equ	1
0008 =                  REVISION	equ	8
0000 =                  BETA_VER	equ	0
                        ;
                        ; Options.  If RAM_BASED is set then the code is put
                        ; into RAM, else it's in ROM.  Very handy for testing
                        ; new code; load the new code with the EPROM version
                        ; and then test away.
                        ;
0000 =                  RAM_BASED	equ	false
af80 =                  RAM_DATA_BASE	equ	$af80
b000 =                  RAM_CODE_BASE	equ	$b000
                        ;
                        ; Set the EPROM base addresses
                        ;
e000 =                  ROM_START	equ	$e000
df80 =                  RAM_START	equ	$df80
                        ;
                        ; This is something that needs more investigation.
                        ; If the SD system isn't reset enough then bad stuff
                        ; happens after the RS button is pressed.  Set this to
                        ; true to only reset the SD system on cold starts.
                        ;
0000 =                  SD_ONLY_COLD	equ	false
                        ;
                        ; Set this to true to include Bob's Tiny BASIC.  It's
                        ; a minimal (ie, tiny) BASIC interpreter good for
                        ; playing around.
                        ;
0000 =                  TINY_BASIC	equ	false
                        ;
                        ; Set this to true to include a command to get/display
                        ; the current time from Corsham Tech SD card.
                        ;
ffff =                  SHOW_RTC	equ	true
                        ;
                        ; Non-printable ASCII constants
                        ;
0000 =                  NUL		equ	$00
0008 =                  BS		equ	$08
000a =                  LF		equ	$0a
000d =                  CR		equ	$0d
001b =                  ESC		equ	$1b
0020 =                  SPC		equ	$20
                        ;
                        ; Intel HEX record types
                        ;
0000 =                  DATA_RECORD	equ	$00
0001 =                  EOF_RECORD	equ	$01
                        ;
                        ; These are various buffer sizes
                        ;
000c =                  FILENAME_SIZE	equ	12
0040 =                  BUFFER_SIZE	equ	64
AS65 Assembler for R6502 [1.42].                                     Page    3
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Max number of bytes per line for hex dump
                        ;
0010 =                  BYTESLINE	equ	16
                        ;
                        ; Flag values used to detect a cold start vs warm
                        ;
0019 =                  COLD_FLAG_1	equ	$19
0062 =                  COLD_FLAG_2	equ	$62
                        ;
                        ;=====================================================
                        ; This macro is used to verify that the current addres
                        ; meets a required value.  Used mostly to guarantee
                        ; changes don't cause entry points to move.
                        ;
                        VERIFY		macro	expected
                        		if * != expected
                        		fail	Not at requested address (expected)
                        		endif
                        		endm
                        ;
                        ;=====================================================
                        ; KIM memory locations
                        ;
                        		bss
00ef =                  		org	$00ef
00ef =                  PCL		ds	1
00f0 =                  PCH		ds	1
00f1 =                  PREG		ds	1
00f2 =                  SPUSER		ds	1
00f3 =                  ACC		ds	1
00f4 =                  YREG		ds	1
00f5 =                  XREG		ds	1
00f6 =                  CHKHI		ds	1
00f7 =                  CHKSUM		ds	1
00f8 =                  INL	    	ds	1
00f9 =                  INH		ds	1
00fa =                  POINTL		ds	1
00fb =                  POINTH		ds	1
00fc =                  TEMP		ds	1
00fd =                  TMPX		ds	1
00fe =                  CHAR		ds	1
00ff =                  MODE		ds	1
                        ;
17e7 =                  		org	$17e7
17e7 =                  CHKL		ds	1
17e8 =                  CHKH		ds	1
17e9 =                  SAVX		ds	3
17ec =                  VEB	    	ds	6
17f2 =                  CNTL30		ds	1
17f3 =                  CNTH30		ds	1
17f4 =                  TIMH		ds	1
17f5 =                  SAL		ds	1
17f6 =                  SAH	    	ds	1
17f7 =                  EAL		ds	1
17f8 =                  EAH	    	ds	1
17f9 =                  ID  		ds	1
                        ;
                        ;=====================================================
                        ; KIM I/O locations
                        ;
1740 =                  SAD		equ	$1740
AS65 Assembler for R6502 [1.42].                                     Page    4
---------------------------------- xKIM.asm ----------------------------------

1741 =                  PADD		equ	$1741
                        ;
                        ;=====================================================
                        ; KIM subroutines located in the ROMs
                        ;
1c1c =                  NMIT		equ	$1c1c	;NMI handler
1c1f =                  IRQT		equ	$1c1f	;IRQ handler
1c22 =                  RST	    	equ	$1c22	;RESET handler
1c77 =                  TTYKB		equ	$1c77	;do keyboard monitor
1e2f =                  CRLF		equ	$1e2f	;print CR/LF
1e1e =                  PRTPNT		equ	$1e1e	;print POINT
1e3b =                  PRTBYT		equ	$1e3b	;print A as two hex digits
1e5a =                  GETCH		equ	$1e5a	;get a key from tty into A
1e9e =                  OUTSP		equ	$1e9e	;print a space
1ea0 =                  OUTCH		equ	$1ea0	;print A to TTY
1dac =                  SHOW		equ	$1dac
1daf =                  SHOW1		equ	$1daf
1f63 =                  INCPT		equ	$1f63	;inc POINTL/POINTH
                        ;
                        ;=====================================================
                        ; I assume the RAM goes from 2000 to DFFF, so carve ou
                        ; a bit for use by the monitor.
                        ;
                        		bss		;uninitialized data area
                        	if	RAM_BASED
                        		org     RAM_DATA_BASE
                        	else
df80 =                  		org	RAM_START
                        	endif
df80 =                  LowestAddress	equ	*
                        ;
                        ; Storage for registers for some monitor calls.
                        ;
df80 =                  saveA		ds	1
df81 =                  saveX		ds	1
df82 =                  saveY		ds	1
                        ;
                        ; Pointer to the subroutine that gets the next input
                        ; character.  Used for doing disk/console input.
                        ;
df83 =                  inputVector	ds	2
                        ;
                        ; Same thing for output.
                        ;
df85 =                  outputVector	ds	2
                        ;
df87 =                  filename	ds	FILENAME_SIZE+1
df94 =                  buffer		ds	BUFFER_SIZE
dfd4 =                  diskBufOffset	ds	1
dfd5 =                  diskBufLength	ds	1
                        ;
dfd6 =                  Temp16L		ds	1
dfd7 =                  Temp16H		ds	1
df81 =                  byteCount	equ	saveX
                        ;
                        ; The clock functions need some storage, but just
                        ; overlay the names onto the buffer.
                        ;
df94 =                  month		equ	buffer
df95 =                  day		equ	month+1
df96 =                  year_high	equ	day+1
df97 =                  year_low	equ	year_high+1
AS65 Assembler for R6502 [1.42].                                     Page    5
---------------------------------- xKIM.asm ----------------------------------

df98 =                  hour		equ	year_low+1
df99 =                  minute		equ	hour+1
df9a =                  second		equ	minute+1
df9b =                  day_of_week	equ	second+1
df9c =                  clock_end	equ	day_of_week+1
                        ;
                        ; The next group of memory are public and can be
                        ; used by user programs, so don't modify where any
                        ; of these are.  If you want to add new public data,
                        ; put them here, always before existing items.  Be
                        ; sure to adjust the ORG.
                        ;
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE-8
                        	else
dff8 =                  		org	ROM_START-8
                        	endif
                        ;
                        ; Before loading a hex file, the MSB of this vector
                        ; is set to FF.  After loading the file, if the MSB
                        ; is no longer FF then the address in this vector is
                        ; jumped to.  Ie, it can auto-run a file.
                        ;
dff8 =                  AutoRun		ds	2
                        ;
                        ; ColdFlag is used to determine if the extended
                        ; monitoring is doing a warm or cold start.
                        ;
dffa =                  ColdFlag	ds	2
                        ;
                        ; Address of a command table for user-created
                        ; extensions to the monitor.
                        ;
dffc =                  ExtensionAddr	ds	2
                        ;
                        ; This is the higest location in RAM usable by user
                        ; programs.  Nobody should go past this address.  If
                        ; you are writing extentions to the monitor, it's
                        ; okay to load before the address and then adjust
                        ; this down to keep others from stomping on your
                        ; extention.
                        ;
                        ; If your program modifies this value, it needs to
                        ; set it back before terminating.
                        ;
dffe =                  HighestAddress	ds	2
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    6
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Code starts at E000 and goes until FFFF, except for
                        ; the 6502 vectors at the end of memory.
                        ;
                        		code
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE
                        	else
e000 =                  		org	ROM_START
                        	endif
                        ;
                        ; Vector table of useful and fun stuff!  These must
                        ; not change or else existing code won't be calling
                        ; the right functions.
                        ;
e000 =                  BASE		equ	*
e000 : 4c60e0           reentry		jmp	extKim	;extended monitor
e003 : 4ca01e           		jmp	OUTCH	;output A to console
e006 : 4c5a1e           		jmp	GETCH	;get a key and echo
e009 : 4c5a1e           		jmp	GETCH	;no echo - KIM can't do it
e00c : 4c5ae0           		jmp	dummyRet	;future - console stat
e00f : 4c34ea           		jmp	putsil	;print string after JSR
e012 : 4c63ea           		jmp	getHex	;get hex value in A
e015 : 4c3b1e           		jmp	PRTBYT	;print A as hex
e018 : 4c9bea           		jmp	getStartAddr
e01b : 4cadea           		jmp	getEndAddr
e01e : 4cbfea           		jmp	getAddrRange
e021 : 4cb3e3           		jmp	doHexDump	;perform a hex dump
e024 : 4c4ee5           		jmp	doEdit		;edit memory
e027 : 4c6ce6           		jmp	loadHexConsole	;load hex file via console
e02a : 4c3de6           		jmp	loadHexFile	;load hex file from SD
e02d : 4cfee8           		jmp	doSDDiskDir	;do a disk directory
e030 : 4cd0ea           		jmp	ComputeOffset	;compute relative offset
                        ;
                        ; SD card functions
                        ;
                        		VERIFY	BASE+$0033
                        
e033 : 4cdbeb           		jmp	xParInit	;initialization
e036 : 4cf5eb           		jmp	xParSetWrite
e039 : 4c05ec           		jmp	xParSetRead
e03c : 4c13ec           		jmp	xParWriteByte
e03f : 4c3eec           		jmp	xParReadByte
e042 : 4c62ec           		jmp	DiskPing
e045 : 4c75ec           		jmp	DiskDir
e048 : 4c7fec           		jmp	DiskDirNext
e04b : 4cb0ec           		jmp	DiskOpenRead
e04e : 4ce1ec           		jmp	DiskRead
e051 : 4c4aed           		jmp	DiskClose
e054 : 4cdcec           		jmp	DiskOpenWrite
e057 : 4c13ed           		jmp	DiskWrite
                        ;
                        ; Even more vectors as I ran out of the reserve area!
                        ;
                        
                        ;
                        ;=====================================================
                        ;=====================================================
                        ; Anything past this point can be moved in future
                        ; releases so none of these functions nor data should
                        ; be directly accessed by user programs!!!
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    7
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is a dummy function for unimplemented functions
                        ; that basically just clears carry and returns.
                        ;
e05a : 18               dummyRet	clc
e05b : 60               		rts
                        ;
e05c : 00               defaultExt	db	0
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    8
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is the start of the extended KIM monitor.
                        ;
e05d : 4c771c           notty		jmp	TTYKB
e060 : a2ff             extKim		ldx	#$ff
e062 : 9a               		txs
e063 : a901             		lda	#$01	;see if in tty mode
e065 : 2c4017           		bit	SAD
e068 : d0f3             		bne	notty	;branch if in keyboard mode
                        
                        	if	~SD_ONLY_COLD
e06a : 20dbeb           		jsr	xParInit
                        	endif
                        ;
                        ; Determine if this is a cold or warm start
                        ;
e06d : adfadf           		lda	ColdFlag
e070 : c919             		cmp	#COLD_FLAG_1
e072 : d00a             		bne	coldStart
e074 : adfbdf           		lda	ColdFlag+1
e077 : c962             		cmp	#COLD_FLAG_2
e079 : d003             		bne	coldStart
e07b : 4cf1e0           		jmp	extKimLoop	;it's a warm start
                        ;
                        ; Cold start
                        ;
e07e : a919             coldStart	lda	#COLD_FLAG_1	;indicate we've done cold
e080 : 8dfadf           		sta	ColdFlag
e083 : a962             		lda	#COLD_FLAG_2
e085 : 8dfbdf           		sta	ColdFlag+1
                        ;
                        ; Point to an empty extension set by default.
                        ;
e088 : a95c             		lda	#defaultExt&$ff	;set extension pointers
e08a : 8dfcdf           		sta	ExtensionAddr
e08d : a9e0             		lda	#defaultExt/256
e08f : 8dfddf           		sta	ExtensionAddr+1
                        ;
                        ; Set HighestAddress to just before our RAM area.
                        ;
e092 : a97f             		lda	#(LowestAddress-1)&$ff
e094 : 8dfedf           		sta	HighestAddress
e097 : a9df             		lda	#(LowestAddress-1)/256
e099 : 8dffdf           		sta	HighestAddress+1
                        	if	SD_ONLY_COLD
                        ;
                        ; Initialize the interface to the SD card.  There seem
                        ; be a problem if this is done too often, so do it jus
                        ; and be done.
                        ;
                        		jsr	xParInit
                        	endif
                        ;
                        ; Display our welcome text
                        ;
e09c : 2034ea           		jsr	putsil
e09f : 0d0a0d0a         		db	CR,LF,CR,LF
e0a3 : 457874656e6465.. 		db	"Extended KIM Monitor v"
e0b9 : 312e3820         		db	VERSION+'0','.',REVISION+'0',' '
                        	if	BETA_VER
                        		db	"BETA "
                        		db	BETA_VER+'0'
AS65 Assembler for R6502 [1.42].                                     Page    9
---------------------------------- xKIM.asm ----------------------------------

                        		db	' '
                        	endif
e0bd : 627920436f7273.. 		db	"by Corsham Technologies, LLC"
e0d9 : 0d0a             		db	CR,LF
e0db : 7777772e636f72.. 		db	"www.corshamtech.com"
e0ee : 0d0a             		db	CR,LF
                        	if	RAM_BASED
                        		db	CR,LF
                        		db	"*** RAM BASED VERSION ***"
                        		db	CR,LF
                        	endif
e0f0 : 00               		db	0
                        ;
                        ; Main command loop.  Put out prompt, get command, etc
                        ; Prints a slightly different prompt for the RAM versi
                        ;
e0f1 : d8               extKimLoop	cld
e0f2 : 205aeb           		jsr	setInputConsole
e0f5 : 2034ea           		jsr	putsil	;output prompt
e0f8 : 0d0a             		db	CR,LF	;feel free to change it
                        	if	RAM_BASED
                        		db	"RAM"
                        	endif
e0fa : 3e00             		db	">",0
e0fc : 205a1e           		jsr	GETCH
e0ff : c90d             		cmp	#CR
e101 : f0ee             		beq	extKimLoop
e103 : c90a             		cmp	#LF
e105 : f0ea             		beq	extKimLoop
e107 : 85f3             		sta	ACC	;save key
                        ;
                        ; Now cycle through the list of commands looking for
                        ; what the user just pressed.
                        ;
e109 : a92f             		lda	#commandTable&$ff
e10b : 85fa             		sta	POINTL
e10d : a9e1             		lda	#commandTable/256
e10f : 85fb             		sta	POINTH
e111 : 20e2e9           		jsr	searchCmd	;try to find it
                        ;
                        ; Hmmm... wasn't one of the built in commands, so
                        ; see if it's an extended command.
                        ;
e114 : adfcdf           		lda	ExtensionAddr
e117 : 85fa             		sta	POINTL
e119 : adfddf           		lda	ExtensionAddr+1
e11c : 85fb             		sta	POINTH
e11e : 20e2e9           		jsr	searchCmd
                        ;
                        ; If that returns, then the command was not found.
                        ; Print that it's unknown.
                        ;
e121 : 2034ea           		jsr	putsil
e124 : 202d204875683f00 		db	" - Huh?",0
e12c : 4cf1e0           cmdFound	jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Vector table of commands.  Each entry consists of a
                        ; single ASCII character (the command), a pointer to
                        ; the function which handles the command, and a pointe
                        ; to a string that describes the command.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   10
---------------------------------- xKIM.asm ----------------------------------

e12f : 3f               commandTable	db	'?'
e130 : 4ce3             		dw	showHelp
e132 : 7be1             		dw	quesDesc
                        ;
                        	if	TINY_BASIC
                        		db	'B'
                        		dw	TBasicCold
                        		dw	bDesc
                        	endif
                        ;
                        	if	SHOW_RTC
e134 : 43               		db	'C'
e135 : 4fed             		dw	doShowClock
e137 : 98e1             		dw	cDesc
                        	endif
e139 : 44               		db	'D'
e13a : e1e8             		dw	doDiskDir
e13c : b1e1             		dw	dDesc
                        ;
e13e : 45               		db	'E'	;edit memory
e13f : 33e5             		dw	editMemory
e141 : cee1             		dw	eDesc
                        ;
e143 : 48               		db	'H'	;hex dump
e144 : a5e3             		dw	hexDump
e146 : e8e1             		dw	hDesc
                        ;
e148 : 4a               		db	'J'	;jump to address
e149 : b1e8             		dw	jumpAddress
e14b : 06e2             		dw	jDesc
                        ;
e14d : 4b               		db	'K'	;return to KIM monitor
e14e : 20e3             		dw	returnKim
e150 : 24e2             		dw	kDesc
                        ;
e152 : 4c               		db	'L'	;load Intel HEX file
e153 : e1e5             		dw	loadHex
e155 : 44e2             		dw	lDesc
                        ;
e157 : 4d               		db	'M'	;perform memory test
e158 : 38e4             		dw	memTest
e15a : 60e2             		dw	mDesc
                        ;
e15c : 4f               		db	'O'	;branch offset calculator
e15d : 72e9             		dw	offCalc
e15f : 7ae2             		dw	oDesc
                        ;
e161 : 50               		db	'P'	;ping remote disk
e162 : c2e8             		dw	pingDisk
e164 : a0e2             		dw	pDesc
                        ;
e166 : 53               		db	'S'	;save memory as hex file
e167 : 78e7             		dw	saveHex
e169 : c3e2             		dw	sDesc
                        ;
e16b : 54               		db	'T'	;type a file on SD
e16c : 1de9             		dw	typeFile
e16e : e5e2             		dw	tDesc
                        ;
e170 : 58               		db	'X'	;return to KIM monitor
e171 : 20e3             		dw	returnKim
e173 : 24e2             		dw	kDesc
AS65 Assembler for R6502 [1.42].                                     Page   11
---------------------------------- xKIM.asm ----------------------------------

                        ;
e175 : 21               		db	'!'	;do cold restart
e176 : 46e3             		dw	doCold
e178 : 02e3             		dw	bangDesc
                        ;
e17a : 00               		db	0	;marks end of table
                        ;
                        ;=====================================================
                        ; Descriptions for each command in the command table.
                        ; This wastes a lot of space... I'm open for any
                        ; suggestions to keep the commands clear but reducing
                        ; the amount of space this table consumes.
                        ;
e17b : 3f202e2e2e2e2e.. quesDesc	db	"? ........... Show this help",0
                        	if	TINY_BASIC
                        bDesc		db	"B ........... Bob's Tiny BASIC",0
                        	endif
                        	if	SHOW_RTC
e198 : 43202e2e2e2e2e.. cDesc		db	"C ........... Show clock",0
                        	endif
e1b1 : 44202e2e2e2e2e.. dDesc		db	"D ........... Disk directory",0
e1ce : 45207878787820.. eDesc		db	"E xxxx ...... Edit memory",0
e1e8 : 48207878787820.. hDesc		db	"H xxxx xxxx . Hex dump memory",0
e206 : 4a207878787820.. jDesc		db	"J xxxx ...... Jump to address",0
e224 : 4b202e2e2e2e2e.. kDesc		db	"K ........... Go to KIM monitor",0
e244 : 4c202e2e2e2e2e.. lDesc		db	"L ........... Load HEX file",0
e260 : 4d207878787820.. mDesc		db	"M xxxx xxxx . Memory test",0
e27a : 4f207878787820.. oDesc		db	"O xxxx xxxx . Calculate branch offset",0
e2a0 : 50202e2e2e2e2e.. pDesc		db	"P ........... Ping disk controller",0
e2c3 : 53207878787820.. sDesc		db	"S xxxx xxxx . Save memory to file",0
e2e5 : 54202e2e2e2e2e.. tDesc		db	"T ........... Type disk file",0
e302 : 21202e2e2e2e2e.. bangDesc	db	"! ........... Do a cold start",0
                        ;
                        ;=====================================================
                        ; Return to KIM monitor.  Before returning, set the
                        ; "open address" to the start of the extended monitor
                        ; so the KIM monitor is pointing to it by default.
                        ;
e320 : 2034ea           returnKim	jsr	putsil
e323 : 0d0a             		db	CR,LF
e325 : 52657475726e69.. 		db	"Returning to KIM..."
e338 : 0d0a00           		db	CR,LF,0
e33b : a900             		lda	#reentry&$0ff
e33d : 85fa             		sta	POINTL	;point back to start...
e33f : a9e0             		lda	#reentry/256
e341 : 85fb             		sta	POINTH	;...of this code
e343 : 4caf1d           		jmp	SHOW1	;return to KIM
                        ;
                        ;=====================================================
                        ; Force a cold start.
                        ;
e346 : eefadf           doCold		inc	ColdFlag	;foul up flag
e349 : 4c60e0           		jmp	extKim		;...and restart
                        ;
                        ;=====================================================
                        ; Command handler for the ? command
                        ;
e34c : 2034ea           showHelp	jsr	putsil
e34f : 0d0a             		db	CR,LF
e351 : 417661696c6162.. 		db	"Available commands:"
e364 : 0d0a0a00         		db	CR,LF,LF,0
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   12
---------------------------------- xKIM.asm ----------------------------------

                        ; Print help for built-in commands...
                        ;
e368 : a92f             		lda	#commandTable&$ff
e36a : 85fa             		sta	POINTL
e36c : a9e1             		lda	#commandTable/256
e36e : 85fb             		sta	POINTH
e370 : 2005ea           		jsr	displayHelp	;display help
                        ;
                        ; Now print help for the extension commands...
                        ;
e373 : adfcdf           		lda	ExtensionAddr
e376 : 85fa             		sta	POINTL
e378 : adfddf           		lda	ExtensionAddr+1
e37b : 85fb             		sta	POINTH
e37d : 2005ea           		jsr	displayHelp
e380 : 202f1e           		jsr	CRLF
e383 : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This is a generic "not done yet" holder.  Any
                        ; unimplemented commands should point here.
                        ;
e386 : 2034ea           NDY		jsr	putsil
e389 : 0d0a             		db	CR,LF
e38b : 536f7272792c20.. 		db	"Sorry, not done yet."
e39f : 0d0a00           		db	CR,LF,0
e3a2 : 4cf1e0           NDYdone		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a hex dump of a region of memory.  This code was
                        ; taken from MICRO issue 5, from an article by
                        ; J.C. Williams.  I changed it a bit, but it's still
                        ; basically the same code.
                        ;
                        ; Slight bug: the starting address is rounded down to
                        ; a multiple of 16.  I'll fix it eventually.
                        ;
e3a5 : 20bfea           hexDump		jsr	getAddrRange
e3a8 : b0f8             		bcs	NDYdone
e3aa : 202f1e           		jsr	CRLF
e3ad : 20b3e3           		jsr	doHexDump	;subroutine does it
e3b0 : 4cf1e0           cmdRet2		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine does a hex dump from the address in
                        ; SAL/H to EAL/H.
                        ;
                        ; Move start address to POINT but rounded down to the
                        ; 16 byte boundary.
                        ;
e3b3 : adf617           doHexDump	lda	SAH
e3b6 : 85fb             		sta	POINTH
e3b8 : adf517           		lda	SAL
e3bb : 29f0             		and	#$f0	;force to 16 byte
e3bd : 85fa             		sta	POINTL
                        ;
                        ; This starts each line.  Set flag to indcate we're
                        ; doing the hex portion, print address, etc.
                        ;
e3bf : a900             hexdump1	lda	#0	;set flag to hex mode
e3c1 : 8df917           		sta	ID
e3c4 : 202f1e           		jsr	CRLF
AS65 Assembler for R6502 [1.42].                                     Page   13
---------------------------------- xKIM.asm ----------------------------------

e3c7 : 201e1e           		jsr	PRTPNT	;print the address
e3ca : a5fa             hexdump2	lda	POINTL	;push start of line...
e3cc : 48               		pha		;...address onto stack
e3cd : a5fb             		lda	POINTH
e3cf : 48               		pha
e3d0 : 202eea           		jsr	space2
e3d3 : a20f             		ldx	#BYTESLINE-1	;number of bytes per line
e3d5 : 202eea           		jsr	space2	;space before data
                        
e3d8 : a000             hexdump3	ldy	#0	;get next byte...
e3da : b1fa             		lda	(POINTL),y
e3dc : 2cf917           		bit	ID	;hex or ASCII mode?
e3df : 1010             		bpl	hexptbt	;branch if hex mode
                        ;
                        ; Print char if printable, else print a dot
                        ;
e3e1 : c920             		cmp	#' '
e3e3 : 9004             		bcc	hexdot
e3e5 : c97e             		cmp	#'~'
e3e7 : 9002             		bcc	hexpr
e3e9 : a92e             hexdot		lda	#'.'
e3eb : 20a01e           hexpr		jsr	OUTCH
e3ee : 4cf7e3           		jmp	hexend
                        ;
                        ; Print character as hex.  
                        ;
e3f1 : 203b1e           hexptbt 	jsr	PRTBYT	;print as hex
e3f4 : 2031ea           		jsr	space	;and follow with a space
                        ;
                        ; See if we just dumped the last address.  If not, the
                        ; increment to the next address and continue.
                        ;
e3f7 : a5fa             hexend  	lda	POINTL	;compare first
e3f9 : cdf717           		cmp	EAL
e3fc : a5fb             		lda	POINTH
e3fe : edf817           		sbc	EAH
                        ;
                        ; Now increment to the next address
                        ;
e401 : 08               		php
e402 : 20631f           		jsr	INCPT
e405 : 28               		plp
e406 : 901a             		bcc	hexlntst
                        ;
e408 : 2cf917           		bit	ID
e40b : 3022             		bmi	hexdone
e40d : ca               		dex
e40e : 3006             		bmi	hexdomap
e410 : 202bea           hexdump5	jsr	space3
e413 : ca               		dex
e414 : 10fa             		bpl	hexdump5
e416 : cef917           hexdomap	dec	ID
e419 : 68               		pla
e41a : 85fb             		sta	POINTH
e41c : 68               		pla
e41d : 85fa             		sta	POINTL
e41f : 4ccae3           		jmp     hexdump2
e422 : ca               hexlntst	dex
e423 : 10b3             		bpl	hexdump3
e425 : 2cf917           		bit	ID
e428 : 10ec             		bpl	hexdomap
e42a : 68               		pla
AS65 Assembler for R6502 [1.42].                                     Page   14
---------------------------------- xKIM.asm ----------------------------------

e42b : 68               		pla
e42c : 4cbfe3           		jmp	hexdump1
                        ;
                        ; Clean up the stack and we're done
                        ;
e42f : 202f1e           hexdone		jsr	CRLF
e432 : 68               		pla
e433 : 68               		pla
e434 : 60               		rts
                        ;
                        ;=====================================================
                        ; This does a memory test of a region of memory.  One
                        ; problem with the KIM is that there is no routine to
                        ; see if a new character is starting, so this loop
                        ; just runs forever unless the user presses RESET.
                        ;
                        ; Asks for the starting and ending locations.
                        ;
                        ; This cycles a rolling bit, then adds a ninth
                        ; pattern to help detect shorted address bits.
                        ; Ie: 01, 02, 04, 08, 10, 20, 40, 80, BA
                        ;
17e7 =                  pattern		equ	CHKL		;re-use some KIM locations
17e8 =                  original	equ	CHKH
                        ;
                        ; Test patterns
                        ;
0001 =                  PATTERN_0	equ	$01
00ba =                  PATTERN_9	equ	$ba
                        ;
e435 : 4cf1e0           cmdRet5		jmp	extKimLoop
e438 : 20bfea           memTest		jsr	getAddrRange	;get range
e43b : b0f8             		bcs	cmdRet5		;branch if abort
                        ;
e43d : 2034ea           		jsr	putsil
e440 : 0d0a             		db	CR,LF
e442 : 54657374696e67.. 		db	"Testing memory.  Press RESET to abort"
e467 : 00               		db	0
e468 : a901             		lda	#PATTERN_0	;only set initial...
e46a : 8de717           		sta	pattern		;..pattern once
                        ;
                        ; Start of loop.  This fills/tests one complete pass
                        ; of memory.
                        ;
e46d : adf517           memTestMain	lda	SAL	;reset pointer to start
e470 : 85fa             		sta	POINTL
e472 : adf617           		lda	SAH
e475 : 85fb             		sta	POINTH
                        ;
                        ; Fill memory with the rolling pattern until the last
                        ; location is filled.
                        ;
e477 : a000             		ldy	#0
e479 : ade717           		lda	pattern
e47c : 8de817           		sta	original
e47f : 91fa             memTestFill	sta	(POINTL),y
e481 : c9ba             		cmp	#PATTERN_9	;at last pattern?
e483 : d005             		bne	memFill3
e485 : a901             		lda	#PATTERN_0	;restart pattern
e487 : 4c8fe4           		jmp	memFill4
                        ;
                        ; Rotate pattern left one bit
AS65 Assembler for R6502 [1.42].                                     Page   15
---------------------------------- xKIM.asm ----------------------------------

                        ;
e48a : 0a               memFill3	asl	a
e48b : 9002             		bcc	memFill4	;branch if not overflow
e48d : a9ba             		lda	#PATTERN_9	;ninth pattern
                        ;
                        ; The new pattern is in A.  Now see if we've reached
                        ; the end of the area to be tested.
                        ;
e48f : 48               memFill4	pha			;save pattern
e490 : a5fa             		lda	POINTL
e492 : cdf717           		cmp	EAL
e495 : d007             		bne	memFill5
e497 : a5fb             		lda	POINTH
e499 : cdf817           		cmp	EAH
e49c : f007             		beq	memCheck
                        ;
                        ; Not done, so move to next address and keep going.
                        ;
e49e : 20631f           memFill5	jsr	INCPT
e4a1 : 68               		pla			;recover pattern
e4a2 : 4c7fe4           		jmp	memTestFill
                        ;
                        ; Okay, memory is filled, so now go back and test it.
                        ; We kept a backup copy of the initial pattern to
                        ; use, but save the current pattern as the starting
                        ; point for the next pass.
                        ;
e4a5 : 68               memCheck	pla
e4a6 : 8de717           		sta	pattern		;for next pass
e4a9 : adf517           		lda	SAL		;reset pointer to start
e4ac : 85fa             		sta	POINTL
e4ae : adf617           		lda	SAH
e4b1 : 85fb             		sta	POINTH
e4b3 : ade817           		lda	original	;restore initial pattern
e4b6 : a000             		ldy	#0
e4b8 : d1fa             memTest2	cmp	(POINTL),y
e4ba : d02c             		bne	memFail
e4bc : c9ba             		cmp	#PATTERN_9
e4be : d004             		bne	memTest3
                        ;
                        ; Time to reload the pattern
                        ;
e4c0 : a901             		lda	#PATTERN_0
e4c2 : d005             		bne	memTest4
                        ;
                        ; Rotate pattern left one bit
                        ;
e4c4 : 0a               memTest3	asl	a
e4c5 : 9002             		bcc	memTest4
e4c7 : a9ba             		lda	#PATTERN_9
                        ;
                        ; The new pattern is in A.
                        ;
e4c9 : 48               memTest4	pha			;save pattern
e4ca : a5fa             		lda	POINTL
e4cc : cdf717           		cmp	EAL
e4cf : d007             		bne	memTest5	;not at end
e4d1 : a5fb             		lda	POINTH
e4d3 : cdf817           		cmp	EAH
e4d6 : f007             		beq	memDone		;at end of pass
                        ;
                        ; Not at end yet, so inc pointer and continue
AS65 Assembler for R6502 [1.42].                                     Page   16
---------------------------------- xKIM.asm ----------------------------------

                        ;
e4d8 : 20631f           memTest5	jsr	INCPT
e4db : 68               		pla
e4dc : 4cb8e4           		jmp	memTest2
                        ;
                        ; Another pass has completed.
                        ;
e4df : 68               memDone		pla
e4e0 : a92e             		lda	#'.'
e4e2 : 20a01e           		jsr	OUTCH
e4e5 : 4c6de4           		jmp	memTestMain
                        ;
                        ; Failure.  Display the failed address, the expected
                        ; value and what was actually there.
                        ;
e4e8 : 48               memFail		pha		;save pattern for error report
e4e9 : 2034ea           		jsr	putsil
e4ec : 0d0a             		db	CR,LF
e4ee : 4661696c757265.. 		db	"Failure at address ",0
e502 : 201e1e           		jsr	PRTPNT
e505 : 2034ea           		jsr	putsil
e508 : 2e202045787065.. 		db	".  Expected ",0
e515 : 68               		pla
e516 : 203b1e           		jsr	PRTBYT
e519 : 2034ea           		jsr	putsil
e51c : 2062757420676f.. 		db	" but got ",0
e526 : a000             		ldy	#0
e528 : b1fa             		lda	(POINTL),y
e52a : 203b1e           		jsr	PRTBYT
e52d : 202f1e           		jsr	CRLF
e530 : 4cf1e0           cmdRet4		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Edit memory.  This waits for a starting address to b
                        ; entered.  It will display the current address and it
                        ; contents.  Possible user inputs and actions:
                        ;
                        ;   Two hex digits will place that value in memory
                        ;   RETURN moves to next address
                        ;   BACKSPACE moves back one address
                        ;
e533 : 2031ea           editMemory      jsr	space
e536 : 209bea           		jsr	getStartAddr
e539 : b0f5             		bcs	cmdRet4
e53b : adf517           		lda	SAL		;move address into...
e53e : 85fa             		sta	POINTL		;...POINT
e540 : adf617           		lda	SAH
e543 : 85fb             		sta	POINTH
e545 : 202f1e           		jsr	CRLF
e548 : 204ee5           		jsr	doEdit
e54b : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine edits memory.  On entry, POINT has
                        ; the first address to edit.  Upon exit, POINT will
                        ; have been updated to next address to edit.
                        ;
                        ; Display the current location
                        ;
e54e : 201e1e           doEdit		jsr	PRTPNT		;print address
e551 : 2031ea           		jsr	space
e554 : a000             		ldy	#0
AS65 Assembler for R6502 [1.42].                                     Page   17
---------------------------------- xKIM.asm ----------------------------------

e556 : b1fa             		lda	(POINTL),y	;get byte
e558 : 203b1e           		jsr	PRTBYT		;print it
e55b : 2031ea           		jsr	space
                        ;
e55e : 2063ea           		jsr	getHex
e561 : b00d             		bcs	editMem2	;not hex
e563 : a000             editMem7	ldy	#0
e565 : 91fa             		sta	(POINTL),y	;save new value
                        ;
                        ; Bump POINT to next location
                        ;
e567 : 202f1e           editMem3	jsr	CRLF
e56a : 20631f           		jsr	INCPT
e56d : 4c4ee5           		jmp	doEdit
                        ;
                        ; Not hex, so see if another command.  Valid commands 
                        ;
                        ;    CR = advance to next memory location
                        ;    BS = move to previous location
                        ;    R  = compute relative offset
                        ;
e570 : c952             editMem2	cmp	#'R'		;compute relative branch
e572 : f017             		beq	editMem4
e574 : c90d             		cmp	#CR
e576 : f0ef             		beq	editMem3	;move to next
e578 : c908             		cmp	#BS
e57a : d00e             		bne     editexit		;else exit
                        ;
                        ; Move back one location
                        ;
e57c : a5fa             		lda	POINTL
e57e : d002             		bne	editMem8
e580 : c6fb             		dec	POINTH
e582 : c6fa             editMem8	dec	POINTL
e584 : 202f1e           		jsr	CRLF
e587 : 4c4ee5           		jmp	doEdit
                        ;
e58a : 60               editexit	rts
                        ;
                        ; They want to calculate a relative offset
                        ;
e58b : 2034ea           editMem4	jsr	putsil
e58e : 656c6174697665.. 		db	"elative offset to: ",0
e5a2 : 20adea           		jsr	getEndAddr
e5a5 : b0a7             		bcs	doEdit		;bad input
                        ;
                        ; Need to load POINTL/POINTH into SAL/SAH and then
                        ; decrement by one.
                        ;
e5a7 : a5fb             		lda	POINTH
e5a9 : 8df617           		sta	SAH
e5ac : a5fa             		lda	POINTL
e5ae : 8df517           		sta	SAL
e5b1 : d003             		bne	editMem5
e5b3 : cef617           		dec	SAH
e5b6 : cef517           editMem5	dec	SAL
                        ;
e5b9 : 20d0ea           		jsr	ComputeOffset
e5bc : 9016             		bcc	editMem6	;value good
e5be : 2034ea           		jsr	putsil
e5c1 : 202d206f757420.. 		db	" - out of range",0
e5d1 : 4c4ee5           		jmp	doEdit
AS65 Assembler for R6502 [1.42].                                     Page   18
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Relative offset is in A.
                        ;
e5d4 : 48               editMem6	pha
e5d5 : 2031ea           		jsr	space
e5d8 : 68               		pla
e5d9 : 48               		pha
e5da : 203b1e           		jsr	PRTBYT		;print it
e5dd : 68               		pla
e5de : 4c63e5           		jmp	editMem7	;store it
                        ;
                        ;=====================================================
                        ; This handles the Load hex command.
                        ;
e5e1 : 2034ea           loadHex		jsr	putsil
e5e4 : 0d0a             		db	CR,LF
e5e6 : 456e7465722066.. 		db	"Enter filename, or Enter to "
e602 : 6c6f6164206672.. 		db	"load from console: ",0
                        ;
e616 : 2002eb           		jsr	getFileName	;get filename
e619 : ad87df           		lda	filename	;null?
e61c : d006             		bne	loaddiskfile
e61e : 206ce6           		jsr	loadHexConsole	;load from console
e621 : 4c30e6           		jmp	loadCheckAuto	;check auto-run
                        ;
                        ; Open the file
                        ;
e624 : a087             loaddiskfile	ldy	#filename&$ff
e626 : a2df             		ldx	#filename/256
e628 : a9ff             		lda	#$ff
e62a : 8df917           		sta	ID		;print dots
e62d : 203de6           		jsr	loadHexFile
                        ;
                        ; If the auto-run vector is no longer $ffff, then jump
                        ; to whatever it points to.
                        ;
e630 : adf9df           loadCheckAuto	lda	AutoRun+1
e633 : c9ff             		cmp	#$ff		;unchanged?
e635 : f003             		beq	lExit11
e637 : 6cf8df           		jmp	(AutoRun)	;execute!
e63a : 4cf1e0           lExit11		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine loads a hex file from the SD.  On
                        ; entry the pointer to the filename is in X (MSB) and
                        ; Y (LSB).
                        ;
e63d : a9ff             loadHexFile	lda	#$ff
e63f : 8df9df           		sta	AutoRun+1
e642 : 8df917           		sta	ID		;we want dots
e645 : 20b0ec           		jsr	DiskOpenRead
e648 : 901c             		bcc	loadHexOk	;opened ok
                        ;
e64a : 2034ea           openfail	jsr	putsil
e64d : 0d0a             		db	CR,LF
e64f : 4661696c656420.. 		db	"Failed to open file"
e662 : 0d0a00           		db	CR,LF,0
e665 : 60               		rts
                        ;
e666 : 2065eb           loadHexOk	jsr	setInputFile	;redirect input
e669 : 4ca4e6           		jmp	loadStart
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   19
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This subroutine is called to load a hex file from
                        ; the console.
                        ;
e66c : a9ff             loadHexConsole	lda	#$ff
e66e : 8df9df           		sta	AutoRun+1
e671 : a900             		lda	#0
e673 : 8df917           		sta	ID		;don't print dots
e676 : 2034ea           		jsr	putsil
e679 : 0d0a             		db	CR,LF
e67b : 57616974696e67.. 		db	"Waiting for file, or ESC to"
e696 : 20657869742e2e2e 		db	" exit..."
e69e : 0d0a00           		db	CR,LF,0
e6a1 : 205aeb           		jsr	setInputConsole
                        ;
                        ; The start of a line.  First character should be a
                        ; colon, but toss out CRs, LFs, etc.  Anything else
                        ; causes an abort.
                        ;
e6a4 : 2057eb           loadStart	jsr	redirectedGetch	;get start of line
e6a7 : c90d             		cmp	#CR
e6a9 : f0f9             		beq	loadStart
e6ab : c90a             		cmp	#LF
e6ad : f0f5             		beq	loadStart
e6af : c93a             		cmp	#':'		;what we expect
e6b1 : d05a             		bne	loadAbortB
                        ;
                        ; Get the header of the record
                        ;
e6b3 : a900             		lda	#0
e6b5 : 8de717           		sta	CHKL		;initialize checksum
                        ;
e6b8 : 2063ea           		jsr	getHex		;get byte count
e6bb : b054             		bcs	loadAbortC
e6bd : 8d81df           		sta	byteCount	;save byte count
e6c0 : 20a7e8           		jsr	updateCrc
e6c3 : 2063ea           		jsr	getHex		;get the MSB of offset
e6c6 : b04d             		bcs	loadAbortD
e6c8 : 85fb             		sta	POINTH
e6ca : 20a7e8           		jsr	updateCrc
e6cd : 2063ea           		jsr	getHex		;get LSB of offset
e6d0 : b047             		bcs	loadAbortE
e6d2 : 85fa             		sta	POINTL
e6d4 : 20a7e8           		jsr	updateCrc
e6d7 : 2063ea           		jsr	getHex		;get the record type
e6da : b041             		bcs	loadAbortF
e6dc : 20a7e8           		jsr	updateCrc
                        ;
                        ; Only handle two record types:
                        ;    00 = data record
                        ;    01 = end of file record
                        ;
e6df : c900             		cmp	#DATA_RECORD
e6e1 : f05d             		beq	loadDataRec
e6e3 : c901             		cmp	#EOF_RECORD
e6e5 : f042             		beq	loadEof
                        ;
                        ; Unknown record type
                        ;
e6e7 : a941             		lda	#'A'		;reason
                        ;
                        ; This is the common error handler for various reasons
AS65 Assembler for R6502 [1.42].                                     Page   20
---------------------------------- xKIM.asm ----------------------------------

                        ; On entry A contains an ASCII character which is outp
                        ; to indicate the specific error reason.
                        ;
e6e9 : 48               loadAbort       pha			;save reason
e6ea : 2034ea           		jsr	putsil
e6ed : 0d0a             		db	CR,LF
e6ef : 41626f7274696e.. 		db	"Aborting, reason: "
e701 : 00               		db	0
e702 : 68               		pla			;restore and...
e703 : 20a01e           		jsr	OUTCH		;...display reason
e706 : 202f1e           		jsr	CRLF
e709 : 205aeb           loadExit	jsr	setInputConsole
e70c : 60               		rts
                        ;
                        ; Various error reason codes.  This was meant to be
                        ; very temporary as I worked out the real problem, but
                        ; this debug code immediately "solved" the problem so
                        ; I just left these as-is until the root cause is
                        ; discovered.
                        ;
e70d : a942             loadAbortB	lda	#'B'
e70f : d0d8             		bne	loadAbort
                        ;
e711 : a943             loadAbortC	lda	#'C'
e713 : d0d4             		bne	loadAbort
                        ;
e715 : a944             loadAbortD	lda	#'D'
e717 : d0d0             		bne	loadAbort
                        ;
e719 : a945             loadAbortE	lda	#'E'
e71b : d0cc             		bne	loadAbort
                        ;
e71d : a946             loadAbortF	lda	#'F'
e71f : d0c8             		bne	loadAbort
                        ;
e721 : a947             loadAbortG	lda	#'G'
e723 : d0c4             		bne	loadAbort
                        ;
e725 : a948             loadAbortH	lda	#'H'
e727 : d0c0             		bne	loadAbort
                        ;
                        ; EOF is easy
                        ;
e729 : 2063ea           loadEof		jsr	getHex		;get checksum
e72c : 205aeb           		jsr	setInputConsole	;reset input vector
e72f : 2034ea           		jsr	putsil
e732 : 0d0a             		db	CR,LF
e734 : 5375636365737321 		db	"Success!"
e73c : 0d0a00           		db	CR,LF,0
e73f : 60               		rts
                        ;
                        ; Data records have more work.  After processing the
                        ; line, print a dot to indicate progress.  This should
                        ; be re-thought as it could slow down loading a really
                        ; big file if the console speed is slow.
                        ;
e740 : ae81df           loadDataRec	ldx	byteCount	;byte count
e743 : a000             		ldy	#0		;offset
e745 : 8e81df           loadData1	stx	byteCount
e748 : 8c82df           		sty	saveY
e74b : 2063ea           		jsr	getHex
e74e : b0d1             		bcs	loadAbortG
AS65 Assembler for R6502 [1.42].                                     Page   21
---------------------------------- xKIM.asm ----------------------------------

e750 : 20a7e8           		jsr	updateCrc
e753 : ac82df           		ldy	saveY
e756 : ae81df           		ldx	byteCount
e759 : 91fa             		sta	(POINTL),y
e75b : c8               		iny
e75c : ca               		dex
e75d : d0e6             		bne	loadData1
                        ;
                        ; All the bytes were read so get the checksum and see
                        ; if it agrees.  The checksum is a twos-complement, so
                        ; just add the checksum into what we've been calculati
                        ; and if the result is zero then the record is good.
                        ;
e75f : 2063ea           		jsr	getHex		;get checksum
e762 : 18               		clc
e763 : 6de717           		adc	CHKL
e766 : d0bd             		bne	loadAbortH	;non-zero is error
                        ;
                        ; If loading from an SD file then print a dot at the
                        ; end of each record.  Doing this for serial input is
                        ; very bad because the terminal program is sending the
                        ; next character while this is sending the dot.  Ie,
                        ; data is lost.
                        ;
e768 : adf917           		lda	ID
e76b : f005             		beq	lrecdone	;jump if not file
e76d : a92e             		lda	#'.'		;sanity indicator when
e76f : 20a01e           		jsr	OUTCH		;...loading from file
e772 : 4ca4e6           lrecdone	jmp	loadStart
e775 : 4cf1e0           lExit1		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory as a
                        ; file on the SD.
                        ;
e778 : 20bfea           saveHex		jsr	getAddrRange	;get range to dump
e77b : b0f8             		bcs	lExit1	;abort on error
                        ;
                        ; Get the filename to save to
                        ;
e77d : 2034ea           		jsr	putsil
e780 : 0d0a             		db	CR,LF
e782 : 456e7465722066.. 		db	"Enter filename, or Enter to "
e79e : 646973706c6179.. 		db	"display to console: ",0
                        ;
e7b3 : 2002eb           		jsr	getFileName	;get filename
e7b6 : ad87df           		lda	filename	;null?
e7b9 : f012             		beq	saveHexConsole	;dump to console
                        ;
                        ; They selected a file, so try to open it.
                        ;
e7bb : a2df             		ldx	#filename>>8
e7bd : a087             		ldy	#filename&$ff
e7bf : 20dcec           		jsr	DiskOpenWrite	;attempt to open file
e7c2 : 9003             		bcc	sopenok		;branch if opened ok
e7c4 : 4c4ae6           		jmp	openfail
                        ;
e7c7 : 20afeb           sopenok		jsr	setOutputFile
e7ca : 4cd0e7           		jmp	savehex2
                        ;
                        ; They are saving to the console.  Set up the output
                        ; vector and do the job.
AS65 Assembler for R6502 [1.42].                                     Page   22
---------------------------------- xKIM.asm ----------------------------------

                        ;
e7cd : 20a4eb           saveHexConsole	jsr	setOutputConsole
                        ;
                        ; Compute the number of bytes to dump
                        ;
e7d0 : 38               savehex2	sec
e7d1 : adf717           		lda	EAL
e7d4 : edf517           		sbc	SAL
e7d7 : 8dd6df           		sta	Temp16L
e7da : adf817           		lda	EAH
e7dd : edf617           		sbc	SAH
e7e0 : 8dd7df           		sta	Temp16H
e7e3 : 9042             		bcc	SDone	;start > end
e7e5 : 0900             		ora	#0
e7e7 : 303e             		bmi	SDone	;more than 32K seems wrong
                        ;
                        ; Add one to the count
                        ;
e7e9 : eed6df           		inc	Temp16L
e7ec : d003             		bne	slab1
e7ee : eed7df           		inc	Temp16H
                        ;
                        ; Move pointer to zero page
                        ;
e7f1 : adf517           slab1		lda	SAL
e7f4 : 85fa             		sta	POINTL
e7f6 : adf617           		lda	SAH
e7f9 : 85fb             		sta	POINTH
                        ;
                        ; Top of each loop.  Start by seeing if there are any 
                        ; left to dump.
                        ;
e7fb : add7df           Sloop1		lda	Temp16H
e7fe : d02a             		bne	Sgo	;more to do
e800 : add6df           		lda	Temp16L
e803 : d025             		bne	Sgo	;more to do
                        ;
                        ; At end of the region, so output an end record.  This
                        ; probably looks like overkill but keep in mind this
                        ; might be going to a file so we can't use the normal
                        ; string put functions.
                        ;
e805 : a93a             		lda	#':'
e807 : 2092eb           		jsr	redirectedOutch
e80a : a900             		lda	#0
e80c : 2078eb           		jsr	HexToOutput
e80f : 2078eb           		jsr	HexToOutput
e812 : 2078eb           		jsr	HexToOutput
e815 : a901             		lda	#1
e817 : 2078eb           		jsr	HexToOutput
e81a : a9ff             		lda	#$ff
e81c : 2078eb           		jsr	HexToOutput
                        ;
                        ; If output to file, flush and close the file.
                        ;
e81f : ad87df           		lda	filename
e822 : f003             		beq	SDone		;it's going to console
e824 : 2095eb           		jsr	CloseOutFile
e827 : 4cf1e0           SDone		jmp	extKimLoop	;back to the monitor
                        ;
                        ; This dumps the next line.  See how many bytes are le
                        ; and if more than BYTESLINE, then just do BYTESLINE.
AS65 Assembler for R6502 [1.42].                                     Page   23
---------------------------------- xKIM.asm ----------------------------------

                        ;
e82a : add7df           Sgo		lda	Temp16H
e82d : d007             		bne	Sdef	;do default number of bytes
e82f : add6df           		lda	Temp16L
e832 : c910             		cmp	#BYTESLINE
e834 : 9002             		bcc	Scnt	;more than max per line
e836 : a910             Sdef		lda	#BYTESLINE
e838 : 8de917           Scnt		sta	SAVX	;for decrementing
e83b : 8df917           		sta	ID	;for subtracting
                        ;
                        ; Put out the header
                        ;
e83e : a93a             		lda	#':'
e840 : 2092eb           		jsr	redirectedOutch
                        ;
e843 : ade917           		lda	SAVX
e846 : 8de717           		sta	CHKL	;start checksum
e849 : 2078eb           		jsr	HexToOutput
                        ;
e84c : a5fb             		lda	POINTH	;starting address
e84e : 20a7e8           		jsr	updateCrc
e851 : 2078eb           		jsr	HexToOutput
e854 : a5fa             		lda	POINTL
e856 : 20a7e8           		jsr	updateCrc
e859 : 2078eb           		jsr	HexToOutput
                        ;
e85c : a900             		lda	#0	;record type - data
e85e : 2078eb           		jsr	HexToOutput
                        ;
                        ; Now print the proper number of bytes
                        ;
e861 : a000             Sloop2		ldy	#0
e863 : b1fa             		lda	(POINTL),y	;get byte
e865 : 20a7e8           		jsr	updateCrc
e868 : 2078eb           		jsr	HexToOutput
e86b : 20631f           		jsr	INCPT	;increment pointer
                        ;
e86e : cee917           sdec		dec	SAVX
e871 : d0ee             		bne	Sloop2
                        ;
                        ; Now print checksum
                        ;
e873 : ade717           		lda	CHKL
e876 : 49ff             		eor	#$ff	;one's complement
e878 : 18               		clc
e879 : 6901             		adc	#1	;two's complement
e87b : 2078eb           		jsr	HexToOutput
                        ;
                        ; Output a CR/LF
                        ;
e87e : a90d             		lda	#CR
e880 : 2092eb           		jsr	redirectedOutch
e883 : a90a             		lda	#LF
e885 : 2092eb           		jsr	redirectedOutch
                        ;
                        ; If saving to disk, output a dot to indicate progress
                        ;
e888 : ad87df           		lda	filename
e88b : f005             		beq	shf2
                        ;
e88d : a92e             		lda	#'.'
e88f : 20a01e           		jsr	OUTCH	;goes to console
AS65 Assembler for R6502 [1.42].                                     Page   24
---------------------------------- xKIM.asm ----------------------------------

                        ;
e892 : 38               shf2		sec
e893 : add6df           		lda	Temp16L
e896 : edf917           		sbc	ID
e899 : 8dd6df           		sta	Temp16L
e89c : add7df           		lda	Temp16H
e89f : e900             		sbc	#0
e8a1 : 8dd7df           		sta	Temp16H
                        ;
e8a4 : 4cfbe7           		jmp	Sloop1
                        ;
                        ;=====================================================
                        ; Adds the character in A to the CRC.  Preserves A.
                        ;
e8a7 : 48               updateCrc	pha
e8a8 : 18               		clc
e8a9 : 6de717           		adc	CHKL
e8ac : 8de717           		sta	CHKL
e8af : 68               		pla
e8b0 : 60               		rts
                        ;
                        ;=====================================================
                        ; Handles the command to prompt for an address and the
                        ; jump to it.
                        ;
e8b1 : 2031ea           jumpAddress     jsr	space
e8b4 : 209bea           		jsr	getStartAddr
e8b7 : b006             		bcs	cmdRet	;branch on bad address
e8b9 : 202f1e           		jsr	CRLF
e8bc : 6cf517           		jmp	(SAL)	;else jump to address
                        ;
e8bf : 4cf1e0           cmdRet		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Ping the Arduno disk controller.  This just sends th
                        ; PING command gets back one character, then returns.
                        ; Not much of a test but is sufficient to prove the
                        ; link is working.
                        ;
e8c2 :                  pingDisk
                        ;		jsr	xParInit	;init interface
e8c2 : 2034ea           		jsr	putsil
e8c5 : 696e672e2e2e2000 		db	"ing... ",0
e8cd : 2062ec           		jsr	DiskPing
e8d0 : 2034ea           		jsr	putsil
e8d3 : 7375636365737321 		db	"success!"
e8db : 0d0a00           		db	CR,LF,0
e8de : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a disk directory of the SD card.
                        ;
e8e1 : 2034ea           doDiskDir	jsr	putsil
e8e4 : 69736b20446972.. 		db	"isk Directory..."
e8f4 : 0d0a00           		db	CR,LF,0
e8f7 : 20fee8           		jsr	doSDDiskDir
e8fa : 4cf1e0           		jmp	extKimLoop
e8fd : 60               doDiskDirEnd	rts
                        ;
                        ;=====================================================
                        ; Subroutine to do a disk directory.  Prints filenames
                        ; to the console.
AS65 Assembler for R6502 [1.42].                                     Page   25
---------------------------------- xKIM.asm ----------------------------------

                        ;
e8fe : 20dbeb           doSDDiskDir	jsr	xParInit
e901 : 2075ec           		jsr	DiskDir
                        ;
                        ; Get/Display each entry
                        ;
e904 : a2df             doDiskDirLoop   ldx	#filename/256	;pointer to buffer
e906 : a087             		ldy	#filename&$ff
e908 : 86f9             		stx	INH		;save for puts
e90a : 84f8             		sty	INL
e90c : 207fec           		jsr	DiskDirNext	;get next entry
e90f : b0ec             		bcs	doDiskDirEnd	;carry = end of list
e911 : 202bea           		jsr	space3
e914 : 2048ea           		jsr	puts		;else print name
e917 : 202f1e           		jsr	CRLF
e91a : 4c04e9           		jmp	doDiskDirLoop	;do next entry
                        ;
                        ;=====================================================
                        ; Type the contents of an SD file to console.
                        ;
e91d : 2034ea           typeFile	jsr	putsil
e920 : 202d20456e7465.. 		db	" - Enter filename: ",0
e934 : 2002eb           		jsr	getFileName
e937 : a087             		ldy	#filename&$ff
e939 : a2df             		ldx	#filename/256
                        ;		jsr	xParInit
e93b : 20b0ec           		jsr	DiskOpenRead
e93e : 901e             		bcc	typeFile1	;opened ok
                        ;
e940 : 2034ea           		jsr	putsil
e943 : 0d0a             		db	CR,LF
e945 : 4661696c656420.. 		db	"Failed to open file"
e958 : 0d0a00           		db	CR,LF,0
e95b : 4cf1e0           		jmp	extKimLoop
                        ;
                        ; Now just keep reading in bytes and displaying them.
                        ;
e95e : 2065eb           typeFile1	jsr	setInputFile	;reading from file
e961 : 2028eb           typeFileLoop	jsr	getNextFileByte
e964 : b006             		bcs	typeEof
e966 : 20a01e           		jsr	OUTCH	;display character
e969 : 4c61e9           		jmp	typeFileLoop
                        ;
e96c : 204aed           typeEof		jsr	DiskClose
e96f : 4cf1e0           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Calculate the offset for a relative branch.  6502
                        ; relative branch calculations are well known.
                        ;
                        ; Offset from branch (BASE) = TARGET - (BASE+2).
                        ; If the result is positive, upper byte must be
                        ; zero.  If negative, upper byte must be FF.
                        ;
                        ; BASE	TARGET	Computed	Actual
                        ; 0200	0200	0200-(0200+2)	FFFE
                        ; 0200	020E	020E-(0200+2)	000C
                        ; 0226	0220	0220-(0226+2)	FFF8
                        ; 0156	015A	015A-(0156+2)	0002
                        ; 015C	012D	012D-(015C+2)	FFCF
                        ; 0200	0300	0300-(0200+2)	00FE - out of range
                        ; 0300	0200	0200-(0300+2)	FEFE - out of range
AS65 Assembler for R6502 [1.42].                                     Page   26
---------------------------------- xKIM.asm ----------------------------------

                        ;
e972 : 2034ea           offCalc		jsr	putsil
e975 : 202d204272616e.. 		db	" - Branch instruction address: "
e994 : 00               		db	0
e995 : 209bea           		jsr	getStartAddr
e998 : b030             		bcs	calcExit
e99a : 2034ea           		jsr	putsil
e99d : 2c206272616e63.. 		db	", branch to: "
e9aa : 00               		db	0
e9ab : 20adea           		jsr	getEndAddr
e9ae : b01a             		bcs	calcExit
e9b0 : 20d0ea           		jsr	ComputeOffset	;does the work
e9b3 : 9018             		bcc	relgood		;if good offset
                        ;
                        ; Branch is out of range.
                        ;
e9b5 : 2034ea           		jsr	putsil
e9b8 : 202d206f757420.. 		db	" - out of range",CR,LF,0
e9ca : 4cf1e0           calcExit	jmp	extKimLoop
                        ;
                        ; Branch is in range so dislay the value.
                        ;
e9cd : 48               relgood		pha			;save offset
e9ce : 2034ea           		jsr	putsil
e9d1 : 204f6666736574.. 		db	" Offset: ",0
e9db : 68               		pla
e9dc : 203b1e           		jsr	PRTBYT
e9df : 4cf1e0           		jmp	extKimLoop
                        ;
                        ; Add new commands here...
                        ;
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   27
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This subroutine will search for a command in a table
                        ; and call the appropriate handler.  See the command
                        ; table near the start of the code for what the format
                        ; is.  If a match is found, pop off the return address
                        ; from the stack and jump to the code.  Else, return.
                        ;
e9e2 : a000             searchCmd	ldy	#0
e9e4 : b1fa             cmdLoop		lda	(POINTL),y
e9e6 : f01c             		beq	cmdNotFound
e9e8 : c5f3             		cmp	ACC	;compare to user's input
e9ea : f007             		beq	cmdMatch
e9ec : c8               		iny		;start of function ptr
e9ed : c8               		iny
e9ee : c8               		iny		;start of help
e9ef : c8               		iny
e9f0 : c8               		iny		;move to next command
e9f1 : d0f1             		bne	cmdLoop
                        ;
                        ; It's found!  Load up the address of the code to call
                        ; pop the return address off the stack and jump to the
                        ; handler.
                        ;
e9f3 : c8               cmdMatch	iny
e9f4 : b1fa             		lda	(POINTL),y	;handler LSB
e9f6 : 48               		pha
e9f7 : c8               		iny
e9f8 : b1fa             		lda	(POINTL),y	;handler MSB
e9fa : 85fb             		sta	POINTH
e9fc : 68               		pla
e9fd : 85fa             		sta	POINTL
e9ff : 68               		pla		;pop return address
ea00 : 68               		pla
ea01 : 6cfa00           		jmp	(POINTL)
                        ;
                        ; Not found, so just return.
                        ;
ea04 : 60               cmdNotFound	rts
                        ;
                        ;=====================================================
                        ; Given a pointer to a command table in POINT, display
                        ; the help text for all commands in the table.
                        ;
ea05 : a000             displayHelp	ldy	#0	;index into command table
ea07 : b1fa             showHelpLoop	lda	(POINTL),y	;get command
ea09 : f01f             		beq	showHelpDone	;jump if at end
                        ;
                        ; Display this entry's descriptive text
                        ;
ea0b : c8               		iny		;skip over command
ea0c : c8               		iny		;skip over function ptr
ea0d : c8               		iny
ea0e : b1fa             		lda	(POINTL),y
ea10 : 85f8             		sta	INL
ea12 : c8               		iny
ea13 : b1fa             		lda	(POINTL),y
ea15 : 85f9             		sta	INH
ea17 : 98               		tya
ea18 : 48               		pha
ea19 : 209e1e           		jsr	OUTSP
ea1c : 209e1e           		jsr	OUTSP
AS65 Assembler for R6502 [1.42].                                     Page   28
---------------------------------- xKIM.asm ----------------------------------

ea1f : 2048ea           		jsr	puts	;print description
ea22 : 202f1e           		jsr	CRLF
ea25 : 68               		pla
ea26 : a8               		tay
ea27 : c8               		iny		;point to next entry
ea28 : d0dd             		bne	showHelpLoop
ea2a : 60               showHelpDone	rts
                        ;
                        ;=====================================================
                        ; Print some spaces.
                        ;
ea2b : 2031ea           space3		jsr	space
ea2e : 2031ea           space2		jsr	space
ea31 : 4c9e1e           space   	jmp	OUTSP
                        ;
                        ;=====================================================
                        ; This prints the null-terminated string that
                        ; immediately follows the JSR to this function.  This
                        ; version was written by Ross Archer and is at:
                        ;
                        ;    www.6502.org/source/io/primm.htm
                        ;
ea34 : 68               putsil		pla
ea35 : 85f8             		sta	INL
ea37 : 68               		pla
ea38 : 85f9             		sta	INH
ea3a : a001             		ldy	#1
ea3c : 204aea           		jsr	putsy
ea3f : e6f8             		inc	INL
ea41 : d002             		bne	puts2
ea43 : e6f9             		inc	INH
ea45 : 6cf800           puts2		jmp	(INL)
                        ;
                        ;=====================================================
                        ; This prints the null terminated string pointed to by
                        ; INL and INH.  Modifies those locations to point to
                        ; the end of the string.
                        ;
ea48 : a000             puts		ldy	#0
ea4a : b1f8             putsy		lda	(INL),y
ea4c : e6f8             		inc	INL
ea4e : d002             		bne	puts1
ea50 : e6f9             		inc	INH
ea52 : 0900             puts1		ora	#0
ea54 : f00c             		beq	putsdone
ea56 : 8c82df           		sty	saveY
ea59 : 20a01e           		jsr	OUTCH	;print character
ea5c : ac82df           		ldy	saveY
ea5f : 4c4aea           		jmp	putsy
ea62 : 60               putsdone	rts
                        ;
                        ;=====================================================
                        ; This gets two hex characters and returns the value
                        ; in A with carry clear.  If a non-hex digit is
                        ; entered, then A contans the offending character and
                        ; carry is set.
                        ;
ea63 : 2079ea           getHex		jsr	getNibble
ea66 : b01e             		bcs	getNibBad
ea68 : 0a               		asl	a
ea69 : 0a               		asl	a
ea6a : 0a               		asl	a
AS65 Assembler for R6502 [1.42].                                     Page   29
---------------------------------- xKIM.asm ----------------------------------

ea6b : 0a               		asl	a
ea6c : 8d80df           		sta	saveA
ea6f : 2079ea           		jsr	getNibble
ea72 : b012             		bcs	getNibBad
ea74 : 0d80df           		ora	saveA
ea77 : 18               		clc
ea78 : 60               		rts
                        ;
                        ; Helper.  Gets next input char and converts to a
                        ; value from 0-F in A and returns C clear.  If not a
                        ; valid hex character, return C set.
                        ;
ea79 : 2057eb           getNibble	jsr	redirectedGetch
ea7c : a20f             		ldx	#nibbleHexEnd-nibbleHex-1
ea7e : dd8bea           getNibble1	cmp	nibbleHex,x
ea81 : f005             		beq	getNibF	;got match
ea83 : ca               		dex
ea84 : 10f8             		bpl	getNibble1
ea86 : 38               getNibBad	sec
ea87 : 60               		rts
                        
ea88 : 8a               getNibF		txa		;index is value
ea89 : 18               		clc
ea8a : 60               		rts
                        ;
ea8b : 30313233343536.. nibbleHex	db	"0123456789ABCDEF"
ea9b =                  nibbleHexEnd	equ	*
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; SAL and SAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
ea9b : 2063ea           getStartAddr	jsr	getHex
ea9e : b00c             		bcs	getDone
eaa0 : 8df617           		sta	SAH
eaa3 : 2063ea           		jsr	getHex
eaa6 : b004             		bcs	getDone
eaa8 : 8df517           		sta	SAL
eaab : 18               		clc
eaac : 60               getDone		rts
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; EAL and EAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eaad : 2063ea           getEndAddr	jsr	getHex
eab0 : b0fa             		bcs	getDone
eab2 : 8df817           		sta	EAH
eab5 : 2063ea           		jsr	getHex
eab8 : b0f2             		bcs	getDone
eaba : 8df717           		sta	EAL
eabd : 18               		clc
eabe : 60               		rts
                        ;
                        ;=====================================================
                        ; Get an address range and leave them in SAL and EAL.
                        ;
eabf : 2031ea           getAddrRange    jsr	space
eac2 : 209bea           		jsr	getStartAddr
eac5 : b0e5             		bcs	getDone
AS65 Assembler for R6502 [1.42].                                     Page   30
---------------------------------- xKIM.asm ----------------------------------

eac7 : a92d             		lda	#'-'
eac9 : 20a01e           		jsr	OUTCH
eacc : 20adea           		jsr	getEndAddr
eacf : 60               		rts
                        ;
                        ;=====================================================
                        ; This computes the relative offset between the
                        ; address in SAL/SAH (address of branch instruction)
                        ; and EAL/EAH (address to jump to).  If a valid range,
                        ; returns C clear and the offset in A.  If the branch
                        ; is out of range, C is set and A undefined.  Modifies
                        ; A, SAL and SAH.
                        ;
ead0 :                  ComputeOffset
                        ;
                        ; Add two to the end (BASE) address.  For calculations
                        ;   BASE = SAL/SAH
                        ;   TARGET = EAL/EAH
                        ;
ead0 : 18               		clc
ead1 : adf517           		lda	SAL
ead4 : 6902             		adc	#2
ead6 : 8df517           		sta	SAL
ead9 : 9003             		bcc	coffsub
eadb : eef617           		inc	SAH
                        ;
                        ; Subtract the BASE (end) address from the TARGET (sta
                        ;
eade : 38               coffsub		sec
eadf : adf717           		lda	EAL
eae2 : edf517           		sbc	SAL
eae5 : 48               		pha		;save for later
eae6 : 8df517           		sta	SAL
eae9 : adf817           		lda	EAH
eaec : edf617           		sbc	SAH
eaef : 8df617           		sta	SAH	;SAL/SAH contain offset
                        ;
                        ; High part must be either FF for negative branch or
                        ; 00 for a positive branch.  Cheat a bit here by rolli
                        ; the MSBit into C and adding to the MSByte.  If the
                        ; result is zero then everything is cool.
                        ;
eaf2 : 68               		pla		;restore LSB of offset
eaf3 : 48               		pha
eaf4 : 0a               		asl	a	;put sign into C
eaf5 : adf617           		lda	SAH
eaf8 : 6900             		adc	#0
eafa : f003             		beq	cogood	;branch if in range
                        ;
eafc : 68               		pla		;clean up stack
eafd : 38               		sec		;error
eafe : 60               		rts
                        ;
eaff : 68               cogood		pla		;get back offset
eb00 : 18               		clc
eb01 : 60               		rts
                        ;
                        ;=====================================================
                        ; Get a disk filename.  The KIM's behavior of echoing
                        ; every key prevents this from being too fancy, but it
                        ; good enough.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   31
---------------------------------- xKIM.asm ----------------------------------

eb02 : a200             getFileName	ldx	#0
eb04 : 205a1e           getFilename1	jsr	GETCH	;get next key
eb07 : c90d             		cmp	#CR	;end of the input?
eb09 : f014             		beq	getFnDone
eb0b : c908             		cmp	#BS	;backspace?
eb0d : f00a             		beq	getFnDel
eb0f : e00c             		cpx	#FILENAME_SIZE	;check size
eb11 : f0f1             		beq	getFilename1	;at length limit
eb13 : 9d87df           		sta	filename,x	;else save it
eb16 : e8               		inx
eb17 : d0eb             		bne	getFilename1
                        ;
eb19 : ca               getFnDel	dex		;back up one
eb1a : 10e8             		bpl	getFilename1
eb1c : e8               		inx		;can't go past start
eb1d : f0e5             		beq	getFilename1
eb1f : a900             getFnDone       lda	#0	;terminate line
eb21 : 9d87df           		sta	filename,x
eb24 : 202f1e           		jsr	CRLF
eb27 : 60               		rts
                        ;
                        ;=====================================================
                        ; This gets the next byte from an open disk file.  If
                        ; there are no more bytes left, this returns C set.
                        ; Else, C is clear and A contains the character.
                        ;
eb28 : aed4df           getNextFileByte ldx 	diskBufOffset
eb2b : ecd5df           		cpx	diskBufLength
eb2e : d014             		bne	hasdata		;branch if still data
                        ;
                        ; There is no data left in the buffer, so read a
                        ; block from the SD system.
                        ;
eb30 : a940             		lda	#BUFFER_SIZE
eb32 : a2df             		ldx	#buffer>>8
eb34 : a094             		ldy	#buffer&$ff
eb36 : 20e1ec           		jsr	DiskRead
eb39 : b012             		bcs	getNextEof
                        ;
                        ; A contains the number of bytes actually read.
                        ;
eb3b : 8dd5df           		sta	diskBufLength	;save length
eb3e : c900             		cmp	#0		;shouldn't happen
eb40 : f00b             		beq	getNextEof
                        ;
eb42 : a200             		ldx	#0
eb44 : bd94df           hasdata		lda	buffer,x
eb47 : e8               		inx
eb48 : 8ed4df           		stx	diskBufOffset
eb4b : 18               		clc
eb4c : 60               		rts
                        ;
eb4d : a900             getNextEof	lda	#0
eb4f : 8dd4df           		sta	diskBufOffset
eb52 : 8dd5df           		sta	diskBufLength
eb55 : 38               		sec
eb56 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   32
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the input vector
                        ; pointer to get the next input character.
                        ;
eb57 : 6c83df           redirectedGetch	jmp	(inputVector)
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to the normal
                        ; console input subroutine.
                        ;
eb5a : a95a             setInputConsole	lda	#GETCH&$ff
eb5c : 8d83df           		sta     inputVector
eb5f : a91e             		lda	#GETCH/256
eb61 : 8d84df           		sta	inputVector+1
eb64 : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to a file read
                        ; subroutine.
                        ;
eb65 : a928             setInputFile    lda	#getNextFileByte&$ff
eb67 : 8d83df           		sta     inputVector
eb6a : a9eb             		lda	#getNextFileByte/256
eb6c : 8d84df           		sta	inputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
eb6f : a900             		lda	#0
eb71 : 8dd4df           		sta	diskBufOffset
eb74 : 8dd5df           		sta	diskBufLength
eb77 : 60               		rts
                        ;
                        ;=====================================================
                        ; Print character in A as two hex digits to the
                        ; current output device (console or file).
                        ;
eb78 : 48               HexToOutput	pha		;save return value
eb79 : 48               		pha
eb7a : 4a               		lsr	a	;move top nibble to bottom
eb7b : 4a               		lsr	a
eb7c : 4a               		lsr	a
eb7d : 4a               		lsr	a
eb7e : 2087eb           		jsr	hexta	;output nibble
eb81 : 68               		pla
eb82 : 2087eb           		jsr	hexta
eb85 : 68               		pla		;restore
eb86 : 60               		rts
                        ;
eb87 : 290f             hexta		and	#%0001111
eb89 : c90a             		cmp	#$0a
eb8b : 18               		clc
eb8c : 3002             		bmi	hexta1
eb8e : 6907             		adc	#7
eb90 : 6930             hexta1		adc	#'0'	;then fall into...
                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the output vector
                        ; pointer to send the character in A to the proper
AS65 Assembler for R6502 [1.42].                                     Page   33
---------------------------------- xKIM.asm ----------------------------------

                        ; device.
                        ;
eb92 : 6c85df           redirectedOutch	jmp	(outputVector)
                        ;
                        ;=====================================================
                        ; This flushes any data remaining in the disk buffer
                        ; and then closes the file.
                        ;
eb95 : add4df           CloseOutFile	lda	diskBufOffset
eb98 : f007             		beq	closeonly
eb9a : a2df             		ldx	#buffer>>8
eb9c : a094             		ldy	#buffer&$ff
eb9e : 2013ed           		jsr	DiskWrite
                        ;
eba1 : 204aed           closeonly	jsr	DiskClose
                        ;
                        ; Fall through...
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to the normal
                        ; console output subroutine.
                        ;
eba4 :                  setOutputConsole
eba4 : a9a0             		lda	#OUTCH&$ff
eba6 : 8d85df           		sta     outputVector
eba9 : a91e             		lda	#OUTCH/256
ebab : 8d86df           		sta	outputVector+1
ebae : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to a file write
                        ; subroutine.
                        ;
ebaf : a9bf             setOutputFile    lda	#putNextFileByte&$ff
ebb1 : 8d85df           		sta     outputVector
ebb4 : a9eb             		lda	#putNextFileByte/256
ebb6 : 8d86df           		sta	outputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ebb9 : a900             		lda	#0
ebbb : 8dd4df           		sta	diskBufOffset
ebbe : 60               		rts
                        ;
                        ;=====================================================
                        ; Add the byte in A to the output buffer.  If the
                        ; buffer is full, flush it to disk.
                        ;
ebbf : aed4df           putNextFileByte	ldx	diskBufOffset
ebc2 : e040             		cpx	#BUFFER_SIZE	;buffer full?
ebc4 : d00d             		bne	pNFB		;no
                        ;
                        ; The buffer is full, so write it out.
                        ;
ebc6 : 48               		pha			;save byte
ebc7 : a940             		lda	#BUFFER_SIZE
ebc9 : a2df             		ldx	#buffer>>8
ebcb : a094             		ldy	#buffer&$ff
ebcd : 2013ed           		jsr	DiskWrite
                        ;
ebd0 : a200             		ldx	#0		;reset index
AS65 Assembler for R6502 [1.42].                                     Page   34
---------------------------------- xKIM.asm ----------------------------------

ebd2 : 68               		pla
ebd3 : 9d94df           pNFB		sta	buffer,x
ebd6 : e8               		inx
ebd7 : 8ed4df           		stx	diskBufOffset
ebda : 60               		rts
                        ;
                        		include	"pario.asm"
                        ;*****************************************************
                        ; These are the low-level I/O routines to talk to the
                        ; Arduino processor connected to the KIM's I/O port.
                        ;
                        ; August 2014, Bob Applegate K2UT, bob@corshamtech.com
                        ;
                        ; Which port bits are used for what:
                        ;
                        ; A0 = Data 0, alternates input/output
                        ; A1 = Data 1, alternates input/output
                        ; A2 = Data 2, alternates input/output
                        ; A3 = Data 3, alternates input/output
                        ; A4 = Data 4, alternates input/output
                        ; A5 = Data 5, alternates input/output
                        ; A6 = Data 6, alternates input/output
                        ; A7 = Data 7, alternates input/output
                        ;
                        ; B0 = Direction bit, always output
                        ; B1 = Write strobe or ACK, always output
                        ; B2 = Read stroke or ACK, always input
                        ;
                        ;----------------------------------------------------
                        ; Bits in the B register
                        ;
0001 =                  DIRECTION	equ	%00000001
0002 =                  PSTROBE		equ	%00000010
0004 =                  ACK		equ	%00000100
                        ;
                        ;----------------------------------------------------
                        ; The KIM has a 6530 with the two I/O ports at $1700.
                        ;
                        ; Addresses of 6530 registers
                        ;
1700 =                  PIABASE		equ	$1700
1700 =                  PIAREGA		equ	PIABASE		;data reg A
1701 =                  PIADDRA		equ	PIABASE+1	;data dir reg A
1702 =                  PIAREGB		equ	PIABASE+2	;data reg B
1703 =                  PIADDRB		equ	PIABASE+3	;data dir reg B
                        		code
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   35
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This is the initialization function.  Call before
                        ; doing anything else with the parallel port.
                        ;
ebdb :                  xParInit
                        ;
                        ; Set up the data direction register for port B so tha
                        ; the DIRECTION and PSTROBE bits are output.  Only tou
                        ; the bits we're using; leave the rest as-is.
                        ;
ebdb : ad0317           		lda	PIADDRB	;get current value
ebde : 29fb             		and	#~ACK
ebe0 : 0903             		ora	#DIRECTION | PSTROBE
ebe2 : 8d0317           		sta	PIADDRB
                        ;
ebe5 : a9ff             		lda	#$ff
ebe7 : 8d0117           		sta	PIADDRA	;set data for write
                        ;
                        ; Now that the data is set, set the direction
                        ; registers.  This prevents weird problems like
                        ; driving lines to the wrong state.
                        ;
ebea : ad0217           		lda	PIAREGB
ebed : 29f9             		and	#~(PSTROBE | ACK)
ebef : 0901             		ora	#DIRECTION
ebf1 : 8d0217           		sta	PIAREGB
ebf4 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   36
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for writing to the Arduino.  Sets up
                        ; direction registers, drives the direction bit, etc.
                        ;
ebf5 : a9ff             xParSetWrite    lda	#$ff	;set bits for output
ebf7 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to output, clear ACK bit
                        ;
ebfa : ad0217           		lda	PIAREGB
ebfd : 29f9             		and	#~(PSTROBE | ACK)
ebff : 0901             		ora	#DIRECTION
ec01 : 8d0217           		sta	PIAREGB
ec04 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   37
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for reading from the Arduino.  Sets up
                        ; direction registers, clears the direction bit, etc.
                        ;
ec05 : a900             xParSetRead     lda	#$00	;set bits for input
ec07 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to input, clear ACK bit
                        ;
ec0a : ad0217           		lda	PIAREGB
ec0d : 29f8             		and	#~(DIRECTION | PSTROBE | ACK)
ec0f : 8d0217           		sta	PIAREGB
ec12 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   38
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This writes a single byte to the Arduino.  On entry,
                        ; the byte to write is in A.  This assumes ParSetWrite
                        ; was already called.
                        ;
                        ; Destroys A, all other registers preserved.
                        ;
                        ; Write cycle:
                        ;
                        ;    1. Wait for other side to lower ACK.
                        ;    2. Put data onto the bus.
                        ;    3. Set DIRECTION and PSTROBE to indicate data
                        ;       is valid and ready to read.
                        ;    4. Wait for ACK line to go high, indicating the
                        ;       other side has read the data.
                        ;    5. Lower PSTROBE.
                        ;    6. Wait for ACK to go low, indicating end of
                        ;       transfer.
                        ;
ec13 : 48               xParWriteByte	pha		;save data
ec14 : ad0217           Parwl22		lda	PIAREGB	;check status
ec17 : 2904             		and	#ACK
ec19 : d0f9             		bne	Parwl22	;wait for ACK to go low
                        ;
                        ; Now put the data onto the bus
                        ;
ec1b : 68               		pla
ec1c : 8d0017           		sta	PIAREGA
                        ;
                        ; Raise the strobe so the Arduino knows there is
                        ; new data.
                        ;
ec1f : ad0217           		lda	PIAREGB
ec22 : 0902             		ora	#PSTROBE
ec24 : 8d0217           		sta	PIAREGB
                        ;
                        ; Wait for ACK to go high, indicating the Arduino has
                        ; pulled the data and is ready for more.
                        ;
ec27 : ad0217           Parwl33		lda	PIAREGB
ec2a : 2904             		and	#ACK
ec2c : f0f9             		beq	Parwl33
                        ;
                        ; Now lower the strobe, then wait for the Arduino to
                        ; lower ACK.
                        ;
ec2e : ad0217           		lda	PIAREGB
ec31 : 29fd             		and	#~PSTROBE
ec33 : 8d0217           		sta	PIAREGB
ec36 : ad0217           Parwl44		lda	PIAREGB
ec39 : 2904             		and	#ACK
ec3b : d0f9             		bne	Parwl44
ec3d : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   39
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This reads a byte from the Arduino and returns it in
                        ; A.  Assumes ParSetRead was called before.
                        ;
                        ; This does not have a time-out.
                        ;
                        ; Preserves all other registers.
                        ;
                        ; Read cycle:
                        ;
                        ;    1. Wait for other side to raise ACK, indicating
                        ;       data is ready.
                        ;    2. Read data.
                        ;    3. Raise PSTROBE indicating data was read.
                        ;    4. Wait for ACK to go low.
                        ;    5. Lower PSTROBE.
                        ;
ec3e : ad0217           xParReadByte    lda	PIAREGB
ec41 : 2904             		and	#ACK	;is their strobe high?
ec43 : f0f9             		beq	xParReadByte	;nope, no data
                        ;
                        ; Data is available, so grab and save it.
                        ;
ec45 : ad0017           		lda	PIAREGA
ec48 : 48               		pha
                        ;
                        ; Now raise our strobe (their ACK), then wait for
                        ; them to lower their strobe.
                        ;
ec49 : ad0217           		lda	PIAREGB
ec4c : 0902             		ora	#PSTROBE
ec4e : 8d0217           		sta	PIAREGB
ec51 : ad0217           Parrlp1		lda	PIAREGB
ec54 : 2904             		and	#ACK
ec56 : d0f9             		bne	Parrlp1	;still active
                        ;
                        ; Lower our ack, then were done.
                        ;
ec58 : ad0217           		lda	PIAREGB
ec5b : 29fd             		and	#~PSTROBE
ec5d : 8d0217           		sta	PIAREGB
ec60 : 68               		pla
ec61 : 60               		rts
                        
                        		include	"parproto.inc"
                        ;*****************************************************
                        ; Parallel port protocol
                        ;
                        ; This is the header file for making applications
                        ; compliant with The Remote Disk Protocol Guide which
                        ; is on the Corsham Technologies web page somewhere:
                        ;
                        ;    www.corshamtech.com
                        ;
                        ; This was updated 06/13/2015 to be compliant with the
                        ; official specification, so the opcode values changed
                        ;
                        ; Another update on 02/10/2019
                        ;
                        ;=====================================================
                        ; Commands from host to Arduino
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   40
---------------------------------- xKIM.asm ----------------------------------

0001 =                  PC_GET_VERSION	equ	$01
0005 =                  PC_PING		equ	$05	;ping Arduino
0006 =                  PC_LED_CONTROL	equ	$06	;LED control
0007 =                  PC_GET_CLOCK	equ	$07	;get clock data
0008 =                  PC_SET_CLOCK	equ	$08	;set clock
0010 =                  PC_GET_DIR	equ	$10	;Get directory
0011 =                  PC_GET_MOUNTED	equ	$11	;Get mounted drive list
0012 =                  PC_MOUNT	equ	$12	;Mount drive
0013 =                  PC_UNMOUNT	equ	$13	;Unmount drive
0014 =                  PC_GET_STATUS	equ	$14	;Get status for one drive
0015 =                  PC_DONE		equ	$15	;Stop data
0015 =                  PC_ABORT	equ	PC_DONE
0016 =                  PC_READ_FILE	equ	$16	;Read regular file (non-DSK)
0017 =                  PC_READ_BYTES	equ	$17	;Read sequential bytes
0018 =                  PC_RD_SECTOR	equ	$18	;Read FLEX sector
0019 =                  PC_WR_SECTOR	equ	$19	;Write FLEX sector
001a =                  PC_GET_MAX	equ	$1a	;Get maximum drives
001b =                  PC_WRITE_FILE   equ	$1b	;Open file for writing
001c =                  PC_WRITE_BYTES	equ	$1c	;Data to be written
001d =                  PC_SAVE_CONFIG	equ	$1d	;Save SD.CFG with current value
001e =                  PC_SET_TIMER	equ	$1e	;Set RTC timer
001f =                  PC_RD_SEC_LONG	equ	$1f	;Read sector with long sec num
0020 =                  PC_WR_SEC_LONG	equ	$20	;Write sector with long sec num
                        ;
                        ;=====================================================
                        ; Responses from Arduino to host
                        ;
0081 =                  PR_VERSION_INFO	equ	$81	;Contains version information
0082 =                  PR_ACK		equ	$82	;ACK (no additional information)
0083 =                  PR_NAK		equ	$83	;NAK - one status byte follows
0085 =                  PR_PONG		equ	$85	;Reply to a ping
0087 =                  PR_CLOCK_DATA	equ	$87	;Clock data
0090 =                  PR_DIR_ENTRY	equ	$90	;Directory entry
0091 =                  PR_DIR_END	equ	$91	;End of directory entries
0092 =                  PR_FILE_DATA	equ	$92	;File data
0093 =                  PR_STATUS	equ	$93	;Drive status
0094 =                  PR_SECTOR_DATA	equ	$94	;Sector data
0095 =                  PR_MOUNT_INFO	equ	$95	;Mount entry
0096 =                  PR_MAX_DRIVES	equ	$96	;Maximum number of drives
                        ;
                        ;=====================================================
                        ; Error codes for NAK events.  Yes, these are in
                        ; decimal due to an error in the original
                        ; documentation.
                        ;
0000 =                  ERR_NONE	equ	0
000a =                  ERR_NOT_MOUNTED	equ	10
000b =                  ERR_MOUNTED	equ	11
000c =                  ERR_NOT_FOUND	equ	12
000d =                  ERR_READ_ONLY	equ	13
000e =                  ERR_BAD_DRIVE	equ	14
000f =                  ERR_BAD_TRACK	equ	15
0010 =                  ERR_BAD_SECTOR	equ	16
0011 =                  ERR_READ_ERROR	equ	17
0012 =                  ERR_WRITE_ERROR	equ	18
0013 =                  ERR_NOT_PRESENT	equ	19
0014 =                  ERR_NOT_IMPL	equ	20	;Command not implemented
                        
                        
                        		include	"diskfunc.asm"
                        		list
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   41
---------------------------------- xKIM.asm ----------------------------------

                        ; This is a collection of functions for performing
                        ; higher level disk functions.  This hides the nasty
                        ; details of communications with the remote disk
                        ; system.
                        ;
                        ; August 20, 2014 - Bob Applegate
                        ;                   bob@corshamtech.com
                        ;
                        ; 06/14/2015 - Bob Applegate
                        ;		Now that there is an official standard
                        ;		for the protocol between the host (this
                        ;		code) and the DCP (Arduino code), this
                        ;		code has been updated to be compliant.
                        ;
                        ; 01/14/2016 - Bob Applegate
                        ;              Finally converted to 6502.
                        ;
                        ;		include	"parproto.inc"
                        ;
                        ; Number of drives emulated
                        ;
0004 =                  DRIVES		equ	4
                        ;
                        ;=====================================================
                        ; This is a sanity check to verify connectivity to the
                        ; Arduino code is working.  Returns C clear if all is
                        ; good, or C set if not.
                        ;
ec62 : 20f5eb           DiskPing	jsr	xParSetWrite
ec65 : a905             		lda	#PC_PING	;command
ec67 : 2013ec           		jsr	xParWriteByte	;send to Arduino
ec6a : 2005ec           		jsr	xParSetRead
ec6d : 203eec           		jsr	xParReadByte	;read their reply
ec70 : 20f5eb           DiskRetGood	jsr	xParSetWrite
ec73 : 18               		clc			;assume it's good
ec74 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   42
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This starts a directory read of the raw drive, not
                        ; the mounted drive.  No input parameters.  This simpl
                        ; sets up for reading the entries, then the user must
                        ; read each entry.
                        ;
                        ; Returns with C clear on success.  If error, C is set
                        ; and A contains the error code.
                        ;
ec75 : 20f5eb           DiskDir		jsr	xParSetWrite
ec78 : a910             		lda	#PC_GET_DIR	;send command
ec7a : 2013ec           		jsr	xParWriteByte
ec7d : 18               		clc		;assume it works
ec7e : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   43
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Read the next directory entry.  On input, X (MSB)
                        ; and Y (LSB) point to a 13 byte area to receive the
                        ; drive data.
                        ;
                        ; Returns C set if end of directory (ie, attempt to
                        ; read and there are none left).  Else, C is clear
                        ; and X/Y point to the null at end of filename.
                        ;
ec7f : 86fb             DiskDirNext	stx	POINTH
ec81 : 84fa             		sty	POINTL
ec83 : 2005ec           		jsr	xParSetRead	;read results
ec86 : 203eec           		jsr	xParReadByte	;get response code
ec89 : c983             		cmp	#PR_NAK		;error?
ec8b : f01e             		beq	DDNErr
ec8d : c991             		cmp	#PR_DIR_END	;end?
ec8f : f01a             		beq	DDNErr
                        ;
                        ; This contains a directory entry.
                        ;
ec91 : a000             		ldy	#0
ec93 : 203eec           DDNloop		jsr	xParReadByte
ec96 : 91fa             		sta	(POINTL),y
ec98 : c900             		cmp	#0	;end?
ec9a : f006             		beq	DDNEnd
ec9c : 20631f           		jsr	INCPT
ec9f : 4c93ec           		jmp	DDNloop
eca2 : 20f5eb           DDNEnd		jsr	xParSetWrite
eca5 : a6fb             		ldx	POINTH
eca7 : a4fa             		ldy	POINTL
eca9 : 18               		clc		;not end of files
ecaa : 60               		rts
                        ;
                        ; Error.  Set C and return.  This is not really
                        ; proper, since this implies a simple end of the
                        ; directory rather than an error.
                        ;
ecab : 20f5eb           DDNErr		jsr	xParSetWrite
ecae : 38               		sec
ecaf : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   44
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This opens a file on the SD for reading.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error (usually means the
                        ; file does not exist.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ecb0 : a916             DiskOpenRead	lda	#PC_READ_FILE
ecb2 :                  DiskOpen
ecb2 : 84f8                     sty	INL	;save ptr to filename
ecb4 : 86f9             		stx	INH
ecb6 : 2013ec           		jsr	xParWriteByte
ecb9 : a0ff             		ldy	#-1
ecbb : c8               DiskOpenLoop	iny
ecbc : b1f8             		lda	(INL),y
ecbe : 2013ec           		jsr	xParWriteByte
ecc1 : b1f8             		lda	(INL),y
ecc3 : d0f6             		bne	DiskOpenLoop
ecc5 : 2005ec           		jsr	xParSetRead
ecc8 : 203eec           		jsr	xParReadByte	;get response
eccb : c982             		cmp	#PR_ACK
eccd : d005             		bne	DiskOpenErr
eccf : 20f5eb           		jsr	xParSetWrite	;back to write mode
ecd2 : 18               		clc
ecd3 : 60               		rts
                        ;
                        ; Got an error.
                        ;
ecd4 : 203eec           DiskOpenErr	jsr	xParReadByte	;get error code
ecd7 : 20f5eb           		jsr	xParSetWrite	;back to write mode
ecda : 38               		sec
ecdb : 60               		rts
                        ;=====================================================
                        ; This opens a file on the SD for writing.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ecdc : a91b             DiskOpenWrite	lda	#PC_WRITE_FILE
ecde : 4cb2ec           		jmp	DiskOpen	;jump into common code
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to read
                        ; from the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to put the data.  On return, C will
                        ; be set if EOF was reached (and no data read), or
                        ; C will be clear and A contains the number of bytes
                        ; actually read into the buffer.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ece1 : 84f8             DiskRead	sty	INL	;save ptr to buffer
ece3 : 86f9             		stx	INH
ece5 : 48               		pha
ece6 : a917             		lda	#PC_READ_BYTES
ece8 : 2013ec           		jsr	xParWriteByte	;command
eceb : 68               		pla		;number of bytes to get
AS65 Assembler for R6502 [1.42].                                     Page   45
---------------------------------- xKIM.asm ----------------------------------

ecec : 2013ec           		jsr	xParWriteByte
ecef : 2005ec           		jsr	xParSetRead	;get ready for response
ecf2 : 203eec           		jsr	xParReadByte	;assume PR_FILE_DATA
ecf5 : 203eec           		jsr	xParReadByte	;length
ecf8 : 48               		pha
ecf9 : aa               		tax			;count
ecfa : f011             		beq	DiskReadEof	;zero = EOF
ecfc : a000             		ldy	#0	;offset
ecfe : 203eec           DiskReadLoop	jsr	xParReadByte
ed01 : 91f8             		sta	(INL),y
ed03 : c8               		iny		;next offset
ed04 : ca               		dex
ed05 : d0f7             		bne	DiskReadLoop
ed07 : 20f5eb           		jsr	xParSetWrite
ed0a : 68               		pla		;retrieve byte count
ed0b : 18               DiskOk		clc	
ed0c : 60               		rts
ed0d : 20f5eb           DiskReadEof	jsr	xParSetWrite
ed10 : 68               		pla
ed11 : 38               		sec
ed12 : 60               		rts
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to write
                        ; to the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to get the data.  On return, C will
                        ; be set if an error was detected, or C will be clear
                        ; if no error.  Note that if A contains 0 on entry,
                        ; no bytes are written.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed13 : c900             DiskWrite	cmp	#0
ed15 : f0f4             		beq	DiskOk
ed17 : 84f8             		sty	INL	;save ptr to filename
ed19 : 86f9             		stx	INH
ed1b : 48               		pha
ed1c : a91c             		lda	#PC_WRITE_BYTES
ed1e : 2013ec           		jsr	xParWriteByte	;command
ed21 : 68               		pla		;number of bytes to write
ed22 : 48               		pha		;save again
ed23 : 2013ec           		jsr	xParWriteByte
ed26 : 68               		pla
ed27 : aa               		tax			;count
ed28 : a000             		ldy	#0	;offset
ed2a : b1f8             DiskWriteLoop	lda	(INL),y	;get next byte
ed2c : 2013ec           		jsr	xParWriteByte
ed2f : c8               		iny		;next offset
ed30 : ca               		dex
ed31 : d0f7             		bne	DiskWriteLoop
ed33 : 2005ec           		jsr	xParSetRead ;read the status
ed36 : 203eec           		jsr	xParReadByte
ed39 : c982             		cmp	#PR_ACK
ed3b : f008             		beq	DiskOk1	;all good
ed3d : 203eec           		jsr	xParReadByte	;read error code
ed40 : 20f5eb           		jsr	xParSetWrite 
ed43 : 38               		sec	
ed44 : 60               		rts
                        ;
ed45 : 20f5eb           DiskOk1		jsr	xParSetWrite
ed48 : 18               		clc
AS65 Assembler for R6502 [1.42].                                     Page   46
---------------------------------- xKIM.asm ----------------------------------

ed49 : 60               		rts
                        ;
                        ;=====================================================
                        ; Call this to close any open file.  No parameters
                        ; and no return status.
                        ;
ed4a : a915             DiskClose	lda	#PC_DONE
ed4c : 4c13ec           		jmp	xParWriteByte
                        
                        
                        
                        ;
                        ;=====================================================
                        ; Show current clock
                        ;
                        	if	SHOW_RTC
ed4f : 20f5eb           doShowClock	jsr	xParSetWrite
ed52 : a907             		lda	#PC_GET_CLOCK
ed54 : 2013ec           		jsr	xParWriteByte
ed57 : 2005ec           		jsr	xParSetRead	;prepare to read
                        ;
ed5a : 203eec           		jsr	xParReadByte
                        ;
                        ; Loop to read the raw data
                        ;
ed5d : a200             		ldx	#0
ed5f : 8e81df           clockread	stx	saveX
ed62 : 203eec           		jsr	xParReadByte
ed65 : ae81df           		ldx	saveX
ed68 : 9d94df           		sta	month,x
ed6b : e8               		inx
ed6c : e008             		cpx	#clock_end-month
ed6e : d0ef             		bne	clockread
                        ;
                        ; Set back to write mode to finish up; all apps are
                        ; supposed to leave the SD interface in write mode.
                        ;
ed70 : 20f5eb           		jsr	xParSetWrite
                        ;
                        ; Now display the data in a user-friendly format.  Eac
                        ; numberic value is in binary, so convert to decimal
                        ; for display.
                        ;
ed73 : 2034ea           		jsr	putsil
ed76 : 0d0a             		db	CR,LF
ed78 : 446174653a2000   		db	"Date: ",0
                        ;
ed7f : ad94df           		lda	month
ed82 : 20c8ed           		jsr	outdec
ed85 : a92f             		lda	#'/'
ed87 : 20a01e           		jsr	OUTCH
ed8a : ad95df           		lda	day
ed8d : 20c8ed           		jsr	outdec
                        ;
                        ; Always force the high part of the year to "/20"
                        ;
ed90 : 2034ea           		jsr	putsil
ed93 : 2f323000         		db	"/20",0
ed97 : ad97df           		lda	year_low
ed9a : 20c8ed           		jsr	outdec
                        ;
                        ; Space over, then do the time
AS65 Assembler for R6502 [1.42].                                     Page   47
---------------------------------- xKIM.asm ----------------------------------

                        ;
ed9d : 2034ea           		jsr	putsil
eda0 : 2c2000           		db	", ",0
                        ;
eda3 : ad98df           		lda	hour
eda6 : 20c8ed           		jsr	outdec
eda9 : a93a             		lda	#':'
edab : 20a01e           		jsr	OUTCH
edae : ad99df           		lda	minute
edb1 : 20c8ed           		jsr	outdec
edb4 : a93a             		lda	#':'
edb6 : 20a01e           		jsr	OUTCH
edb9 : ad9adf           		lda	second
edbc : 20c8ed           		jsr	outdec
                        ;
edbf : 2034ea           		jsr	putsil
edc2 : 0d0a00           		db	CR,LF,0
edc5 : 4c60e0           		jmp	extKim	;return to monitor
                        ;
                        ;=====================================================
                        ; Given a binary value in A, display it as two decimal
                        ; digits.  The input can't be greater than 99.  Always
                        ; print a leading zero if less than 10.
                        ;
edc8 : a000             outdec		ldy	#0	;counts 10s
edca : c90a             out1		cmp	#10
edcc : 9007             		bcc	out2	;below 10
edce : c8               		iny		;count 10
edcf : 38               		sec
edd0 : e90a             		sbc	#10
edd2 : 4ccaed           		jmp	out1
                        ;
edd5 : 48               out2		pha		;save ones
edd6 : 98               		tya		;get tens
edd7 : 20dbed           		jsr	out3	;print tens digit
edda : 68               		pla		;restore ones
                        ;
eddb : 0930             out3		ora	#'0'
eddd : 20a01e           		jsr	OUTCH
ede0 : 60               		rts
                        
                        	endif
                        ;
                        	if	TINY_BASIC
                        		include "mytb.asm"
                        	endif
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   48
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; These are the 6502 vectors.  On the Corsham
                        ; Technologies RAM/ROM board the user can select
                        ; whether or not to use these based on a switch
                        ; setting on the board.
                        ;
fffa =                  		org	$fffa
fffa : 1c1c             NMI		dw	NMIT	;in KIM
fffc : 221c             RESET		dw	RST	;in KIM
fffe : 1f1c             IRQ		dw	IRQT	;in KIM
                        ;
                        		end
                        
                        
No errors in pass 2.
Wrote binary from address $e000 through $ffff.
Total size 8192 bytes.
