AS65 Assembler for R6502 [1.42].                                     Page    1
---------------------------------- xKIM.asm ----------------------------------

2640 lines read, no errors in pass 1.
                        ;*****************************************************
                        ; Extended KIM monitor, written 2015 by Bob Applegate
                        ; K2UT - bob@corshamtech.com
                        ;
                        ; This code was written as part of the Corsham Tech
                        ; 60K RAM/EPROM board.  It provides a working area for
                        ; an extended version of the KIM-1 monitor capabilitie
                        ; mostly based on a TTY interface, not the keypad and
                        ; hex display.
                        ;
                        ; This extensions contains an assortment of basic
                        ; tools that are missing from the KIM-1's built in
                        ; console monitor.  It has commands for loading hex
                        ; files (as opposed to KIM format), accessing an SD
                        ; card system, memory edit, hex dump, etc.  It also
                        ; has the ability for user-defined extensions to be
                        ; added.  And a number of common entry points have
                        ; vectors so the monitor can be modified without
                        ; breaking programs that use it.
                        ;
                        ; I'm not claiming copyright; I wrote most of this,
                        ; but also borrowed from others (credit is given in
                        ; those sections of code).  All of the portions I've
                        ; written are free to use, but please keep my name
                        ; in the comments somewhere.  Even better, consider
                        ; buying a board from us: www.corshamtech.com
                        ;
                        ; 12/01/2015	Bob Applegate
                        ;		Initial development - V0.X
                        ; 03/15/2016	Bob Applegate
                        ;		v1.0 - First official release
                        ; 01/03/2017	Bob Applegate
                        ;		v1.1 - Added S command
                        ; 09/20/2018	Bob Applegate
                        ;		v1.2 - Added auto-run vector
                        ; 01/25/2019	Bob Applegate
                        ;		v1.3 - Added 'X' command.
                        ;		Added the 'C' command to get time.
                        ; 03/09/2019	Bob Applegate
                        ;		v1.4 - Added 'O' command.
                        ; 07/26/2020	Bob Applegate
                        ;		v1.5 - Fixed bug that caused the S
                        ;		command to create empty file with no
                        ;		contents.
                        ;		Minor typo fixes.
                        ;		Added CLD instructions.
                        ; 11/14/2020	Bob Applegate
                        ;		v1.6 - On SD error, display reason code.
                        ; 09/15/2021	Bob Applegate
                        ;		v1.7
                        ;		Added offset calculator command O.
                        ;		Added R command in Edit mode.
                        ;		Removed '.' when loading from console.
                        ; 09/20/2021	Bob Applegate
                        ;		v1.8
                        ;		Made a lot of the command handlers
                        ;		into subroutines and added vectors so
                        ;		external programs can call them.
                        ;		Fixed bugs in Edit mode.
                        ; 12/06/2023	Christopher 'Zoggins' Mallery
                        ;		v1.9
AS65 Assembler for R6502 [1.42].                                     Page    2
---------------------------------- xKIM.asm ----------------------------------

                        ;		Added IEC Save and Load commands
                        ;
                        ;*****************************************************
                        ;
                        ; Useful constants
                        ;
0000 =                  false		equ	0
ffff =                  true		equ	~false
                        ;
                        ; Version number
                        ;
0001 =                  VERSION		equ	1
0009 =                  REVISION	equ	9
0000 =                  BETA_VER	equ	0
                        ;
                        ; Options.  If RAM_BASED is set then the code is put
                        ; into RAM, else it's in ROM.  Very handy for testing
                        ; new code; load the new code with the EPROM version
                        ; and then test away.
                        ;
0000 =                  RAM_BASED	equ	false
af80 =                  RAM_DATA_BASE	equ	$af80
b000 =                  RAM_CODE_BASE	equ	$b000
                        ;
                        ; Set the EPROM base addresses
                        ;
e000 =                  ROM_START	equ	$e000
df80 =                  RAM_START	equ	$df80
                        ;
                        ; This is something that needs more investigation.
                        ; If the SD system isn't reset enough then bad stuff
                        ; happens after the RS button is pressed.  Set this to
                        ; true to only reset the SD system on cold starts.
                        ;
0000 =                  SD_ONLY_COLD	equ	false
                        ;
                        ; Set this to true to include Bob's Tiny BASIC.  It's
                        ; a minimal (ie, tiny) BASIC interpreter good for
                        ; playing around.
                        ;
0000 =                  TINY_BASIC	equ	false
                        ;
                        ; Set this to true to include a command to get/display
                        ; the current time from Corsham Tech SD card.
                        ;
ffff =                  SHOW_RTC	equ	true
                        ;
                        ; Non-printable ASCII constants
                        ;
0000 =                  NUL		equ	$00
0008 =                  BS		equ	$08
000a =                  LF		equ	$0a
000d =                  CR		equ	$0d
001b =                  ESC		equ	$1b
0020 =                  SPC		equ	$20
                        ;
                        ; Intel HEX record types
                        ;
0000 =                  DATA_RECORD	equ	$00
0001 =                  EOF_RECORD	equ	$01
                        ;
                        ; These are various buffer sizes
AS65 Assembler for R6502 [1.42].                                     Page    3
---------------------------------- xKIM.asm ----------------------------------

                        ;
000c =                  FILENAME_SIZE	equ	12
0040 =                  BUFFER_SIZE	equ	64
                        ;
                        ; Max number of bytes per line for hex dump
                        ;
0010 =                  BYTESLINE	equ	16
                        ;
                        ; Flag values used to detect a cold start vs warm
                        ;
0019 =                  COLD_FLAG_1	equ	$19
0062 =                  COLD_FLAG_2	equ	$62
                        ;
                        ;=====================================================
                        ; This macro is used to verify that the current addres
                        ; meets a required value.  Used mostly to guarantee
                        ; changes don't cause entry points to move.
                        ;
                        VERIFY		macro	expected
                        		if * != expected
                        		fail	Not at requested address (expected)
                        		endif
                        		endm
                        ;
                        ;=====================================================
                        ; KIM memory locations
                        ;
                        		bss
00ef =                  		org	$00ef
00ef =                  PCL		ds	1
00f0 =                  PCH		ds	1
00f1 =                  PREG		ds	1
00f2 =                  SPUSER		ds	1
00f3 =                  ACC		ds	1
00f4 =                  YREG		ds	1
00f5 =                  XREG		ds	1
00f6 =                  CHKHI		ds	1
00f7 =                  CHKSUM		ds	1
00f8 =                  INL	    	ds	1
00f9 =                  INH		ds	1
00fa =                  POINTL		ds	1
00fb =                  POINTH		ds	1
00fc =                  TEMP		ds	1
00fd =                  TMPX		ds	1
00fe =                  CHAR		ds	1
00ff =                  MODE		ds	1
                        ;
17e7 =                  		org	$17e7
17e7 =                  CHKL		ds	1
17e8 =                  CHKH		ds	1
17e9 =                  SAVX		ds	3
17ec =                  VEB	    	ds	6
17f2 =                  CNTL30		ds	1
17f3 =                  CNTH30		ds	1
17f4 =                  TIMH		ds	1
17f5 =                  SAL		ds	1
17f6 =                  SAH	    	ds	1
17f7 =                  EAL		ds	1
17f8 =                  EAH	    	ds	1
17f9 =                  ID  		ds	1
                        ;
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    4
---------------------------------- xKIM.asm ----------------------------------

                        ; KIM I/O locations
                        ;
1740 =                  SAD		equ	$1740
1741 =                  PADD		equ	$1741
                        ;
                        ;=====================================================
                        ; KIM subroutines located in the ROMs
                        ;
1c1c =                  NMIT		equ	$1c1c	;NMI handler
1c1f =                  IRQT		equ	$1c1f	;IRQ handler
1c22 =                  RST	    	equ	$1c22	;RESET handler
1c77 =                  TTYKB		equ	$1c77	;do keyboard monitor
1e2f =                  CRLF		equ	$1e2f	;print CR/LF
1e1e =                  PRTPNT		equ	$1e1e	;print POINT
1e3b =                  PRTBYT		equ	$1e3b	;print A as two hex digits
1e5a =                  GETCH		equ	$1e5a	;get a key from tty into A
1e9e =                  OUTSP		equ	$1e9e	;print a space
1ea0 =                  OUTCH		equ	$1ea0	;print A to TTY
1dac =                  SHOW		equ	$1dac
1daf =                  SHOW1		equ	$1daf
1f63 =                  INCPT		equ	$1f63	;inc POINTL/POINTH
                        ;
                        ;=====================================================
                        ; KIM IEC save and load memory locations
                        ;
17f5 =                  IECSAL		equ $17F5
17f6 =                  IECSAH		equ $17F6
17f7 =                  IECEAL		equ $17F7
17f8 =                  IECEAH		equ $17F8
17f9 =                  IECFID		equ $17F9
f148 =                  IECSAV		equ $F148
f000 =                  IECLOD		equ $F000
                        ;
                        ;=====================================================
                        ; I assume the RAM goes from 2000 to DFFF, so carve ou
                        ; a bit for use by the monitor.
                        ;
                        		bss		;uninitialized data area
                        	if	RAM_BASED
                        		org     RAM_DATA_BASE
                        	else
df80 =                  		org	RAM_START
                        	endif
df80 =                  LowestAddress	equ	*
                        ;
                        ; Storage for registers for some monitor calls.
                        ;
df80 =                  saveA		ds	1
df81 =                  saveX		ds	1
df82 =                  saveY		ds	1
                        ;
                        ; Pointer to the subroutine that gets the next input
                        ; character.  Used for doing disk/console input.
                        ;
df83 =                  inputVector	ds	2
                        ;
                        ; Same thing for output.
                        ;
df85 =                  outputVector	ds	2
                        ;
df87 =                  filename	ds	FILENAME_SIZE+1
df94 =                  buffer		ds	BUFFER_SIZE
AS65 Assembler for R6502 [1.42].                                     Page    5
---------------------------------- xKIM.asm ----------------------------------

dfd4 =                  diskBufOffset	ds	1
dfd5 =                  diskBufLength	ds	1
                        ;
dfd6 =                  Temp16L		ds	1
dfd7 =                  Temp16H		ds	1
df81 =                  byteCount	equ	saveX
                        ;
                        ; The clock functions need some storage, but just
                        ; overlay the names onto the buffer.
                        ;
df94 =                  month		equ	buffer
df95 =                  day		equ	month+1
df96 =                  year_high	equ	day+1
df97 =                  year_low	equ	year_high+1
df98 =                  hour		equ	year_low+1
df99 =                  minute		equ	hour+1
df9a =                  second		equ	minute+1
df9b =                  day_of_week	equ	second+1
df9c =                  clock_end	equ	day_of_week+1
                        ;
                        ; The next group of memory are public and can be
                        ; used by user programs, so don't modify where any
                        ; of these are.  If you want to add new public data,
                        ; put them here, always before existing items.  Be
                        ; sure to adjust the ORG.
                        ;
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE-8
                        	else
dff8 =                  		org	ROM_START-8
                        	endif
                        ;
                        ; Before loading a hex file, the MSB of this vector
                        ; is set to FF.  After loading the file, if the MSB
                        ; is no longer FF then the address in this vector is
                        ; jumped to.  Ie, it can auto-run a file.
                        ;
dff8 =                  AutoRun		ds	2
                        ;
                        ; ColdFlag is used to determine if the extended
                        ; monitoring is doing a warm or cold start.
                        ;
dffa =                  ColdFlag	ds	2
                        ;
                        ; Address of a command table for user-created
                        ; extensions to the monitor.
                        ;
dffc =                  ExtensionAddr	ds	2
                        ;
                        ; This is the higest location in RAM usable by user
                        ; programs.  Nobody should go past this address.  If
                        ; you are writing extentions to the monitor, it's
                        ; okay to load before the address and then adjust
                        ; this down to keep others from stomping on your
                        ; extention.
                        ;
                        ; If your program modifies this value, it needs to
                        ; set it back before terminating.
                        ;
dffe =                  HighestAddress	ds	2
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    6
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Code starts at E000 and goes until FFFF, except for
                        ; the 6502 vectors at the end of memory.
                        ;
                        		code
                        	if	RAM_BASED
                        		org	RAM_CODE_BASE
                        	else
e000 =                  		org	ROM_START
                        	endif
                        ;
                        ; Vector table of useful and fun stuff!  These must
                        ; not change or else existing code won't be calling
                        ; the right functions.
                        ;
e000 =                  BASE		equ	*
e000 : 4c60e0           reentry		jmp	extKim	;extended monitor
e003 : 4ca01e           		jmp	OUTCH	;output A to console
e006 : 4c5a1e           		jmp	GETCH	;get a key and echo
e009 : 4c5a1e           		jmp	GETCH	;no echo - KIM can't do it
e00c : 4c5ae0           		jmp	dummyRet	;future - console stat
e00f : 4cbfea           		jmp	putsil	;print string after JSR
e012 : 4ceeea           		jmp	getHex	;get hex value in A
e015 : 4c3b1e           		jmp	PRTBYT	;print A as hex
e018 : 4c26eb           		jmp	getStartAddr
e01b : 4c38eb           		jmp	getEndAddr
e01e : 4c4aeb           		jmp	getAddrRange
e021 : 4c3ee4           		jmp	doHexDump	;perform a hex dump
e024 : 4cd9e5           		jmp	doEdit		;edit memory
e027 : 4cf7e6           		jmp	loadHexConsole	;load hex file via console
e02a : 4cc8e6           		jmp	loadHexFile	;load hex file from SD
e02d : 4c89e9           		jmp	doSDDiskDir	;do a disk directory
e030 : 4c5beb           		jmp	ComputeOffset	;compute relative offset
                        ;
                        ; SD card functions
                        ;
                        		VERIFY	BASE+$0033
                        
e033 : 4c66ec           		jmp	xParInit	;initialization
e036 : 4c80ec           		jmp	xParSetWrite
e039 : 4c90ec           		jmp	xParSetRead
e03c : 4c9eec           		jmp	xParWriteByte
e03f : 4cc9ec           		jmp	xParReadByte
e042 : 4cedec           		jmp	DiskPing
e045 : 4c00ed           		jmp	DiskDir
e048 : 4c0aed           		jmp	DiskDirNext
e04b : 4c3bed           		jmp	DiskOpenRead
e04e : 4c6ced           		jmp	DiskRead
e051 : 4cd5ed           		jmp	DiskClose
e054 : 4c67ed           		jmp	DiskOpenWrite
e057 : 4c9eed           		jmp	DiskWrite
                        ;
                        ; Even more vectors as I ran out of the reserve area!
                        ;
                        
                        ;
                        ;=====================================================
                        ;=====================================================
                        ; Anything past this point can be moved in future
                        ; releases so none of these functions nor data should
                        ; be directly accessed by user programs!!!
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page    7
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is a dummy function for unimplemented functions
                        ; that basically just clears carry and returns.
                        ;
e05a : 18               dummyRet	clc
e05b : 60               		rts
                        ;
e05c : 00               defaultExt	db	0
                        		page
AS65 Assembler for R6502 [1.42].                                     Page    8
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This is the start of the extended KIM monitor.
                        ;
e05d : 4c771c           notty		jmp	TTYKB
e060 : a2ff             extKim		ldx	#$ff
e062 : 9a               		txs
e063 : a901             		lda	#$01	;see if in tty mode
e065 : 2c4017           		bit	SAD
e068 : d0f3             		bne	notty	;branch if in keyboard mode
                        
                        	if	~SD_ONLY_COLD
e06a : 2066ec           		jsr	xParInit
                        	endif
                        ;
                        ; Determine if this is a cold or warm start
                        ;
e06d : adfadf           		lda	ColdFlag
e070 : c919             		cmp	#COLD_FLAG_1
e072 : d00a             		bne	coldStart
e074 : adfbdf           		lda	ColdFlag+1
e077 : c962             		cmp	#COLD_FLAG_2
e079 : d003             		bne	coldStart
e07b : 4c2ee1           		jmp	extKimLoop	;it's a warm start
                        ;
                        ; Cold start
                        ;
e07e : a919             coldStart	lda	#COLD_FLAG_1	;indicate we've done cold
e080 : 8dfadf           		sta	ColdFlag
e083 : a962             		lda	#COLD_FLAG_2
e085 : 8dfbdf           		sta	ColdFlag+1
                        ;
                        ; Point to an empty extension set by default.
                        ;
e088 : a95c             		lda	#defaultExt&$ff	;set extension pointers
e08a : 8dfcdf           		sta	ExtensionAddr
e08d : a9e0             		lda	#defaultExt/256
e08f : 8dfddf           		sta	ExtensionAddr+1
                        ;
                        ; Set HighestAddress to just before our RAM area.
                        ;
e092 : a97f             		lda	#(LowestAddress-1)&$ff
e094 : 8dfedf           		sta	HighestAddress
e097 : a9df             		lda	#(LowestAddress-1)/256
e099 : 8dffdf           		sta	HighestAddress+1
                        	if	SD_ONLY_COLD
                        ;
                        ; Initialize the interface to the SD card.  There seem
                        ; be a problem if this is done too often, so do it jus
                        ; and be done.
                        ;
                        		jsr	xParInit
                        	endif
                        ;
                        ; Display our welcome text
                        ;
e09c : 20bfea           		jsr	putsil
e09f : 0d0a0d0a         		db	CR,LF,CR,LF
e0a3 : 457874656e6465.. 		db	"Extended KIM Monitor v"
e0b9 : 312e3920         		db	VERSION+'0','.',REVISION+'0',' '
                        	if	BETA_VER
                        		db	"BETA "
                        		db	BETA_VER+'0'
AS65 Assembler for R6502 [1.42].                                     Page    9
---------------------------------- xKIM.asm ----------------------------------

                        		db	' '
                        	endif
e0bd : 62792052657472.. 		db  "by RetroSpy Technologies"
e0d5 : 0d0a             		db	CR,LF				
e0d7 : 726574726f2d73.. 		db  "retro-spy.com"
e0e4 : 0d0a             		db	CR,LF
e0e6 : 0d0a             		db	CR,LF
e0e8 : 4261736564206f.. 		db	"Based on the work of Corsham Technologies, LLC"
e116 : 0d0a             		db	CR,LF
e118 : 7777772e636f72.. 		db	"www.corshamtech.com"
e12b : 0d0a             		db	CR,LF
                        	if	RAM_BASED
                        		db	CR,LF
                        		db	"*** RAM BASED VERSION ***"
                        		db	CR,LF
                        	endif
e12d : 00               		db	0
                        ;
                        ; Main command loop.  Put out prompt, get command, etc
                        ; Prints a slightly different prompt for the RAM versi
                        ;
e12e : d8               extKimLoop	cld
e12f : 20e5eb           		jsr	setInputConsole
e132 : 20bfea           		jsr	putsil	;output prompt
e135 : 0d0a             		db	CR,LF	;feel free to change it
                        	if	RAM_BASED
                        		db	"RAM"
                        	endif
e137 : 3e00             		db	">",0
e139 : 205a1e           		jsr	GETCH
e13c : c90d             		cmp	#CR
e13e : f0ee             		beq	extKimLoop
e140 : c90a             		cmp	#LF
e142 : f0ea             		beq	extKimLoop
e144 : 85f3             		sta	ACC	;save key
                        ;
                        ; Now cycle through the list of commands looking for
                        ; what the user just pressed.
                        ;
e146 : a96c             		lda	#commandTable&$ff
e148 : 85fa             		sta	POINTL
e14a : a9e1             		lda	#commandTable/256
e14c : 85fb             		sta	POINTH
e14e : 206dea           		jsr	searchCmd	;try to find it
                        ;
                        ; Hmmm... wasn't one of the built in commands, so
                        ; see if it's an extended command.
                        ;
e151 : adfcdf           		lda	ExtensionAddr
e154 : 85fa             		sta	POINTL
e156 : adfddf           		lda	ExtensionAddr+1
e159 : 85fb             		sta	POINTH
e15b : 206dea           		jsr	searchCmd
                        ;
                        ; If that returns, then the command was not found.
                        ; Print that it's unknown.
                        ;
e15e : 20bfea           		jsr	putsil
e161 : 202d204875683f00 		db	" - Huh?",0
e169 : 4c2ee1           cmdFound	jmp	extKimLoop
                        ;
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   10
---------------------------------- xKIM.asm ----------------------------------

                        ; Vector table of commands.  Each entry consists of a
                        ; single ASCII character (the command), a pointer to
                        ; the function which handles the command, and a pointe
                        ; to a string that describes the command.
                        ;
e16c : 3f               commandTable	db	'?'
e16d : d7e3             		dw	showHelp
e16f : c2e1             		dw	quesDesc
                        ;
                        	if	TINY_BASIC
                        		db	'B'
                        		dw	TBasicCold
                        		dw	bDesc
                        	endif
                        ;
                        	if	SHOW_RTC
e171 : 43               		db	'C'
e172 : daed             		dw	doShowClock
e174 : dfe1             		dw	cDesc
                        	endif
e176 : 44               		db	'D'
e177 : 6ce9             		dw	doDiskDir
e179 : f8e1             		dw	dDesc
                        ;
e17b : 45               		db	'E'	;edit memory
e17c : bee5             		dw	editMemory
e17e : 15e2             		dw	eDesc
                        ;
e180 : 48               		db	'H'	;hex dump
e181 : 30e4             		dw	hexDump
e183 : 2fe2             		dw	hDesc
                        ;
e185 : 4a               		db	'J'	;jump to address
e186 : 3ce9             		dw	jumpAddress
e188 : 4de2             		dw	jDesc
                        ;
e18a : 4b               		db	'K'	;return to KIM monitor
e18b : abe3             		dw	returnKim
e18d : 6be2             		dw	kDesc
                        ;
e18f : 4c               		db	'L'	;load Intel HEX file
e190 : 6ce6             		dw	loadHex
e192 : 8be2             		dw	lDesc
                        ;
e194 : 4d               		db	'M'	;perform memory test
e195 : c3e4             		dw	memTest
e197 : a7e2             		dw	mDesc
                        ;
e199 : 4f               		db	'O'	;branch offset calculator
e19a : fde9             		dw	offCalc
e19c : c1e2             		dw	oDesc
                        ;
e19e : 50               		db	'P'	;ping remote disk
e19f : 4de9             		dw	pingDisk
e1a1 : e7e2             		dw	pDesc
                        ;
e1a3 : 53               		db	'S'	;save memory as hex file
e1a4 : 03e8             		dw	saveHex
e1a6 : 0ae3             		dw	sDesc
                        ;
e1a8 : 54               		db	'T'	;type a file on SD
e1a9 : a8e9             		dw	typeFile
AS65 Assembler for R6502 [1.42].                                     Page   11
---------------------------------- xKIM.asm ----------------------------------

e1ab : 2ce3             		dw	tDesc
                        ;
e1ad : 58               		db	'X'	;return to KIM monitor
e1ae : abe3             		dw	returnKim
e1b0 : 6be2             		dw	kDesc
                        ;
e1b2 : 59               		db	'Y'	;save memory to IEC
e1b3 : 6fee             		dw	saveIEC
e1b5 : 49e3             		dw	yDesc
                        ;
e1b7 : 5a               		db	'Z'	;load memory from IEC
e1b8 : b8ee             		dw	loadIEC
e1ba : 6ae3             		dw	zDesc
                        ;
e1bc : 21               		db	'!'	;do cold restart
e1bd : d1e3             		dw	doCold
e1bf : 8de3             		dw	bangDesc
                        ;
e1c1 : 00               		db	0	;marks end of table
                        ;
                        ;=====================================================
                        ; Descriptions for each command in the command table.
                        ; This wastes a lot of space... I'm open for any
                        ; suggestions to keep the commands clear but reducing
                        ; the amount of space this table consumes.
                        ;
e1c2 : 3f202e2e2e2e2e.. quesDesc	db	"? ........... Show this help",0
                        	if	TINY_BASIC
                        bDesc		db	"B ........... Bob's Tiny BASIC",0
                        	endif
                        	if	SHOW_RTC
e1df : 43202e2e2e2e2e.. cDesc		db	"C ........... Show clock",0
                        	endif
e1f8 : 44202e2e2e2e2e.. dDesc		db	"D ........... Disk directory",0
e215 : 45207878787820.. eDesc		db	"E xxxx ...... Edit memory",0
e22f : 48207878787820.. hDesc		db	"H xxxx xxxx . Hex dump memory",0
e24d : 4a207878787820.. jDesc		db	"J xxxx ...... Jump to address",0
e26b : 4b202e2e2e2e2e.. kDesc		db	"K ........... Go to KIM monitor",0
e28b : 4c202e2e2e2e2e.. lDesc		db	"L ........... Load HEX file",0
e2a7 : 4d207878787820.. mDesc		db	"M xxxx xxxx . Memory test",0
e2c1 : 4f207878787820.. oDesc		db	"O xxxx xxxx . Calculate branch offset",0
e2e7 : 50202e2e2e2e2e.. pDesc		db	"P ........... Ping disk controller",0
e30a : 53207878787820.. sDesc		db	"S xxxx xxxx . Save memory to file",0
e32c : 54202e2e2e2e2e.. tDesc		db	"T ........... Type disk file",0
e349 : 5920787878782d.. yDesc		db  "Y xxxx-xxxx . Save memory to IEC",0
e36a : 5a202e2e2e2e2e.. zDesc		db 	"Z ........... Load memory from IEC",0
e38d : 21202e2e2e2e2e.. bangDesc	db	"! ........... Do a cold start",0
                        ;
                        ;=====================================================
                        ; Return to KIM monitor.  Before returning, set the
                        ; "open address" to the start of the extended monitor
                        ; so the KIM monitor is pointing to it by default.
                        ;
e3ab : 20bfea           returnKim	jsr	putsil
e3ae : 0d0a             		db	CR,LF
e3b0 : 52657475726e69.. 		db	"Returning to KIM..."
e3c3 : 0d0a00           		db	CR,LF,0
e3c6 : a900             		lda	#reentry&$0ff
e3c8 : 85fa             		sta	POINTL	;point back to start...
e3ca : a9e0             		lda	#reentry/256
e3cc : 85fb             		sta	POINTH	;...of this code
e3ce : 4caf1d           		jmp	SHOW1	;return to KIM
AS65 Assembler for R6502 [1.42].                                     Page   12
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; Force a cold start.
                        ;
e3d1 : eefadf           doCold		inc	ColdFlag	;foul up flag
e3d4 : 4c60e0           		jmp	extKim		;...and restart
                        ;
                        ;=====================================================
                        ; Command handler for the ? command
                        ;
e3d7 : 20bfea           showHelp	jsr	putsil
e3da : 0d0a             		db	CR,LF
e3dc : 417661696c6162.. 		db	"Available commands:"
e3ef : 0d0a0a00         		db	CR,LF,LF,0
                        ;
                        ; Print help for built-in commands...
                        ;
e3f3 : a96c             		lda	#commandTable&$ff
e3f5 : 85fa             		sta	POINTL
e3f7 : a9e1             		lda	#commandTable/256
e3f9 : 85fb             		sta	POINTH
e3fb : 2090ea           		jsr	displayHelp	;display help
                        ;
                        ; Now print help for the extension commands...
                        ;
e3fe : adfcdf           		lda	ExtensionAddr
e401 : 85fa             		sta	POINTL
e403 : adfddf           		lda	ExtensionAddr+1
e406 : 85fb             		sta	POINTH
e408 : 2090ea           		jsr	displayHelp
e40b : 202f1e           		jsr	CRLF
e40e : 4c2ee1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This is a generic "not done yet" holder.  Any
                        ; unimplemented commands should point here.
                        ;
e411 : 20bfea           NDY		jsr	putsil
e414 : 0d0a             		db	CR,LF
e416 : 536f7272792c20.. 		db	"Sorry, not done yet."
e42a : 0d0a00           		db	CR,LF,0
e42d : 4c2ee1           NDYdone		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a hex dump of a region of memory.  This code was
                        ; taken from MICRO issue 5, from an article by
                        ; J.C. Williams.  I changed it a bit, but it's still
                        ; basically the same code.
                        ;
                        ; Slight bug: the starting address is rounded down to
                        ; a multiple of 16.  I'll fix it eventually.
                        ;
e430 : 204aeb           hexDump		jsr	getAddrRange
e433 : b0f8             		bcs	NDYdone
e435 : 202f1e           		jsr	CRLF
e438 : 203ee4           		jsr	doHexDump	;subroutine does it
e43b : 4c2ee1           cmdRet2		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine does a hex dump from the address in
                        ; SAL/H to EAL/H.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   13
---------------------------------- xKIM.asm ----------------------------------

                        ; Move start address to POINT but rounded down to the
                        ; 16 byte boundary.
                        ;
e43e : adf617           doHexDump	lda	SAH
e441 : 85fb             		sta	POINTH
e443 : adf517           		lda	SAL
e446 : 29f0             		and	#$f0	;force to 16 byte
e448 : 85fa             		sta	POINTL
                        ;
                        ; This starts each line.  Set flag to indcate we're
                        ; doing the hex portion, print address, etc.
                        ;
e44a : a900             hexdump1	lda	#0	;set flag to hex mode
e44c : 8df917           		sta	ID
e44f : 202f1e           		jsr	CRLF
e452 : 201e1e           		jsr	PRTPNT	;print the address
e455 : a5fa             hexdump2	lda	POINTL	;push start of line...
e457 : 48               		pha		;...address onto stack
e458 : a5fb             		lda	POINTH
e45a : 48               		pha
e45b : 20b9ea           		jsr	space2
e45e : a20f             		ldx	#BYTESLINE-1	;number of bytes per line
e460 : 20b9ea           		jsr	space2	;space before data
                        
e463 : a000             hexdump3	ldy	#0	;get next byte...
e465 : b1fa             		lda	(POINTL),y
e467 : 2cf917           		bit	ID	;hex or ASCII mode?
e46a : 1010             		bpl	hexptbt	;branch if hex mode
                        ;
                        ; Print char if printable, else print a dot
                        ;
e46c : c920             		cmp	#' '
e46e : 9004             		bcc	hexdot
e470 : c97e             		cmp	#'~'
e472 : 9002             		bcc	hexpr
e474 : a92e             hexdot		lda	#'.'
e476 : 20a01e           hexpr		jsr	OUTCH
e479 : 4c82e4           		jmp	hexend
                        ;
                        ; Print character as hex.  
                        ;
e47c : 203b1e           hexptbt 	jsr	PRTBYT	;print as hex
e47f : 20bcea           		jsr	space	;and follow with a space
                        ;
                        ; See if we just dumped the last address.  If not, the
                        ; increment to the next address and continue.
                        ;
e482 : a5fa             hexend  	lda	POINTL	;compare first
e484 : cdf717           		cmp	EAL
e487 : a5fb             		lda	POINTH
e489 : edf817           		sbc	EAH
                        ;
                        ; Now increment to the next address
                        ;
e48c : 08               		php
e48d : 20631f           		jsr	INCPT
e490 : 28               		plp
e491 : 901a             		bcc	hexlntst
                        ;
e493 : 2cf917           		bit	ID
e496 : 3022             		bmi	hexdone
e498 : ca               		dex
AS65 Assembler for R6502 [1.42].                                     Page   14
---------------------------------- xKIM.asm ----------------------------------

e499 : 3006             		bmi	hexdomap
e49b : 20b6ea           hexdump5	jsr	space3
e49e : ca               		dex
e49f : 10fa             		bpl	hexdump5
e4a1 : cef917           hexdomap	dec	ID
e4a4 : 68               		pla
e4a5 : 85fb             		sta	POINTH
e4a7 : 68               		pla
e4a8 : 85fa             		sta	POINTL
e4aa : 4c55e4           		jmp     hexdump2
e4ad : ca               hexlntst	dex
e4ae : 10b3             		bpl	hexdump3
e4b0 : 2cf917           		bit	ID
e4b3 : 10ec             		bpl	hexdomap
e4b5 : 68               		pla
e4b6 : 68               		pla
e4b7 : 4c4ae4           		jmp	hexdump1
                        ;
                        ; Clean up the stack and we're done
                        ;
e4ba : 202f1e           hexdone		jsr	CRLF
e4bd : 68               		pla
e4be : 68               		pla
e4bf : 60               		rts
                        ;
                        ;=====================================================
                        ; This does a memory test of a region of memory.  One
                        ; problem with the KIM is that there is no routine to
                        ; see if a new character is starting, so this loop
                        ; just runs forever unless the user presses RESET.
                        ;
                        ; Asks for the starting and ending locations.
                        ;
                        ; This cycles a rolling bit, then adds a ninth
                        ; pattern to help detect shorted address bits.
                        ; Ie: 01, 02, 04, 08, 10, 20, 40, 80, BA
                        ;
17e7 =                  pattern		equ	CHKL		;re-use some KIM locations
17e8 =                  original	equ	CHKH
                        ;
                        ; Test patterns
                        ;
0001 =                  PATTERN_0	equ	$01
00ba =                  PATTERN_9	equ	$ba
                        ;
e4c0 : 4c2ee1           cmdRet5		jmp	extKimLoop
e4c3 : 204aeb           memTest		jsr	getAddrRange	;get range
e4c6 : b0f8             		bcs	cmdRet5		;branch if abort
                        ;
e4c8 : 20bfea           		jsr	putsil
e4cb : 0d0a             		db	CR,LF
e4cd : 54657374696e67.. 		db	"Testing memory.  Press RESET to abort"
e4f2 : 00               		db	0
e4f3 : a901             		lda	#PATTERN_0	;only set initial...
e4f5 : 8de717           		sta	pattern		;..pattern once
                        ;
                        ; Start of loop.  This fills/tests one complete pass
                        ; of memory.
                        ;
e4f8 : adf517           memTestMain	lda	SAL	;reset pointer to start
e4fb : 85fa             		sta	POINTL
e4fd : adf617           		lda	SAH
AS65 Assembler for R6502 [1.42].                                     Page   15
---------------------------------- xKIM.asm ----------------------------------

e500 : 85fb             		sta	POINTH
                        ;
                        ; Fill memory with the rolling pattern until the last
                        ; location is filled.
                        ;
e502 : a000             		ldy	#0
e504 : ade717           		lda	pattern
e507 : 8de817           		sta	original
e50a : 91fa             memTestFill	sta	(POINTL),y
e50c : c9ba             		cmp	#PATTERN_9	;at last pattern?
e50e : d005             		bne	memFill3
e510 : a901             		lda	#PATTERN_0	;restart pattern
e512 : 4c1ae5           		jmp	memFill4
                        ;
                        ; Rotate pattern left one bit
                        ;
e515 : 0a               memFill3	asl	a
e516 : 9002             		bcc	memFill4	;branch if not overflow
e518 : a9ba             		lda	#PATTERN_9	;ninth pattern
                        ;
                        ; The new pattern is in A.  Now see if we've reached
                        ; the end of the area to be tested.
                        ;
e51a : 48               memFill4	pha			;save pattern
e51b : a5fa             		lda	POINTL
e51d : cdf717           		cmp	EAL
e520 : d007             		bne	memFill5
e522 : a5fb             		lda	POINTH
e524 : cdf817           		cmp	EAH
e527 : f007             		beq	memCheck
                        ;
                        ; Not done, so move to next address and keep going.
                        ;
e529 : 20631f           memFill5	jsr	INCPT
e52c : 68               		pla			;recover pattern
e52d : 4c0ae5           		jmp	memTestFill
                        ;
                        ; Okay, memory is filled, so now go back and test it.
                        ; We kept a backup copy of the initial pattern to
                        ; use, but save the current pattern as the starting
                        ; point for the next pass.
                        ;
e530 : 68               memCheck	pla
e531 : 8de717           		sta	pattern		;for next pass
e534 : adf517           		lda	SAL		;reset pointer to start
e537 : 85fa             		sta	POINTL
e539 : adf617           		lda	SAH
e53c : 85fb             		sta	POINTH
e53e : ade817           		lda	original	;restore initial pattern
e541 : a000             		ldy	#0
e543 : d1fa             memTest2	cmp	(POINTL),y
e545 : d02c             		bne	memFail
e547 : c9ba             		cmp	#PATTERN_9
e549 : d004             		bne	memTest3
                        ;
                        ; Time to reload the pattern
                        ;
e54b : a901             		lda	#PATTERN_0
e54d : d005             		bne	memTest4
                        ;
                        ; Rotate pattern left one bit
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   16
---------------------------------- xKIM.asm ----------------------------------

e54f : 0a               memTest3	asl	a
e550 : 9002             		bcc	memTest4
e552 : a9ba             		lda	#PATTERN_9
                        ;
                        ; The new pattern is in A.
                        ;
e554 : 48               memTest4	pha			;save pattern
e555 : a5fa             		lda	POINTL
e557 : cdf717           		cmp	EAL
e55a : d007             		bne	memTest5	;not at end
e55c : a5fb             		lda	POINTH
e55e : cdf817           		cmp	EAH
e561 : f007             		beq	memDone		;at end of pass
                        ;
                        ; Not at end yet, so inc pointer and continue
                        ;
e563 : 20631f           memTest5	jsr	INCPT
e566 : 68               		pla
e567 : 4c43e5           		jmp	memTest2
                        ;
                        ; Another pass has completed.
                        ;
e56a : 68               memDone		pla
e56b : a92e             		lda	#'.'
e56d : 20a01e           		jsr	OUTCH
e570 : 4cf8e4           		jmp	memTestMain
                        ;
                        ; Failure.  Display the failed address, the expected
                        ; value and what was actually there.
                        ;
e573 : 48               memFail		pha		;save pattern for error report
e574 : 20bfea           		jsr	putsil
e577 : 0d0a             		db	CR,LF
e579 : 4661696c757265.. 		db	"Failure at address ",0
e58d : 201e1e           		jsr	PRTPNT
e590 : 20bfea           		jsr	putsil
e593 : 2e202045787065.. 		db	".  Expected ",0
e5a0 : 68               		pla
e5a1 : 203b1e           		jsr	PRTBYT
e5a4 : 20bfea           		jsr	putsil
e5a7 : 2062757420676f.. 		db	" but got ",0
e5b1 : a000             		ldy	#0
e5b3 : b1fa             		lda	(POINTL),y
e5b5 : 203b1e           		jsr	PRTBYT
e5b8 : 202f1e           		jsr	CRLF
e5bb : 4c2ee1           cmdRet4		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Edit memory.  This waits for a starting address to b
                        ; entered.  It will display the current address and it
                        ; contents.  Possible user inputs and actions:
                        ;
                        ;   Two hex digits will place that value in memory
                        ;   RETURN moves to next address
                        ;   BACKSPACE moves back one address
                        ;
e5be : 20bcea           editMemory      jsr	space
e5c1 : 2026eb           		jsr	getStartAddr
e5c4 : b0f5             		bcs	cmdRet4
e5c6 : adf517           		lda	SAL		;move address into...
e5c9 : 85fa             		sta	POINTL		;...POINT
e5cb : adf617           		lda	SAH
AS65 Assembler for R6502 [1.42].                                     Page   17
---------------------------------- xKIM.asm ----------------------------------

e5ce : 85fb             		sta	POINTH
e5d0 : 202f1e           		jsr	CRLF
e5d3 : 20d9e5           		jsr	doEdit
e5d6 : 4c2ee1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine edits memory.  On entry, POINT has
                        ; the first address to edit.  Upon exit, POINT will
                        ; have been updated to next address to edit.
                        ;
                        ; Display the current location
                        ;
e5d9 : 201e1e           doEdit		jsr	PRTPNT		;print address
e5dc : 20bcea           		jsr	space
e5df : a000             		ldy	#0
e5e1 : b1fa             		lda	(POINTL),y	;get byte
e5e3 : 203b1e           		jsr	PRTBYT		;print it
e5e6 : 20bcea           		jsr	space
                        ;
e5e9 : 20eeea           		jsr	getHex
e5ec : b00d             		bcs	editMem2	;not hex
e5ee : a000             editMem7	ldy	#0
e5f0 : 91fa             		sta	(POINTL),y	;save new value
                        ;
                        ; Bump POINT to next location
                        ;
e5f2 : 202f1e           editMem3	jsr	CRLF
e5f5 : 20631f           		jsr	INCPT
e5f8 : 4cd9e5           		jmp	doEdit
                        ;
                        ; Not hex, so see if another command.  Valid commands 
                        ;
                        ;    CR = advance to next memory location
                        ;    BS = move to previous location
                        ;    R  = compute relative offset
                        ;
e5fb : c952             editMem2	cmp	#'R'		;compute relative branch
e5fd : f017             		beq	editMem4
e5ff : c90d             		cmp	#CR
e601 : f0ef             		beq	editMem3	;move to next
e603 : c908             		cmp	#BS
e605 : d00e             		bne     editexit		;else exit
                        ;
                        ; Move back one location
                        ;
e607 : a5fa             		lda	POINTL
e609 : d002             		bne	editMem8
e60b : c6fb             		dec	POINTH
e60d : c6fa             editMem8	dec	POINTL
e60f : 202f1e           		jsr	CRLF
e612 : 4cd9e5           		jmp	doEdit
                        ;
e615 : 60               editexit	rts
                        ;
                        ; They want to calculate a relative offset
                        ;
e616 : 20bfea           editMem4	jsr	putsil
e619 : 656c6174697665.. 		db	"elative offset to: ",0
e62d : 2038eb           		jsr	getEndAddr
e630 : b0a7             		bcs	doEdit		;bad input
                        ;
                        ; Need to load POINTL/POINTH into SAL/SAH and then
AS65 Assembler for R6502 [1.42].                                     Page   18
---------------------------------- xKIM.asm ----------------------------------

                        ; decrement by one.
                        ;
e632 : a5fb             		lda	POINTH
e634 : 8df617           		sta	SAH
e637 : a5fa             		lda	POINTL
e639 : 8df517           		sta	SAL
e63c : d003             		bne	editMem5
e63e : cef617           		dec	SAH
e641 : cef517           editMem5	dec	SAL
                        ;
e644 : 205beb           		jsr	ComputeOffset
e647 : 9016             		bcc	editMem6	;value good
e649 : 20bfea           		jsr	putsil
e64c : 202d206f757420.. 		db	" - out of range",0
e65c : 4cd9e5           		jmp	doEdit
                        ;
                        ; Relative offset is in A.
                        ;
e65f : 48               editMem6	pha
e660 : 20bcea           		jsr	space
e663 : 68               		pla
e664 : 48               		pha
e665 : 203b1e           		jsr	PRTBYT		;print it
e668 : 68               		pla
e669 : 4ceee5           		jmp	editMem7	;store it
                        ;
                        ;=====================================================
                        ; This handles the Load hex command.
                        ;
e66c : 20bfea           loadHex		jsr	putsil
e66f : 0d0a             		db	CR,LF
e671 : 456e7465722066.. 		db	"Enter filename, or Enter to "
e68d : 6c6f6164206672.. 		db	"load from console: ",0
                        ;
e6a1 : 208deb           		jsr	getFileName	;get filename
e6a4 : ad87df           		lda	filename	;null?
e6a7 : d006             		bne	loaddiskfile
e6a9 : 20f7e6           		jsr	loadHexConsole	;load from console
e6ac : 4cbbe6           		jmp	loadCheckAuto	;check auto-run
                        ;
                        ; Open the file
                        ;
e6af : a087             loaddiskfile	ldy	#filename&$ff
e6b1 : a2df             		ldx	#filename/256
e6b3 : a9ff             		lda	#$ff
e6b5 : 8df917           		sta	ID		;print dots
e6b8 : 20c8e6           		jsr	loadHexFile
                        ;
                        ; If the auto-run vector is no longer $ffff, then jump
                        ; to whatever it points to.
                        ;
e6bb : adf9df           loadCheckAuto	lda	AutoRun+1
e6be : c9ff             		cmp	#$ff		;unchanged?
e6c0 : f003             		beq	lExit11
e6c2 : 6cf8df           		jmp	(AutoRun)	;execute!
e6c5 : 4c2ee1           lExit11		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; This subroutine loads a hex file from the SD.  On
                        ; entry the pointer to the filename is in X (MSB) and
                        ; Y (LSB).
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   19
---------------------------------- xKIM.asm ----------------------------------

e6c8 : a9ff             loadHexFile	lda	#$ff
e6ca : 8df9df           		sta	AutoRun+1
e6cd : 8df917           		sta	ID		;we want dots
e6d0 : 203bed           		jsr	DiskOpenRead
e6d3 : 901c             		bcc	loadHexOk	;opened ok
                        ;
e6d5 : 20bfea           openfail	jsr	putsil
e6d8 : 0d0a             		db	CR,LF
e6da : 4661696c656420.. 		db	"Failed to open file"
e6ed : 0d0a00           		db	CR,LF,0
e6f0 : 60               		rts
                        ;
e6f1 : 20f0eb           loadHexOk	jsr	setInputFile	;redirect input
e6f4 : 4c2fe7           		jmp	loadStart
                        ;
                        ;=====================================================
                        ; This subroutine is called to load a hex file from
                        ; the console.
                        ;
e6f7 : a9ff             loadHexConsole	lda	#$ff
e6f9 : 8df9df           		sta	AutoRun+1
e6fc : a900             		lda	#0
e6fe : 8df917           		sta	ID		;don't print dots
e701 : 20bfea           		jsr	putsil
e704 : 0d0a             		db	CR,LF
e706 : 57616974696e67.. 		db	"Waiting for file, or ESC to"
e721 : 20657869742e2e2e 		db	" exit..."
e729 : 0d0a00           		db	CR,LF,0
e72c : 20e5eb           		jsr	setInputConsole
                        ;
                        ; The start of a line.  First character should be a
                        ; colon, but toss out CRs, LFs, etc.  Anything else
                        ; causes an abort.
                        ;
e72f : 20e2eb           loadStart	jsr	redirectedGetch	;get start of line
e732 : c90d             		cmp	#CR
e734 : f0f9             		beq	loadStart
e736 : c90a             		cmp	#LF
e738 : f0f5             		beq	loadStart
e73a : c93a             		cmp	#':'		;what we expect
e73c : d05a             		bne	loadAbortB
                        ;
                        ; Get the header of the record
                        ;
e73e : a900             		lda	#0
e740 : 8de717           		sta	CHKL		;initialize checksum
                        ;
e743 : 20eeea           		jsr	getHex		;get byte count
e746 : b054             		bcs	loadAbortC
e748 : 8d81df           		sta	byteCount	;save byte count
e74b : 2032e9           		jsr	updateCrc
e74e : 20eeea           		jsr	getHex		;get the MSB of offset
e751 : b04d             		bcs	loadAbortD
e753 : 85fb             		sta	POINTH
e755 : 2032e9           		jsr	updateCrc
e758 : 20eeea           		jsr	getHex		;get LSB of offset
e75b : b047             		bcs	loadAbortE
e75d : 85fa             		sta	POINTL
e75f : 2032e9           		jsr	updateCrc
e762 : 20eeea           		jsr	getHex		;get the record type
e765 : b041             		bcs	loadAbortF
e767 : 2032e9           		jsr	updateCrc
AS65 Assembler for R6502 [1.42].                                     Page   20
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Only handle two record types:
                        ;    00 = data record
                        ;    01 = end of file record
                        ;
e76a : c900             		cmp	#DATA_RECORD
e76c : f05d             		beq	loadDataRec
e76e : c901             		cmp	#EOF_RECORD
e770 : f042             		beq	loadEof
                        ;
                        ; Unknown record type
                        ;
e772 : a941             		lda	#'A'		;reason
                        ;
                        ; This is the common error handler for various reasons
                        ; On entry A contains an ASCII character which is outp
                        ; to indicate the specific error reason.
                        ;
e774 : 48               loadAbort       pha			;save reason
e775 : 20bfea           		jsr	putsil
e778 : 0d0a             		db	CR,LF
e77a : 41626f7274696e.. 		db	"Aborting, reason: "
e78c : 00               		db	0
e78d : 68               		pla			;restore and...
e78e : 20a01e           		jsr	OUTCH		;...display reason
e791 : 202f1e           		jsr	CRLF
e794 : 20e5eb           loadExit	jsr	setInputConsole
e797 : 60               		rts
                        ;
                        ; Various error reason codes.  This was meant to be
                        ; very temporary as I worked out the real problem, but
                        ; this debug code immediately "solved" the problem so
                        ; I just left these as-is until the root cause is
                        ; discovered.
                        ;
e798 : a942             loadAbortB	lda	#'B'
e79a : d0d8             		bne	loadAbort
                        ;
e79c : a943             loadAbortC	lda	#'C'
e79e : d0d4             		bne	loadAbort
                        ;
e7a0 : a944             loadAbortD	lda	#'D'
e7a2 : d0d0             		bne	loadAbort
                        ;
e7a4 : a945             loadAbortE	lda	#'E'
e7a6 : d0cc             		bne	loadAbort
                        ;
e7a8 : a946             loadAbortF	lda	#'F'
e7aa : d0c8             		bne	loadAbort
                        ;
e7ac : a947             loadAbortG	lda	#'G'
e7ae : d0c4             		bne	loadAbort
                        ;
e7b0 : a948             loadAbortH	lda	#'H'
e7b2 : d0c0             		bne	loadAbort
                        ;
                        ; EOF is easy
                        ;
e7b4 : 20eeea           loadEof		jsr	getHex		;get checksum
e7b7 : 20e5eb           		jsr	setInputConsole	;reset input vector
e7ba : 20bfea           		jsr	putsil
e7bd : 0d0a             		db	CR,LF
AS65 Assembler for R6502 [1.42].                                     Page   21
---------------------------------- xKIM.asm ----------------------------------

e7bf : 5375636365737321 		db	"Success!"
e7c7 : 0d0a00           		db	CR,LF,0
e7ca : 60               		rts
                        ;
                        ; Data records have more work.  After processing the
                        ; line, print a dot to indicate progress.  This should
                        ; be re-thought as it could slow down loading a really
                        ; big file if the console speed is slow.
                        ;
e7cb : ae81df           loadDataRec	ldx	byteCount	;byte count
e7ce : a000             		ldy	#0		;offset
e7d0 : 8e81df           loadData1	stx	byteCount
e7d3 : 8c82df           		sty	saveY
e7d6 : 20eeea           		jsr	getHex
e7d9 : b0d1             		bcs	loadAbortG
e7db : 2032e9           		jsr	updateCrc
e7de : ac82df           		ldy	saveY
e7e1 : ae81df           		ldx	byteCount
e7e4 : 91fa             		sta	(POINTL),y
e7e6 : c8               		iny
e7e7 : ca               		dex
e7e8 : d0e6             		bne	loadData1
                        ;
                        ; All the bytes were read so get the checksum and see
                        ; if it agrees.  The checksum is a twos-complement, so
                        ; just add the checksum into what we've been calculati
                        ; and if the result is zero then the record is good.
                        ;
e7ea : 20eeea           		jsr	getHex		;get checksum
e7ed : 18               		clc
e7ee : 6de717           		adc	CHKL
e7f1 : d0bd             		bne	loadAbortH	;non-zero is error
                        ;
                        ; If loading from an SD file then print a dot at the
                        ; end of each record.  Doing this for serial input is
                        ; very bad because the terminal program is sending the
                        ; next character while this is sending the dot.  Ie,
                        ; data is lost.
                        ;
e7f3 : adf917           		lda	ID
e7f6 : f005             		beq	lrecdone	;jump if not file
e7f8 : a92e             		lda	#'.'		;sanity indicator when
e7fa : 20a01e           		jsr	OUTCH		;...loading from file
e7fd : 4c2fe7           lrecdone	jmp	loadStart
e800 : 4c2ee1           lExit1		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory as a
                        ; file on the SD.
                        ;
e803 : 204aeb           saveHex		jsr	getAddrRange	;get range to dump
e806 : b0f8             		bcs	lExit1	;abort on error
                        ;
                        ; Get the filename to save to
                        ;
e808 : 20bfea           		jsr	putsil
e80b : 0d0a             		db	CR,LF
e80d : 456e7465722066.. 		db	"Enter filename, or Enter to "
e829 : 646973706c6179.. 		db	"display to console: ",0
                        ;
e83e : 208deb           		jsr	getFileName	;get filename
e841 : ad87df           		lda	filename	;null?
AS65 Assembler for R6502 [1.42].                                     Page   22
---------------------------------- xKIM.asm ----------------------------------

e844 : f012             		beq	saveHexConsole	;dump to console
                        ;
                        ; They selected a file, so try to open it.
                        ;
e846 : a2df             		ldx	#filename>>8
e848 : a087             		ldy	#filename&$ff
e84a : 2067ed           		jsr	DiskOpenWrite	;attempt to open file
e84d : 9003             		bcc	sopenok		;branch if opened ok
e84f : 4cd5e6           		jmp	openfail
                        ;
e852 : 203aec           sopenok		jsr	setOutputFile
e855 : 4c5be8           		jmp	savehex2
                        ;
                        ; They are saving to the console.  Set up the output
                        ; vector and do the job.
                        ;
e858 : 202fec           saveHexConsole	jsr	setOutputConsole
                        ;
                        ; Compute the number of bytes to dump
                        ;
e85b : 38               savehex2	sec
e85c : adf717           		lda	EAL
e85f : edf517           		sbc	SAL
e862 : 8dd6df           		sta	Temp16L
e865 : adf817           		lda	EAH
e868 : edf617           		sbc	SAH
e86b : 8dd7df           		sta	Temp16H
e86e : 9042             		bcc	SDone	;start > end
e870 : 0900             		ora	#0
e872 : 303e             		bmi	SDone	;more than 32K seems wrong
                        ;
                        ; Add one to the count
                        ;
e874 : eed6df           		inc	Temp16L
e877 : d003             		bne	slab1
e879 : eed7df           		inc	Temp16H
                        ;
                        ; Move pointer to zero page
                        ;
e87c : adf517           slab1		lda	SAL
e87f : 85fa             		sta	POINTL
e881 : adf617           		lda	SAH
e884 : 85fb             		sta	POINTH
                        ;
                        ; Top of each loop.  Start by seeing if there are any 
                        ; left to dump.
                        ;
e886 : add7df           Sloop1		lda	Temp16H
e889 : d02a             		bne	Sgo	;more to do
e88b : add6df           		lda	Temp16L
e88e : d025             		bne	Sgo	;more to do
                        ;
                        ; At end of the region, so output an end record.  This
                        ; probably looks like overkill but keep in mind this
                        ; might be going to a file so we can't use the normal
                        ; string put functions.
                        ;
e890 : a93a             		lda	#':'
e892 : 201dec           		jsr	redirectedOutch
e895 : a900             		lda	#0
e897 : 2003ec           		jsr	HexToOutput
e89a : 2003ec           		jsr	HexToOutput
AS65 Assembler for R6502 [1.42].                                     Page   23
---------------------------------- xKIM.asm ----------------------------------

e89d : 2003ec           		jsr	HexToOutput
e8a0 : a901             		lda	#1
e8a2 : 2003ec           		jsr	HexToOutput
e8a5 : a9ff             		lda	#$ff
e8a7 : 2003ec           		jsr	HexToOutput
                        ;
                        ; If output to file, flush and close the file.
                        ;
e8aa : ad87df           		lda	filename
e8ad : f003             		beq	SDone		;it's going to console
e8af : 2020ec           		jsr	CloseOutFile
e8b2 : 4c2ee1           SDone		jmp	extKimLoop	;back to the monitor
                        ;
                        ; This dumps the next line.  See how many bytes are le
                        ; and if more than BYTESLINE, then just do BYTESLINE.
                        ;
e8b5 : add7df           Sgo		lda	Temp16H
e8b8 : d007             		bne	Sdef	;do default number of bytes
e8ba : add6df           		lda	Temp16L
e8bd : c910             		cmp	#BYTESLINE
e8bf : 9002             		bcc	Scnt	;more than max per line
e8c1 : a910             Sdef		lda	#BYTESLINE
e8c3 : 8de917           Scnt		sta	SAVX	;for decrementing
e8c6 : 8df917           		sta	ID	;for subtracting
                        ;
                        ; Put out the header
                        ;
e8c9 : a93a             		lda	#':'
e8cb : 201dec           		jsr	redirectedOutch
                        ;
e8ce : ade917           		lda	SAVX
e8d1 : 8de717           		sta	CHKL	;start checksum
e8d4 : 2003ec           		jsr	HexToOutput
                        ;
e8d7 : a5fb             		lda	POINTH	;starting address
e8d9 : 2032e9           		jsr	updateCrc
e8dc : 2003ec           		jsr	HexToOutput
e8df : a5fa             		lda	POINTL
e8e1 : 2032e9           		jsr	updateCrc
e8e4 : 2003ec           		jsr	HexToOutput
                        ;
e8e7 : a900             		lda	#0	;record type - data
e8e9 : 2003ec           		jsr	HexToOutput
                        ;
                        ; Now print the proper number of bytes
                        ;
e8ec : a000             Sloop2		ldy	#0
e8ee : b1fa             		lda	(POINTL),y	;get byte
e8f0 : 2032e9           		jsr	updateCrc
e8f3 : 2003ec           		jsr	HexToOutput
e8f6 : 20631f           		jsr	INCPT	;increment pointer
                        ;
e8f9 : cee917           sdec		dec	SAVX
e8fc : d0ee             		bne	Sloop2
                        ;
                        ; Now print checksum
                        ;
e8fe : ade717           		lda	CHKL
e901 : 49ff             		eor	#$ff	;one's complement
e903 : 18               		clc
e904 : 6901             		adc	#1	;two's complement
e906 : 2003ec           		jsr	HexToOutput
AS65 Assembler for R6502 [1.42].                                     Page   24
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ; Output a CR/LF
                        ;
e909 : a90d             		lda	#CR
e90b : 201dec           		jsr	redirectedOutch
e90e : a90a             		lda	#LF
e910 : 201dec           		jsr	redirectedOutch
                        ;
                        ; If saving to disk, output a dot to indicate progress
                        ;
e913 : ad87df           		lda	filename
e916 : f005             		beq	shf2
                        ;
e918 : a92e             		lda	#'.'
e91a : 20a01e           		jsr	OUTCH	;goes to console
                        ;
e91d : 38               shf2		sec
e91e : add6df           		lda	Temp16L
e921 : edf917           		sbc	ID
e924 : 8dd6df           		sta	Temp16L
e927 : add7df           		lda	Temp16H
e92a : e900             		sbc	#0
e92c : 8dd7df           		sta	Temp16H
                        ;
e92f : 4c86e8           		jmp	Sloop1
                        ;
                        ;=====================================================
                        ; Adds the character in A to the CRC.  Preserves A.
                        ;
e932 : 48               updateCrc	pha
e933 : 18               		clc
e934 : 6de717           		adc	CHKL
e937 : 8de717           		sta	CHKL
e93a : 68               		pla
e93b : 60               		rts
                        ;
                        ;=====================================================
                        ; Handles the command to prompt for an address and the
                        ; jump to it.
                        ;
e93c : 20bcea           jumpAddress     jsr	space
e93f : 2026eb           		jsr	getStartAddr
e942 : b006             		bcs	cmdRet	;branch on bad address
e944 : 202f1e           		jsr	CRLF
e947 : 6cf517           		jmp	(SAL)	;else jump to address
                        ;
e94a : 4c2ee1           cmdRet		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Ping the Arduno disk controller.  This just sends th
                        ; PING command gets back one character, then returns.
                        ; Not much of a test but is sufficient to prove the
                        ; link is working.
                        ;
e94d :                  pingDisk
                        ;		jsr	xParInit	;init interface
e94d : 20bfea           		jsr	putsil
e950 : 696e672e2e2e2000 		db	"ing... ",0
e958 : 20edec           		jsr	DiskPing
e95b : 20bfea           		jsr	putsil
e95e : 7375636365737321 		db	"success!"
e966 : 0d0a00           		db	CR,LF,0
AS65 Assembler for R6502 [1.42].                                     Page   25
---------------------------------- xKIM.asm ----------------------------------

e969 : 4c2ee1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Do a disk directory of the SD card.
                        ;
e96c : 20bfea           doDiskDir	jsr	putsil
e96f : 69736b20446972.. 		db	"isk Directory..."
e97f : 0d0a00           		db	CR,LF,0
e982 : 2089e9           		jsr	doSDDiskDir
e985 : 4c2ee1           		jmp	extKimLoop
e988 : 60               doDiskDirEnd	rts
                        ;
                        ;=====================================================
                        ; Subroutine to do a disk directory.  Prints filenames
                        ; to the console.
                        ;
e989 : 2066ec           doSDDiskDir	jsr	xParInit
e98c : 2000ed           		jsr	DiskDir
                        ;
                        ; Get/Display each entry
                        ;
e98f : a2df             doDiskDirLoop   ldx	#filename/256	;pointer to buffer
e991 : a087             		ldy	#filename&$ff
e993 : 86f9             		stx	INH		;save for puts
e995 : 84f8             		sty	INL
e997 : 200aed           		jsr	DiskDirNext	;get next entry
e99a : b0ec             		bcs	doDiskDirEnd	;carry = end of list
e99c : 20b6ea           		jsr	space3
e99f : 20d3ea           		jsr	puts		;else print name
e9a2 : 202f1e           		jsr	CRLF
e9a5 : 4c8fe9           		jmp	doDiskDirLoop	;do next entry
                        ;
                        ;=====================================================
                        ; Type the contents of an SD file to console.
                        ;
e9a8 : 20bfea           typeFile	jsr	putsil
e9ab : 202d20456e7465.. 		db	" - Enter filename: ",0
e9bf : 208deb           		jsr	getFileName
e9c2 : a087             		ldy	#filename&$ff
e9c4 : a2df             		ldx	#filename/256
                        ;		jsr	xParInit
e9c6 : 203bed           		jsr	DiskOpenRead
e9c9 : 901e             		bcc	typeFile1	;opened ok
                        ;
e9cb : 20bfea           		jsr	putsil
e9ce : 0d0a             		db	CR,LF
e9d0 : 4661696c656420.. 		db	"Failed to open file"
e9e3 : 0d0a00           		db	CR,LF,0
e9e6 : 4c2ee1           		jmp	extKimLoop
                        ;
                        ; Now just keep reading in bytes and displaying them.
                        ;
e9e9 : 20f0eb           typeFile1	jsr	setInputFile	;reading from file
e9ec : 20b3eb           typeFileLoop	jsr	getNextFileByte
e9ef : b006             		bcs	typeEof
e9f1 : 20a01e           		jsr	OUTCH	;display character
e9f4 : 4cece9           		jmp	typeFileLoop
                        ;
e9f7 : 20d5ed           typeEof		jsr	DiskClose
e9fa : 4c2ee1           		jmp	extKimLoop
                        ;
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   26
---------------------------------- xKIM.asm ----------------------------------

                        ; Calculate the offset for a relative branch.  6502
                        ; relative branch calculations are well known.
                        ;
                        ; Offset from branch (BASE) = TARGET - (BASE+2).
                        ; If the result is positive, upper byte must be
                        ; zero.  If negative, upper byte must be FF.
                        ;
                        ; BASE	TARGET	Computed	Actual
                        ; 0200	0200	0200-(0200+2)	FFFE
                        ; 0200	020E	020E-(0200+2)	000C
                        ; 0226	0220	0220-(0226+2)	FFF8
                        ; 0156	015A	015A-(0156+2)	0002
                        ; 015C	012D	012D-(015C+2)	FFCF
                        ; 0200	0300	0300-(0200+2)	00FE - out of range
                        ; 0300	0200	0200-(0300+2)	FEFE - out of range
                        ;
e9fd : 20bfea           offCalc		jsr	putsil
ea00 : 202d204272616e.. 		db	" - Branch instruction address: "
ea1f : 00               		db	0
ea20 : 2026eb           		jsr	getStartAddr
ea23 : b030             		bcs	calcExit
ea25 : 20bfea           		jsr	putsil
ea28 : 2c206272616e63.. 		db	", branch to: "
ea35 : 00               		db	0
ea36 : 2038eb           		jsr	getEndAddr
ea39 : b01a             		bcs	calcExit
ea3b : 205beb           		jsr	ComputeOffset	;does the work
ea3e : 9018             		bcc	relgood		;if good offset
                        ;
                        ; Branch is out of range.
                        ;
ea40 : 20bfea           		jsr	putsil
ea43 : 202d206f757420.. 		db	" - out of range",CR,LF,0
ea55 : 4c2ee1           calcExit	jmp	extKimLoop
                        ;
                        ; Branch is in range so dislay the value.
                        ;
ea58 : 48               relgood		pha			;save offset
ea59 : 20bfea           		jsr	putsil
ea5c : 204f6666736574.. 		db	" Offset: ",0
ea66 : 68               		pla
ea67 : 203b1e           		jsr	PRTBYT
ea6a : 4c2ee1           		jmp	extKimLoop
                        ;
                        ; Add new commands here...
                        ;
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   27
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This subroutine will search for a command in a table
                        ; and call the appropriate handler.  See the command
                        ; table near the start of the code for what the format
                        ; is.  If a match is found, pop off the return address
                        ; from the stack and jump to the code.  Else, return.
                        ;
ea6d : a000             searchCmd	ldy	#0
ea6f : b1fa             cmdLoop		lda	(POINTL),y
ea71 : f01c             		beq	cmdNotFound
ea73 : c5f3             		cmp	ACC	;compare to user's input
ea75 : f007             		beq	cmdMatch
ea77 : c8               		iny		;start of function ptr
ea78 : c8               		iny
ea79 : c8               		iny		;start of help
ea7a : c8               		iny
ea7b : c8               		iny		;move to next command
ea7c : d0f1             		bne	cmdLoop
                        ;
                        ; It's found!  Load up the address of the code to call
                        ; pop the return address off the stack and jump to the
                        ; handler.
                        ;
ea7e : c8               cmdMatch	iny
ea7f : b1fa             		lda	(POINTL),y	;handler LSB
ea81 : 48               		pha
ea82 : c8               		iny
ea83 : b1fa             		lda	(POINTL),y	;handler MSB
ea85 : 85fb             		sta	POINTH
ea87 : 68               		pla
ea88 : 85fa             		sta	POINTL
ea8a : 68               		pla		;pop return address
ea8b : 68               		pla
ea8c : 6cfa00           		jmp	(POINTL)
                        ;
                        ; Not found, so just return.
                        ;
ea8f : 60               cmdNotFound	rts
                        ;
                        ;=====================================================
                        ; Given a pointer to a command table in POINT, display
                        ; the help text for all commands in the table.
                        ;
ea90 : a000             displayHelp	ldy	#0	;index into command table
ea92 : b1fa             showHelpLoop	lda	(POINTL),y	;get command
ea94 : f01f             		beq	showHelpDone	;jump if at end
                        ;
                        ; Display this entry's descriptive text
                        ;
ea96 : c8               		iny		;skip over command
ea97 : c8               		iny		;skip over function ptr
ea98 : c8               		iny
ea99 : b1fa             		lda	(POINTL),y
ea9b : 85f8             		sta	INL
ea9d : c8               		iny
ea9e : b1fa             		lda	(POINTL),y
eaa0 : 85f9             		sta	INH
eaa2 : 98               		tya
eaa3 : 48               		pha
eaa4 : 209e1e           		jsr	OUTSP
eaa7 : 209e1e           		jsr	OUTSP
AS65 Assembler for R6502 [1.42].                                     Page   28
---------------------------------- xKIM.asm ----------------------------------

eaaa : 20d3ea           		jsr	puts	;print description
eaad : 202f1e           		jsr	CRLF
eab0 : 68               		pla
eab1 : a8               		tay
eab2 : c8               		iny		;point to next entry
eab3 : d0dd             		bne	showHelpLoop
eab5 : 60               showHelpDone	rts
                        ;
                        ;=====================================================
                        ; Print some spaces.
                        ;
eab6 : 20bcea           space3		jsr	space
eab9 : 20bcea           space2		jsr	space
eabc : 4c9e1e           space   	jmp	OUTSP
                        ;
                        ;=====================================================
                        ; This prints the null-terminated string that
                        ; immediately follows the JSR to this function.  This
                        ; version was written by Ross Archer and is at:
                        ;
                        ;    www.6502.org/source/io/primm.htm
                        ;
eabf : 68               putsil		pla
eac0 : 85f8             		sta	INL
eac2 : 68               		pla
eac3 : 85f9             		sta	INH
eac5 : a001             		ldy	#1
eac7 : 20d5ea           		jsr	putsy
eaca : e6f8             		inc	INL
eacc : d002             		bne	puts2
eace : e6f9             		inc	INH
ead0 : 6cf800           puts2		jmp	(INL)
                        ;
                        ;=====================================================
                        ; This prints the null terminated string pointed to by
                        ; INL and INH.  Modifies those locations to point to
                        ; the end of the string.
                        ;
ead3 : a000             puts		ldy	#0
ead5 : b1f8             putsy		lda	(INL),y
ead7 : e6f8             		inc	INL
ead9 : d002             		bne	puts1
eadb : e6f9             		inc	INH
eadd : 0900             puts1		ora	#0
eadf : f00c             		beq	putsdone
eae1 : 8c82df           		sty	saveY
eae4 : 20a01e           		jsr	OUTCH	;print character
eae7 : ac82df           		ldy	saveY
eaea : 4cd5ea           		jmp	putsy
eaed : 60               putsdone	rts
                        ;
                        ;=====================================================
                        ; This gets two hex characters and returns the value
                        ; in A with carry clear.  If a non-hex digit is
                        ; entered, then A contans the offending character and
                        ; carry is set.
                        ;
eaee : 2004eb           getHex		jsr	getNibble
eaf1 : b01e             		bcs	getNibBad
eaf3 : 0a               		asl	a
eaf4 : 0a               		asl	a
eaf5 : 0a               		asl	a
AS65 Assembler for R6502 [1.42].                                     Page   29
---------------------------------- xKIM.asm ----------------------------------

eaf6 : 0a               		asl	a
eaf7 : 8d80df           		sta	saveA
eafa : 2004eb           		jsr	getNibble
eafd : b012             		bcs	getNibBad
eaff : 0d80df           		ora	saveA
eb02 : 18               		clc
eb03 : 60               		rts
                        ;
                        ; Helper.  Gets next input char and converts to a
                        ; value from 0-F in A and returns C clear.  If not a
                        ; valid hex character, return C set.
                        ;
eb04 : 20e2eb           getNibble	jsr	redirectedGetch
eb07 : a20f             		ldx	#nibbleHexEnd-nibbleHex-1
eb09 : dd16eb           getNibble1	cmp	nibbleHex,x
eb0c : f005             		beq	getNibF	;got match
eb0e : ca               		dex
eb0f : 10f8             		bpl	getNibble1
eb11 : 38               getNibBad	sec
eb12 : 60               		rts
                        
eb13 : 8a               getNibF		txa		;index is value
eb14 : 18               		clc
eb15 : 60               		rts
                        ;
eb16 : 30313233343536.. nibbleHex	db	"0123456789ABCDEF"
eb26 =                  nibbleHexEnd	equ	*
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; SAL and SAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eb26 : 20eeea           getStartAddr	jsr	getHex
eb29 : b00c             		bcs	getDone
eb2b : 8df617           		sta	SAH
eb2e : 20eeea           		jsr	getHex
eb31 : b004             		bcs	getDone
eb33 : 8df517           		sta	SAL
eb36 : 18               		clc
eb37 : 60               getDone		rts
                        ;
                        ;=====================================================
                        ; Gets a four digit hex address amd places it in
                        ; EAL and EAH.  Returns C clear if all is well, or C
                        ; set on error and A contains the character.
                        ;
eb38 : 20eeea           getEndAddr	jsr	getHex
eb3b : b0fa             		bcs	getDone
eb3d : 8df817           		sta	EAH
eb40 : 20eeea           		jsr	getHex
eb43 : b0f2             		bcs	getDone
eb45 : 8df717           		sta	EAL
eb48 : 18               		clc
eb49 : 60               		rts
                        ;
                        ;=====================================================
                        ; Get an address range and leave them in SAL and EAL.
                        ;
eb4a : 20bcea           getAddrRange    jsr	space
eb4d : 2026eb           		jsr	getStartAddr
eb50 : b0e5             		bcs	getDone
AS65 Assembler for R6502 [1.42].                                     Page   30
---------------------------------- xKIM.asm ----------------------------------

eb52 : a92d             		lda	#'-'
eb54 : 20a01e           		jsr	OUTCH
eb57 : 2038eb           		jsr	getEndAddr
eb5a : 60               		rts
                        ;
                        ;=====================================================
                        ; This computes the relative offset between the
                        ; address in SAL/SAH (address of branch instruction)
                        ; and EAL/EAH (address to jump to).  If a valid range,
                        ; returns C clear and the offset in A.  If the branch
                        ; is out of range, C is set and A undefined.  Modifies
                        ; A, SAL and SAH.
                        ;
eb5b :                  ComputeOffset
                        ;
                        ; Add two to the end (BASE) address.  For calculations
                        ;   BASE = SAL/SAH
                        ;   TARGET = EAL/EAH
                        ;
eb5b : 18               		clc
eb5c : adf517           		lda	SAL
eb5f : 6902             		adc	#2
eb61 : 8df517           		sta	SAL
eb64 : 9003             		bcc	coffsub
eb66 : eef617           		inc	SAH
                        ;
                        ; Subtract the BASE (end) address from the TARGET (sta
                        ;
eb69 : 38               coffsub		sec
eb6a : adf717           		lda	EAL
eb6d : edf517           		sbc	SAL
eb70 : 48               		pha		;save for later
eb71 : 8df517           		sta	SAL
eb74 : adf817           		lda	EAH
eb77 : edf617           		sbc	SAH
eb7a : 8df617           		sta	SAH	;SAL/SAH contain offset
                        ;
                        ; High part must be either FF for negative branch or
                        ; 00 for a positive branch.  Cheat a bit here by rolli
                        ; the MSBit into C and adding to the MSByte.  If the
                        ; result is zero then everything is cool.
                        ;
eb7d : 68               		pla		;restore LSB of offset
eb7e : 48               		pha
eb7f : 0a               		asl	a	;put sign into C
eb80 : adf617           		lda	SAH
eb83 : 6900             		adc	#0
eb85 : f003             		beq	cogood	;branch if in range
                        ;
eb87 : 68               		pla		;clean up stack
eb88 : 38               		sec		;error
eb89 : 60               		rts
                        ;
eb8a : 68               cogood		pla		;get back offset
eb8b : 18               		clc
eb8c : 60               		rts
                        ;
                        ;=====================================================
                        ; Get a disk filename.  The KIM's behavior of echoing
                        ; every key prevents this from being too fancy, but it
                        ; good enough.
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   31
---------------------------------- xKIM.asm ----------------------------------

eb8d : a200             getFileName	ldx	#0
eb8f : 205a1e           getFilename1	jsr	GETCH	;get next key
eb92 : c90d             		cmp	#CR	;end of the input?
eb94 : f014             		beq	getFnDone
eb96 : c908             		cmp	#BS	;backspace?
eb98 : f00a             		beq	getFnDel
eb9a : e00c             		cpx	#FILENAME_SIZE	;check size
eb9c : f0f1             		beq	getFilename1	;at length limit
eb9e : 9d87df           		sta	filename,x	;else save it
eba1 : e8               		inx
eba2 : d0eb             		bne	getFilename1
                        ;
eba4 : ca               getFnDel	dex		;back up one
eba5 : 10e8             		bpl	getFilename1
eba7 : e8               		inx		;can't go past start
eba8 : f0e5             		beq	getFilename1
ebaa : a900             getFnDone       lda	#0	;terminate line
ebac : 9d87df           		sta	filename,x
ebaf : 202f1e           		jsr	CRLF
ebb2 : 60               		rts
                        ;
                        ;=====================================================
                        ; This gets the next byte from an open disk file.  If
                        ; there are no more bytes left, this returns C set.
                        ; Else, C is clear and A contains the character.
                        ;
ebb3 : aed4df           getNextFileByte ldx 	diskBufOffset
ebb6 : ecd5df           		cpx	diskBufLength
ebb9 : d014             		bne	hasdata		;branch if still data
                        ;
                        ; There is no data left in the buffer, so read a
                        ; block from the SD system.
                        ;
ebbb : a940             		lda	#BUFFER_SIZE
ebbd : a2df             		ldx	#buffer>>8
ebbf : a094             		ldy	#buffer&$ff
ebc1 : 206ced           		jsr	DiskRead
ebc4 : b012             		bcs	getNextEof
                        ;
                        ; A contains the number of bytes actually read.
                        ;
ebc6 : 8dd5df           		sta	diskBufLength	;save length
ebc9 : c900             		cmp	#0		;shouldn't happen
ebcb : f00b             		beq	getNextEof
                        ;
ebcd : a200             		ldx	#0
ebcf : bd94df           hasdata		lda	buffer,x
ebd2 : e8               		inx
ebd3 : 8ed4df           		stx	diskBufOffset
ebd6 : 18               		clc
ebd7 : 60               		rts
                        ;
ebd8 : a900             getNextEof	lda	#0
ebda : 8dd4df           		sta	diskBufOffset
ebdd : 8dd5df           		sta	diskBufLength
ebe0 : 38               		sec
ebe1 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   32
---------------------------------- xKIM.asm ----------------------------------

                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the input vector
                        ; pointer to get the next input character.
                        ;
ebe2 : 6c83df           redirectedGetch	jmp	(inputVector)
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to the normal
                        ; console input subroutine.
                        ;
ebe5 : a95a             setInputConsole	lda	#GETCH&$ff
ebe7 : 8d83df           		sta     inputVector
ebea : a91e             		lda	#GETCH/256
ebec : 8d84df           		sta	inputVector+1
ebef : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the input vector to point to a file read
                        ; subroutine.
                        ;
ebf0 : a9b3             setInputFile    lda	#getNextFileByte&$ff
ebf2 : 8d83df           		sta     inputVector
ebf5 : a9eb             		lda	#getNextFileByte/256
ebf7 : 8d84df           		sta	inputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ebfa : a900             		lda	#0
ebfc : 8dd4df           		sta	diskBufOffset
ebff : 8dd5df           		sta	diskBufLength
ec02 : 60               		rts
                        ;
                        ;=====================================================
                        ; Print character in A as two hex digits to the
                        ; current output device (console or file).
                        ;
ec03 : 48               HexToOutput	pha		;save return value
ec04 : 48               		pha
ec05 : 4a               		lsr	a	;move top nibble to bottom
ec06 : 4a               		lsr	a
ec07 : 4a               		lsr	a
ec08 : 4a               		lsr	a
ec09 : 2012ec           		jsr	hexta	;output nibble
ec0c : 68               		pla
ec0d : 2012ec           		jsr	hexta
ec10 : 68               		pla		;restore
ec11 : 60               		rts
                        ;
ec12 : 290f             hexta		and	#%0001111
ec14 : c90a             		cmp	#$0a
ec16 : 18               		clc
ec17 : 3002             		bmi	hexta1
ec19 : 6907             		adc	#7
ec1b : 6930             hexta1		adc	#'0'	;then fall into...
                        ;
                        ;=====================================================
                        ; This is a helper function used for redirected I/O.
                        ; It simply does a jump through the output vector
                        ; pointer to send the character in A to the proper
AS65 Assembler for R6502 [1.42].                                     Page   33
---------------------------------- xKIM.asm ----------------------------------

                        ; device.
                        ;
ec1d : 6c85df           redirectedOutch	jmp	(outputVector)
                        ;
                        ;=====================================================
                        ; This flushes any data remaining in the disk buffer
                        ; and then closes the file.
                        ;
ec20 : add4df           CloseOutFile	lda	diskBufOffset
ec23 : f007             		beq	closeonly
ec25 : a2df             		ldx	#buffer>>8
ec27 : a094             		ldy	#buffer&$ff
ec29 : 209eed           		jsr	DiskWrite
                        ;
ec2c : 20d5ed           closeonly	jsr	DiskClose
                        ;
                        ; Fall through...
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to the normal
                        ; console output subroutine.
                        ;
ec2f :                  setOutputConsole
ec2f : a9a0             		lda	#OUTCH&$ff
ec31 : 8d85df           		sta     outputVector
ec34 : a91e             		lda	#OUTCH/256
ec36 : 8d86df           		sta	outputVector+1
ec39 : 60               		rts
                        ;
                        ;=====================================================
                        ; Set up the output vector to point to a file write
                        ; subroutine.
                        ;
ec3a : a94a             setOutputFile    lda	#putNextFileByte&$ff
ec3c : 8d85df           		sta     outputVector
ec3f : a9ec             		lda	#putNextFileByte/256
ec41 : 8d86df           		sta	outputVector+1
                        ;
                        ; Clear counts and offsets so the next read will
                        ; cause the file to be read.
                        ;
ec44 : a900             		lda	#0
ec46 : 8dd4df           		sta	diskBufOffset
ec49 : 60               		rts
                        ;
                        ;=====================================================
                        ; Add the byte in A to the output buffer.  If the
                        ; buffer is full, flush it to disk.
                        ;
ec4a : aed4df           putNextFileByte	ldx	diskBufOffset
ec4d : e040             		cpx	#BUFFER_SIZE	;buffer full?
ec4f : d00d             		bne	pNFB		;no
                        ;
                        ; The buffer is full, so write it out.
                        ;
ec51 : 48               		pha			;save byte
ec52 : a940             		lda	#BUFFER_SIZE
ec54 : a2df             		ldx	#buffer>>8
ec56 : a094             		ldy	#buffer&$ff
ec58 : 209eed           		jsr	DiskWrite
                        ;
ec5b : a200             		ldx	#0		;reset index
AS65 Assembler for R6502 [1.42].                                     Page   34
---------------------------------- xKIM.asm ----------------------------------

ec5d : 68               		pla
ec5e : 9d94df           pNFB		sta	buffer,x
ec61 : e8               		inx
ec62 : 8ed4df           		stx	diskBufOffset
ec65 : 60               		rts
                        ;
                        		include	"pario.asm"
                        ;*****************************************************
                        ; These are the low-level I/O routines to talk to the
                        ; Arduino processor connected to the KIM's I/O port.
                        ;
                        ; August 2014, Bob Applegate K2UT, bob@corshamtech.com
                        ;
                        ; Which port bits are used for what:
                        ;
                        ; A0 = Data 0, alternates input/output
                        ; A1 = Data 1, alternates input/output
                        ; A2 = Data 2, alternates input/output
                        ; A3 = Data 3, alternates input/output
                        ; A4 = Data 4, alternates input/output
                        ; A5 = Data 5, alternates input/output
                        ; A6 = Data 6, alternates input/output
                        ; A7 = Data 7, alternates input/output
                        ;
                        ; B0 = Direction bit, always output
                        ; B1 = Write strobe or ACK, always output
                        ; B2 = Read stroke or ACK, always input
                        ;
                        ;----------------------------------------------------
                        ; Bits in the B register
                        ;
0001 =                  DIRECTION	equ	%00000001
0002 =                  PSTROBE		equ	%00000010
0004 =                  ACK		equ	%00000100
                        ;
                        ;----------------------------------------------------
                        ; The KIM has a 6530 with the two I/O ports at $1700.
                        ;
                        ; Addresses of 6530 registers
                        ;
1700 =                  PIABASE		equ	$1700
1700 =                  PIAREGA		equ	PIABASE		;data reg A
1701 =                  PIADDRA		equ	PIABASE+1	;data dir reg A
1702 =                  PIAREGB		equ	PIABASE+2	;data reg B
1703 =                  PIADDRB		equ	PIABASE+3	;data dir reg B
                        		code
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   35
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This is the initialization function.  Call before
                        ; doing anything else with the parallel port.
                        ;
ec66 :                  xParInit
                        ;
                        ; Set up the data direction register for port B so tha
                        ; the DIRECTION and PSTROBE bits are output.  Only tou
                        ; the bits we're using; leave the rest as-is.
                        ;
ec66 : ad0317           		lda	PIADDRB	;get current value
ec69 : 29fb             		and	#~ACK
ec6b : 0903             		ora	#DIRECTION | PSTROBE
ec6d : 8d0317           		sta	PIADDRB
                        ;
ec70 : a9ff             		lda	#$ff
ec72 : 8d0117           		sta	PIADDRA	;set data for write
                        ;
                        ; Now that the data is set, set the direction
                        ; registers.  This prevents weird problems like
                        ; driving lines to the wrong state.
                        ;
ec75 : ad0217           		lda	PIAREGB
ec78 : 29f9             		and	#~(PSTROBE | ACK)
ec7a : 0901             		ora	#DIRECTION
ec7c : 8d0217           		sta	PIAREGB
ec7f : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   36
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for writing to the Arduino.  Sets up
                        ; direction registers, drives the direction bit, etc.
                        ;
ec80 : a9ff             xParSetWrite    lda	#$ff	;set bits for output
ec82 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to output, clear ACK bit
                        ;
ec85 : ad0217           		lda	PIAREGB
ec88 : 29f9             		and	#~(PSTROBE | ACK)
ec8a : 0901             		ora	#DIRECTION
ec8c : 8d0217           		sta	PIAREGB
ec8f : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   37
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This sets up for reading from the Arduino.  Sets up
                        ; direction registers, clears the direction bit, etc.
                        ;
ec90 : a900             xParSetRead     lda	#$00	;set bits for input
ec92 : 8d0117           		sta	PIADDRA
                        ;
                        ; Set direction flag to input, clear ACK bit
                        ;
ec95 : ad0217           		lda	PIAREGB
ec98 : 29f8             		and	#~(DIRECTION | PSTROBE | ACK)
ec9a : 8d0217           		sta	PIAREGB
ec9d : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   38
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This writes a single byte to the Arduino.  On entry,
                        ; the byte to write is in A.  This assumes ParSetWrite
                        ; was already called.
                        ;
                        ; Destroys A, all other registers preserved.
                        ;
                        ; Write cycle:
                        ;
                        ;    1. Wait for other side to lower ACK.
                        ;    2. Put data onto the bus.
                        ;    3. Set DIRECTION and PSTROBE to indicate data
                        ;       is valid and ready to read.
                        ;    4. Wait for ACK line to go high, indicating the
                        ;       other side has read the data.
                        ;    5. Lower PSTROBE.
                        ;    6. Wait for ACK to go low, indicating end of
                        ;       transfer.
                        ;
ec9e : 48               xParWriteByte	pha		;save data
ec9f : ad0217           Parwl22		lda	PIAREGB	;check status
eca2 : 2904             		and	#ACK
eca4 : d0f9             		bne	Parwl22	;wait for ACK to go low
                        ;
                        ; Now put the data onto the bus
                        ;
eca6 : 68               		pla
eca7 : 8d0017           		sta	PIAREGA
                        ;
                        ; Raise the strobe so the Arduino knows there is
                        ; new data.
                        ;
ecaa : ad0217           		lda	PIAREGB
ecad : 0902             		ora	#PSTROBE
ecaf : 8d0217           		sta	PIAREGB
                        ;
                        ; Wait for ACK to go high, indicating the Arduino has
                        ; pulled the data and is ready for more.
                        ;
ecb2 : ad0217           Parwl33		lda	PIAREGB
ecb5 : 2904             		and	#ACK
ecb7 : f0f9             		beq	Parwl33
                        ;
                        ; Now lower the strobe, then wait for the Arduino to
                        ; lower ACK.
                        ;
ecb9 : ad0217           		lda	PIAREGB
ecbc : 29fd             		and	#~PSTROBE
ecbe : 8d0217           		sta	PIAREGB
ecc1 : ad0217           Parwl44		lda	PIAREGB
ecc4 : 2904             		and	#ACK
ecc6 : d0f9             		bne	Parwl44
ecc8 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   39
---------------------------------- xKIM.asm ----------------------------------

                        ;*****************************************************
                        ; This reads a byte from the Arduino and returns it in
                        ; A.  Assumes ParSetRead was called before.
                        ;
                        ; This does not have a time-out.
                        ;
                        ; Preserves all other registers.
                        ;
                        ; Read cycle:
                        ;
                        ;    1. Wait for other side to raise ACK, indicating
                        ;       data is ready.
                        ;    2. Read data.
                        ;    3. Raise PSTROBE indicating data was read.
                        ;    4. Wait for ACK to go low.
                        ;    5. Lower PSTROBE.
                        ;
ecc9 : ad0217           xParReadByte    lda	PIAREGB
eccc : 2904             		and	#ACK	;is their strobe high?
ecce : f0f9             		beq	xParReadByte	;nope, no data
                        ;
                        ; Data is available, so grab and save it.
                        ;
ecd0 : ad0017           		lda	PIAREGA
ecd3 : 48               		pha
                        ;
                        ; Now raise our strobe (their ACK), then wait for
                        ; them to lower their strobe.
                        ;
ecd4 : ad0217           		lda	PIAREGB
ecd7 : 0902             		ora	#PSTROBE
ecd9 : 8d0217           		sta	PIAREGB
ecdc : ad0217           Parrlp1		lda	PIAREGB
ecdf : 2904             		and	#ACK
ece1 : d0f9             		bne	Parrlp1	;still active
                        ;
                        ; Lower our ack, then were done.
                        ;
ece3 : ad0217           		lda	PIAREGB
ece6 : 29fd             		and	#~PSTROBE
ece8 : 8d0217           		sta	PIAREGB
eceb : 68               		pla
ecec : 60               		rts
                        
                        		include	"parproto.inc"
                        ;*****************************************************
                        ; Parallel port protocol
                        ;
                        ; This is the header file for making applications
                        ; compliant with The Remote Disk Protocol Guide which
                        ; is on the Corsham Technologies web page somewhere:
                        ;
                        ;    www.corshamtech.com
                        ;
                        ; This was updated 06/13/2015 to be compliant with the
                        ; official specification, so the opcode values changed
                        ;
                        ; Another update on 02/10/2019
                        ;
                        ;=====================================================
                        ; Commands from host to Arduino
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   40
---------------------------------- xKIM.asm ----------------------------------

0001 =                  PC_GET_VERSION	equ	$01
0005 =                  PC_PING		equ	$05	;ping Arduino
0006 =                  PC_LED_CONTROL	equ	$06	;LED control
0007 =                  PC_GET_CLOCK	equ	$07	;get clock data
0008 =                  PC_SET_CLOCK	equ	$08	;set clock
0010 =                  PC_GET_DIR	equ	$10	;Get directory
0011 =                  PC_GET_MOUNTED	equ	$11	;Get mounted drive list
0012 =                  PC_MOUNT	equ	$12	;Mount drive
0013 =                  PC_UNMOUNT	equ	$13	;Unmount drive
0014 =                  PC_GET_STATUS	equ	$14	;Get status for one drive
0015 =                  PC_DONE		equ	$15	;Stop data
0015 =                  PC_ABORT	equ	PC_DONE
0016 =                  PC_READ_FILE	equ	$16	;Read regular file (non-DSK)
0017 =                  PC_READ_BYTES	equ	$17	;Read sequential bytes
0018 =                  PC_RD_SECTOR	equ	$18	;Read FLEX sector
0019 =                  PC_WR_SECTOR	equ	$19	;Write FLEX sector
001a =                  PC_GET_MAX	equ	$1a	;Get maximum drives
001b =                  PC_WRITE_FILE   equ	$1b	;Open file for writing
001c =                  PC_WRITE_BYTES	equ	$1c	;Data to be written
001d =                  PC_SAVE_CONFIG	equ	$1d	;Save SD.CFG with current value
001e =                  PC_SET_TIMER	equ	$1e	;Set RTC timer
001f =                  PC_RD_SEC_LONG	equ	$1f	;Read sector with long sec num
0020 =                  PC_WR_SEC_LONG	equ	$20	;Write sector with long sec num
                        ;
                        ;=====================================================
                        ; Responses from Arduino to host
                        ;
0081 =                  PR_VERSION_INFO	equ	$81	;Contains version information
0082 =                  PR_ACK		equ	$82	;ACK (no additional information)
0083 =                  PR_NAK		equ	$83	;NAK - one status byte follows
0085 =                  PR_PONG		equ	$85	;Reply to a ping
0087 =                  PR_CLOCK_DATA	equ	$87	;Clock data
0090 =                  PR_DIR_ENTRY	equ	$90	;Directory entry
0091 =                  PR_DIR_END	equ	$91	;End of directory entries
0092 =                  PR_FILE_DATA	equ	$92	;File data
0093 =                  PR_STATUS	equ	$93	;Drive status
0094 =                  PR_SECTOR_DATA	equ	$94	;Sector data
0095 =                  PR_MOUNT_INFO	equ	$95	;Mount entry
0096 =                  PR_MAX_DRIVES	equ	$96	;Maximum number of drives
                        ;
                        ;=====================================================
                        ; Error codes for NAK events.  Yes, these are in
                        ; decimal due to an error in the original
                        ; documentation.
                        ;
0000 =                  ERR_NONE	equ	0
000a =                  ERR_NOT_MOUNTED	equ	10
000b =                  ERR_MOUNTED	equ	11
000c =                  ERR_NOT_FOUND	equ	12
000d =                  ERR_READ_ONLY	equ	13
000e =                  ERR_BAD_DRIVE	equ	14
000f =                  ERR_BAD_TRACK	equ	15
0010 =                  ERR_BAD_SECTOR	equ	16
0011 =                  ERR_READ_ERROR	equ	17
0012 =                  ERR_WRITE_ERROR	equ	18
0013 =                  ERR_NOT_PRESENT	equ	19
0014 =                  ERR_NOT_IMPL	equ	20	;Command not implemented
                        
                        
                        		include	"diskfunc.asm"
                        		list
                        ;=====================================================
AS65 Assembler for R6502 [1.42].                                     Page   41
---------------------------------- xKIM.asm ----------------------------------

                        ; This is a collection of functions for performing
                        ; higher level disk functions.  This hides the nasty
                        ; details of communications with the remote disk
                        ; system.
                        ;
                        ; August 20, 2014 - Bob Applegate
                        ;                   bob@corshamtech.com
                        ;
                        ; 06/14/2015 - Bob Applegate
                        ;		Now that there is an official standard
                        ;		for the protocol between the host (this
                        ;		code) and the DCP (Arduino code), this
                        ;		code has been updated to be compliant.
                        ;
                        ; 01/14/2016 - Bob Applegate
                        ;              Finally converted to 6502.
                        ;
                        ;		include	"parproto.inc"
                        ;
                        ; Number of drives emulated
                        ;
0004 =                  DRIVES		equ	4
                        ;
                        ;=====================================================
                        ; This is a sanity check to verify connectivity to the
                        ; Arduino code is working.  Returns C clear if all is
                        ; good, or C set if not.
                        ;
eced : 2080ec           DiskPing	jsr	xParSetWrite
ecf0 : a905             		lda	#PC_PING	;command
ecf2 : 209eec           		jsr	xParWriteByte	;send to Arduino
ecf5 : 2090ec           		jsr	xParSetRead
ecf8 : 20c9ec           		jsr	xParReadByte	;read their reply
ecfb : 2080ec           DiskRetGood	jsr	xParSetWrite
ecfe : 18               		clc			;assume it's good
ecff : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   42
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This starts a directory read of the raw drive, not
                        ; the mounted drive.  No input parameters.  This simpl
                        ; sets up for reading the entries, then the user must
                        ; read each entry.
                        ;
                        ; Returns with C clear on success.  If error, C is set
                        ; and A contains the error code.
                        ;
ed00 : 2080ec           DiskDir		jsr	xParSetWrite
ed03 : a910             		lda	#PC_GET_DIR	;send command
ed05 : 209eec           		jsr	xParWriteByte
ed08 : 18               		clc		;assume it works
ed09 : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   43
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; Read the next directory entry.  On input, X (MSB)
                        ; and Y (LSB) point to a 13 byte area to receive the
                        ; drive data.
                        ;
                        ; Returns C set if end of directory (ie, attempt to
                        ; read and there are none left).  Else, C is clear
                        ; and X/Y point to the null at end of filename.
                        ;
ed0a : 86fb             DiskDirNext	stx	POINTH
ed0c : 84fa             		sty	POINTL
ed0e : 2090ec           		jsr	xParSetRead	;read results
ed11 : 20c9ec           		jsr	xParReadByte	;get response code
ed14 : c983             		cmp	#PR_NAK		;error?
ed16 : f01e             		beq	DDNErr
ed18 : c991             		cmp	#PR_DIR_END	;end?
ed1a : f01a             		beq	DDNErr
                        ;
                        ; This contains a directory entry.
                        ;
ed1c : a000             		ldy	#0
ed1e : 20c9ec           DDNloop		jsr	xParReadByte
ed21 : 91fa             		sta	(POINTL),y
ed23 : c900             		cmp	#0	;end?
ed25 : f006             		beq	DDNEnd
ed27 : 20631f           		jsr	INCPT
ed2a : 4c1eed           		jmp	DDNloop
ed2d : 2080ec           DDNEnd		jsr	xParSetWrite
ed30 : a6fb             		ldx	POINTH
ed32 : a4fa             		ldy	POINTL
ed34 : 18               		clc		;not end of files
ed35 : 60               		rts
                        ;
                        ; Error.  Set C and return.  This is not really
                        ; proper, since this implies a simple end of the
                        ; directory rather than an error.
                        ;
ed36 : 2080ec           DDNErr		jsr	xParSetWrite
ed39 : 38               		sec
ed3a : 60               		rts
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   44
---------------------------------- xKIM.asm ----------------------------------

                        ;=====================================================
                        ; This opens a file on the SD for reading.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error (usually means the
                        ; file does not exist.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed3b : a916             DiskOpenRead	lda	#PC_READ_FILE
ed3d :                  DiskOpen
ed3d : 84f8                     sty	INL	;save ptr to filename
ed3f : 86f9             		stx	INH
ed41 : 209eec           		jsr	xParWriteByte
ed44 : a0ff             		ldy	#-1
ed46 : c8               DiskOpenLoop	iny
ed47 : b1f8             		lda	(INL),y
ed49 : 209eec           		jsr	xParWriteByte
ed4c : b1f8             		lda	(INL),y
ed4e : d0f6             		bne	DiskOpenLoop
ed50 : 2090ec           		jsr	xParSetRead
ed53 : 20c9ec           		jsr	xParReadByte	;get response
ed56 : c982             		cmp	#PR_ACK
ed58 : d005             		bne	DiskOpenErr
ed5a : 2080ec           		jsr	xParSetWrite	;back to write mode
ed5d : 18               		clc
ed5e : 60               		rts
                        ;
                        ; Got an error.
                        ;
ed5f : 20c9ec           DiskOpenErr	jsr	xParReadByte	;get error code
ed62 : 2080ec           		jsr	xParSetWrite	;back to write mode
ed65 : 38               		sec
ed66 : 60               		rts
                        ;=====================================================
                        ; This opens a file on the SD for writing.  On entry,
                        ; X (MSB) and Y (LSB) point to a null-terminated
                        ; filename to open.  On return, C is clear if the file
                        ; is open, or C set if an error.
                        ;
                        ; Assumes write mode has been set.  Returns with it se
                        ;
ed67 : a91b             DiskOpenWrite	lda	#PC_WRITE_FILE
ed69 : 4c3ded           		jmp	DiskOpen	;jump into common code
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to read
                        ; from the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to put the data.  On return, C will
                        ; be set if EOF was reached (and no data read), or
                        ; C will be clear and A contains the number of bytes
                        ; actually read into the buffer.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed6c : 84f8             DiskRead	sty	INL	;save ptr to buffer
ed6e : 86f9             		stx	INH
ed70 : 48               		pha
ed71 : a917             		lda	#PC_READ_BYTES
ed73 : 209eec           		jsr	xParWriteByte	;command
ed76 : 68               		pla		;number of bytes to get
AS65 Assembler for R6502 [1.42].                                     Page   45
---------------------------------- xKIM.asm ----------------------------------

ed77 : 209eec           		jsr	xParWriteByte
ed7a : 2090ec           		jsr	xParSetRead	;get ready for response
ed7d : 20c9ec           		jsr	xParReadByte	;assume PR_FILE_DATA
ed80 : 20c9ec           		jsr	xParReadByte	;length
ed83 : 48               		pha
ed84 : aa               		tax			;count
ed85 : f011             		beq	DiskReadEof	;zero = EOF
ed87 : a000             		ldy	#0	;offset
ed89 : 20c9ec           DiskReadLoop	jsr	xParReadByte
ed8c : 91f8             		sta	(INL),y
ed8e : c8               		iny		;next offset
ed8f : ca               		dex
ed90 : d0f7             		bne	DiskReadLoop
ed92 : 2080ec           		jsr	xParSetWrite
ed95 : 68               		pla		;retrieve byte count
ed96 : 18               DiskOk		clc	
ed97 : 60               		rts
ed98 : 2080ec           DiskReadEof	jsr	xParSetWrite
ed9b : 68               		pla
ed9c : 38               		sec
ed9d : 60               		rts
                        ;
                        ;=====================================================
                        ; On entry, A contains the number of bytes to write
                        ; to the file, X (MSB) and Y (LSB) point to the
                        ; buffer where to get the data.  On return, C will
                        ; be set if an error was detected, or C will be clear
                        ; if no error.  Note that if A contains 0 on entry,
                        ; no bytes are written.
                        ;
                        ; Modifies A, X and Y.  Also modifies INL and INH
                        ; (00F8 and 00F9).
                        ;
ed9e : c900             DiskWrite	cmp	#0
eda0 : f0f4             		beq	DiskOk
eda2 : 84f8             		sty	INL	;save ptr to filename
eda4 : 86f9             		stx	INH
eda6 : 48               		pha
eda7 : a91c             		lda	#PC_WRITE_BYTES
eda9 : 209eec           		jsr	xParWriteByte	;command
edac : 68               		pla		;number of bytes to write
edad : 48               		pha		;save again
edae : 209eec           		jsr	xParWriteByte
edb1 : 68               		pla
edb2 : aa               		tax			;count
edb3 : a000             		ldy	#0	;offset
edb5 : b1f8             DiskWriteLoop	lda	(INL),y	;get next byte
edb7 : 209eec           		jsr	xParWriteByte
edba : c8               		iny		;next offset
edbb : ca               		dex
edbc : d0f7             		bne	DiskWriteLoop
edbe : 2090ec           		jsr	xParSetRead ;read the status
edc1 : 20c9ec           		jsr	xParReadByte
edc4 : c982             		cmp	#PR_ACK
edc6 : f008             		beq	DiskOk1	;all good
edc8 : 20c9ec           		jsr	xParReadByte	;read error code
edcb : 2080ec           		jsr	xParSetWrite 
edce : 38               		sec	
edcf : 60               		rts
                        ;
edd0 : 2080ec           DiskOk1		jsr	xParSetWrite
edd3 : 18               		clc
AS65 Assembler for R6502 [1.42].                                     Page   46
---------------------------------- xKIM.asm ----------------------------------

edd4 : 60               		rts
                        ;
                        ;=====================================================
                        ; Call this to close any open file.  No parameters
                        ; and no return status.
                        ;
edd5 : a915             DiskClose	lda	#PC_DONE
edd7 : 4c9eec           		jmp	xParWriteByte
                        
                        
                        
                        ;
                        ;=====================================================
                        ; Show current clock
                        ;
                        	if	SHOW_RTC
edda : 2080ec           doShowClock	jsr	xParSetWrite
eddd : a907             		lda	#PC_GET_CLOCK
eddf : 209eec           		jsr	xParWriteByte
ede2 : 2090ec           		jsr	xParSetRead	;prepare to read
                        ;
ede5 : 20c9ec           		jsr	xParReadByte
                        ;
                        ; Loop to read the raw data
                        ;
ede8 : a200             		ldx	#0
edea : 8e81df           clockread	stx	saveX
eded : 20c9ec           		jsr	xParReadByte
edf0 : ae81df           		ldx	saveX
edf3 : 9d94df           		sta	month,x
edf6 : e8               		inx
edf7 : e008             		cpx	#clock_end-month
edf9 : d0ef             		bne	clockread
                        ;
                        ; Set back to write mode to finish up; all apps are
                        ; supposed to leave the SD interface in write mode.
                        ;
edfb : 2080ec           		jsr	xParSetWrite
                        ;
                        ; Now display the data in a user-friendly format.  Eac
                        ; numberic value is in binary, so convert to decimal
                        ; for display.
                        ;
edfe : 20bfea           		jsr	putsil
ee01 : 0d0a             		db	CR,LF
ee03 : 446174653a2000   		db	"Date: ",0
                        ;
ee0a : ad94df           		lda	month
ee0d : 2053ee           		jsr	outdec
ee10 : a92f             		lda	#'/'
ee12 : 20a01e           		jsr	OUTCH
ee15 : ad95df           		lda	day
ee18 : 2053ee           		jsr	outdec
                        ;
                        ; Always force the high part of the year to "/20"
                        ;
ee1b : 20bfea           		jsr	putsil
ee1e : 2f323000         		db	"/20",0
ee22 : ad97df           		lda	year_low
ee25 : 2053ee           		jsr	outdec
                        ;
                        ; Space over, then do the time
AS65 Assembler for R6502 [1.42].                                     Page   47
---------------------------------- xKIM.asm ----------------------------------

                        ;
ee28 : 20bfea           		jsr	putsil
ee2b : 2c2000           		db	", ",0
                        ;
ee2e : ad98df           		lda	hour
ee31 : 2053ee           		jsr	outdec
ee34 : a93a             		lda	#':'
ee36 : 20a01e           		jsr	OUTCH
ee39 : ad99df           		lda	minute
ee3c : 2053ee           		jsr	outdec
ee3f : a93a             		lda	#':'
ee41 : 20a01e           		jsr	OUTCH
ee44 : ad9adf           		lda	second
ee47 : 2053ee           		jsr	outdec
                        ;
ee4a : 20bfea           		jsr	putsil
ee4d : 0d0a00           		db	CR,LF,0
ee50 : 4c60e0           		jmp	extKim	;return to monitor
                        ;
                        ;=====================================================
                        ; Given a binary value in A, display it as two decimal
                        ; digits.  The input can't be greater than 99.  Always
                        ; print a leading zero if less than 10.
                        ;
ee53 : a000             outdec		ldy	#0	;counts 10s
ee55 : c90a             out1		cmp	#10
ee57 : 9007             		bcc	out2	;below 10
ee59 : c8               		iny		;count 10
ee5a : 38               		sec
ee5b : e90a             		sbc	#10
ee5d : 4c55ee           		jmp	out1
                        ;
ee60 : 48               out2		pha		;save ones
ee61 : 98               		tya		;get tens
ee62 : 2066ee           		jsr	out3	;print tens digit
ee65 : 68               		pla		;restore ones
                        ;
ee66 : 0930             out3		ora	#'0'
ee68 : 20a01e           		jsr	OUTCH
ee6b : 60               		rts
                        
                        	endif
                        ;
                        	if	TINY_BASIC
                        		include "mytb.asm"
                        	endif
                        		page
AS65 Assembler for R6502 [1.42].                                     Page   48
---------------------------------- xKIM.asm ----------------------------------

                        		
                        ;
                        ;=====================================================
                        ; Handles the command to save a region of memory to
                        ; an IEC device.
                        ;
ee6c : 4c2ee1           lExit111		jmp	extKimLoop
ee6f : 204aeb           saveIEC		jsr	getAddrRange	;get range to dump
ee72 : b0f8             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;
ee74 : 20bfea           		jsr	putsil
ee77 : 0d0a             		db	CR,LF
ee79 : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
ee92 : 20eeea           		jsr	getHex
ee95 : b0d5             		bcs	lExit111		;abort on error
ee97 : 8df917           		sta	IECFID
                        		
ee9a : adf617           		lda SAH
ee9d : 8df617           		sta IECSAH
eea0 : adf517           		lda SAL
eea3 : 8df517           		sta IECSAL
eea6 : adf817           		lda EAH
eea9 : 8df817           		sta IECEAH
eeac : adf717           		lda EAL
eeaf : 8df717           		sta IECEAL
                        		
eeb2 : 4c48f1           		jmp IECSAV
eeb5 : 4c2ee1           		jmp	extKimLoop
                        ;
                        ;=====================================================
                        ; Handles the command to load a region of memory from
                        ; an IEC device.
                        ;
eeb8 : 204aeb           loadIEC		jsr	getAddrRange	;get range to dump
eebb : b0af             			bcs	lExit111		;abort on error
                        ;
                        ; Get the file id to save to
                        ;
eebd : 20bfea           		jsr	putsil
eec0 : 0d0a             		db	CR,LF
eec2 : 456e7465722066.. 		db	"Enter file ID ($01-$FE):",0
eedb : 20eeea           		jsr	getHex
eede : b08c             		bcs	lExit111		;abort on error
eee0 : 8df917           		sta	IECFID
                        		
eee3 : 4c00f0           		jmp IECLOD
eee6 : 4c2ee1           		jmp	extKimLoop
                        
                        ;
                        ;=====================================================
                        ; These are the 6502 vectors.  On the Corsham
                        ; Technologies RAM/ROM board the user can select
                        ; whether or not to use these based on a switch
                        ; setting on the board.
                        ;
fffa =                  		org	$fffa
fffa : 1c1c             NMI		dw	NMIT	;in KIM
fffc : 221c             RESET		dw	RST	;in KIM
fffe : 1f1c             IRQ		dw	IRQT	;in KIM
                        ;
AS65 Assembler for R6502 [1.42].                                     Page   49
---------------------------------- xKIM.asm ----------------------------------

                        		end
                        
                        
No errors in pass 2.
Wrote binary from address $e000 through $ffff.
Total size 8192 bytes.
